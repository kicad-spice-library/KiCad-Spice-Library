***********
* QW-LASER MODEL
*
* Pablo V. Mena, Steve. A. Javro, Sung Mo Kang
* Copyright 1997 The Board of Trustees of the University of Illinois
* All rights reserved
* Reprinted with Permission from The Board of Trustees of the University of Illinois.
* 
*                          __________
*                         |          |     PF
*                 P o-----|P       PF|------o
*                         |   intr.  |      |
*                         |   laser  |     <
*                         |   cavity |      > Rout
*                         |          |     < 
*                         |          |      |
*                 N o-----|N         |    -----
*                         |__________|     ---
*                                           -
* 
************
* QW-LASER INTRINSIC CAVITY SUBCIRCUITS
* each qw cavity subckt has the name qwlas<a><b>. <a><b> identify the model.
*   - a = model number = 1:  one-level rate-equation model
*                        2a: two-level rate-equation model with SCH recombination
*                        2b: two-level rate-equation model w/o SCH recombination
*   - b = level number = 1: full logarithmic gain
*                        2: simplified logarithmic gain
*                        3: gain linearized about optical transparency
*                        4: gain linearized about threshold
*
* The quantum-well (QW) laser model is implemented as an equivalent circuit 
* composed of both parasitic elements (series and parallel) and one of two possible 
* intrinsic cavity models. The first cavity model is based on the two basic rate 
* equations, one for QW carrier concentration, the other for photon density in the 
* active region. The second cavity model includes a third rate equation for carriers 
* in the barrier, or confinement, layers adjoining the QWs. Unimolecular, radiative, 
* and Auger recombination are each accounted for in the model. Furthermore, one of 
* four different gain terms can be chosen. These terms include both logarithmic 
* gain expressions as well as linearized ones. Finally, the parasitic elements of 
* the model can account for series resistance, additional series diode effects, and 
* shunting resistance or capacitance.
* 
****************
*SRC=QWLAS11;QWLAS11;Diodes;Laser;1L Rate Full Log Gain
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS11 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1}
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} 
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} 
BR1 P N I={2*AP*TN}*I(VT1)+{4*ETAI*B*TN^2/(1.60219E-19*NW*VACT)}*I(VT1)*I(VT1)+
+   {8*ETAI^2*C*TN^3/(1.60219E-19*NW*VACT)^2}*I(VT1)*I(VT1)*I(VT1)
BS1 P N I={LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8)}*
+   (V(M)+{DEL2})*(V(M)+{DEL2})*LN({DELTA}+
+   {2*A*ETAI*TN/(1.60219E-19*NW*VACT)/(A*NO+B*NO^2+C*NO^3)}*I(VT1)+
+   {4*B*(ETAI*TN/(1.60219E-19*NW*VACT))^2/(A*NO+B*NO^2+C*NO^3)}*I(VT1)*I(VT1)+
+   {8*C*(ETAI*TN/(1.60219E-19*NW*VACT))^3/(A*NO+B*NO^2+C*NO^3)}*I(VT1)*
+   I(VT1)*I(VT1))/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*
+   (V(M)+{DEL2}))
RPH M 0 1
CPH M 0 {2*TP} 
BR2 0 M I=({2*ETAI*ETAC*TN*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VT1)+
+   {4*ETAI^2*ETAC*TN^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*
+   I(VT1)*I(VT1)+
+   {8*ETAI^3*ETAC*TN^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VT1)*I(VT1)*I(VT1))/(V(M)+{DEL2})
BS2 0 M I={TP*NW*GAMMA*VGR*GO}*(V(M)+{DEL2})*LN({DELTA}+
+   {2*A*ETAI*TN/(1.60219E-19*NW*VACT)/(A*NO+B*NO^2+C*NO^3)}*I(VT1)+
+   {4*B*(ETAI*TN/(1.60219E-19*NW*VACT))^2/(A*NO+B*NO^2+C*NO^3)}*I(VT1)*I(VT1)+
+   {8*C*(ETAI*TN/(1.60219E-19*NW*VACT))^3/(A*NO+B*NO^2+C*NO^3)}*I(VT1)*
+   I(VT1)*I(VT1))/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*
+   (V(M)+{DEL2}))-{DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} N={N*TF} TT={2*TN}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.ENDS
***********
*SRC=QWLAS12;QWLAS12;Diodes;Laser;1L Rate Simple Log Gain
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS12 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17 
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*NW*VACT*NE/(2*ETAI*TN)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*NW*VACT*NE/(2*ETAI*TN)}
BR1 P N I={2*AP*TN}*I(VT1)+{4*ETAI*B*TN^2/(1.60219E-19*NW*VACT)}*I(VT1)*I(VT1)+
+   {8*ETAI^2*C*TN^3/(1.60219E-19*NW*VACT)^2}*I(VT1)*I(VT1)*I(VT1)
BS1 P N I={M*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8)}*
+   (V(M)+{DEL2})*(V(M)+{DEL2})*LN({DELTA}+{2*ETAI*TN/(1.60219E-19*NW*VACT*NO)}*
+   I(VT1))/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*
+   (V(M)+{DEL2}))
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({2*ETAI*ETAC*TN*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VT1)+
+   {4*ETAI^2*ETAC*TN^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*
+   I(VT1)*I(VT1)+
+   {8*ETAI^3*ETAC*TN^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VT1)*I(VT1)*I(VT1))/(V(M)+{DEL2})
BS2 0 M I={M*TP*NW*GAMMA*VGR*GO}*(V(M)+{DEL2})*
+   LN({DELTA}+{2*ETAI*TN/(1.60219E-19*NW*VACT*NO)}*I(VT1))/(1+
+   {EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))-
+   {DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} N={N*TF} TT={2*TN}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.ENDS
***********
*SRC=QWLAS13;QWLAS13;Diodes;Laser;1L Rate gain linearized
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS13 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17 
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*NW*VACT*NE/(2*ETAI*TN)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*NW*VACT*NE/(2*ETAI*TN)}
BR1 P N I={2*AP*TN}*I(VT1)+{4*ETAI*B*TN^2/(1.60219E-19*NW*VACT)}*I(VT1)*I(VT1)+
+   {8*ETAI^2*C*TN^3/(1.60219E-19*NW*VACT)^2}*I(VT1)*I(VT1)*I(VT1)
BS1 P N I=(V(M)+{DEL2})*(V(M)+{DEL2})*
+   ({2*M*LAMBDA*TP*GAMMA*VGR*GO*TN/(ETAC*VACT*NO*6.6262E-34*3E8)}*I(VT1)-
+   {M*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8)})/
+   (1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({2*ETAI*ETAC*TN*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VT1)+
+   {4*ETAI^2*ETAC*TN^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*
+   I(VT1)*I(VT1)+
+   {8*ETAI^3*ETAC*TN^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VT1)*I(VT1)*I(VT1))/(V(M)+{DEL2})
BS2 0 M I=(V(M)+{DEL2})*({2*M*TP*GAMMA*VGR*GO*ETAI*TN/(1.60219E-19*VACT*NO)}*I(VT1)-
+   {M*TP*NW*GAMMA*VGR*GO})/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*
+   (V(M)+{DEL2})*(V(M)+{DEL2}))-{DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} N={N*TF} TT={2*TN}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.ENDS
***********
*SRC=QWLAS14;QWLAS14;Diodes;Laser;1L Rate gain linearized
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS14 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17 
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*NW*VACT*NE/(2*ETAI*TN)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*NW*VACT*NE/(2*ETAI*TN)}
BR1 P N I={2*AP*TN}*I(VT1)+{4*ETAI*B*TN^2/(1.60219E-19*NW*VACT)}*I(VT1)*I(VT1)+
+   {8*ETAI^2*C*TN^3/(1.60219E-19*NW*VACT)^2}*I(VT1)*I(VT1)*I(VT1)
BS1 P N I=(V(M)+{DEL2})*(V(M)+{DEL2})*
+   ({2*M*LAMBDA*TP*GAMMA*VGR*GO*TN/(ETAC*VACT*NTH*6.6262E-34*3E8)}*I(VT1)-
+   {(LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8))*
+    (M-LOG((A*NTH+B*NTH^2+C*NTH^3)/(A*NO+B*NO^2+C*NO^3)))})/
+    (1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({2*ETAI*ETAC*TN*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VT1)+
+   {4*ETAI^2*ETAC*TN^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*
+   I(VT1)*I(VT1)+
+   {8*ETAI^3*ETAC*TN^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VT1)*I(VT1)*I(VT1))/(V(M)+{DEL2})
BS2 0 M I=(V(M)+{DEL2})*({2*M*TP*GAMMA*VGR*GO*ETAI*TN/(1.60219E-19*VACT*NTH)}*I(VT1)-
+   {TP*NW*GAMMA*VGR*GO*
+    (M-LOG((A*NTH+B*NTH^2+C*NTH^3)/(A*NO+B*NO^2+C*NO^3)))})/
+    (1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))-
+   {DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*NW*VACT*NE/(2*ETAI*TN)} N={N*TF} TT={2*TN}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.ENDS
***********
*SRC=QWLAS2A1;QWLAS2A1;Diodes;Laser;2L Rate w/SCH Full Log Gain
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS2A1 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17 
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} 
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
BRB P N I={2*AB*TCAPT}*I(VT1)+{4*ETAI*BB*TCAPT^2/(1.60219E-19*VBARR)}*I(VT1)*I(VT1)+
+   {8*ETAI^2*CB*TCAPT^3/(1.60219E-19*VBARR)^2}*I(VT1)*I(VT1)*I(VT1)
F1 N P VTW1 1
DW1  W NTW1 DW1MOD
ICW1 W NTW1 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} 
VTW1 NTW1 0 0
DW2  W 0 DW2MOD
ICW2 W 0 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} 
BR1 W 0 I={2*A*TEM}*I(VTW1)+{2*ETAI*B*TEM^2/(1.60219E-19*NW*VACT)}*I(VTW1)*I(VTW1)+
+   {2*ETAI^2*C*TEM^3/(1.60219E-19*NW*VACT)^2}*I(VTW1)*I(VTW1)*I(VTW1)
BS1 W 0 I={2*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8)}*
+   (V(M)+{DEL2})*(V(M)+{DEL2})*LN({DELTA}+
+   {A*ETAI*TEM/(1.60219E-19*NW*VACT)/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)+
+   {B*(ETAI*TEM/(1.60219E-19*NW*VACT))^2/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)*I(VTW1)+
+   {C*(ETAI*TEM/(1.60219E-19*NW*VACT))^3/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)*
+   I(VTW1)*I(VTW1))/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*
+   (V(M)+{DEL2}))
F2 0 W VT1 4
RPH M 0 1
CPH M 0 {2*TP} 
BR2 0 M I=({ETAI*ETAC*TEM*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VTW1)+
+   {ETAI^2*ETAC*TEM^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*I(VTW1)
+   *I(VTW1)+
+   {ETAI^3*ETAC*TEM^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VTW1)*I(VTW1)*I(VTW1))/(V(M)+{DEL2})
BS2 0 M I={TP*NW*GAMMA*VGR*GO}*(V(M)+{DEL2})*LN({DELTA}+
+   {A*ETAI*TEM/(1.60219E-19*NW*VACT)/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)+
+   {B*(ETAI*TEM/(1.60219E-19*NW*VACT))^2/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)*I(VTW1)+
+   {C*(ETAI*TEM/(1.60219E-19*NW*VACT))^3/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)*
+   I(VTW1)*I(VTW1))/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*
+   (V(M)+{DEL2}))-{DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF} TT={2*TCAPT}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.MODEL DW1MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF}
.MODEL DW2MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF} TT={2*TEM}
.ENDS
***********
*SRC=QWLAS2A2;QWLAS2A2;Diodes;Laser;2L Rate w/SCH Simple Log Gain
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS2A2 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17 
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
BRB P N I={2*AB*TCAPT}*I(VT1)+{4*ETAI*BB*TCAPT^2/(1.60219E-19*VBARR)}*I(VT1)*I(VT1)+
+   {8*ETAI^2*CB*TCAPT^3/(1.60219E-19*VBARR)^2}*I(VT1)*I(VT1)*I(VT1)
F1 N P VTW1 1
DW1  W NTW1 DW1MOD
ICW1 W NTW1 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
VTW1 NTW1 0 0
DW2  W 0 DW2MOD
ICW2 W 0 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
BR1 W 0 I={2*A*TEM}*I(VTW1)+{2*ETAI*B*TEM^2/(1.60219E-19*NW*VACT)}*I(VTW1)*I(VTW1)+
+   {2*ETAI^2*C*TEM^3/(1.60219E-19*NW*VACT)^2}*I(VTW1)*I(VTW1)*I(VTW1)
BS1 W 0 I={2*M*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8)}*
+   (V(M)+{DEL2})*(V(M)+{DEL2})*LN({DELTA}+{ETAI*TEM/(1.60219E-19*NW*VACT*NO)}*
+   I(VTW1))/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*
+   (V(M)+{DEL2}))
F2 0 W VT1 4
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({ETAI*ETAC*TEM*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VTW1)+
+   {ETAI^2*ETAC*TEM^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*I(VTW1)
+   *I(VTW1)+
+   {ETAI^3*ETAC*TEM^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VTW1)*I(VTW1)*I(VTW1))/(V(M)+{DEL2})
BS2 0 M I={M*TP*NW*GAMMA*VGR*GO}*(V(M)+{DEL2})*
+   LN({DELTA}+{ETAI*TEM/(1.60219E-19*NW*VACT*NO)}*I(VTW1))/(1+
+   {EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))-
+   {DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF} TT={2*TCAPT}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.MODEL DW1MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF}
.MODEL DW2MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF} TT={2*TEM}
.ENDS
***********
*SRC=QWLAS2A3;QWLAS2A3;Diodes;Laser;2L Rate w/SCH gain linearized
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS2A3 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17 
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
BRB P N I={2*AB*TCAPT}*I(VT1)+{4*ETAI*BB*TCAPT^2/(1.60219E-19*VBARR)}*I(VT1)*I(VT1)+
+   {8*ETAI^2*CB*TCAPT^3/(1.60219E-19*VBARR)^2}*I(VT1)*I(VT1)*I(VT1)
F1 N P VTW1 1
DW1  W NTW1 DW1MOD
ICW1 W NTW1 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
VTW1 NTW1 0 0
DW2  W 0 DW2MOD
ICW2 W 0 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
BR1 W 0 I={2*A*TEM}*I(VTW1)+{2*ETAI*B*TEM^2/(1.60219E-19*NW*VACT)}*I(VTW1)*I(VTW1)+
+   {2*ETAI^2*C*TEM^3/(1.60219E-19*NW*VACT)^2}*I(VTW1)*I(VTW1)*I(VTW1)
BS1 W 0 I=(V(M)+{DEL2})*(V(M)+{DEL2})*
+   ({2*M*LAMBDA*TP*GAMMA*VGR*GO*TEM/(ETAC*VACT*NO*6.6262E-34*3E8)}*I(VTW1)-
+   {2*M*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8)})/
+   (1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))
F2 0 W VT1 4
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({ETAI*ETAC*TEM*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VTW1)+
+   {ETAI^2*ETAC*TEM^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*I(VTW1)
+   *I(VTW1)+
+   {ETAI^3*ETAC*TEM^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VTW1)*I(VTW1)*I(VTW1))/(V(M)+{DEL2})
BS2 0 M I=(V(M)+{DEL2})*({M*TP*GAMMA*VGR*GO*ETAI*TEM/(1.60219E-19*VACT*NO)}*I(VTW1)-
+   {M*TP*NW*GAMMA*VGR*GO})/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*
+   (V(M)+{DEL2})*(V(M)+{DEL2}))-{DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF} TT={2*TCAPT}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.MODEL DW1MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF}
.MODEL DW2MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF} TT={2*TEM}
.ENDS
***********
*SRC=QWLAS2A4;QWLAS2A4;Diodes;Laser;2L Rate w/SCH gain linearized
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS2A4 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17 
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
BRB P N I={2*AB*TCAPT}*I(VT1)+{4*ETAI*BB*TCAPT^2/(1.60219E-19*VBARR)}*I(VT1)*I(VT1)+
+   {8*ETAI^2*CB*TCAPT^3/(1.60219E-19*VBARR)^2}*I(VT1)*I(VT1)*I(VT1)
F1 N P VTW1 1
DW1  W NTW1 DW1MOD
ICW1 W NTW1 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
VTW1 NTW1 0 0
DW2  W 0 DW2MOD
ICW2 W 0 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
BR1 W 0 I={2*A*TEM}*I(VTW1)+{2*ETAI*B*TEM^2/(1.60219E-19*NW*VACT)}*I(VTW1)*I(VTW1)+
+   {2*ETAI^2*C*TEM^3/(1.60219E-19*NW*VACT)^2}*I(VTW1)*I(VTW1)*I(VTW1)
BS1 W 0 I=(V(M)+{DEL2})*(V(M)+{DEL2})*
+   ({2*M*LAMBDA*TP*GAMMA*VGR*GO*TEM/(ETAC*VACT*NTH*6.6262E-34*3E8)}*I(VTW1)-
+   {(2*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8))*
+    (M-LOG((A*NTH+B*NTH^2+C*NTH^3)/(A*NO+B*NO^2+C*NO^3)))})/
+   (1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))
F2 0 W VT1 4
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({ETAI*ETAC*TEM*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VTW1)+
+   {ETAI^2*ETAC*TEM^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*I(VTW1)
+   *I(VTW1)+
+   {ETAI^3*ETAC*TEM^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VTW1)*I(VTW1)*I(VTW1))/(V(M)+{DEL2})
BS2 0 M I=(V(M)+{DEL2})*({M*TP*GAMMA*VGR*GO*ETAI*TEM/(1.60219E-19*VACT*NTH)}*I(VTW1)-
+   {TP*NW*GAMMA*VGR*GO*
+    (M-LOG((A*NTH+B*NTH^2+C*NTH^3)/(A*NO+B*NO^2+C*NO^3)))})/
+   (1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))-
+   {DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF} TT={2*TCAPT}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.MODEL DW1MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF}
.MODEL DW2MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF} TT={2*TEM}
.ENDS
***********
*SRC=QWLAS2B1;QWLAS2B1;Diodes;Laser;2L Rate w/oSCH Full Log Gain
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS2B1 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
F1 N P VTW1 1
DW1  W NTW1 DW1MOD
ICW1 W NTW1 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
VTW1 NTW1 0 0
DW2  W 0 DW2MOD
ICW2 W 0 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
BR1 W 0 I={2*A*TEM}*I(VTW1)+{2*ETAI*B*TEM^2/(1.60219E-19*NW*VACT)}*I(VTW1)*I(VTW1)+
+   {2*ETAI^2*C*TEM^3/(1.60219E-19*NW*VACT)^2}*I(VTW1)*I(VTW1)*I(VTW1)
BS1 W 0 I={2*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8)}*
+   (V(M)+{DEL2})*(V(M)+{DEL2})*LN({DELTA}+
+   {A*ETAI*TEM/(1.60219E-19*NW*VACT)/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)+
+   {B*(ETAI*TEM/(1.60219E-19*NW*VACT))^2/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)*I(VTW1)+
+   {C*(ETAI*TEM/(1.60219E-19*NW*VACT))^3/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)*
+   I(VTW1)*I(VTW1))/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*
+   (V(M)+{DEL2}))
F2 0 W VT1 4
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({ETAI*ETAC*TEM*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VTW1)+
+   {ETAI^2*ETAC*TEM^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*I(VTW1)
+   *I(VTW1)+
+   {ETAI^3*ETAC*TEM^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VTW1)*I(VTW1)*I(VTW1))/(V(M)+{DEL2})
BS2 0 M I={TP*NW*GAMMA*VGR*GO}*(V(M)+{DEL2})*LN({DELTA}+
+   {A*ETAI*TEM/(1.60219E-19*NW*VACT)/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)+
+   {B*(ETAI*TEM/(1.60219E-19*NW*VACT))^2/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)*I(VTW1)+
+   {C*(ETAI*TEM/(1.60219E-19*NW*VACT))^3/(A*NO+B*NO^2+C*NO^3)}*I(VTW1)*
+   I(VTW1)*I(VTW1))/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*
+   (V(M)+{DEL2}))-{DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF} TT={2*TCAPT}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.MODEL DW1MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF}
.MODEL DW2MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF} TT={2*TEM}
.ENDS
***********
*SRC=QWLAS2B2;QWLAS2B2;Diodes;Laser;2L Rate w/oSCH Simple Log Gain
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS2B2 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
F1 N P VTW1 1
DW1  W NTW1 DW1MOD
ICW1 W NTW1 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
VTW1 NTW1 0 0
DW2  W 0 DW2MOD
ICW2 W 0 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
BR1 W 0 I={2*A*TEM}*I(VTW1)+{2*ETAI*B*TEM^2/(1.60219E-19*NW*VACT)}*I(VTW1)*I(VTW1)+
+   {2*ETAI^2*C*TEM^3/(1.60219E-19*NW*VACT)^2}*I(VTW1)*I(VTW1)*I(VTW1)
BS1 W 0 I={2*M*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8)}*
+   (V(M)+{DEL2})*(V(M)+{DEL2})*LN({DELTA}+{ETAI*TEM/(1.60219E-19*NW*VACT*NO)}*
+   I(VTW1))/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*
+   (V(M)+{DEL2}))
F2 0 W VT1 4
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({ETAI*ETAC*TEM*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VTW1)+
+   {ETAI^2*ETAC*TEM^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*I(VTW1)
+   *I(VTW1)+
+   {ETAI^3*ETAC*TEM^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VTW1)*I(VTW1)*I(VTW1))/(V(M)+{DEL2})
BS2 0 M I={M*TP*NW*GAMMA*VGR*GO}*(V(M)+{DEL2})*
+   LN({DELTA}+{ETAI*TEM/(1.60219E-19*NW*VACT*NO)}*I(VTW1))/(1+
+   {EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))-
+   {DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF} TT={2*TCAPT}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.MODEL DW1MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF}
.MODEL DW2MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF} TT={2*TEM}
.ENDS
***********
*SRC=QWLAS2B3;QWLAS2B3;Diodes;Laser;2L Rate w/oSCH gain linearized
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS2B3 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
F1 N P VTW1 1
DW1  W NTW1 DW1MOD
ICW1 W NTW1 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
VTW1 NTW1 0 0
DW2  W 0 DW2MOD
ICW2 W 0 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
BR1 W 0 I={2*A*TEM}*I(VTW1)+{2*ETAI*B*TEM^2/(1.60219E-19*NW*VACT)}*I(VTW1)*I(VTW1)+
+   {2*ETAI^2*C*TEM^3/(1.60219E-19*NW*VACT)^2}*I(VTW1)*I(VTW1)*I(VTW1)
BS1 W 0 I=(V(M)+{DEL2})*(V(M)+{DEL2})*
+   ({2*M*LAMBDA*TP*GAMMA*VGR*GO*TEM/(ETAC*VACT*NO*6.6262E-34*3E8)}*I(VTW1)-
+   {2*M*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8)})/
+   (1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))
F2 0 W VT1 4
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({ETAI*ETAC*TEM*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VTW1)+
+   {ETAI^2*ETAC*TEM^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*I(VTW1)
+   *I(VTW1)+
+   {ETAI^3*ETAC*TEM^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VTW1)*I(VTW1)*I(VTW1))/(V(M)+{DEL2})
BS2 0 M I=(V(M)+{DEL2})*({M*TP*GAMMA*VGR*GO*ETAI*TEM/(1.60219E-19*VACT*NO)}*I(VTW1)-
+   {M*TP*NW*GAMMA*VGR*GO})/(1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*
+   (V(M)+{DEL2})*(V(M)+{DEL2}))-{DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF} TT={2*TCAPT}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.MODEL DW1MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF}
.MODEL DW2MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF} TT={2*TEM}
.ENDS
***********
*SRC=QWLAS2B4;QWLAS2B4;Diodes;Laser;2L Rate w/oSCH gain linearized
*SYM=QWL1
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT QWLAS2B4 P N PF {NE=1E12 N=2 NE2=1E12 NW2=1 ETAI=1 LAMBDA=850E-9 NW=1 VACT=1E-17
+ GAMMA=0.1 VGR=1E8 TP=3E-12 ETAC=0.35 NO=1E24 NTH=1.25E24 GO=1E5
+ M=1 EPS=1E-23 TN=1E-8 AP=0 A=1E8 B=1E-16 C=0 AB=1E8 BB=1E-16 CB=1E-40
+ BETAA=0 BETAB=1E-6 BETAC=0 VBARR=1E-16 TCAPT=1e-12 TEM=1e-9
+ CJO=0 MPOW=0.5 VJ=1 FC=0.5 DELTA=1e-60 DEL2=0 TF=1 }
D1 P NT1 D1MOD
IC1 P NT1 {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
VT1 NT1 N 0
D2 P N D2MOD
IC2 P N {1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)}
F1 N P VTW1 1
DW1  W NTW1 DW1MOD
ICW1 W NTW1 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
VTW1 NTW1 0 0
DW2  W 0 DW2MOD
ICW2 W 0 {1.60219E-19*NW*VACT*NE2/(ETAI*TEM)}
BR1 W 0 I={2*A*TEM}*I(VTW1)+{2*ETAI*B*TEM^2/(1.60219E-19*NW*VACT)}*I(VTW1)*I(VTW1)+
+   {2*ETAI^2*C*TEM^3/(1.60219E-19*NW*VACT)^2}*I(VTW1)*I(VTW1)*I(VTW1)
BS1 W 0 I=(V(M)+{DEL2})*(V(M)+{DEL2})*
+   ({2*M*LAMBDA*TP*GAMMA*VGR*GO*TEM/(ETAC*VACT*NTH*6.6262E-34*3E8)}*I(VTW1)-
+   {(2*LAMBDA*TP*1.60219E-19*NW*GAMMA*VGR*GO/(ETAI*ETAC*6.6262E-34*3E8))*
+    (M-LOG((A*NTH+B*NTH^2+C*NTH^3)/(A*NO+B*NO^2+C*NO^3)))})/
+   (1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))
F2 0 W VT1 4
RPH M 0 1
CPH M 0 {2*TP}
BR2 0 M I=({ETAI*ETAC*TEM*6.6262E-34*3E8*BETAA*A/(1.60219E-19*LAMBDA)}*I(VTW1)+
+   {ETAI^2*ETAC*TEM^2*6.6262E-34*3E8*BETAB*B/((1.60219E-19)^2*LAMBDA*NW*VACT)}*I(VTW1)
+   *I(VTW1)+
+   {ETAI^3*ETAC*TEM^3*6.6262E-34*3E8*BETAC*C/((1.60219E-19)^3*LAMBDA*(NW*VACT)^2)}*
+   I(VTW1)*I(VTW1)*I(VTW1))/(V(M)+{DEL2})
BS2 0 M I=(V(M)+{DEL2})*({M*TP*GAMMA*VGR*GO*ETAI*TEM/(1.60219E-19*VACT*NTH)}*I(VTW1)-
+   {TP*NW*GAMMA*VGR*GO*
+    (M-LOG((A*NTH+B*NTH^2+C*NTH^3)/(A*NO+B*NO^2+C*NO^3)))})/
+   (1+{EPS*GAMMA*LAMBDA*TP/(ETAC*VACT*6.6262E-34*3E8)}*(V(M)+{DEL2})*(V(M)+{DEL2}))-
+   {DEL2}
BPF PF 0 V=(V(M)+{DEL2})*(V(M)+{DEL2})
.MODEL D1MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF}
.MODEL D2MOD D IS={1.60219E-19*VBARR*NE/(2*ETAI*TCAPT)} N={N*TF} TT={2*TCAPT}
+              CJO={CJO} M={MPOW} VJ={VJ} FC={FC}
.MODEL DW1MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF}
.MODEL DW2MOD D IS={1.60219E-19*NW*VACT*NE2/(ETAI*TEM)} N={NW2*TF} TT={2*TEM}
.ENDS
***********

