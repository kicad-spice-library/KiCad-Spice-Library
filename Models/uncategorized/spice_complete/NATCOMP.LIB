***************
* LIBRARY OF NATIONAL SEMICONDUCTOR BIPOLAR MACROMODELS.
* FILE:  NSBJTCMP.LIB
*
* THIS LIBRARY OF MACROMODELS IS BEING SUPPLIED TO USERS AS
* AN AID TO CIRCUIT DESIGNS.  WHILE IT REFLECTS REASONABLY 
* CLOSE SIMILARITY TO THE ACTUAL DEVICE IN TERMS OF
* PERFORMANCE, IT IS NOT SUGGESTED AS A REPLACEMENT FOR
* BREADBOARDING.  SIMULATION SHOULD BE USED AS A SUPPLEMENT
* TO TRADITIONAL LAB TESTING.
*
* USERS SHOULD VERY CAREFULLY NOTE THE FOLLOWING FACTORS
* REGARDING THESE MODELS:
*
* 1. MODEL PERFORMANCE IN GENERAL WILL REFLECT TYPICAL
* BASELINE SPECS FOR A GIVEN DEVICE, AND CERTAIN ASPECTS OF
* PERFORMANCE MAY NOT BE MODELED FULLY.
*
* 2. WHILE REASONABLE CARE HAS BEEN TAKEN IN THEIR
* PREPARATION, WE CANNOT BE RESPONSIBLE FOR CORRECT
* APPLICATION ON ANY AND ALL COMPUTER SYSTEMS.
*
* 3. MODEL USERS ARE HEREBY NOTIFIED THAT THESE MODELS ARE
* SUPPLIED "AS IS", WITH NO DIRECT OR IMPLIED RESPONSIBILITY
* ON THE PART OF NATIONAL SEMICONDUCTOR FOR THEIR OPERATION
* WITHIN A CUSTOMER CIRCUIT OR SYSTEM.  FURTHER, NATIONAL
* SEMICONDUCTOR RESERVES THE RIGHT TO CHANGE THESE MODELS
* WITHOUT PRIOR NOTICE.
*
* 4. IN ALL CASES, THE CURRENT DATA SHEET INFORMATION FOR A
* GIVEN REAL DEVICE IS YOUR FINAL DESIGN GUIDELINE, AND IS
* THE ONLY ACTUAL PERFORMANCE GUARANTEE.  FOR FURTHER
* TECHNICAL INFORMATION, REFER TO INDIVIDUAL DEVICE DATA
* SHEETS.
*
* 5. THE CURRENT MODELS PRESENTLY DO NOT SIMULATE
* TEMPERATURE OR NOISE EFFECTS CORRECTLY. 
*
* 6. PLEASE SEND US YOUR COMMENTS.  THANK YOU FOR CHOOSING
*  TO USE NATIONAL SEMICONDUCTOR AMPLIFIER PRODUCTS
*
* MODELS DEVELOPED AND UNDER COPYRIGHT BY:
* NATIONAL SEMICONDUCTOR CORPORATION
* AMPLIFIER PRODUCTS APPLICATIONS ENGINEERING, M/S: C2-505
* 2900 SEMICONDUCTOR DRIVE  
* P.O. BOX 58090
* SANTA CLARA, CA 95052-8090
*
* FOR ORDERING OR TECHNICAL INFORMATION ON THESE MODELS, CONTACT:
* NATIONAL SEMICONDUCTOR'S CUSTOMER RESPONSE CENTER
*                 7:00 A.M.--7:00 P.M.  U.S. CENTRAL TIME
*                                (800) 272-9959
* 
* REVISION HISTORY:
* 3.20    16-SEP-94  D. STOLITZKA     ADDED LM6685
* 
*******
*SRC=LM6685;LM6685;Comparators;National;Latched Output
*SYM=LM6685
*LM6685 ULTRA FAST SINGLE LATCHED COMPARATOR
*
* CONNECTIONS:      
*               IN(+)
*               | IN (-)
*               | | OUT (+)
*               | | | V (+)
*               | | | | V (-)
*               | | | | |  OUT(-)
*               | | | | | | LATCH
*               | | | | | | | GND
*               | | | | | | | |          
.SUBCKT LM6685  3 2 7 1 4 8 5 6 
RC1    1 11 513.6
RC2    1 12 513.6
IEE    13 4 DC 0.1M
Q1     11 2 13 MQ1
Q2     12 3 13 MQ2
ECOM1  50 51 2 6 0.5
ECOM2  51 52 3 6 0.5
VCOM   52 6 DC 0
RCOM1  50 6 1MEG
RLCH   5 6 1MEG
ILCH   5 6 100U
GLT    6 70 5 6 1M
RLT    70 6 1K
CLT    70 6 0.5P
ELCH1  15 6 11 12 2
ELCH2  16 6 70 6 1.348
HLCH   14 15 VCOM 10
RLCH1  14 17 1
VLCH   18 16 DC 3.294
CLCH   19 6 0.2N
RLCH2  19 6 1000MEG
MOS1   17 18 19 19 MMOS1
RSUP1  1 6 .402K
RSUP2  6 4 .340K
GB     6 20 19 6 .1
RB     20 6 1.5K
HT1    22 27 VG1 32.8
HT2    26 21 VG2 32.8
VT1    27 6 DC 0.744
VT2    26 6 DC -0.744
DB1    20 22 MD1
DB2    21 20 MD1
GT     6 25 20 6 1M
RT     25 6 1K
CT     25 6 2.12P
GC     6 30 25 6 8
RC     30 6 1K
VC1    31 6 DC 9.5
VC2    6 32 DC 9.5
DC1    30 31 MDC1
DC2    32 30 MDC1
GD     6 35 30 6 1M
RD     35 6 1K
CD     35 6 .2P
G1     6 36 2 3 1
D1     37 6 MD1
D2     6 38 MD1
VG1    36 37 DC 0
VG2    38 36 DC 0
GO1    6 40 35 6 .0432
RO1    40 6 1
VO1    41 40 DC -0.72
RL1    7 4 5K
QO1    6 41 7 MQO1
GO2    6 60 35 6 -.0432
RO2    60 6 1
VO2    61 60 DC -0.72
RL2    8 4 5K
QO2    6 61 8 MQO1
.MODEL MQ1 NPN IS=1.0117E-14 BF=11.345 XTI=3.005 XTB=1
.MODEL MQ2 NPN IS=1E-14 BF=11.658 XTI=3 XTB=0.9
.MODEL MMOS1 NMOS VTO=1.4 KP=1000
.MODEL MD1 D N=0.9
.MODEL MDC1 D N=.9
.MODEL MQO1 NPN BF=100 IS=1E-12 XTI=.1 EG=0.8
.ENDS 
******