********** 
*       SA08 PWM MODEL FEATURES INCLUDE: 
* 
*   1 PULSE BY PULSE TIME DOMAIN PERFORMANCE 
*   2 OUTPUT RESISTANCE AND CAPACITANCE 
*   3 ACCURATE CATCH DIODE I V CHARACTERISTIC 
*   4 ACCURATE RON AND VON 
*   5 INCREASING IQ ON THE VS SUPPLY VS FREQUENCY 
*   6 DEADTIME BETWEEN SWITCH ON TIMES 
*   7 ACCURATE IQ ON THE VCC LINE 
*   8 REDUCTION IN IQ VCC IN SHUTDOWN 
*   9 INTERNAL CMOS BUFFER ON THE CLK IN PIN 
*  10 ILIM / SHUTDOWN FUNCTIONS  
* 
*     NOTE: THIS MODEL DOES NOT SUPPORT THE FLAG PIN 
*     NOTE: THERMAL SHUTDOWN IS NOT MODELED 
*     NOTE: HIGH SIDE CURRENT LIMIT IS NOT MODELED    
* 
*****   SOME NOTES ON USING SPICE FOR PWM SIMULATION  ***** 
* 
* 
*  REQUIRES BERKELEY SPICE 3E.3P OR LATER. YOUR SIMULATOR 
*  MUST SUPPORT THE BERKELEY STANDARD VOLTAGE CONTROLLED 
*  SWITCH ELEMENT (S) 
*  OR 
*  REQUIRES PSPICE 4.04 OR LATER 
* 
*  BE PREPARED FOR LONG SIMULATIONS 
* 
*  IF YOUR SIMULATION STOPS DUE TO "TIMESTEP TOO SMALL" 
*  ERRORS YOU MAY WANT TO INCREASE ITL4 TO 100, INCREASE 
*  RELTOL TO 0.003 TO 0.01, INCREASE ABSTOL TO 1E-11 TO 
*  1E-9, INCREASE GMIN TO 1E-11 TO 1E-7, INCREASE VNTOL 
*  TO 1E-5 TO 1E-4, OR INCREASE CHGTOL TO 1E-11 TO 1E-9. 
*  ANY OR ALL OF THESE SIMULATOR OPTIONS MAY BE REQUIRED 
*  DEPENDING ON YOUR SIMULATOR. NEWER SIMULATORS USUALLY 
*  REQUIRE FEWER OF THEM. 
* 
*  TO AID THE INITIAL TRANSIENT CONVERGANCE, SET UP THE 
*  INITIAL VALUE OF CLOCK IN AS FOLLOWS: 
*        IF PWM IN > 5V, CLOCK IN INITIAL = 5 VOLTS 
*        IF PWM IN < 5V, CLOCK IN INITIAL = 0 VOLTS 
* 
*  TO AID THE INITIAL DC CONVERGANCE SET ITL1 TO 500. 
* 
*  THE CLOCK IS A GENERATOR STATEMENT INSIDE THE MODEL. 
*  WHERE THE REAL PART HAS AN OSCILLATOR FOLLOWED BY A 
*  DIGITAL DIVIDE BY TWO, THE MODEL HAS THE GENERATOR 
*  AT HALF OF THE CLOCK FREQUENCY. IF YOU NEED TO SYNCH- 
*  RONIZE THE PWM CLOCK TO AN EXTERNAL SOURCE, LOOK IN 
*  YOUR SIMULATOR LIBRARY FOR A SIMPLE FAST RUNNING 
*  BEHAVIORAL FREQUENCY DIVIDER OR DFLOP. BECAUSE THE 
*  EXTENTIONS THAT ALLOW BEHAVIORAL MODELS VARY WIDELY 
*  AMONG VENDORS NONE ARE INCLUDED HERE. AT THE BOTTOM 
*  OF THIS LIBRARY FILE YOU WILL FIND A COMPONENT LEVEL 
*  FREQUENCY DIVIDER SUBCIRCUIT (FDIV) WILL WORK WITH 
*  ALL SIMULATORS. IT WORKS AT FIVE VOLTS (PROVIDE 5V 
*  AND GROUND ON THE SUPPLIES OF FDIV) AND CAN PROVIDE 
*  FREQUENCY DIVISION BY 2 TO GO BETWEEN YOUR EXTERNAL 
*  SYNCH SIGNAL AND THE CLOCK INPUT OF THE PWM MODEL. 
*  BECAUSE FDIV IS COMPONENT LEVEL IT IS SLOW AND WILL 
*  EXTEND THE EXECUTION TIME BY AS MUCH AS 2 TO 1. 
* 
*  A GOOD WAY TO TEST SIMULATION ACCURACY IS TO FIX THE 
*  PWM SIGNAL INPUT TO A FIXED VOLTAGE THAT IS EQUIVALENT 
*  TO HALFWAY BETWEEN 50-50 DUTY FACTOR AND 100 PERCENT 
*  DUTY FACTOR. RUN A SIMULATION THAT HAS TIME FOR OUTPUT 
*  FILTER SETTLING. IF THE OUTPUT VOLTAGE WANDERS AROUND 
*  RATHER THAN STAYS SETTLED TO LESS THAN ONE PERCENT YOU 
*  SHOULD REDUCE THE TIMESTEP MAX (TMAX) PARAMETER IN THE 
*  TRAN LINE. THIS IS THE FOURTH TIME PARAMETER ON THAT 
*  LINE AND IS OPTIONAL LIKE THE TSTART PARAMETER (THIRD 
*  TIME PARAMETER ON THAT LINE). IF YOU ARE NOT USING 
*  TSTART YOU MUST SET IT TO ZERO SO THE SIMULATOR WILL 
*  RECOGNIZE TMAX AS TMAX RATHER THAN TSTART. FOR BERKELEY 
*  SPICE 3 TMAX WILL USUALLY BE IN THE 500 TO 150 NS RANGE. 
*  FOR PSPICE TMAX WILL USUALLY BE IN THE 250 TO 50 NS 
*  RANGE. 
* 
*  IF YOUR SIMULATION CANNOT GET PAST INITIAL CONVERGANCE 
*  FOR THE TRANSIENT ANALYSIS, RAMP VCC AND VS UP FROM 
*  ZERO VOLTS TO NORMAL IN THE FIRST 10 MICROSECONDS OR SO. 
*  THIS WILL NOT ONLY HELP THE TRAN ANALYSIS GET STARTED 
*  BUT WILL ALLOW YOU TO SEE THE INITIAL VOLTAGES AND 
*  CURRENTS FOR LATER SETTING OF NODESETS. LOOK FOR 
*  OUTPUT FILTER INDUCTOR CURRENTS AND CAPACITOR VOLTAGES 
*  TO ALLOW SETTING UP INITIAL CONDITIONS ON THOSE PARTS. 
*  SETTING INITIAL CONDITIONS CAN ALLOW YOUR OUTPUT FILTER 
*  TO GET TO OPERATING CONDITIONS MORE QUICKLY. DON'T 
*  FORGET TO USE THE UIC OPTION IN THE TRAN LINE TO FORCE 
*  USE OF INITIAL CONDITIONS. 
* 
*BEGIN MODEL SA08 FOR PSPICE 
*PINOUT ORDER CKIN CKOUT PWM GND ILIM ISENB BOUT +VS VCC AOUT ISENA 
*PINOUT         1    2    3   5    6    7     8   9   10  11    12
.SUBCKT SA08_AM 1 2 3 5 6 7 8 9 10 11 12
V7 44 5 0 PULSE 0 5 0 25N 25N 22.22U 44.44U
RE2 41 5 1E9
E1 14 5 23 15 1
D5 12 11 D1M
R13 27 5 10
R42 5 14 1E9
R39 67 63 10E3
C17 63 5 29E-12
R46 27 13 10E3
C18 13 5 29E-12
R1 40 11 0.085
R2 19 8 0.085
R3 64 12 0.085
R4 39 7 0.085
D4 13 27 D2M
D8 63 67 D2M
R14 32 23 1E3
R37 23 27 143E3
E4 32 5 68 5 1
E3 5 31 41 27 -1
R43 44 2 100
C22 2 5 10E-12
V2 41 5 4.5
D1 11 9 D1M
E9 16 5 65 25 1000
D6 7 8 D1M
D7 8 9 D1M
V9 28 5 0.1
C4 9 34 300E-12
C5 9 37 300E-12
C6 11 35 300E-12
C7 8 33 300E-12
R10 34 11 0.7
R12 35 12 0.7
R18 37 8 0.7
R20 33 7 0.7
S5 61 27 14 5 S5M
C19 66 5 1000E-12
C20 29 5 2000E-12
R15 26 29 10
E13 43 5 42 38 1
R16 43 30 300
C21 30 5 1E-6
R45 30 5 1E9
C10 65 5 1000E-12
R21 65 6 1E3
D2 46 47 DN
D3 48 46 DN
R22 46 16 1E3
V10 48 5 0.8
V11 47 0 4.1
R23 28 25 1E3
R24 57 25 1E6
R25 57 50 1E3
R26 59 50 1E3
R27 52 25 100E3
R19 60 5 1
C8 57 52 5E-12
V13 56 5 5
V8 22 5 2.5
Q1 57 53 5 QFN
Q2 57 54 0 QFN
Q3 59 55 0 QFN
Q4 59 51 5 QFN
R28 53 46 50E3
R29 54 59 50E3
R30 55 49 50E3
R31 51 57 50E3
C9 53 5 1E-12
C11 51 5 1E-12
C12 55 5 1E-12
C13 54 5 1E-12
R33 58 15 55E3
C15 15 5 470E-12
M1 49 62 5 5 M1M
M2 49 62 50 50 M2M
R34 62 1 50
C16 62 5 5E-12
E10 58 5 49 0 2
V12 50 5 5
R35 61 5 1E9
S7 26 42 22 13 S7M
S8 26 60 13 22 S7M
R36 68 5 10E6
R38 38 42 1
V4 38 5 10
G1 5 10 30 5 0.36
R40 10 69 500
S10 69 5 57 5 S1M
I2 10 5 0.05
GQ1 5 9 VALUE = {(-0.09)*((0.6+(V(9)/600))*(0.45+V(30)*(-1000)))}
R49 65 5 10E6
R51 66 68 50
R52 3 66 50
R53 67 5 10
VD1 9 71 0.85
VD2 9 72 0.85
VD3 11 73 0.85
VD4 8 74 0.85
S1 71 40 13 5 S1M
S2 72 19 63 5 S1M
S3 73 64 63 5 S1M
S4 74 39 13 5 S1M
S6 67 31 57 5 S6M
S9 56 61 57 5 S1M
.MODEL QFN NPN
.MODEL M1M NMOS (CBD=17E-12 CBS=17E-12 CGBO=26E-9 
+ CGDO=18E-9 CGSO=20E-9 IS=31E-15 KP=4.3E-3 RD=50 
+ RS=50 VTO=2.25)
.MODEL M2M PMOS (CBD=17E-12 CBS=17E-12 CGBO=26E-9 
+ CGDO=18E-9 CGSO=20E-9 IS=31E-15 KP=4.3E-3 RD=50 
+ RS=50 VTO=-2.25)
.MODEL S1M VSWITCH RON=0.01 ROFF=1E6 VON=2.55 VOFF=2.5
.MODEL S5M VSWITCH RON=1 ROFF=1E3 VON=0.001 VOFF=0
.MODEL S6M VSWITCH RON=0.1 ROFF=1E4 VON=2.91 VOFF=2.5
.MODEL S7M VSWITCH RON=1 ROFF=1E5 VON=0.1 VOFF=0
.MODEL D1M D (IS=2.645E-15 M=0.333 N=1.61 RS=0.086 VJ=0.75)
.MODEL D2M D (IS=1.68E-08 M=0.333 N=1.89 RS=0.452 VJ=0.75)
.MODEL DN D (BV=100 CJO=4E-12 IS=7E-09 M=0.45 N=2 RS=0.8 TT=6E-9 VJ=0.6)
.ENDS 
*END OF MODEL SA08 
********** 
*       SA12 PWM MODEL FEATURES INCLUDE: 
* 
*   1 PULSE BY PULSE TIME DOMAIN PERFORMANCE 
*   2 OUTPUT RESISTANCE AND CAPACITANCE 
*   3 ACCURATE CATCH DIODE I V CHARACTERISTIC 
*   4 ACCURATE RON TEMPERATURE COEFFICIENT 
*   5 INCREASING IQ ON THE VS SUPPLY VS FREQUENCY 
*   6 DEADTIME BETWEEN SWITCH ON TIMES 
*   7 ACCURATE IQ ON THE VCC LINE 
*   8 REDUCTION IN IQ VCC IN SHUTDOWN 
*   9 INTERNAL CMOS BUFFER ON THE CLK IN PIN 
*  10 ILIM / SHUTDOWN FUNCTIONS  
* 
*     NOTE: THERMAL SHUTDOWN IS NOT MODELED 
*     NOTE: HIGH SIDE CURRENT LIMIT IS NOT MODELED    
* 
*       SOME NOTES ON USING SPICE FOR PWM SIMULATION 
* 
*  REQUIRES BERKELEY SPICE 3E.3P OR LATER. YOUR SIMULATOR 
*  MUST SUPPORT THE BERKELEY STANDARD VOLTAGE CONTROLLED 
*  SWITCH ELEMENT (S) 
*  OR 
*  REQUIRES PSPICE 4.04 OR LATER 
* 
*  BE PREPARED FOR LONG SIMULATIONS 
* 
*  IF YOUR SIMULATION STOPS DUE TO "TIMESTEP TOO SMALL" 
*  ERRORS YOU MAY WANT TO INCREASE ITL4 TO 100, INCREASE 
*  RELTOL TO 0.003 TO 0.01, INCREASE ABSTOL TO 1E-11 TO 
*  1E-9, INCREASE GMIN TO 1E-11 TO 1E-7, INCREASE VNTOL 
*  TO 1E-5 TO 1E-4, OR INCREASE CHGTOL TO 1E-11 TO 1E-9. 
*  ANY OR ALL OF THESE SIMULATOR OPTIONS MAY BE REQUIRED 
*  DEPENDING ON YOUR SIMULATOR. NEWER SIMULATORS USUALLY 
*  REQUIRE FEWER OF THEM. 
* 
*  TO AID THE INITIAL TRANSIENT CONVERGANCE, SET UP THE 
*  INITIAL VALUE OF CLOCK IN AS FOLLOWS: 
*        IF PWM IN > 5V, CLOCK IN INITIAL = 5 VOLTS 
*        IF PWM IN < 5V, CLOCK IN INITIAL = 0 VOLTS 
* 
*  TO AID THE INITIAL DC CONVERGANCE SET ITL1 TO 500. 
* 
*  THE CLOCK IS A GENERATOR STATEMENT INSIDE THE MODEL. 
*  WHERE THE REAL PART HAS AN OSCILLATOR FOLLOWED BY A 
*  DIGITAL DIVIDE BY TWO, THE MODEL HAS THE GENERATOR 
*  AT HALF OF THE CLOCK FREQUENCY. IF YOU NEED TO SYNCH- 
*  RONIZE THE PWM CLOCK TO AN EXTERNAL SOURCE, LOOK IN 
*  YOUR SIMULATOR LIBRARY FOR A SIMPLE FAST RUNNING 
*  BEHAVIORAL FREQUENCY DIVIDER OR DFLOP. BECAUSE THE 
*  EXTENTIONS THAT ALLOW BEHAVIORAL MODELS VARY WIDELY 
*  AMONG VENDORS NONE ARE INCLUDED HERE. AT THE BOTTOM 
*  OF THIS LIBRARY FILE YOU WILL FIND A COMPONENT LEVEL 
*  FREQUENCY DIVIDER SUBCIRCUIT (FDIV) WILL WORK WITH 
*  ALL SIMULATORS. IT WORKS AT FIVE VOLTS (PROVIDE 5V 
*  AND GROUND ON THE SUPPLIES OF FDIV) AND CAN PROVIDE 
*  FREQUENCY DIVISION BY 2 TO GO BETWEEN YOUR EXTERNAL 
*  SYNCH SIGNAL AND THE CLOCK INPUT OF THE PWM MODEL. 
*  BECAUSE FDIV IS COMPONENT LEVEL IT IS SLOW AND WILL 
*  EXTEND THE EXECUTION TIME BY AS MUCH AS 2 TO 1. 
* 
*  A GOOD WAY TO TEST SIMULATION ACCURACY IS TO FIX THE 
*  PWM SIGNAL INPUT TO A FIXED VOLTAGE THAT IS EQUIVALENT 
*  TO HALFWAY BETWEEN 50-50 DUTY FACTOR AND 100 PERCENT 
*  DUTY FACTOR. RUN A SIMULATION THAT HAS TIME FOR OUTPUT 
*  FILTER SETTLING. IF THE OUTPUT VOLTAGE WANDERS AROUND 
*  RATHER THAN STAYS SETTLED TO LESS THAN ONE PERCENT YOU 
*  SHOULD REDUCE THE TIMESTEP MAX (TMAX) PARAMETER IN THE 
*  TRAN LINE. THIS IS THE FOURTH TIME PARAMETER ON THAT 
*  LINE AND IS OPTIONAL LIKE THE TSTART PARAMETER (THIRD 
*  TIME PARAMETER ON THAT LINE). IF YOU ARE NOT USING 
*  TSTART YOU MUST SET IT TO ZERO SO THE SIMULATOR WILL 
*  RECOGNIZE TMAX AS TMAX RATHER THAN TSTART. FOR BERKELEY 
*  SPICE 3 TMAX WILL USUALLY BE IN THE 500 TO 150 NS RANGE. 
*  FOR PSPICE TMAX WILL USUALLY BE IN THE 250 TO 50 NS 
*  RANGE. 
* 
*  IF YOUR SIMULATION CANNOT GET PAST INITIAL CONVERGANCE 
*  FOR THE TRANSIENT ANALYSIS, RAMP VCC AND VS UP FROM 
*  ZERO VOLTS TO NORMAL IN THE FIRST 10 MICROSECONDS OR SO. 
*  THIS WILL NOT ONLY HELP THE TRAN ANALYSIS GET STARTED 
*  BUT WILL ALLOW YOU TO SEE THE INITIAL VOLTAGES AND 
*  CURRENTS FOR LATER SETTING OF NODESETS. LOOK FOR 
*  OUTPUT FILTER INDUCTOR CURRENTS AND CAPACITOR VOLTAGES 
*  TO ALLOW SETTING UP INITIAL CONDITIONS ON THOSE PARTS. 
*  SETTING INITIAL CONDITIONS CAN ALLOW YOUR OUTPUT FILTER 
*  TO GET TO OPERATING CONDITIONS MORE QUICKLY. DON'T 
*  FORGET TO USE THE UIC OPTION IN THE TRAN LINE TO FORCE 
*  USE OF INITIAL CONDITIONS. 
* 
*BEGIN MODEL SA12 FOR PSPICE 
*PINOUT ORDER CKIN CKOUT +PWM -PWM GND ILIM ISENB BOUT +VS VCC AOUT ISENA 
*PINOUT         1    2     3    4   5    6    7     8   9   10  11    12
.SUBCKT SA12_AM 1 2 3 4 5 6 7 8 9 10 11 12
V7 44 5 0 PULSE 0 5 0 25N 25N 2.5U 5U
RE2 41 5 1E9
E1 14 5 23 4 1
D5 12 11 D5M
R13 27 5 10
R42 5 14 1E9
R39 67 63 10E3
C17 63 5 29E-12
R46 27 13 10E3
C18 13 5 29E-12
R1 40 11 R1M 0.49
R2 19 8 R1M 0.49
R3 64 12 R1M 0.49
R4 39 7 R1M 0.49
D4 13 27 D2M
D8 63 67 D2M
R14 32 23 1E3
R37 23 27 143E3
E4 32 5 66 5 1
E3 5 31 41 27 -1
R43 44 2 100
C22 2 5 10E-12
V2 41 5 4.5
D1 11 9 D1M
E9 16 5 65 25 1000
D6 7 8 D1M
D7 8 9 D1M
V9 28 5 0.1
C4 9 34 225E-12
C5 9 37 225E-12
C6 11 35 225E-12
C7 8 33 225E-12
R10 34 11 0.93
R12 35 12 0.93
R18 37 8 0.93
R20 33 7 0.93
S5 61 27 14 5 S5M
C19 15 5 1000E-12
C20 29 5 2000E-12
R15 26 29 10
E13 43 5 42 38 1
R16 43 30 300
C21 30 5 1E-6
R17 3 15 50
R45 30 5 1E9
C10 65 5 1000E-12
R21 65 6 1E3
D2 46 47 DN
D3 48 46 DN
R22 46 16 1E3
V10 48 5 0.8
V11 47 0 4.1
R23 28 25 1E3
R24 57 25 1E6
R25 57 50 1E3
R26 59 50 1E3
R27 52 25 100E3
R19 60 5 1
C8 57 52 5E-12
V13 56 5 5
V8 22 5 2.5
Q1 57 53 5 QFN
Q2 57 54 0 QFN
Q3 59 55 0 QFN
Q4 59 51 5 QFN
R28 53 46 50E3
R29 54 59 50E3
R30 55 49 50E3
R31 51 57 50E3
C9 53 5 1E-12
C11 51 5 1E-12
C12 55 5 1E-12
C13 54 5 1E-12
R33 58 4 28.1E3
C15 4 5 102E-12
M1 49 62 5 5 M1M
M2 49 62 50 50 M2M
R34 62 1 50
C16 62 5 5E-12
E10 58 5 49 0 2
V12 50 5 5
R35 61 5 1E9
S7 26 42 22 13 S7M
S8 26 60 13 22 S7M
R36 66 5 10E6
R38 38 42 1
V4 38 5 10
G1 5 10 30 5 0.36
R40 10 69 500
S10 69 5 57 5 S1M
I2 10 5 0.05
GQ1 5 9 VALUE = {(-0.2)*((0.4+(V(9)/200))*(0.1+V(30)*(-225)))}
R49 65 5 10E6
R51 15 66 50
R52 67 5 10
S1 9 40 13 5 S1M
S2 9 19 63 5 S1M
S3 11 64 63 5 S1M
S4 8 39 13 5 S1M
S6 67 31 57 5 S6M
S9 56 61 57 5 S1M
.MODEL R1M RES (TC1=7.374E-3 TC2=2.598E-5)
.MODEL QFN NPN
.MODEL M1M NMOS (CBD=17E-12 CBS=17E-12 CGBO=26E-9 
+ CGDO=18E-9 CGSO=20E-9 IS=31E-15 KP=4.3E-3 RD=50 
+ RS=50 VTO=2.25)
.MODEL M2M PMOS (CBD=17E-12 CBS=17E-12 CGBO=26E-9 
+ CGDO=18E-9 CGSO=20E-9 IS=31E-15 KP=4.3E-3 RD=50 
+ RS=50 VTO=-2.25)
.MODEL S1M  VSWITCH RON=0.01 ROFF=1E6 VON=2.505 VOFF=0
.MODEL S5M  VSWITCH RON=1 ROFF=1E3 VON=0.001 VOFF=0
.MODEL S6M  VSWITCH RON=0.1 ROFF=1E4 VON=2.51 VOFF=0
.MODEL S7M  VSWITCH RON=1 ROFF=1E5 VON=0.1 VOFF=0
.MODEL D1M D (IS=2.832E-12 M=0.333 N=1.61 RS=0.043 VJ=0.75)
.MODEL D2M D (IS=1.68E-08 M=0.333 N=1.89 RS=0.452 VJ=0.75)
.MODEL D5M D (IS=2.832E-12 M=0.333 N=1.61 RS=0.043 VJ=0.75)
.MODEL DN D (BV=100 CJO=4E-12 IS=7E-9 M=0.45 N=2 RS=0.8 TT=6E-9 VJ=0.6)
.ENDS 
*END OF MODEL SA12 
********** 
*       SA50 PWM MODEL FEATURES INCLUDE: 
* 
*  1 PULSE BY PULSE TIME DOMAIN PERFORMANCE 
*  2 OUTPUT RESISTANCE AND CAPACITANCE 
*  3 ACCURATE RON TEMPERATURE COEFFICIENT 
*  4 INCREASING IQ ON THE VS SUPPLY VS FREQUENCY 
*  5 DEADTIME BETWEEN SWITCH ON TIMES 
*  6 ACCURATE IQ ON THE VCC LINE 
* 
* 
*       SOME NOTES ON USING SPICE FOR PWM SIMULATION 
* 
*  REQUIRES BERKELEY SPICE 3E.3P OR LATER. YOUR SIMULATOR 
*  MUST SUPPORT THE BERKELEY STANDARD VOLTAGE CONTROLLED 
*  SWITCH ELEMENT (S) 
*  OR 
*  REQUIRES PSPICE 4.04 OR LATER 
* 
*  BE PREPARED FOR LONG SIMULATIONS 
* 
*  IF YOUR SIMULATION STOPS DUE TO "TIMESTEP TOO SMALL" 
*  ERRORS YOU MAY WANT TO INCREASE ITL4 TO 100, INCREASE 
*  RELTOL TO 0.003 TO 0.01, INCREASE ABSTOL TO 1E-11 TO 
*  1E-9, INCREASE GMIN TO 1E-11 TO 1E-7, INCREASE VNTOL 
*  TO 1E-5 TO 1E-4, OR INCREASE CHGTOL TO 1E-11 TO 1E-9. 
*  ANY OR ALL OF THESE SIMULATOR OPTIONS MAY BE REQUIRED 
*  DEPENDING ON YOUR SIMULATOR. NEWER SIMULATORS USUALLY 
*  REQUIRE FEWER OF THEM. 
* 
*  INITIAL CONVERGANCE CAN BE AFFECTED BY WHETHER THE 
*  PWM INPUT IS ABOVE OR BELOW THE HALFWAY VOLTAGE. 
* 
*  TO AID THE INITIAL DC CONVERGANCE SET ITL1 TO 500. 
* 
*  A GOOD WAY TO TEST SIMULATION ACCURACY IS TO FIX THE 
*  PWM SIGNAL INPUT TO A FIXED VOLTAGE THAT IS EQUIVALENT 
*  TO HALFWAY BETWEEN 50-50 DUTY FACTOR AND 100 PERCENT 
*  DUTY FACTOR. RUN A SIMULATION THAT HAS TIME FOR OUTPUT 
*  FILTER SETTLING. IF THE OUTPUT VOLTAGE WANDERS AROUND 
*  RATHER THAN STAYS SETTLED TO LESS THAN ONE PERCENT YOU 
*  SHOULD REDUCE THE TIMESTEP MAX (TMAX) PARAMETER IN THE 
*  TRAN LINE. THIS IS THE FOURTH TIME PARAMETER ON THAT 
*  LINE AND IS OPTIONAL LIKE THE TSTART PARAMETER (THIRD 
*  TIME PARAMETER ON THAT LINE). IF YOU ARE NOT USING 
*  TSTART YOU MUST SET IT TO ZERO SO THE SIMULATOR WILL 
*  RECOGNIZE TMAX AS TMAX RATHER THAN TSTART. FOR BERKELEY 
*  SPICE 3 TMAX WILL USUALLY BE IN THE 500 TO 150 NS RANGE. 
*  FOR PSPICE TMAX WILL USUALLY BE IN THE 250 TO 50 NS 
*  RANGE. 
* 
*  IF YOUR SIMULATION CANNOT GET PAST INITIAL CONVERGANCE 
*  FOR THE TRANSIENT ANALYSIS, RAMP VCC AND VS UP FROM 
*  ZERO VOLTS TO NORMAL IN THE FIRST 10 MICROSECONDS OR SO. 
*  THIS WILL NOT ONLY HELP THE TRAN ANALYSIS GET STARTED 
*  BUT WILL ALLOW YOU TO SEE THE INITIAL VOLTAGES AND 
*  CURRENTS FOR LATER SETTING OF NODESETS. LOOK FOR 
*  OUTPUT FILTER INDUCTOR CURRENTS AND CAPACITOR VOLTAGES 
*  TO ALLOW SETTING UP INITIAL CONDITIONS ON THOSE PARTS. 
*  SETTING INITIAL CONDITIONS CAN ALLOW YOUR OUTPUT FILTER 
*  TO GET TO OPERATING CONDITIONS MORE QUICKLY. DON'T 
*  FORGET TO USE THE UIC OPTION IN THE TRAN LINE TO FORCE 
*  USE OF INITIAL CONDITIONS. 
* 
*BEGIN MODEL SA50 FOR PSPICE 
*PINOUT ORDER IS RSB BOUT +VS AOUT RSA VCC GND IN 
*PINOUT           1    2   3    4   5   6   7   8
.SUBCKT SA50_AM 1 2 3 4 5 6 7 8 
* THIS LINE IS CLOCK GEN
V1 23 7 0 PULSE 0 1 100N 100N 100N 11.395U 22.222U
E1 14 7 15 24 1
RE2 26 7 1E9
E3 30 7 8 7 1
D5 5 4 D1M
S1 3 45 59 7 SM1
S2 3 9 22 7 SM1
S3 4 47 59 7 SM1
S4 2 21 22 7 SM1
R1 9 4 R1M 0.25
R2 45 2 R1M 0.25
R3 47 5 R1M 0.25
R4 21 1 R1M 0.25
S5 16 13 14 7 SM5
R25 29 7 1
S7 50 42 32 22 SM7
V2 31 7 10
S8 50 29 22 32 SM8
R13 23 7 1E9
R14 7 12 1E9
EQ1 48 7 VALUE = {V(12)*V(23)}
V8 32 7 2.5
R36 30 15 1E3
I1 3 7 0.011
D1 4 3 D1M
R5 48 24 51.575E3
D6 1 2 D1M
D7 2 3 D1M
I2 6 7 0.0026
C4 3 34 75E-12
C5 3 37 75E-12
C6 4 35 75E-12
C7 2 33 75E-12
C8 24 7 317.4E-12
R10 34 4 2.8
R12 35 5 2.8
R18 37 2 2.8
R20 33 1 2.8
R37 15 13 143E3
C15 8 7 10E-12
E2 7 28 26 13 -1
V11 26 7 4.5
D4 19 48 D4M
R15 19 24 981.427E3
R16 48 24 50E12
R39 28 59 10E3
Q1 8 25 12 QPU
.MODEL QPU PNP
C10 51 7 2000E-12
R27 50 51 10
I4 25 12 10E-9
R17 8 7 10E6
R19 13 7 10
R28 31 42 1
V13 16 7 5
E13 53 7 42 31 1
R29 53 54 300
C11 54 7 1E-6
G2 7 3 54 7 14
E14 12 7 6 7 1
R42 7 14 1E9
R30 3 7 6700
R31 6 7 1795
R45 54 7 1E9
C13 59 7 20E-12
R46 13 22 10E3
C14 22 7 20E-12
D2 22 13 D2M
R35 16 7 1E9
D3 59 28 D2M
.MODEL R1M RES (TC1=7.374E-3 TC2=2.598E-5)
.MODEL SM1 VSWITCH RON=0.01 ROFF=1E6 VON=2.55 VOFF=2.50
.MODEL SM5 VSWITCH RON=1 ROFF=1E3 VON=0.001 VOFF=0
.MODEL SM7 VSWITCH RON=1 ROFF=1E5 VON=0.1 VOFF=0
.MODEL SM8 VSWITCH RON=1 ROFF=1E5 VON=0.1 VOFF=0
.MODEL D1M D (IS=4.5E-14 M=0.333 N=1.61 RS=0.112 VJ=0.75)
.MODEL D4M D (CJO=1E-12 IS=1.68E-08 M=0.333 N=1.89 RS=0.452 VJ=0.75)
.MODEL D2M D (IS=1.68E-08 M=0.333 N=1.89 RS=0.452 VJ=0.75)
.ENDS 
*END OF MODEL SA50 
********** 
*       SA51 PWM MODEL FEATURES INCLUDE: 
* 
*  1 PULSE BY PULSE TIME DOMAIN PERFORMANCE 
*  2 OUTPUT RESISTANCE AND CAPACITANCE 
*  3 ACCURATE RON TEMPERATURE COEFFICIENT 
*  4 INCREASING IQ ON THE VS SUPPLY VS FREQUENCY 
*  5 DEADTIME BETWEEN SWITCH ON TIMES 
*  6 ACCURATE IQ ON THE VCC LINE 
*  7 DISABLE FUNCTION 
* 
* 
*       SOME NOTES ON USING SPICE FOR PWM SIMULATION 
* 
*  REQUIRES BERKELEY SPICE 3E.3P OR LATER. YOUR SIMULATOR 
*  MUST SUPPORT THE BERKELEY STANDARD VOLTAGE CONTROLLED 
*  SWITCH ELEMENT (S) 
*  OR 
*  REQUIRES PSPICE 4.04 OR LATER 
* 
*  BE PREPARED FOR LONG SIMULATIONS 
* 
*  IF YOUR SIMULATION STOPS DUE TO "TIMESTEP TOO SMALL" 
*  ERRORS YOU MAY WANT TO INCREASE ITL4 TO 100, INCREASE 
*  RELTOL TO 0.003 TO 0.01, INCREASE ABSTOL TO 1E-11 TO 
*  1E-9, INCREASE GMIN TO 1E-11 TO 1E-7, INCREASE VNTOL 
*  TO 1E-5 TO 1E-4, OR INCREASE CHGTOL TO 1E-11 TO 1E-9. 
*  ANY OR ALL OF THESE SIMULATOR OPTIONS MAY BE REQUIRED 
*  DEPENDING ON YOUR SIMULATOR. NEWER SIMULATORS USUALLY 
*  REQUIRE FEWER OF THEM. 
* 
*  TO AID THE INITIAL DC CONVERGANCE SET ITL1 TO 500. 
* 
*  TO AID TRANSIENT INITIAL CONVERGANCE, YOU MAY WANT 
*  TO ADJUST THE INITIAL STATE OF THE INPUT. 
*  NOTE THAT BOTH THE FOLLWING STATEMENTS GIVE THE 
*  SAME DUTY FACTOR BUT HAVE OPPOSITE INITIAL VALUES. 
* 
*  VIN XX XX 0 PULSE 0 5 1U 50N 50N 1.5U 2U 
*  VIN XX XX 5 PULSE 5 0 1U 50N 50N 0.5U 2U 
* 
*  A GOOD WAY TO TEST SIMULATION ACCURACY IS TO FIX THE 
*  PWM CLOCK INPUT TO A 75 PERCENT DUTY FACTOR AND RUN 
*  A SIMULATION THAT HAS TIME FOR OUTPUT FILTER SETTLING. 
*  IF THE OUTPUT VOLTAGE WANDERS AROUND RATHER THAN STAY 
*  SETTLED TO LESS THAN ONE PERCENT YOU SHOULD REDUCE 
*  THE TIMESTEP MAX (TMAX) PARAMETER IN THE TRAN LINE. 
*  THIS IS THE FOURTH TIME PARAMETER ON THAT LINE AND IS 
*  OPTIONAL LIKE THE TSTART PARAMETER (THIRD TIME 
*  PARAMETER ON THAT LINE). IF YOU ARE NOT USING TSTART 
*  YOU MUST SET IT TO ZERO SO THE SIMULATOR WILL RECOGNIZE 
*  TMAX AS TMAX RATHER THAN TSTART. FOR BERKELEY SPICE 3 
*  TMAX WILL USUALLY BE IN THE 500 TO 150 NS RANGE. FOR 
*  PSPICE TMAX WILL USUALLY BE IN THE 250 TO 50 NS RANGE. 
* 
*  IF YOUR SIMULATION CANNOT GET PAST INITIAL CONVERGANCE 
*  FOR THE TRANSIENT ANALYSIS, RAMP VCC AND VS UP FROM 
*  ZERO VOLTS TO NORMAL IN THE FIRST 10 MICROSECONDS OR SO. 
*  THIS WILL NOT ONLY HELP THE TRAN ANALYSIS GET STARTED 
*  BUT WILL ALLOW YOU TO SEE THE INITIAL VOLTAGES AND 
*  CURRENTS FOR LATER SETTING OF NODESETS. LOOK FOR 
*  OUTPUT FILTER INDUCTOR CURRENTS AND CAPACITOR VOLTAGES 
*  TO ALLOW SETTING UP INITIAL CONDITIONS ON THOSE PARTS. 
*  SETTING INITIAL CONDITIONS CAN ALLOW YOUR OUTPUT FILTER 
*  TO GET TO OPERATING CONDITIONS MORE QUICKLY. DON'T 
*  FORGET TO USE THE UIC OPTION IN THE TRAN LINE TO FORCE 
*  USE OF INITIAL CONDITIONS. 
* 
*BEGIN MODEL SA51 FOR PSPICE 
*PINOUT ORDER IS +VS BOUT VCC DIS GND IN AOUT RSEN 
*PINOUT           1    2   3   4   5   6   7    8
.SUBCKT SA51_AM 1 2 3 4 5 6 7 8
E1 14 5 15 30 1
RE2 26 5 1E9
D5 8 7 D1M
S1 1 27 59 5 S1M
S2 1 48 22 5 S1M
S3 7 47 59 5 S1M
S4 2 21 22 5 S1M
S5 19 13 14 5 S5M
R5 30 5 15E3
R1 48 7 R1M 0.25
R2 27 2 R1M 0.25
R3 47 8 R1M 0.25
R4 21 8 R1M 0.25
R25 29 5 1
S7 50 42 32 22 S7M
V2 31 5 10
S8 50 29 22 32 S7M
S9 56 19 45 5 S9M
G1 5 3 10 5 0.36
V14 49 5 5
S10 45 5 4 5 S10M
V8 32 5 2.5
R36 16 15 1E3
R15 24 30 105E3
D1 7 1 D1M
Q1 4 46 3 QPU
C8 30 5 0.01E-6
D6 8 2 D1M
D7 2 1 D1M
Q2 6 25 24 QPU
I2 25 24 10E-9
C4 1 34 75E-12
C5 1 37 75E-12
C6 7 35 75E-12
C7 2 33 75E-12
R16 6 23 3E3
R10 34 7 2.8
R12 35 8 2.8
R18 37 2 2.8
R20 33 8 2.8
R37 15 13 143E3
C15 23 5 10E-12
E2 5 28 26 13 -1
V11 26 5 4.5
I3 3 5 0.0026
C9 30 5 10E-12
E3 16 5 23 5 1
R13 23 5 1E6
R39 28 59 10E3
R14 30 5 1E6
C10 51 5 2000E-12
R27 50 51 10
I1 46 3 1E-6
C12 4 5 10E-12
I4 1 5 0.011
R19 13 5 10
R28 31 42 1
V13 56 5 5
R26 49 45 100
E13 53 5 42 31 1
R29 53 10 300
C11 10 5 1E-6
G2 5 1 10 5 14
E14 24 5 3 5 1
R42 5 14 1E9
R30 1 5 6700
R31 3 5 1795
R45 10 5 1E9
C13 59 5 20E-12
R46 13 22 10E3
C14 22 5 20E-12
D2 22 13 D2M
R35 19 5 1E9
D3 59 28 D2M
.MODEL QPU PNP
.MODEL R1M RES (TC1=7.374E-3 TC2=2.598E-5)
.MODEL S1M VSWITCH RON=0.01 ROFF=1E6 VON=2.55 VOFF=2.5
.MODEL S5M VSWITCH RON=1 ROFF=1E3 VON=0.001 VOFF=0
.MODEL S7M VSWITCH RON=1 ROFF=1E5 VON=0.1 VOFF=0
.MODEL S9M VSWITCH RON=0.1 ROFF=1E3 VON=2.91 VOFF=2.5
.MODEL S10M VSWITCH RON=1 ROFF=1E3 VON=1.1 VOFF=1
.MODEL D1M D (IS=4.5E-14 M=0.333 N=1.61 RS=0.112 VJ=0.75)
.MODEL D2M D (IS=1.68E-12 M=0.333 N=1.89 RS=0.452 VJ=0.75)
.ENDS 
*END OF MODEL SA51 
********** 
*       SA60 PWM MODEL FEATURES INCLUDE: 
* 
*  1 PULSE BY PULSE TIME DOMAIN PERFORMANCE 
*  2 OUTPUT RESISTANCE AND CAPACITANCE 
*  3 ACCURATE RON TEMPERATURE COEFFICIENT 
*  4 INCREASING IQ ON THE VS SUPPLY VS FREQUENCY 
*  5 DEADTIME BETWEEN SWITCH ON TIMES 
*  6 ACCURATE IQ ON THE VCC LINE 
* 
*       SOME NOTES ON USING SPICE FOR PWM SIMULATION 
* 
*  REQUIRES BERKELEY SPICE 3E.3P OR LATER. YOUR SIMULATOR 
*  MUST SUPPORT THE BERKELEY STANDARD VOLTAGE CONTROLLED 
*  SWITCH ELEMENT (S) 
*  OR 
*  REQUIRES PSPICE 4.04 OR LATER 
* 
*  BE PREPARED FOR LONG SIMULATIONS. 
* 
*  IF YOUR SIMULATION RUNS INTO TIMESTEP TOO SMALL 
*  PROBLEMS YOU MAY WANT TO INCREASE ITL4 TO 100, 
*  RELTOL TO 0.003 TO 0.01, ABSTOL TO 1E-9 TO 1E-11, 
*  GMIN TO 1E-7 TO 1E-11, VNTOL TO 1E-4 TO 1E-5, 
*  OR CHGTOL TO 1E-9 TO 1E-11. 
* 
*  A GOOD WAY TO TEST SIMULATION ACCURACY IS TO FIX THE 
*  PWM SIGNAL INPUT TO A FIXED VOLTAGE THAT IS EQUIVALENT 
*  TO HALFWAY BETWEEN 50-50 DUTY FACTOR AND 100 PERCENT 
*  DUTY FACTOR. RUN A SIMULATION THAT HAS TIME FOR OUTPUT 
*  FILTER SETTLING. IF THE OUTPUT VOLTAGE WANDERS AROUND 
*  RATHER THAN STAYS SETTLED TO LESS THAN ONE PERCENT YOU 
*  SHOULD REDUCE THE TIMESTEP MAX (TMAX) PARAMETER IN THE 
*  TRAN LINE. THIS IS THE FOURTH TIME PARAMETER ON THAT 
*  LINE AND IS OPTIONAL LIKE THE TSTART PARAMETER (THIRD 
*  TIME PARAMETER ON THAT LINE). IF YOU ARE NOT USING 
*  TSTART YOU MUST SET IT TO ZERO SO THE SIMULATOR WILL 
*  RECOGNIZE TMAX AS TMAX RATHER THAN TSTART. FOR BERKELEY 
*  SPICE 3 TMAX WILL USUALLY BE IN THE 250 TO 150 NS RANGE. 
*  FOR PSPICE TMAX WILL USUALLY BE IN THE 100 TO 50 NS 
*  RANGE. 
* 
*  IF YOUR SIMULATION CANNOT GET PAST INITIAL CONVERGENCE 
*  FOR THE TRANSIENT ANALYSIS, RAMP VCC AND VS UP FROM 
*  ZERO VOLTS TO NORMAL IN THE FIRST MICROSECOND OR SO. 
*  THIS WILL NOT ONLY HELP THE TRAN ANALYSIS GET STARTED 
*  BUT WILL ALLOW YOU TO SEE THE INITIAL VOLTAGES FOR LATER 
*  SETTING OF NODESETS. FURTHER INTO THE ANALYSIS LOOK FOR 
*  OUTPUT FILTER INDUCTOR CURRENTS AND CAPACITOR VOLTAGES 
*  TO ALLOW SETTING UP INITIAL CONDITIONS ON THOSE PARTS. 
*  SETTING INITIAL CONDITIONS CAN ALLOW YOUR OUTPUT FILTER 
*  TO GET TO OPERATING CONDITIONS MORE QUICKLY. DON'T 
*  FORGET TO USE THE UIC OPTION IN THE TRAN LINE TO FORCE 
*  USE OF INITIAL CONDITIONS. 
* 
* BEGIN MODEL SA60 FOR PSPICE 
* PINOUT ORDER IS AGND CF DIS ANAIN GND VCC ISENA AOUT VS BOUT ISENB 
*                   1   2  3    4    6   7    8     9  10  11    12 
* NOTE THAT YOUR EXTERNAL CF SHOULD HAVE IC=0 (INITIAL CONDITION)
.SUBCKT SA60_AM 1 2 3 4 6 7 8 9 10 11 12
E1 14 1 15 2 1
D5 8 9 D1M
R25 21 6 1
V2 31 6 10
V14 49 6 5
S3 19 13 14 1 PSW3
V8 32 6 2.5
RV8 32 6 1E9
R36 4 15 1E3
R5 16 24 1E3
D1 9 10 D1M
Q1 3 46 7 QPU
R21 24 25 1.91E3
D6 12 11 D1M
D7 11 10 D1M
E9 16 1 27 1 0.5
RA2 27 0 1E9
R22 2 23 49E3
C4 10 34 150E-12
C5 10 37 150E-12
C6 9 35 150E-12
C7 11 33 150E-12
R10 34 9 1.4
C9 2 1 47.4E-12 IC=0
R12 35 8 1.4
R18 37 11 1.4
R20 33 12 1.4
R1 48 9 RM1 0.16
R2 5 11 RM1 0.16
R3 47 8 RM1 0.16
R4 39 12 RM1 0.16
R37 15 13 143E3
E2 6 28 26 13 -1
RE2 26 0 1E9
V11 26 1 5
S4 50 21 22 32 PSW4
R39 28 59 10E3
S5 50 42 32 22 PSW4
C10 51 6 2E-9
R27 50 51 10
I1 46 7 1U
C12 3 6 10E-12
S12 23 1 57 24 PSW12
R24 27 23 2.575E3
E11 55 1 23 1 1
V1 55 25 -0.1
R19 13 6 10
V10 57 2 -0.01
RV10 57 1 1E9
R28 31 42 1
V13 56 6 5
S15 10 48 22 6 PSW15
S16 10 5 59 6 PSW15
S17 9 47 59 6 PSW15
S18 11 39 22 6 PSW15
S19 45 6 3 6 PSW19
R26 49 45 1
E13 53 6 42 31 1
R29 53 54 300
C11 54 6 1E-6
G2 6 10 54 6 28
E14 27 1 7 1 1
R42 1 14 1E9
R30 10 6 9330
R31 7 1 454
R44 55 1 1E9
R45 54 6 1E9
C13 59 6 29E-12
R46 13 22 10E3
C14 22 6 29E-12
D2 22 13 D2M
S1 56 19 45 6 PSW1
R35 19 6 1E9
D3 59 28 D2M
.MODEL QPU PNP
.MODEL RM1 RES (TC1=7.374E-3 TC2=2.598E-5)
.MODEL PSW1 VSWITCH RON=1 ROFF=1E3 VON=2.91 VOFF=2.5
.MODEL PSW3 VSWITCH RON=1 ROFF=1E3 VON=0.001 VOFF=0
.MODEL PSW4 VSWITCH RON=1 ROFF=1E5 VON=2 VOFF=0
.MODEL PSW12 VSWITCH RON=10 ROFF=1E6 VON=0.001 VOFF=0
.MODEL PSW15 VSWITCH RON=0.01 ROFF=1E6 VON=2.55 VOFF=2.5
.MODEL PSW19 VSWITCH RON=1 ROFF=1E3 VON=1.5 VOFF=1
.MODEL D1M D (IS=9E-14 M=0.333 N=1.61 RS=0.056 VJ=0.75)
.MODEL D2M D (IS=1.68E-08 M=0.333 N=1.89 RS=0.5 VJ=0.75)
.ENDS 
* END MODEL SA60 FOR PSPICE 
********** 
* FREQUENCY DIVIDER 
* USE AT VDD = 5 VOLTS  VSS = 0 VOLTS 
* INPUT SWING CAN BE AS LITTLE 
* AS 0.9 TO 3.7 VOLTS BUT 0 TO 5 
* ALLOWS FASTER EXECUTION 
* OUTPUT SWING IS 5 VOLTS 
*BEGIN MODEL FREQUENCY DIVIDE BY 2 
*FOR BERKELEY SPICE AND PSPICE 
*PINOUT ORDER IS IN OUT VDD VSS 
*PINOUT           1  2   3   4
.SUBCKT FDIV_AM 1 2 3 4
M1 9 11 3 3 FMP
M2 10 12 9 3 FMP
M3 10 12 4 4 FMN
M4 10 11 4 4 FMN
M5 15 16 3 3 FMP
M6 12 17 15 3 FMP
M7 12 17 4 4 FMN
M8 12 16 4 4 FMN
M9 21 2 3 3 FMP
M10 17 16 21 3 FMP
M11 17 16 4 4 FMN
M12 17 2 4 4 FMN
M13 27 11 3 3 FMP
M14 2 17 27 3 FMP
M15 2 17 4 4 FMN
M16 2 11 4 4 FMN
M17 33 1 3 3 FMP
M18 11 10 33 3 FMP
M19 11 10 4 4 FMN
M20 11 1 4 4 FMN
M21 20 12 3 3 FMP
M22 20 12 4 4 FMN
M23 40 11 3 3 FMP
M24 40 11 4 4 FMN
M25 19 1 3 3 FMP
M26 19 1 4 4 FMN
M27 47 19 3 3 FMP
M28 47 40 3 3 FMP
M29 47 20 3 3 FMP
M30 47 19 50 4 FMN
M31 50 40 51 4 FMN
M32 51 20 4 4 FMN
M33 16 47 3 3 FMP
M34 16 47 4 4 FMN
RC1 1 4 1E15
RC2 2 4 1E15
RC3 3 4 1E15
C1 4 16 1E-12
.MODEL FMP PMOS (CBD=4E-12 CBS=4E-12 
+ IS=30E-15 KP=2E-3 VTO=-3.0)
.MODEL FMN NMOS (CBD=4E-12 CBS=4E-12 
+ IS=30E-15 KP=2E-3 VTO=3.5)
.ENDS 
*END OF MODEL FDIV 
**********
