**********
* Copyright Intusoft 1988-1997
* All Rights Reserved
**********
**********
*SRC=O66092;O66092;Opto-Isolators;Bipolar Output;Micropac
*SYM=OPTO
.SUBCKT O66092 1 2 3 4 5
*        ISOLATOR ANODE CATHODE COL BASE EMITTER
*TEMPERATURE STABLE DEVICE
VM 1 6
D1 6 2 LED
H1 7 0 VM .0055
R1 7 8 1K
C1 8 0 150P
G1 3 4 8 0 1
Q1 3 4 5 MPSA18
.MODEL LED D(N=1.9 RS=.1 CJO=40P IS=3.4E-14)
.MODEL MPSA18 NPN (IS=86.4F NF=1 BF=468 VAF=120 IKF=.12
+ ISE=8.94P NE=2 BR=4 NR=1 VAR=26 IKR=.18 RE=.515 RB=2.06
+ RC=.206 XTB=1.5 CJE=7.50P CJC=6.77P TF=353P TR=87.4N)
*   Motorola 45 Volt  .2 Amp  450 MHz  SiNPN  Transistor  01-26-1991
.ENDS
**********
*SRC=4N35;ON35;Opto-Isolators;Bipolar Output;High Speed 3.55kV 100%
*SYM=OPTO
.SUBCKT ON35   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N35
* Current Transfer Ratio (CTR) = (H*G*BF) = 100%
VM 1 6
D1 6 2 LED
H1 7 0 VM .0021
R1 7 8 1K
C1 8 0 100PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=3.36 RS=0.6 CJO=40PF IS=21.6E-9)
.MODEL QOPTO NPN(IS=7.0E-12 NF=1.23 CJC=2PF
+ CJE=7PF TF=2.5NS TR=2.5NS BF=500 BR=10
+ IKF=750MA VAF=40)
.ENDS
**********
*SRC=4N55;ON55;Opto-Isolators;Bipolar Output;High Speed 500V 20%
*SYM=OPTO
.SUBCKT ON55   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N55
* Current Transfer Ratio (CTR) = (H*G*BF) = 20%
VM 1 6
D1 6 2 LED
H1 7 0 VM .00133
R1 7 8 1K
C1 8 0 80PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=1.58 RS=3.5 CJO=120PF IS=5.34E-19)
.MODEL QOPTO NPN(IS=6.67E-12 NF=1.23 CJC=2PF
+ CJE=7PF TF=.1NS TR=50NS BF=150 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=4N22;ON22;Opto-Isolators;Bipolar Output;High Gain 1kV 25%
*SYM=OPTO
.SUBCKT ON22   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N22
* Current Transfer Ratio (CTR) = (H*G*BF) = 25%
VM 1 6
D1 6 2 LED
H1 7 0 VM .0005; was .8M
R1 7 8 1K
C1 8 0 100PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2.50 RS=.0001 CJO=40PF IS=306E-12)
.MODEL QOPTO NPN(IS=7.0E-12 NF=1.23 CJC=4PF
+ CJE=14PF TF=10NS TR=10NS BF=500 BR=10
+ IKF=750MA VAF=40)
.ENDS
**********
*SRC=4N23;ON23;Opto-Isolators;Bipolar Output;High Gain 1kV 60%
*SYM=OPTO
.SUBCKT ON23   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N23
* Current Transfer Ratio (CTR) = (H*G*BF) = 60%
VM 1 6
D1 6 2 LED
H1 7 0 VM .0012; was 1.6M
R1 7 8 1K
C1 8 0 100PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2.5 RS=0.0001 CJO=40PF IS=306E-12)
.MODEL QOPTO NPN(IS=7.0E-12 NF=1.23 CJC=4PF
+ CJE=14PF TF=10NS TR=10NS BF=500 BR=10
+ IKF=750MA VAF=40)
.ENDS
**********
*SRC=4N24;ON24;Opto-Isolators;Bipolar Output;High Gain 1kV 100%
*SYM=OPTO
.SUBCKT ON24   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N24
* Current Transfer Ratio (CTR) = (H*G*BF) = 100%
VM 1 6
D1 6 2 LED
H1 7 0 VM .002; was .00275
R1 7 8 1K
C1 8 0 100PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2.50 RS=.0001 CJO=40PF IS=306E-12)
.MODEL QOPTO NPN(IS=7.0E-12 NF=1.23 CJC=4PF
+ CJE=14PF TF=10NS TR=10NS BF=500 BR=10
+ IKF=750MA VAF=40)
.ENDS
**********
*SRC=6N135;ON135;Opto-Isolators;TTL Comp.;TTL Comp. 3kV 7%
*SYM=OPTO
.SUBCKT ON135   1     2       3   4    5
* OPTO ISOLATOR  ANODE CATHODE COL BASE EMITTER 
*TI 6N135
* Current Transfer Ratio (CTR) = (H*G*BF) = 7%
VM 1 6
D1 6 2 LED
H1 7 0 VM .00056; was .00144
R1 7 8 1K
C1 8 0 100PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2.63 RS=1.25 CJO=60PF IS=757E-15)
.MODEL QOPTO NPN(IS=7.0E-12 NF=1.23 CJC=2PF
+ CJE=7PF TF=.55NS TR=10NS BF=125 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=6N136;ON136;Opto-Isolators;TTL Comp.;TTL Comp. 3kV 19%
*SYM=OPTO
.SUBCKT ON136   1     2       3   4    5
* OPTO ISOLATOR  ANODE CATHODE COL BASE EMITTER 
*TI 6N136
* Current Transfer Ratio (CTR) = (H*G*BF) = 19%
VM 1 6
D1 6 2 LED
H1 7 0 VM .00152; was .00432
R1 7 8 1K
C1 8 0 100PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2.63 RS=1.25 CJO=60PF IS=757E-15)
.MODEL QOPTO NPN(IS=7.0E-12 NF=1.23 CJC=2PF
+ CJE=7PF TF=.55NS TR=10NS BF=125 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=HCPL4502;HCPL4502;Opto-Isolators;TTL Comp.;TTL Comp. 3kV 24%
*SYM=OPTONB
.SUBCKT HCPL4502   1     2       3    5
* OPTO ISOLATOR     ANODE CATHODE COL EMITTER 
*HP HCPL4502
* Current Transfer Ratio (CTR) = (H*G*BF) = 24%
VM 1 6
D1 6 2 LED
H1 7 0 VM .00192; was .00432
R1 7 8 1K
C1 8 0 100PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2.63 RS=1.25 CJO=60PF IS=757E-15)
.MODEL QOPTO NPN(IS=7.0E-12 NF=1.23 CJC=2PF
+ CJE=7PF TF=.55NS TR=10NS BF=125 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=4N25;ON25;Opto-Isolators;Bipolar Output;High Speed 2.5kV 20%
*SYM=OPTO
.SUBCKT ON25   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N25
* Current Transfer Ratio (CTR) = (H*G*BF) = 20%
VM 1 6
D1 6 2 LED
H1 7 0 VM .000615384; was .00154
R1 7 8 1K
C1 8 0 454PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2 RS=.7 CJO=40PF IS=2.5E-12)
.MODEL QOPTO NPN(IS=2.5E-11 NF=1.3 CJC=15PF
+ CJE=45PF TF=.44NS TR=150NS BF=325 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=4N26;ON26;Opto-Isolators;Bipolar Output;High Speed 1.5kV 20%
*SYM=OPTO
.SUBCKT ON26   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N26
* Current Transfer Ratio (CTR) = (H*G*BF) = 20%
VM 1 6
D1 6 2 LED
H1 7 0 VM .000615384; was .00154
R1 7 8 1K
C1 8 0 454PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2 RS=.7 CJO=40PF IS=2.5E-12)
.MODEL QOPTO NPN(IS=2.5E-11 NF=1.3 CJC=15PF
+ CJE=45PF TF=.44NS TR=150NS BF=325 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=4N27;ON27;Opto-Isolators;Bipolar Output;High Speed 1.5kV 10%
*SYM=OPTO
.SUBCKT ON27   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N27
* Current Transfer Ratio (CTR) = (H*G*BF) = 10%
VM 1 6
D1 6 2 LED
H1 7 0 VM .30769M; was .000923
R1 7 8 1K
C1 8 0 454PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2 RS=.7 CJO=40PF IS=2.5E-12)
.MODEL QOPTO NPN(IS=2.5E-11 NF=1.3 CJC=15PF
+ CJE=45PF TF=.44NS TR=150NS BF=325 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=4N28;ON28;Opto-Isolators;Bipolar Output;High Speed 500V 10%
*SYM=OPTO
.SUBCKT ON28   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N28
* Current Transfer Ratio (CTR) = (H*G*BF) = 10%
VM 1 6
D1 6 2 LED
H1 7 0 VM .30769M; was .000923
R1 7 8 1K
C1 8 0 454PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=2 RS=.7 CJO=40PF IS=2.5E-12)
.MODEL QOPTO NPN(IS=2.5E-11 NF=1.3 CJC=15PF
+ CJE=45PF TF=.44NS TR=150NS BF=325 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=4N36;ON36;Opto-Isolators;Bipolar Output;High Speed 2.5kV 100%
*SYM=OPTO
.SUBCKT ON36   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N36
* Current Transfer Ratio (CTR) = (H*G*BF) = 100%
VM 1 6
D1 6 2 LED
H1 7 0 VM .002; was .0021
R1 7 8 1K
C1 8 0 100PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=3.36 RS=0.6 CJO=40PF IS=21.6E-9)
.MODEL QOPTO NPN(IS=7.0E-12 NF=1.23 CJC=2PF
+ CJE=7PF TF=2.5NS TR=2.5NS BF=500 BR=10
+ IKF=750MA VAF=40)
.ENDS
**********
*SRC=4N37;ON37;Opto-Isolators;Bipolar Output;High Speed 1.5kV 100%
*SYM=OPTO
.SUBCKT ON37   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N37
* Current Transfer Ratio (CTR) = (H*G*BF) = 100%
VM 1 6
D1 6 2 LED
H1 7 0 VM .002; was .0021
R1 7 8 1K
C1 8 0 100PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=3.36 RS=0.6 CJO=40PF IS=21.6E-9)
.MODEL QOPTO NPN(IS=7.0E-12 NF=1.23 CJC=2PF
+ CJE=7PF TF=2.5NS TR=2.5NS BF=500 BR=10
+ IKF=750MA VAF=40)
.ENDS
**********
*SRC=4N48;ON48;Opto-Isolators;Bipolar Output;High CTR 1kV 100%
*SYM=OPTO
.SUBCKT ON48   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N48
* Current Transfer Ratio (CTR) = (H*G*BF) = 100%
VM 1 6
D1 6 2 LED
H1 7 0 VM .002; was .0100
R1 7 8 1K
C1 8 0 1304PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=1.5 RS=4.7 CJO=40PF IS=2.5E-15)
.MODEL QOPTO NPN(IS=2.5E-14 NF=1.23 CJC=180PF
+ CJE=180PF TF=200PS TR=100NS BF=500 BR=10
+ IKF=400MA VAF=100)
.ENDS
**********
*SRC=4N47;ON47;Opto-Isolators;Bipolar Output;High CTR 1kV 50%
*SYM=OPTO
.SUBCKT ON47   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N47
* Current Transfer Ratio (CTR) = (H*G*BF) = 50%
VM 1 6
D1 6 2 LED
H1 7 0 VM .001; was .0008
R1 7 8 1K
C1 8 0 1304PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=1.5 RS=4.7 CJO=40PF IS=2.5E-15)
.MODEL QOPTO NPN(IS=2.5E-14 NF=1.23 CJC=180PF
+ CJE=180PF TF=200PS TR=100NS BF=500 BR=10
+ IKF=400MA VAF=100)
.ENDS
**********
*SRC=4N49;ON49;Opto-Isolators;Bipolar Output;High CTR 1kV 200%
*SYM=OPTO
.SUBCKT ON49   1     2       3   4    5
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*TI 4N49
* Current Transfer Ratio (CTR) = (H*G*BF) = 200%
VM 1 6
D1 6 2 LED
H1 7 0 VM .004; was .0120
R1 7 8 1K
C1 8 0 1304PF
G1 3 4 8 0 1
Q1 3 4 5 QOPTO
.MODEL LED D(N=1.5 RS=4.7 CJO=40PF IS=2.5E-15)
.MODEL QOPTO NPN(IS=2.5E-14 NF=1.23 CJC=200PF
+ CJE=200PF TF=200PS TR=100NS BF=500 BR=10
+ IKF=400MA VAF=100)
.ENDS
**********
*SRC=H11L1;H11L1;Opto-Isolators;Schmitt Trig;Schmitt Trig. 500V
*SYM=OPTOST
.SUBCKT H11L1  3     17      16  18  4
* OPTO ISOLATOR ANODE CATHODE VCC GND OUT 
*Motorola H11L1 - Schmitt Trigger Output
.MODEL QOPTO NPN(BF=500 VAF=100 IKF=100M CJC=5P CJE=100P 
+TR=15N TF=15N)
RIN 1 22 1E4
C1 22 0 52.174P
E1 2 0 22 0 1
.MODEL DLED D(N=2 CJO=18P RS=.8)
Q1 4 12 18 QOPTO
E3 9 0 2 11 10
R5 2 0 1G
R6 11 0 1G
R7 9 12 1K
H1 1 0 VM 1K
VM 3 13 
D1 13 17 DLED
E2 11 0 16 0 .6 30M
.ENDS
**********
*SRC=H11L2;H11L2;Opto-Isolators;Schmitt Trig;Schmitt Trig. 500V
*SYM=OPTOST
.SUBCKT H11L2  3     17      16  18  4
* OPTO ISOLATOR ANODE CATHODE VCC GND OUT 
*Motorola H11L2 - Schmitt Trigger Output
.MODEL QOPTO NPN(BF=500 VAF=100 IKF=100M CJC=5P CJE=100P 
+TR=15N TF=15N)
RIN 1 22 1E4
C1 22 0 52.174P
E1 2 0 22 0 1
.MODEL DLED D(N=2 CJO=18P RS=.8)
Q1 4 12 18 QOPTO
E3 9 0 2 11 10
R5 2 0 1G
R6 11 0 1G
R7 9 12 1K
H1 1 0 VM .2K
VM 3 13 
D1 13 17 DLED
E2 11 0 16 0 .6 30M
.ENDS
**********
*SRC=H11B1;H11B1;Opto-Isolators;Darlington;Darlington 500kV 500%
*SYM=OPTOD
.SUBCKT H11B1  15    10      5   2    30
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*Motorola H11B1 - Darlington Output
Q1 5 2 4 QPWR .1
Q2 5 4 30 QPWR
.MODEL QPWR NPN (IS=120F NF=1 BF=197 VAF=160 IKF=.1
+ ISE=212F NE=2 BR=4 NR=1 VAR=28 IKR=.15 RE=4 RB=16
+ RC=1.6 XTB=1.5 CJE=74.4P CJC=12.3P TF=1P TR=57U)
.MODEL LED D(N=1.25 IS=1E-12 RS=.7 CJO=18P )
D1 11 10 LED
V1 15 11 
H1 14 0 V1 731U
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
.ENDS
**********
*SRC=H11B2;H11B2;Opto-Isolators;Darlington;Darlington 500V 200%
*SYM=OPTOD
.SUBCKT H11B2  15    10      5   2    30
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*Motorola H11B2 - Darlington Output
Q1 5 2 4 QPWR .1
Q2 5 4 30 QPWR
.MODEL QPWR NPN (IS=120F NF=1 BF=197 VAF=160 IKF=.1
+ ISE=212F NE=2 BR=4 NR=1 VAR=28 IKR=.15 RE=4 RB=16
+ RC=1.6 XTB=1.5 CJE=74.4P CJC=12.3P TF=1P TR=57U)
.MODEL LED D(N=1.25 IS=1E-12 RS=.7 CJO=18P )
D1 11 10 LED
V1 15 11 
H1 14 0 V1 292U
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
.ENDS
**********
*SRC=H11B3;H11B3;Opto-Isolators;Darlington;Darlington 500V 100%
*SYM=OPTOD
.SUBCKT H11B3  15    10      5   2    30
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*Motorola H11B3 - Darlington Output
Q1 5 2 4 QPWR .1
Q2 5 4 30 QPWR
.MODEL QPWR NPN (IS=120F NF=1 BF=197 VAF=160 IKF=.1
+ ISE=212F NE=2 BR=4 NR=1 VAR=28 IKR=.15 RE=4 RB=16
+ RC=1.6 XTB=1.5 CJE=74.4P CJC=12.3P TF=1P TR=57U)
.MODEL LED D(N=1.25 IS=1E-12 RS=.7 CJO=18P )
D1 11 10 LED
V1 15 11 
H1 14 0 V1 146U
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
.ENDS
**********
*SRC=H11A1;H11A1;Opto-Isolators;Bipolar Output;High Speed 500V 100%
*SYM=OPTO
.SUBCKT H11A1  5     12      20  2    21
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*Motorola H11A1
.MODEL QH11A NPN (IS=15.2F NF=1 BF=650 VAF=98.5 IKF=90M
+ ISE=2.34P NE=2 BR=4 NR=1 VAR=16 IKR=.135 RE=63 RB=252
+ RC=25.2 XTB=1.5 CJE=9P CJC=19P TF=397P TR=75.7N)
*   30 Volt  .15 Amp  400 MHz  SiNPN  Transistor  08-25-1991
.MODEL LED D(N=2 IS=1E-12 RS=1.5 CJO=18P )
VM 5 17 
H1 14 0 VM 2M
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
Q1 20 2 21 QH11A
D1 17 12 LED
.ENDS
**********
*SRC=H11A2;H11A2;Opto-Isolators;Bipolar Output;High Speed 500V 70%
*SYM=OPTO
.SUBCKT H11A2  5     12      20  2    21
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*Motorola H11A2
.MODEL QH11A NPN (IS=15.2F NF=1 BF=650 VAF=98.5 IKF=90M
+ ISE=2.34P NE=2 BR=4 NR=1 VAR=16 IKR=.135 RE=63 RB=252
+ RC=25.2 XTB=1.5 CJE=9P CJC=19P TF=397P TR=75.7N)
*   30 Volt  .15 Amp  400 MHz  SiNPN  Transistor  08-25-1991
.MODEL LED D(N=2 IS=1E-12 RS=1.5 CJO=18P )
VM 5 17 
H1 14 0 VM 1.4M
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
Q1 20 2 21 QH11A
D1 17 12 LED
.ENDS
**********
*SRC=H11A3;H11A3;Opto-Isolators;Bipolar Output;High Speed 500V 70%
*SYM=OPTO
.SUBCKT H11A3  5     12      20  2    21
* OPTO ISOLATOR ANODE CATHODE COL BASE EMITTER 
*Motorola H11A3
.MODEL QH11A NPN (IS=15.2F NF=1 BF=650 VAF=98.5 IKF=90M
+ ISE=2.34P NE=2 BR=4 NR=1 VAR=16 IKR=.135 RE=63 RB=252
+ RC=25.2 XTB=1.5 CJE=9P CJC=19P TF=397P TR=75.7N)
*   30 Volt  .15 Amp  400 MHz  SiNPN  Transistor  08-25-1991
.MODEL LED D(N=2 IS=1E-12 RS=1.5 CJO=18P )
VM 5 17 
H1 14 0 VM 1.4M
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
Q1 20 2 21 QH11A
D1 17 12 LED
.ENDS
**********
*SRC=TIL186-1;TIL186-1;Opto-Isolators;Bipolar Output;AC Input 2.5kV 10%
*SYM=OPTOAC
.SUBCKT TIL186-1 26    24      29  2    20
* OPTO ISOLATOR   ANODE CATHODE COL BASE EMITTER 
*TI TIL186-1 AC Input
.MODEL QIL186 NPN (IS=5.07F NF=1 BF=715 VAF=133 IKF=30M
+ ISE=709F NE=2 BR=4 NR=1 VAR=28 IKR=45M RE=-171 RB=-689
+ RC=-6.84 XTB=1.5 CJE=31.3P CJC=13.32P TF=529P TR=93.7N)
*   55 Volt  .05 Amp  693 MHz  SiNPN  Transistor  08-25-1991
.MODEL LED D(N=2 IS=1E-12 RS=1.5 CJO=18P )
VM1 26 17 
H1 14 0 POLY(2) VM1 VM2 0 182U 182U 
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
Q1 29 2 20 QIL186
D2 24 5 LED
VM2 5 26 
D1 17 24 LED
.ENDS
**********
*SRC=TIL186-2;TIL186-2;Opto-Isolators;Bipolar Output;AC Input 2.5kV 20%
*SYM=OPTOAC
.SUBCKT TIL186-2 26    24      29  2    20
* OPTO ISOLATOR   ANODE CATHODE COL BASE EMITTER 
*TI TIL186-2 AC Input
.MODEL QIL186 NPN (IS=5.07F NF=1 BF=715 VAF=133 IKF=30M
+ ISE=709F NE=2 BR=4 NR=1 VAR=28 IKR=45M RE=-171 RB=-689
+ RC=-6.84 XTB=1.5 CJE=31.3P CJC=13.32P TF=529P TR=93.7N)
*   55 Volt  .05 Amp  693 MHz  SiNPN  Transistor  08-25-1991
.MODEL LED D(N=2 IS=1E-12 RS=1.5 CJO=18P )
VM1 26 17 
H1 14 0 POLY(2) VM1 VM2 0 364U 364U 
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
Q1 29 2 20 QIL186
D2 24 5 LED
VM2 5 26 
D1 17 24 LED
.ENDS
**********
*SRC=TIL186-3;TIL186-3;Opto-Isolators;Bipolar Output;AC Input 2.5kV 50%
*SYM=OPTOAC
.SUBCKT TIL186-3 26    24      29  2    20
* OPTO ISOLATOR   ANODE CATHODE COL BASE EMITTER 
*TI TIL186-3 AC Input
.MODEL QIL186 NPN (IS=5.07F NF=1 BF=715 VAF=133 IKF=30M
+ ISE=709F NE=2 BR=4 NR=1 VAR=28 IKR=45M RE=-171 RB=-689
+ RC=-6.84 XTB=1.5 CJE=31.3P CJC=13.32P TF=529P TR=93.7N)
*   55 Volt  .05 Amp  693 MHz  SiNPN  Transistor  08-25-1991
.MODEL LED D(N=2 IS=1E-12 RS=1.5 CJO=18P )
VM1 26 17 
H1 14 0 POLY(2) VM1 VM2 0 910U 190U 
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
Q1 29 2 20 QIL186
D2 24 5 LED
VM2 5 26 
D1 17 24 LED
.ENDS
**********
*SRC=TIL186-4;TIL186-4;Opto-Isolators;Bipolar Output;AC Input 2.5kV 100%
*SYM=OPTOAC
.SUBCKT TIL186-4 26    24      29  2    20
* OPTO ISOLATOR   ANODE CATHODE COL BASE EMITTER 
*TI TIL186-4 AC Input
.MODEL QIL186 NPN (IS=5.07F NF=1 BF=715 VAF=133 IKF=30M
+ ISE=709F NE=2 BR=4 NR=1 VAR=28 IKR=45M RE=-171 RB=-689
+ RC=-6.84 XTB=1.5 CJE=31.3P CJC=13.32P TF=529P TR=93.7N)
*   55 Volt  .05 Amp  693 MHz  SiNPN  Transistor  08-25-1991
.MODEL LED D(N=2 IS=1E-12 RS=1.5 CJO=18P )
VM1 26 17 
H1 14 0 POLY(2) VM1 VM2 0 1.82M 1.82M
G1 2 0 6 0 -1
R1 14 6 1K
C1 6 0 500P
Q1 29 2 20 QIL186
D2 24 5 LED
VM2 5 26 
D1 17 24 LED
.ENDS
**********
*SRC=4N39;ON39;Opto-Isolators;SCR Output;7.5kV
*SYM=OPTOSCR
.SUBCKT ON39  17   18  1  3  2
*             LA   LC  A  K  G
QP  6 4 1  POUT; OFF
QN  4 6 5  NOUT; OFF
RF  6 4    10.6MEG
RR  1 4    7.11MEG
RGK 6 5    5.62K
RG  2 6    92.3
RK  3 5    .233
DF  6 4    ZF
DR  1 4    ZR
DGK 6 5    ZGK
.MODEL ZF   D (IS=.12F IBV=3.75U BV=200 RS=1.6MEG)
.MODEL ZR   D (IS=.12F IBV=3.75U BV=266)
.MODEL ZGK  D (IS=.12F IBV=3.75U BV=6)
.MODEL POUT PNP (IS=120F BF=1 CJE=1.34P)
.MODEL NOUT NPN (IS=120F BF=100 RC=.933
+ CJE=1.34P CJC=286F TF=18.95U TR=1.27M)
.MODEL DLED D (IS=140P RS=520M N=2.63 BV=6 IBV=10U
+ CJO=50P VJ=.75 M=.333 TT=432N)
H1 2 0 VLED -.8  93.3
VLED 17 16
D1 16 18 DLED
.ENDS
**********
*SRC=4N40;ON40;Opto-Isolators;SCR Output;7.5kV
*SYM=OPTOSCR
.SUBCKT ON40     17   18  1 3 2
*    TERMINALS:  LA   LC  A K G
QP  6 4 1  POUT; OFF
QN  4 6 5  NOUT; OFF
RF  6 4    16MEG
RR  1 4    10.6MEG
RGK 6 5    5.62K
RG  2 6    92.3
RK  3 5    .233
DF  6 4    ZF
DR  1 4    ZR
DGK 6 5    ZGK
.MODEL ZF   D (IS=.12F IBV=5U BV=400 RS=2.4MEG)
.MODEL ZR   D (IS=.12F IBV=5U BV=533)
.MODEL ZGK  D (IS=.12F IBV=5U BV=6)
.MODEL POUT PNP (IS=120F BF=1 CJE=1.34P)
.MODEL NOUT NPN (IS=120F BF=100 RC=.933
+ CJE=1.34P CJC=268F TF=14.47U TR=637U)
.MODEL DLED D (IS=140P RS=520M N=2.63 BV=6 IBV=10U
+ CJO=50P VJ=.75 M=.333 TT=432N)
H1 2 0 VLED -.8  93.3
VLED 17 16
D1 16 18 DLED
.ENDS 
**********
*SRC=IL300;IL300;Opto-Isolators;Dual;C.P.Clare Linear
*SYM=IL300
.SUBCKT IL300  1 2 3 4 5 6 
D1 13 1 DOPTO 
.MODEL DOPTO D(N=2 RS=.7 CJO=40P IS=2.5E-12) 
G1 6 5 10 0 .007 
G2 3 4 8 0 .007 
H1 14 0 V1 1 
H2 15 0 V1 1 
R4 14 10 1K 
R5 15 8 1K 
C1 8 0 500P 
C2 10 0 500P 
V1 2 13 0 
.ENDS 
**********
*SRC=LOC110;LOC110;Opto-Isolators;Dual;Siemens Linear
*SYM=LOC110
.SUBCKT LOC110 1 2 3 4 5 6 
D1 13 1 DOPTO 
.MODEL DOPTO D(N=2 RS=.7 CJO=40P IS=2.5E-12) 
G1 6 5 10 0 .007 
G2 3 4 8 0 .007 
H1 14 0 V1 1 
H2 15 0 V1 1 
R4 14 10 1K 
R5 15 8 1K 
C1 8 0 600P 
C2 10 0 600P 
V1 2 13 0 
.ENDS 
**********
*SRC=MCT6;MCT6;Opto-Isolators;Dual;GI High Gain 500V 50%
*SYM=MCT6
.SUBCKT MCT6   1     2    3   4    5   6    7   8
*OPTO ISOLATOR ANODE1 CATHODE1  CATHODE2  ANODE2 EMITTER2  COL2   COL1  EMITTER 1
*GI   MCT6
VM 1 61
D1 61  2  LED
H1 71 0 VM .00154
R1 71 81 1K
C1 81 0 454PF
G1 7 41 81 0 1
Q1 7 41 8 QOPTO
RBASE1 41  8  1G
*
VM2 4 62
D2 62 3  LED
H2 72 0 VM2  .00154
R2 72 82 1K
C2 82 0 454PF
G2 6 44  82 0 1
Q2 6 44 5  QOPTO
RBASE2 44  5  1G
*
.MODEL LED D(N=2 RS=.7 CJO=40PF IS=2.5E-12)
.MODEL QOPTO NPN(IS=2.5E-11 NF=1.3 CJC=15PF
+ CJE=45PF TF=.44NS TR=150NS BF=325 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=MCT66;MCT66;Opto-Isolators;Dual;GI High Gain 500V 15%
*SYM=MCT6
.SUBCKT MCT66  1     2    3   4    5   6    7   8
*OPTO ISOLATOR ANODE1 CATHODE1  CATHODE2  ANODE2 EMITTER2  COL2   COL1  EMITTER 1
*GI   MCT6
VM 1 61
D1 61  2  LED
H1 71 0 VM .0005
R1 71 81 1K
C1 81 0 454PF
G1 7 41 81 0 1
Q1 7 41 8 QOPTO
RBASE1 41  8  1G
*
VM2 4 62
D2 62 3  LED
H2 72 0 VM2  .0005
R2 72 82 1K
C2 82 0 454PF
G2 6 44  82 0 1
Q2 6 44 5  QOPTO
RBASE2 44  5  1G
*
.MODEL LED D(N=2 RS=.7 CJO=40PF IS=2.5E-12)
.MODEL QOPTO NPN(IS=2.5E-11 NF=1.3 CJC=15PF
+ CJE=45PF TF=.44NS TR=150NS BF=325 BR=10
+ IKF=100MA VAF=100)
.ENDS
**********
*SRC=HCPL7820;HCPL7820;Isolation Amps;H-P;High CMR
*SYM=HCPL7820
.SUBCKT HCPL7820 1  2  3  4  5  6  7  8
*Connections:          VDD1 VIN+ VIN- GND1 GND2 VOUT- VOUT+ VDD2
*High Common Mode rejection analog isolation amplifier
DVDDP 2 1 DCLAMP
DVDDN 3 1 DCLAMP
RSHP 2 9 530
RSHN 3 10 530
DINP 4 9 DCLAMP
DINN 4 10 DCLAMP
RINP 4 2 280K
RINN 4 3 280K
IINP 4 2 1U
IINN 4 3 1U
.MODEL DCLAMP D IS=8E-13
EDIFF 11 5 9 10 1
XMOD 11 5 12 5 MODULATOR
XLIMIT 12 5 13 5 LIMITER
XFILTER 13 14 5 FILTER
VOCM 15 5 DC 2.56
EOUTP 16 15 14 5 4
EOUTN 17 15 14 5 -4
ROP 16 7 1.2
RON 17 6 1.2
GVDD1 1 4 POLY(1) 13 5 4.545M 7.627M -15.18M -5.565M
RVDD1 1 4 755 
GVDD2 8 5 POLY(1) 13 5 7.106M -1.486M 3.535M 1.881M -16.32M 
RVDD2 8 5 2K
.ENDS
*
.SUBCKT MODULATOR 1 10 21 20
XINNOISE 1 5 INNOISE
XINT1 5 2 13 10 INTEGRATOR
XZOH1 13 10 3 10 ZOH
XINT2 3 2 14 10 INTEGRATOR
XZOH2 14 10 4 10 ZOH
EMODNOISE 4 30 POLY(2) 15 10 16 10 0.0 0.0 0.0 0.0 1.0
XMODNOISE 15 10 MODNOISE 
ENOISEMULT 16 10 POLY(1) 21 20 1 0 18.7 0 -110 0 6064 
XLIMIT 30 10 2 10 LIMITER 
TDELAY 2 10 6 10 Z0=1 TD=250N 
GDELAY 6 10 6 10 1 
EOUT 21 20 6 10 1 
.ENDS MODULATOR
*
.SUBCKT INTEGRATOR 1 2 3 4
GIN 4 5 1 2 1
CINT 5 4 200N
RGAIN 5 4 1300
DLIM1 5 4 DINT
DLIM2 4 5 DINT
EOUT 3 4 5 4 1
.MODEL DINT D IS=1E-35
.ENDS INTEGRATOR
*
.SUBCKT INNOISE 1 2
VNOISE 3 2 DC 0
RNOISE 3 2 16.466M
HOUT 1 2 VNOISE 551.8
.ENDS INNOISE
*
.SUBCKT MODNOISE 1 2
VNOISE 3 2 DC 0
RNOISE 3 2 16.466M
HOUT 1 2 VNOISE 44530
.ENDS MODNOISE
*
.SUBCKT ZOH  1 2 3 4
EINBUF 5 2 1 2 1
TDELAY 5 2 6 2 Z0=1 TD=100N
GLOAD 6 2 6 2 1
GINT 2 7 5 6 1
CINT 7 2 100N
RLEAK 5 7 1G
EOUTBUF 3 4 7 2 1
.ENDS ZOH
*
.SUBCKT LIMITER 1 2 3 4
ELIMP 5 2 1 2 50
VLIMN 6 2 DC -22.5M
DLIMP 5 7 DLIMIT
DLIMN 6 7 DLIMIT
ILIM 7 2 0.64
IOS 2 6 0.32
HLIM 3 4 VLIMN 1
.MODEL DLIMIT D RS=24.889
.ENDS LIMITER
*
.SUBCKT FILTER 1 2 3
EOPAMP 2 3 4 3 1
EFBAMP 5 3 6 3 1
R1 1 7 23083
R1A 7 8 23083
R2 8 9 23083
R2A 9 6 23083
R3 6 10 23083
R3A 10 11 23083
R4 11 12 23083
R4A 12 4 23083
C1A 7 3 3.946p
C1 5 8 11.935p
C2 6 3 3.099p
C3 11 2 10.69p
C4 4 3 2.552p
CF1 8 3 1.445p
CF1B 9 3 0.966p
CF2B 10 3 0.966p
CF3 11 3 1.359p
CF3B 12 3 0.994p
.ENDS FILTER
*****
*SRC=HCNR200;HCNR200;Isolation Amps;H-P;Temp Insensitive
*SYM=HCNR200
.SUBCKT HCNR200  1 2 3 4 5 6
*Connections: LED Cathode Anode  PD1 Cathode  PD1 Anode  PD2 Anode  PD2 Cathode 
*Temperature Insensitive isolation amplifier
* LED circuit
QLED1 8 2 7 QCPL .5
QLED2 9 2 7 QCPL .5
VLED 7 1 DC .685
VPD1 1 8 DC -2
VPD2 1 9 DC -2
* Input photodiode circuit
DPD1 4 3 DPHOTO
FPD1 3 4 POLY(1) VPD1 0 1 ;32.5 -450K 3.1G
CPD1 4 3 8P
* Output photodiode circuit
* Deleting the semicolon from the FPD2 statement below will 
* increase the dc nonlinearity of the model from 0% to 0.01% 
* for photodiode currents between 5nA and 50uA. 
DPD2 5 6 DPHOTO 
FPD2 6 5 POLY(1) VPD2 0 1 ;32.5 -450K 3.1G 
CPD2 5 6 8P
* Photodiode model
.MODEL DPHOTO D(IS=4.5E-12 RS=150 N=1.3 XTI=4 EG=1.11 
+ CJO=14P M=1.96 VJ=1.9)
* LED/Optical-coupling transistor model
.Model QCPL NPN(IS=2.214E-19 BF=10m NF=1.010 IKF=11.00m ISE=1.167P 
+ NE=1.737 RB=3.469 VAF=100 TF=1.77U CJE=80P)
.ENDS
*****
*SRC=HCPL4506;HCPL4506;Opto-Isolators;H-P;High CMR
*SYM=HCPL4506
.SUBCKT HCPL4506 102 103 105 106 107 108
*Connections: Anode Cathode Common Output RL Vcc
* High CMR opto isolator for Intelligent Power Module Apps
Land 20  102 20n
Lcth 23  103 20n
Lgnd 100 105 20n
Lout 17  106 20n
Lres 117 107 20n
Lvdd 1   108 20n
Xled 20 23 22 23 led
Rled 22 23 1meg
Vpd  4 100 2.7
Dpd  6 4 dmod1
Gpd  4 6 22 23 1.06m
Ia1    4    9 100u
Ia2    4   10 200u
Ra1    6    9 rmod1 36k
Ra2    9   16 rmod1 3k
Ra3   16   10 rmod1 24k
Da1    6    9 dmod2
Qa1    9    6  100 npnmod1
Qa2   10   16  100 npnmod1 3
Qout   17   10  100 npnmod1 3
Dqout  100 17 dmod3 1.430
Dclp2   10   17 dmod2 2
Rl    1  117 rmod2 20k
.MODEL RMOD1 R TC1=7.269E-3 TC2=1.646E-5
.MODEL RMOD2 R TC1=4.916E-4 TC2=6.431E-6
.MODEL DMOD1 D IS=2.1E-13 N=1.1 CJO=19.7p M=.4609 
+ VJ=.6302 TT=.1n 
.MODEL DMOD2 D N=1.072 TT=.1N CJO=.267P VJ=.69 M=.495 EG=.69 
+ IS=8.56E-12 RS=875.8 
.MODEL DMOD3 D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07 
.MODEL NPNMOD1 NPN IS=2.06E-16 VAF=92.2 NF=.998 
+ BF=297 ISE=5.990E-17 NE=1.308 IKF=9.568E-3 RE=3.039 RC=36.19 
+ RB=323.1 IRB=30.88U RBM=66.12 BR=892.9M ISC=8.844E-16 
+ NR=1.005 VAR=2.872 NC=1.1 IKR=5.201E-3 CJE=.23P MJE=.7278 
+ VJE=.9627 CJC=.157P MJC=.3924 VJC=.580 CJS=.505P MJS=.1387
+ VJS=.3757 FC=.5 XTB=1.52 TR=0.2E-7 XCJC=.35 TF=.116N PTF=46.26 
+ XTF=213.8 VTF=.568 ITF=113.2E-3 
.ENDS
*
.SUBCKT LED  1 2 7 4 
RSERIES 1 5 2.768 
DELECT 5 2 LEDNOR 
ELED 6 4 5 2 1 
DOPTIC 6 8 LEDNORC 
VSENSE 8 4 DC 0 
FPHOTO 4 3 VSENSE 1 
RL 3 4 1 RMOD
.MODEL RMOD R tc1=-0.004 
EOUT 7 4 3 4 1
.MODEL LEDNOR D IS=330E-21 N=1.5 XTI=3 EG=1.52 BV=10.38 
+ IBV=100u CJO=60p VJ=.75 M=.3333 FC=.5 TT=20n 
.MODEL LEDNORC D IS=330E-21 N=1.5 XTI=3 EG=1.52 BV=10.38
+ IBV=100u 
+ VJ=.75 M=.3333 FC=.5 
.ENDS
*****

