This file contains 4 PSpice files for the Designers Guide to Servo Simulation
using PSpice by Dr Vincent G Bello appearing in the March 28 and April 11,
1991 issues of EDN.
The file names are:
SERVO.LIB
TACHBL.CIR
TACHCL.CIR
TACHLOOP.CIR
Using an ASCII editor, separate this file into the individual files at the
indicated positions shown below. The circuits can then be simulated using
PSpice 4.04 or above.
*******************************************************************************
* FILE SERVO.LIB
******************* INCLUDE THE LINES BELOW IN THE FILE SERVO.LIB *************
* PSPICE LISTINGS FOR EDN SERVO ARTICLE BY DR. V. BELLO.
* NOTE: USE PSPICE 4.04 OR HIGHER TO RUN THESE CIRCUITS.
* SERVO LIBRARY FOR EDN ARTICLE, LISTINGS 1-4 PLUS OTHERS
* CREATED BY DR. V. BELLO , NORDEN SYSTEMS
* LIST OF SUBCIRCUITS IN LIBRARY
* COULOMB
* DEMOD
* GEAR
* GEARBL
* INERTIA
* MASSUNB
* MOTOR
* OPAMP
* POWAMP
* POWAMPDZ
* ROTLD
* SPRING
* STATIC
* VISCOUS
*
.SUBCKT INERTIA 1 PARAMS: J=1P
* NODE 1 IS SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 0 IS SPACE REFERENCE
* CURRENT INTO NODE 1 IS TORQUE DELIVERED IN OZ-IN
* UNITS: OZ,IN,RAD/SEC
* J IS ROTATIONAL INERTIA IN OZ-IN-SEC/RAD/SEC
CJ 1 0 {J}
.ENDS INERTIA
*
.SUBCKT SPRING 1 2 PARAMS: KS=1G
* NODE 1 IS INPUT SHAFT SPEED IN RAD/SEC
* NODE 2 IS OUTPUT SHAFT SPEED IN RAD/SEC
* CURRENT INTO NODE 1 IS TORQUE DELIVERED IN OZ-IN
* UNITS: OZ,IN,RAD/SEC
* KS IS ROTATIONAL SPRING CONSTANT IN OZ-IN/RAD
LK 1 2 {1/KS}
.ENDS SPRING
*
.SUBCKT VISCOUS 1 2 PARAMS: B=1P
* NODE 1 IS INPUT SHAFT SPEED IN RAD/SEC
* NODE 2 IS REFERENCE SHAFT SPEED IN RAD/SEC
* CURRENT INTO NODE 1 IS TORQUE DELIVERED IN OZ-IN
* UNITS: OZ,IN,RAD/SEC
* B IS ROTATIONAL VISCOUS FRICTION IN OZ-IN/RAD/SEC
RB 1 2 {1/B}
.ENDS VISCOUS
*
.SUBCKT COULOMB 1 2 PARAMS: FC=1P
* NODE 1 IS INPUT SHAFT SPEED IN RAD/SEC
* NODE 2 IS REFERENCE SHAFT SPEED IN RAD/SEC
* CURRENT INTO NODE 1 IS TORQUE DELIVERED IN OZ-IN
* UNITS: OZ,IN,RAD/SEC
* FC IS ROTATIONAL COULOMB FRICTION IN OZ-IN
E3 3 0 TABLE {V(1,2)} = (-.1M,-1) (.1M,1)
R3 3 0 1G
GFC 1 2 VALUE = {FC*V(3)}
.ENDS COULOMB
*
.SUBCKT STATIC 1 2 PARAMS: FS=1P WS=1U
* NODE 1 IS SHAFT SPEED IN RAD/SEC
* NODE 2 IS REFERENCE SPEED IN RAD/SEC
* CURRENT INTO NODE 1 IS TORQUE DELIVERED IN OZ-IN
* UNITS : OZ,IN,RAD/SEC
* FS IS STATIC FRICTION IN OZ-IN
* WS IS STATIC FRICTION WIDTH IN RAD/SEC
R1 1 2 1G
R3 3 0 1G
E3 3 0 TABLE{V(1,2)/WS}=(-1,0) (-.9,-1) (-.1,-1) (.1,1) (.9,1) (1,0)
GFS 1 2 VALUE = {FS*V(3)}
.ENDS STATIC
*
.SUBCKT MASSUNB 1 PARAMS:MU=0
* MASS UNBALANCE AS A FUNCTION OF ANGULAR POSITION
* NODE 1 IS SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 0 IS SPACE REFERENCE
* CURRENT INTO NODE 1 IS TORQUE DELIVERED IN RAD/SEC
* UNITS : OZ,IN,RAD/SEC
* MU IS LOAD MASS UNBALANCE IN OZ-IN
* CIRCUIT TO CALCULATE SHAFT POSITION = V(3) IN RADIANS
GP 0 3 1 0 1.0
C3 3 0 1.0
R3 3 0 1G
GMU 1 0 VALUE = {MU*COS(V(3))}
.ENDS MASSUNB
*
.SUBCKT ROTLD 1 2 PARAMS:J=1P B=1N KS=1G BKS=1N FC=0 MU=0
* ROTATIONAL LOAD MODEL INCLUDING INERTIA, VISCOUS FRICTION,
* SHAFT SPRING CONSTANT, COULOMB FRICTION, AND MASS UNBALANCE
* NODE 1 IS SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 2 IS PLATFORM SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 0 IS SPACE REFERENCE
* CURRENT INTO NODE 1 IS TORQUE DELIVERED IN OZ-IN
* UNITS : OZ,IN,RAD/SEC
* J IS INERTIA OF LOAD IN OZ-IN-SEC/RAD/SEC
* B IS VISCOUS FRICTION OF LOAD IN OZ-IN/RAD/SEC
* KS IS SHAFT SPRING CONSTANT IN OZ-IN/RAD
* BKS IS SHAFT SPRING DAMPING IN OZ-IN/RAD/SEC
* FC IS COULOMB FRICTION IN OZ-IN
* MU IS MASS UNBALANCE IN OZ-IN
LKS 1 3 {1/KS}
RKS 1 3 {1/BKS}
CJ 3 0 {J}
RB 3 2 {1/B}
* CIRCUIT TO MODEL COULOMB FRICTION
E4 4 0 TABLE {V(3,2)} = (-.1M,-1) (.1M,1)
R4 4 0 1G
GFC 3 2 VALUE = {FC*V(4)}
* CONSTANT MASS UNBALANCE
IMU 3 0 {MU}
.ENDS ROTLD
*
.SUBCKT GEAR 1 2 3 4 PARAMS: N1=1 J1=1P B1=1P F1=0 K1=1G BK1=1N
+                            N2=1 J2=1P B2=1P F2=0 K2=1G BK2=1N
* NODE 1 IS INPUT SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 2 IS INPUT PLATFORM SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 3 IS OUTPUT SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 4 IS OUTPUT PLATFORM SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 0 IS SPACE REFERENCE
* CURRENT INTO NODE 1 IS TORQUE DELIVERED IN OZ-IN
* CURRENT OUT OF NODE 3 IS TORQUE DELIVERED IN OZ-IN
* UNITS: OZ,IN,RAD/SEC
* N1 IS NUMBER OF TEETH ON INPUT GEAR
* J1 IS INPUT GEAR INERTIA IN OZ-IN-SEC/RAD/SEC
* B1 IS INPUT GEAR VISCOUS FRICTION IN OZ-IN/RAD/SEC
* F1 IS INPUT GEAR COULOMB FRICTION IN OZ-IN
* K1 IS INPUT GEAR SHAFT SPRING CONSTANT IN OZ-IN/RAD
* BK1 IS INPUT GEAR SHAFT DAMPING IN OZ-IN/RAD/SEC
* N2 IS NUMBER OF TEETH ON OUTPUT GEAR
* J2 IS OUTPUT GEAR INERTIA IN OZ-IN-SEC/RAD/SEC
* B2 IS OUTPUT GEAR VISCOUS FRICTION IN OZ-IN/RAD/SEC
* F2 IS OUTPUT GEAR COULOMB FRICTION IN OZ-IN
* K2 IS OUTPUT GEAR SHAFT SPRING CONSTANT IN OZ-IN/RAD
* BK2 IS OUTPUT GEAR SHAFT DAMPING IN OZ-IN/RAD/SEC
LK1 1 5 {1/K1}
RK1 1 5 {1/BK1}
CJ1 5 0 {J1}
RB1 5 2 {1/B1}
E8 8 0 TABLE {V(5,2)} = (-.1M,-1) (.1M,1)
R8 8 0 1G
GFC1 5 2 VALUE = {F1*V(8)}
VT1 5 7
EB1 7 2 VALUE = {(N2/N1)*V(6,4)}
GT2 4 6 VALUE = {(N2/N1)*I(VT1)}
CJ2 6 0 {J2}
RB2 6 4 {1/B2}
E9 9 0 TABLE {V(6,4)} = (-.1M,-1) (.1M,1)
R9 9 0 1G
GFC2 6 4 VALUE = {F2*V(9)}
LK2 6 3 {1/K2}
RK2 6 3 {1/BK2}
.ENDS GEAR
*
.SUBCKT GEARBL 1 2 3 4 PARAMS: N1=1 J1=1P B1=1P F1=0 K1=1G BK1=1N
+                              N2=1 J2=1P B2=1P F2=0 K2=1G BK2=1N
+                              HD=1U
* NODE 1 IS INPUT SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 2 IS INPUT PLATFORM SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 3 IS OUTPUT SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 4 IS OUTPUT PLATFORM SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 0 IS SPACE REFERENCE
* CURRENT INTO NODE 1 IS TORQUE DELIVERED IN OZ-IN
* CURRENT OUT OF NODE 3 IS TORQUE DELIVERED IN OZ-IN
* UNITS: OZ,IN,RAD/SEC
* N1 IS NUMBER OF TEETH ON INPUT GEAR
* J1 IS INPUT GEAR INERTIA IN OZ-IN-SEC/RAD/SEC
* B1 IS INPUT GEAR VISCOUS FRICTION IN OZ-IN/RAD/SEC
* F1 IS INPUT GEAR COULOMB FRICTION IN OZ-IN
* K1 IS INPUT GEAR SHAFT SPRING CONSTANT IN OZ-IN/RAD
* BK1 IS INPUT GEAR SHAFT DAMPING IN OZ-IN/RAD/SEC
* N2 IS NUMBER OF TEETH ON OUTPUT GEAR
* J2 IS OUTPUT GEAR INERTIA
* B2 IS OUTPUT GEAR VISCOUS FRICTION
* F2 IS OUTPUT GEAR COULOMB FRICTION
* K2 IS OUTPUT GEAR SHAFT SPRING CONSTANT
* BK2 IS OUTPUT GEAR SHAFT DAMPING IN OZ-IN/RAD/SEC
* HD IS BACKLASH HALF WIDTH IN RAD REFERRED TO OUTPUT GEAR
LK1 1 5 {1/K1}
RK1 1 5 {1/BK1}
CJ1 5 0 {J1}
RB1 5 2 {1/B1}
E8 8 0 TABLE {V(5,2)} = (-.1M,-1) (.1M,1)
R8 8 0 1G
GFC1 5 2 VALUE = {F1*V(8)}
VT1 5 11
EB1 7 2 VALUE = {(N2/N1)*V(6,4)}
GT2 4 6 VALUE = {(N2/N1)*I(VT1)}
CJ2 6 0 {J2}
RB2 6 4 {1/B2}
E9 9 0 TABLE {V(6,4)} = (-.1M,-1) (.1M,1)
R9 9 0 1G
GFC2 6 4 VALUE = {F2*V(9)}
LK2 6 3 {1/K2}
RK2 6 3 {1/BK2}
*
* CIRCUIT TO CALCULATE BACKLASH HALF WIDTH
*
* V(10) = GEAR OFFSET ANGLE IN RADS REFERRED TO OUTPUT GEAR
* V(10) = INTEGRAL[(W1-N*W2)/N]=(THETA1-N*THETA2)/N=THETA1/N-THETA2
GW 0 10 VALUE = {V(5,7)/(N2/N1)}
CW 10 0 1
RW 10 0 1K
* CALCULATE POSITIVE BACKLASH SWITCH CLOSURE = V(12)
* V(12) = 0 WHEN V(10) < HD
* V(12) = 1 WHEN V(10) > 1.01*HD
ESP 12 0 TABLE {V(10)/HD} = (.99,0) (1,0) (1.01,1) (1.02,1)
RSP 12 0 1G
* CALCULATE NEGATIVE BACKLASH SWITCH CLOSURE = V(13)
* V(13) = 0 WHEN V(10) > -HD
* V(13) = 1 WHEN V(10) < -1.01*HD
ESN 13 0 TABLE {V(10)/HD} = (-1.02,1) (-1.01,1) (-1,0) (-.99,0)
RSN 13 0 1G
* INPUT TORQUE DIRECTION SENSORS
* IF TORQUE < 0 OR V(5,7) < 0 THEN SET V(15)=0 ,
* OPEN POSITIVE BACKLASH SWITCH FOR NEGATIVE TORQUE OR ROTATION
ETSP 15 0 TABLE {V(5,7)+I(VT1)} = (-10M,0) (0,0) (10M,1) (20M,1)
RTSP 15 0 1G
* IF TORQUE > 0 OR V(5,7) > 0 THEN SET V(16)=0 ,
* OPEN NEGATIVE BACKLASH SWITCH FOR POSITIVE TORQUE OR ROTATION
ETSN 16 0 TABLE {V(5,7)+I(VT1)} = (-20M,1) (-10M,1) (0,0) (10M,0)
RTSN 16 0 1G
* BACKLASH SWITCH TO DISCONNECT INPUT GEAR FROM OUTPUT GEAR
GC 0 20 VALUE = {V(12)*V(15)+V(13)*V(16)}
RC 20 0 1
CC 20 0 10UF
SBL 11 7 20 0 RBL
.MODEL RBL VSWITCH (RON=1M ROFF=1MEG VON=1 VOFF=0)
.ENDS GEARBL
*
.SUBCKT MOTOR 1 2 3 4 5 PARAMS:RA=1M LA=1NH KE=7.06M KT=1 JM=1P
+                              BM=1N KTC=1M KS=1G BKS=1N
+                              CONV=.10472
* DC MOTOR WITH ARMATURE CONTROL
* NODE 1 IS POSITIVE MOTOR ARMATURE VOLTAGE
* NODE 2 IS NEGATIVE MOTOR ARMATURE VOLTAGE
* NODE 3 IS MOTOR SHAFT SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 4 IS MOTOR PLATFORM SPEED IN RAD/SEC RELATIVE TO SPACE
* NODE 0 IS SPACE REFERENCE
* NODE 5 IS TACH OUTPUT IN VOLTS OF MOTOR SPEED RELATIVE TO PLATFORM
* CURRENT OUT OF NODE 3 IS TORQUE DELIVERED IN OZ-IN
* UNITS : OZ,IN,RAD/SEC
* RA IS ARMATURE RESISTANCE IN OHMS
* LA IS ARMATURE INDUCTANCE IN HENRIES
* KE IS MOTOR BACK EMF CONSTANT IN VOLTS/RAD/SEC
* KT IS MOTOR TORQUE CONSTANT IN OZ-IN/AMP
* JM IS MOTOR INERTIA IN OZ-IN-SEC/RAD/SEC
* BM IS MOTOR VISCOUS FRICTION IN OZ-IN/RAD/SEC
* KS IS MOTOR SPRING CONSTANT IN OZ-IN/RAD
* BKS IS SHAFT SPRING DAMPING IN OZ-IN/RAD/SEC
* KTC IS TACHOMETER GAIN CONSTANT IN VOLTS/RPM
* CONV IS RPM TO RAD/SEC CONVERSION CONSTANT = 2PI/60
RA 1 8 {RA}
LA 8 6 {LA}
VA 6 7
EB 7 2 VALUE = {KE*V(9,4)}
GTM 4 9 VALUE = {KT*I(VA)}
CJM 9 0 {JM}
RBM 9 4 {1/BM}
LKS 9 3 {1/KS}
RKS 9 3 {1/BKS}
ETC 5 0 VALUE = {KTC*V(3,4)/CONV}
RTC 5 0 1G
RL 3 4 1G
.ENDS MOTOR
*
.SUBCKT OPAMP 1 2 3 PARAMS: G1=1 WP=1G VLIM=100
* DIFFERENTIAL AMPLIFIER WITH VOLTAGE LIMITING
* NODE 1 IS POSITIVE INPUT
* NODE 2 IS NEGATIVE INPUT
* NODE 3 IS OUTPUT
* G1 = DC AMPLIFIER GAIN
* WP = REAL POLE IN RAD/SEC
* VLIM = MAXIMUM AMPLIFIER OUTPUT VOLTAGE
RI 1 2 1G
R3 3 0 1G
GT 0 5 VALUE = {G1*V(1,2)/1MEG}
RT 5 0 1MEG
CT 5 0 {1/(WP*1MEG)}
ET 3 0 5 0 1
* VOLTAGE CLAMP CIRCUIT
VP 7 0 {VLIM-.6}
VM 6 0 {.6-VLIM}
D1 6 5 DS
D2 5 7 DS
.MODEL DS D(RS=1M TT=1NS CJO=1PF)
.ENDS OPAMP
*
.SUBCKT POWAMP 1 2 PARAMS: KA=1 ILIM=100 VLIM=100  K1FB=1 RO=1M WP=1G
* MOTOR POWER AMPLIFIER WITH CURRENT FEEDBACK AND NO DEADZONE
* NODE 1 IS INPUT VOLTAGE , NODE 2 IS OUTPUT VOLTAGE
* KA = POWER AMP VOLTAGE GAIN
* K1FB IS CURRENT FEEDBACK RATIO IN VOLTS/AMP
* ILIM = MAXIMUM OUTPUT CURRENT
* VLIM = MAXIMUM OUTPUT VOLTAGE
* RO = OUTPUT RESISTANCE
* WP = OPEN LOOP POLE IN RAD/SEC (GBW=KA*FP)
RI 1 0 1G
* CURRENT COMMAND LIMIT CIRCUIT
EC 15 0 TABLE {V(1)/(ILIM*K1FB)} = (-1,-1) (1,1)
RC 15 0 1G
* SUMMING JUNCTION WITH CURRENT FEEDBACK CIRCUIT
ED 7 0 VALUE = {V(15)*ILIM*.5-.5*I(VO)}
R6 7 0 1K
* VOLTAGE GAIN AND LIMIT CIRCUIT
G5 0 5 VALUE = {KA*V(7)/150K}
R5 5 0 2MEG
R7 5 19 150K
C7 19 0 .0047UF
C5 5 0 {1/(150K*WP)}
D7 20 5 DS OFF
D8 5 21 DS OFF
* OUTPUT CIRCUIT
EO 3 0 5 0 1
VO 4 2
R2 2 0 1G
RO 3 4 {RO}
* OUTPUT VOLTAGE CLAMP CIRCUIT
VP 21 0 {VLIM}
VN 20 0 {-VLIM}
.MODEL DS D(RS=1M TT=1US CJO=1PF)
.ENDS POWAMP
*
.SUBCKT DEMOD 1 2 PARAMS:G1=1 WP=1G Z=1 WN=1G
* NODE 1 IS DEMODULATOR INPUT
* NODE 2 IS DEMODULATOR OUTPUT
* G1 = DC GAIN
* WP = REAL POLE IN RAD/SEC
* Z = DAMPING FACTOR OF COMPLEX POLE PAIR
* WN = NATURAL FREQUENCY IN RAD/SEC OF COMPLEX POLE PAIR
RI 1 0 1G
R2 2 0 1G
* ED 2 0 LAPLACE {V(1)} = {G1/((1+S/WP)*(1+S*2*Z/WN+S*S/(WN*WN)))}
E3 3 0 1 0 1
R3 4 0 {.5/Z}
L3 3 4 {1/WN}
C3 4 0 {1/WN}
G5 0 5 4 0 1
R5 5 0 1
C5 5 0 {1/WP}
ED 2 0 VALUE = {G1*V(5)}
.ENDS DEMOD
*
.SUBCKT POWAMPDZ 1 2 PARAMS: KA=1 ILIM=100 VLIM=100 DZ=1M K1FB=1 RO=1M WP=1G
* MOTOR POWER AMPLIFIER WITH CURRENT FEEDBACK AND DEADZONE
* NODE 1 IS INPUT VOLTAGE , NODE 2 IS OUTPUT VOLTAGE
* KA = POWER AMP VOLTAGE GAIN
* K1FB IS CURRENT FEEDBACK RATIO IN VOLTS/AMP
* ILIM = MAXIMUM OUTPUT CURRENT
* VLIM = MAXIMUM OUTPUT VOLTAGE
* DZ = INPUT VOLTAGE DEADZONE
* RO = OUTPUT RESISTANCE
* WP = OPEN LOOP POLE IN RAD/SEC (GBW=KA*FP)
RI 1 0 1G
* DEADZONE CIRCUIT
EZ 6 0 1 0 1
R8 8 0 1K
D1 6 8 DZ
D2 8 6 DZ
* CURRENT COMMAND LIMIT CIRCUIT
EC 15 0 TABLE {V(8)/(ILIM*K1FB)} = (-1,-1) (1,1)
RC 15 0 1G
* SUMMING JUNCTION WITH CURRENT FEEDBACK
ED 7 0 VALUE = {V(15)*ILIM*.5-I(VO)*.5}
R6 7 0 1K
.MODEL DZ D(N= {2*DZ} RS=.1 TT=1US CJO=1PF)
* VOLTAGE GAIN AND LIMIT CIRCUIT
G5 0 5 VALUE = {KA*V(7)/150K}
R5 5 0 2MEG
R7 5 19 150K
C7 19 0 .0047UF
C5 5 0 {1/(150K*WP)}
D7 20 5 DS OFF
D8 5 21 DS OFF
* OUTPUT CIRCUIT
EO 3 0 5 0 1
VO 4 2
R2 2 0 1G
RO 3 4 {RO}
* OUTPUT VOLTAGE CLAMP CIRCUIT
VP 21 0 {VLIM}
VN 20 0 {-VLIM}
.MODEL DS D(RS=1M TT=1US CJO=1PF)
.ENDS POWAMPDZ
*******************************************************************************
* END OF FILE SERVO.LIB
*******************************************************************************
* FILE TACHBL.CIR
******************* INCLUDE THE LINES BELOW IN THE FILE TACHBL.CIR ************
ANTENNA SERVO - TACH CLOSED LOOP SIMULATION WITH BACKLASH
* FOR EDN SERVO ARTICLE FIGURE 7D WITH BACKLASH
* TACHBL.CIR
* NODE NAMES STARTING WITH W ARE SPEED IN RAD/SEC
* CURRENT AT SPEED NODES IS TORQUE IN OZ-IN
*
* PARAMETERS
.PARAM PI2=6.2831853
*
* INPUT TACH VELOCITY COMMAND IN VOLTS : 7.5V=1000R/S AT MOTOR
VIN 1 0 AC 1 PWL(0S 0V 1MS -7.5V .2S -7.5V .201S 7.5V .4S 7.5V .401S 0)
*
* BODY TO SPACE SPEED INPUT IN RAD/SEC
VWB WB 0 AC 0 PWL(0 0 1MS 0)
*
* TACHOMETER LOOP FEEDBACK AMPLIFIER
R13 1 62 22.6K
R14 6 62 22.6K
R15 62 2 162K
C15 62 2 390PF
X6 0 62 2 OPAMP PARAMS: G1=200K WP={5HZ*PI2} VLIM=13.5V
*
* MOTOR POWER AMPLIFIER
* FOR AC DO NOT USE DEADZONE
XPA 2 3 POWAMPDZ PARAMS: KA=120 K1FB=3 ILIM=3.5A VLIM=130V DZ=.2V
+                        RO=1 WP={100KHZ*PI2}
* XPA 2 3 POWAMP PARAMS: KA=120 K1FB=3 ILIM=3.5A VLIM=130V RO=1
* +                      WP={100KHZ*PI2}
*
* MOTOR MODEL
VIA 3 13
XM 13 0 WM WB 7 MOTOR PARAMS: RA=8.4 LA=12MH KE=72M KT=9.1 JM=.62M
+                             BM=1.2M KTC=.25M
VTM WM WG1
*
* GEAR MODEL
* FOR AC SET F1=F2=0
* XG WG1 WB WG2 WB GEAR PARAMS: N1=1 N2=401 K2=4.73MEG BK2=6873
XG WG1 WB WG2 WB GEARBL PARAMS: N1=1 N2=401 K2=4.73MEG BK2=6873
+                               HD=1.2M
*
* ROTATIONAL LOAD MODEL
VTL WG2 WL
* FOR AC SET FC=0
XL WL WB ROTLD PARAMS: J=272 B=35
*
* TACHOMETER DEMODULATOR MODEL
XD 7 10 DEMOD PARAMS: G1=3.14 WP={19.5KHZ*PI2} Z=.47 WN={202HZ*PI2}
*
* SET LOL=1KH , COL=1KF , VOL=1VAC FOR AC OPEN LOOP
* LOOP GAIN = VDB(10) VP(10)
LOL 10 6 1NH
COL 6 11 1PF
VOL 11 0 AC 0
*
* ANALYSIS COMMANDS
*
* .AC DEC 50 1HZ 100HZ
* .AC DEC 50 1HZ 1KHZ
.PRINT AC VDB(10) VP(10) VDB(2) VDB(3) VDB(7) VDB([WM]) VDB([WL])
+         VP([WL])
.TRAN 5MS 600MS 0MS 5MS
.PRINT TRAN V(1) V(6) V(2) V(3) V([WM]) V([WL]) V(7)
.PRINT TRAN I(VIA) I(VTM) I(VTL)
.OP
.PROBE
.OPTIONS ACCT LIST NODE
.OPTIONS RELTOL=1M ABSTOL=1U VNTOL=1U
.OPTIONS ITL1=100 ITL2=20 ITL5=10000 CHGTOL=1C
*
.LIB SERVO.LIB
.END
*******************************************************************************
* END OF FILE TACHBL.CIR
*******************************************************************************
* FILE TACHCL.CIR
******************* INCLUDE THE LINES BELOW IN THE FILE TACHCL.CIR ************
ANTENNA SERVO - TACH CLOSED LOOP SIMULATION - AC & STEP RESPONSE
* FOR EDN SERVO ARTICLE FIGURES 7B, C, AND D.
* TACHCL.CIR
* NODE NAMES STARTING WITH W ARE SPEED IN RAD/SEC
* CURRENT AT SPEED NODES IS TORQUE IN OZ-IN
*
* PARAMETERS
.PARAM PI2=6.2831853
*
* INPUT TACH VELOCITY COMMAND IN VOLTS : 7.5V=1000R/S AT MOTOR
VIN 1 0 AC 1 PWL(0S 0V 1MS -7.5V .2S -7.5V .201S 7.5V .4S 7.5V .401S 0)
*
* BODY TO SPACE SPEED INPUT IN RAD/SEC
VWB WB 0 AC 0 PWL(0 0 1MS 0)
*
* TACHOMETER LOOP FEEDBACK AMPLIFIER
R13 1 62 22.6K
R14 6 62 22.6K
R15 62 2 162K
C15 62 2 390PF
X6 0 62 2 OPAMP PARAMS: G1=200K WP={5HZ*PI2} VLIM=13.5V
*
* MOTOR POWER AMPLIFIER
* FOR AC DO NOT USE DEADZONE
* XPA 2 3 POWAMPDZ PARAMS: KA=120 K1FB=3 ILIM=3.5A VLIM=130V DZ=.2V
* +                        RO=1 WP={100KHZ*PI2}
XPA 2 3 POWAMP PARAMS: KA=120 K1FB=3 ILIM=3.5A VLIM=130V RO=1
+                      WP={100KHZ*PI2}
*
* MOTOR MODEL
VIA 3 13
XM 13 0 WM WB 7 MOTOR PARAMS: RA=8.4 LA=12MH KE=72M KT=9.1 JM=.62M
+                             BM=1.2M KTC=.25M
VTM WM WG1
*
* GEAR MODEL
* FOR AC SET F1=F2=0
XG WG1 WB WG2 WB GEAR PARAMS: N1=1 N2=401 K2=4.73MEG BK2=6873
* XG WG1 WB WG2 WB GEARBL PARAMS: N1=1 N2=401 K2=4.73MEG BK2=6873
* +                               HD=1.2M
*
* ROTATIONAL LOAD MODEL
VTL WG2 WL
* FOR AC SET FC=0
XL WL WB ROTLD PARAMS: J=272 B=35
*
* TACHOMETER DEMODULATOR MODEL
XD 7 10 DEMOD PARAMS: G1=3.14 WP={19.5KHZ*PI2} Z=.47 WN={202HZ*PI2}
*
* SET LOL=1KH , COL=1KF , VOL=1VAC FOR AC OPEN LOOP
* LOOP GAIN = VDB(10) VP(10)
LOL 10 6 1NH
COL 6 11 1PF
VOL 11 0 AC 0
*
* ANALYSIS COMMANDS
*
* .AC DEC 50 1HZ 100HZ
.AC DEC 50 1HZ 1KHZ
.PRINT AC VDB(10) VP(10) VDB(2) VDB(3) VDB(7) VDB([WM]) VDB([WL])
+         VP([WL])
.TRAN 5MS 600MS 0MS 5MS
.PRINT TRAN V(1) V(6) V(2) V(3) V([WM]) V([WL]) V(7)
.PRINT TRAN I(VIA) I(VTM) I(VTL)
.OP
.PROBE
.OPTIONS ACCT LIST NODE
.OPTIONS RELTOL=1M ABSTOL=1U VNTOL=1U
.OPTIONS ITL1=100 ITL2=20 ITL5=10000 CHGTOL=1C
*
.LIB SERVO.LIB
.END
*******************************************************************************
* END OF FILE TACHCL.CIR
*******************************************************************************
* FILE TACHLOOP.CIR
******************* INCLUDE THE LINES BELOW IN THE FILE TACHLOOP.CIR **********
ANTENNA SERVO - TACH OPEN LOOP SIMULATION
* FOR EDN SERVO ARTICLE FIGURE 7A.
* TACHLOOP.CIR
* NODE NAMES STARTING WITH W ARE SPEED IN RAD/SEC
* CURRENT AT SPEED NODES IS TORQUE IN OZ-IN
*
* PARAMETERS
.PARAM PI2=6.2831853
*
* INPUT TACH VELOCITY COMMAND IN VOLTS : 7.5V=1000R/S AT MOTOR
VIN 1 0 AC 0 PWL(0S 0V 1MS -7.5V .2S -7.5V .201S 7.5V .4S 7.5V .401S 0)
*
* BODY TO SPACE SPEED INPUT IN RAD/SEC
VWB WB 0 AC 0 PWL(0 0 1MS 0)
*
* TACHOMETER LOOP FEEDBACK AMPLIFIER
R13 1 62 22.6K
R14 6 62 22.6K
R15 62 2 162K
C15 62 2 390PF
X6 0 62 2 OPAMP PARAMS: G1=200K WP={5HZ*PI2} VLIM=13.5V
*
* MOTOR POWER AMPLIFIER
* FOR AC DO NOT USE DEADZONE
* XPA 2 3 POWAMPDZ PARAMS: KA=120 K1FB=3 ILIM=3.5A VLIM=130V DZ=.2V
* +                        RO=1 WP={100KHZ*PI2}
XPA 2 3 POWAMP PARAMS: KA=120 K1FB=3 ILIM=3.5A VLIM=130V RO=1
+                      WP={100KHZ*PI2}
*
* MOTOR MODEL
VIA 3 13
XM 13 0 WM WB 7 MOTOR PARAMS: RA=8.4 LA=12MH KE=72M KT=9.1 JM=.62M
+                             BM=1.2M KTC=.25M
VTM WM WG1
*
* GEAR MODEL
* FOR AC SET F1=F2=0
XG WG1 WB WG2 WB GEAR PARAMS: N1=1 N2=401 K2=4.73MEG BK2=6873
* XG WG1 WB WG2 WB GEARBL PARAMS: N1=1 N2=401 K2=4.73MEG BK2=6873
* +                               HD=1.2M
*
* ROTATIONAL LOAD MODEL
VTL WG2 WL
* FOR AC SET FC=0
XL WL WB ROTLD PARAMS: J=272 B=35
*
* TACHOMETER DEMODULATOR MODEL
XD 7 10 DEMOD PARAMS: G1=3.14 WP={19.5KHZ*PI2} Z=.47 WN={202HZ*PI2}
*
* SET LOL=1KH , COL=1KF , VOL=1VAC FOR AC OPEN LOOP
* LOOP GAIN = VDB(10) VP(10)
LOL 10 6 1KH
COL 6 11 1KF
VOL 11 0 AC 1
*
* ANALYSIS COMMANDS
*
* .AC DEC 50 1HZ 100HZ
.AC DEC 50 1HZ 1KHZ
.PRINT AC VDB(10) VP(10) VDB(2) VDB(3) VDB(7) VDB([WM]) VDB([WL])
+         VP([WL])
* .TRAN 5MS 600MS 0MS 5MS
.PRINT TRAN V(1) V(6) V(2) V(3) V([WM]) V([WL]) V(7)
.PRINT TRAN I(VIA) I(VTM) I(VTL)
.OP
.PROBE
.OPTIONS ACCT LIST NODE
.OPTIONS RELTOL=1M ABSTOL=1U VNTOL=1U
.OPTIONS ITL1=100 ITL2=20 ITL5=10000 CHGTOL=1C
*
.LIB SERVO.LIB
.END
*******************************************************************************
* END OF FILE TACHLOOP.CIR
*******************************************************************************

