******************
* HARRIS SEMICONDUCTOR VARISTOR IsSpice4 MODEL
* BASED ON TYPICAL V-I CHARACTERISTICS
* FOR THE FOLLOWING TYPES:
*
* V131HA40, V151HA40, V251HA40, V271HA40, V321HA40, V421HA40
* V481HA40, V511HA40, V571HA40, V661HA40, V751HA40
*
*****************
.SUBCKT HARMOV 1 2 {T=1 C=1pF L=1nH B1=10 B2=1 B3=0 B4=0}
R_SER  1 3 100u
L_SER  3 4 {L}
V_I_SEN 4 5 0V
H_I_SEN 6 0 V_I_SEN 10k
R_I_SEN 6 0 1G
BE_VAR 5 2 V={T} * (10^({B1}+{B2}*(LOG(V(100))-4) + {B3} * EXP(-1*LOG(V(100))+4) 
+ + {B4} * EXP(LOG(V(100))-4))
+ - 10^({B1}+{B2}*(LOG(-1*V(200))-4) + {B3} * EXP(-1*LOG(-1*V(200))+4) 
+ + {B4} * EXP(LOG(-1*V(200))-4))
+ + V(300))

C_PAR 4 2 {C}
BE_SW 7 0 V=V(999) * 10^({B1}+{B2} * (-7)+{B3} * EXP(7)+{B4} * EXP(-7))
*R_SW 7 0 1G
V_SW 999 0 1

* LIMIT(V(6),1m,10G)
BL1 100 0 V= V(6) < 1m ? 1m : V(6) > 10G ? 10G : V(6)

* LIMIT(V(6),-10G,-1m)
BL2 200 0 V= V(6) < -10g ? -10g : V(6) > -1m ? -1m : V(6)

* LIMIT((V(6)*1k*V(7)),-V(7),V(7)))
BL3 300 0 V= V(6)*1k*V(7) < -V(7) ? -V(7) : V(6)*1k*V(7) > V(7) ? V(7) : V(6)*1k*V(7)

.ENDS
* Original syntax
* E_VAR 5 2 VALUE=T*(PWR(10,B1+B2*(LOG10(LIMIT(V(6),1m,10G))-4)+B3*EXP(-LOG10(LIMIT(V(6),1m,10G))+4)
* + +B4*EXP(LOG10(LIMIT(V(6),1m,10G))-4))
* + -PWR(10,B1+B2*(LOG10(-LIMIT(V(6),-10G,-1m))-4)+B3*EXP(-LOG10(-LIMIT(V(6),-10G,-1m))+4)
* + +B4*EXP(LOG10(-LIMIT(V(6),-10G,-1m))-4))
* + +LIMIT((V(6)*1k*V(7)),-V(7),V(7)))
*****************
*SRC=V131HA40h;V131HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V131HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=10000pf L=20nH B1=2.393 B2=0.0265 B3=-627.8E-6 B4=0.003345}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V151HA40h;V151HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V151HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=8000pf L=20nH B1=2.472 B2=0.02655 B3=-615.6E-6 B4=0.003346}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V251HA40h;V251HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V251HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=5000pF L=20nH B1=2.683 B2=0.02644 B3=-612.9E-6 B4=0.003353}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V271HA40h;V271HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V271HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=4500pf L=20nH B1=2.725 B2=0.02642 B3=-618.9E-6 B4=0.003354}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V321HA40h;V321HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V321HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=3800pf L=20nH B1=2.799 B2=0.02644 B3=-618.3E-6 B4=0.003352}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V421HA40h;V421HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V421HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=3000pf L=20nH B1=2.924 B2=0.02643 B3=-620.7E-6 B4=0.003353}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V481HA40h;V481HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V481HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=2700pf L=20nH B1=2.966 B2=0.02639 B3=-618.9E-6 B4=0.003357}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V511HA40h;V511HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V511HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=2500pf L=20nH B1=3.005 B2=0.02643 B3=-616.0E-6 B4=0.003354}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V571HA40h;V571HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V571HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=2200pf L=20nH B1=3.051 B2=0.02642 B3=-618.3E-6 B4=0.003354}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V661HA40h;V661HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V661HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=2000pf L=20nH B1=3.113 B2=0.02640 B3=-619.6E-6 B4=0.003356}
.ENDS
*INCLUDE HARMOV.LIB
*****************
*SRC=V751HA40h;V751HA40h;Varistors;Harris;
*SYM=MOV
.SUBCKT V751HA40h 1 2 {TOL=0}
X1 1 2 HARMOV {T=1+TOL/100 C=1800pF L=20nH B1=3.171 B2=0.02644 B3=-617.3E-6 B4=0.003353}
.ENDS
*INCLUDE HARMOV.LIB
****************

