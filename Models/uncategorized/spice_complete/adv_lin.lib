*
* LIBRARY OF ADVANCED LINEAR DEVICES, INC. DEFINITIONS
*
*   COPYRIGHT 1993 ADVANCED LINEAR DEVICES, INC.
*   THIS LIBRARY OR ANY PART MAY BE COPIED WITHOUT THE EXPRESS OR
*   WRITTEN CONSENT OF ADVANCED LINEAR DEVICES, INC. HOWEVER, IF A CLEAN COPY 
*   OF THIS LIBRARY IS NEEDED, PLEASE CONTACT YOUR ALD REPRESENTITVE.
* ISPICE VERSION
* $REVISION:  1.4 $
* $AUTHOR:    JPG  $
* $DATE:    1 MAR 1993 00:00:00  $
* -----------------------------------------------------------------------------
*
* THIS LIBRARY OF MACROMODELS IS BEING SUPPLIED TO USERS AS AN AID TO CIRCUIT
* DESIGN. WHILE IT REFLECTS REASONABLY CLOSE SIMILARITY TO THE ACTUAL DEVICE
* IN TERMS OF PERFORMANCE, IT IS NOT SUGGESTED AS A REPLACEMENT FOR BREAD-
* BOARDING. SIMULATION SHOULD BE USED AS A FORERUNNER OR A SUPPLEMENT TO
* TRADITIONAL LAB TESTING.
*
* USERS SHOULD VERY CAREFULLY NOTE THE FOLLOWING FACTORS REGARDING THESE
* MODELS:
*
*     THE MODELS CONTAINED HEREIN REPRESENT ADVANCED LINEAR DEVICES LINE OF
*     "RAIL TO RAIL" OPERATIONAL AMPLIFIERS. THESE SPECIAL MODELS HAVE BEEN 
*     TAILORED TO SPECIFICALLY MODEL ALD'S LINE OF SILICON GATE CMOS AMPLIFIERS.
*     THE MACROMODELS ALLOW FOR THE MODELING OF INPUT AND OUTPUT CHARACTER-
*     ISTICS THAT INCLUDE THE RAIL TO RAIL OPERATION. IN ADDITION, THE INPUT 
*     IMPEDANCE CHARACTERISTICS OF THE AMPLIFIERS MOSFET INPUT STAGES ARE
*     PRESERVED.
*
*     MODEL PERFORMANCE IN GENERAL WILL REFLECT TYPICAL BASELINE SPECS FOR A
*     GIVEN DEVICE, AND CERTAIN ASPECTS OF PERFORMANCE MAY NOT BE MODELED
*     FULLY.
*
*     WHILE REASONABLE CARE HAS BEEN TAKEN IN THEIR PREPARATION, WE CANNOT BE
*     RESPONSIBLE FOR CORRECT APPLICATION ON ANY AND ALL COMPUTER SYSTEMS.
*
*     IN ALL CASES, THE CURRENT DATA SHEET INFORMATION FOR A GIVEN REAL DEVICE
*     IS YOUR FINAL DESIGN GUIDELINE, AND IS THE ONLY ACTUAL PERFORMANCE
*     GUARANTEE.  FOR FURTHER TECHNICAL INFORMATION, REFER TO INDIVIDUAL DEVICE
*     DATA SHEETS.
*
*     MODEL USERS ARE HEREBY NOTIFIED THAT THESE MODELS ARE SUPPLIED "AS IS",
*     WITH NO DIRECT OR IMPLIED RESPONSIBILITY ON THE PART OF ADVANCED LINEAR
*     DEVICES FOR THEIR OPERATION WITHIN A CUSTOMER CIRCUIT OR SYSTEM. FURTHER,
*     ALD RESERVES THE RIGHT TO CHANGE THESE MODELS WITHOUT PRIOR NOTICE. 
*
*     THE MACROMODELS LISTED IN THIS LIBRARY REPRESENT TYPICAL OPERATIONAL 
*     AMPLIFIER CHARACTERISTICS, WITHOUT OFFSET (VOS) ADJUSTMENT, OR OTHER 
*     PARAMETERS THAT WOULD ALLOW FOR MONTE CARLO SIMULATION OF THESE CIRCUITS.
*     
*     ADVANCED LINEAR DEVICES PROVIDES A SEPARATE LIBRARY OF ASIC STANDARD CELL
*     FUNCTIONS SUCH AS OPERATIONAL AMPLIFIERS, TIMERS, AND COMPARATORS AS WELL
*     AS RESISTORS, CAPACITORS, AND INDIVIDUAL TRANSISTORS AND DIODES FOR FUNC-
*     TION SPECIFIC INTEGRATED CIRCUIT DESIGN. THIS ASIC VERIFICATION LIBRARY 
*     CONTAINS A COMPREHENSIVE SET OF PARAMETRIC VALUES TO BE USED IN MONTE 
*     CARLO SIMULATION OF USER DESIGNED INTEGRATED CIRCUITS. CONTACT YOUR 
*     ADVANCED LINEAR DEVICES REPRESENTATIVE FOR MORE INFORMATION. 
* -----------------------------------------------------------------------------
*    
* YOUR FEEDBACK AND SUGGESTIONS ON THESE (AND FUTURE) MODELS WILL BE
* APPRECIATED.
*
*
* ADVANCED LINEAR DEVICES, INC.
* CUSTOMER SUPPORT GROUP
* 1180F MIRALOMA WAY
* SUNNYVALE, CALIFORNIA. 94086-4606
* TEL: (408) 720-8737 
* FAX: (408) 720-8297
* -----------------------------------------------------------------------------
*
********
* 1701 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91.
*
* CONNECTIONS:      NON-INVERTING INPUT
*                   | INVERTING INPUT
*                   | | POSITIVE POWER SUPPLY
*                   | | | NEGATIVE POWER SUPPLY
*                   | | | | OUTPUT
*                   | | | | |
.SUBCKT ALD1701_AL     1 2 3 4 5
*
  C1   11 12 4.5E-12
  C2    6  7 15.00E-12
  D13   1  3 DX
  D41   4  1 DX
  D23   2  3 DX
  D32   4  2 DX
  DC    5 53 DY
  DE   54  5 DY
  DLP  90 91 DY
  DLN  92 90 DY
  DP    4  3 DX
  EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
  FB    7 99 POLY(5) VB VC VE VLP VLN 0 40E3 -40E3 40E3 40E3 -40E3
  GA    6  0 11 12 500.0E-6
  GCM   0  6 10 101 4.4E-9
  ISS   3 10 DC 1.40E-6
  ISX   3 101 DC .70E-6
  HLIM 90  0 VLIM 1K
  M1   11   2 10  31 MX
  M2   12   1 10  31 MX
  M3   121 99 101 31 MX
  R2    6  9 100.0E3
  RD1   4 11  15.E3
  RD2   4 12  15.E3
  RD3   4 121 15.E3
  RO1   8  5 200
  RO2   7 99 145E3
  RP    3  4 20.0E3
  VB    9  0 DC 0
  VC    3 53 DC 75.00E-3
  VE   54  4 DC 75.00E-3
  VLIM  7  8 DC 0
  VLP  91  0 DC 1
  VLN   0 92 DC 1
  VX   31  3 DC .98
.MODEL DX D(IS=1.0E-15 CJO=.3P)
.MODEL DY D(IS=1.0E-15 N=.1)
.MODEL MX PMOS (LEVEL=1 VTO=-.82 KP=45E-6 CGBO=9E-9 GAMMA=0)
.ENDS
********
* 1702 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUITS
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91.
*
* CONNECTIONS:       NON-INVERTING INPUT
*                    | INVERTING INPUT
*                    | | POSITIVE POWER SUPPLY
*                    | | | NEGATIVE POWER SUPPLY
*                    | | | | OUTPUT
*                    | | | | |
.SUBCKT ALD1702_AL      1 2 3 4 5
*
  C1   11 12 1.7E-12
  C2    6  7 27.27E-12
  D13   1  3 DX
  D41   4  1 DX
  D23   2  3 DX
  D32   4  2 DX
  DC    5 53 DY
  DE   54  5 DY
  DLP  90 91 DY
  DLN  92 90 DY
  DP    4  3 DX
  EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
  FB    7 99 POLY(5) VB VC VE VLP VLN 0 37.7E3 -50E3 50E3 50E3 -50E3
  GA    6  0 11 12 872E-6
  GCM   0  6 10 101 19E-9
  ISS   3 10 DC  5.0E-6
  ISX   3 101 DC 2.5E-6
  HLIM 90  0 VLIM .1K
  M1   11   2 10  31 MX
  M2   12   1 10  31 MX
  M3   121 99 101 31 MX
  R2    6  9  100E3
  RD1   4 11  13.35E3
  RD2   4 12  13.35E3
  RD3   4 121 13.35E3
  RO1   8  5  1.0E2
  RO2   7 99  43.0E3
  RP    3  4  4.54E3
  VB    9  0 DC 0.0
  VC    3 53 DC 90.00E-3
  VE   54  4 DC 90.00E-3
  VLIM  7  8 DC 0
  VLP  91  0 DC 0.8
  VLN   0 92 DC 0.8
  VX   31  3 DC 1.03
.MODEL DX D(IS=1.0E-15 CJO=.3P)
.MODEL DY D(IS=1.0E-15 N=.1)
.MODEL MX PMOS (LEVEL=1 VTO=-.85 KP=116E-6 CGBO=9.0E-9 GAMMA=0)
.ENDS
********
* 1703 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUITS
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91.
*                                                                                               
* CONNECTIONS:       NON-INVERTING INPUT
*                    | INVERTING INPUT
*                    | | POSITIVE POWER SUPPLY
*                    | | | NEGATIVE POWER SUPPLY
*                    | | | | OUTPUT
*                    | | | | |
.SUBCKT ALD1703_AL     1 2 3 4 5
*
  C1   11 12 1.7E-12
  C2    6  7 27.27E-12
  D13   1  3 DX
  D41   4  1 DX
  D23   2  3 DX
  D32   4  2 DX
  DC    5 53 DY
  DE   54  5 DY
  DLP  90 91 DY
  DLN  92 90 DY
  DP    4  3 DX
  EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
  FB    7 99 POLY(5) VB VC VE VLP VLN 0 37.7E3 -50E3 50E3 50E3 -50E3
  GA    6  0 11 12 872E-6
  GCM   0  6 10 101 18E-9
  ISS   3 10 DC  5.0E-6
  ISX   3 101 DC 2.5E-6
  HLIM 90  0 VLIM .1K
  M1   11   2 10  31 MX
  M2   12   1 10  31 MX
  M3   121 99 101 31 MX
  R2    6  9  100E3
  RD1   4 11  13.35E3
  RD2   4 12  13.35E3
  RD3   4 121 13.35E3
  RO1   8  5  1.0E2
  RO2   7 99  43.0E3
  RP    3  4  4.54E3
  VB    9  0 DC 0.0
  VC    3 53 DC 90.00E-3
  VE   54  4 DC 90.00E-3
  VLIM  7  8 DC 0
  VLP  91  0 DC 0.8
  VLN   0 92 DC 0.8
  VX   31  3 DC -.7
.MODEL DX D(IS=1.0E-15 CJO=.3P)
.MODEL DY D(IS=1.0E-15 N=.1)
.MODEL MX PMOS (LEVEL=1 VTO=+.25 KP=116E-6 CGBO=9.0E-9 GAMMA=0)
.ENDS
********
* 1704 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91.
*
* CONNECTIONS:       NON-INVERTING INPUT
*                    | INVERTING INPUT
*                    | | POSITIVE POWER SUPPLY
*                    | | | NEGATIVE POWER SUPPLY
*                    | | | | OUTPUT
*                    | | | | |
.SUBCKT ALD1704_AL      1 2 3 4 5
*
  C1   11 12 1.5E-12
  C2    6  7 30.E-12
  D13   1  3 DX
  D41   4  1 DX
  D23   2  3 DX
  D32   4  2 DX
  DC    5 53 DY
  DE   54  5 DY
  DLP  90 91 DY
  DLN  92 90 DY
  DP    4  3 DX
  EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
  FB    7 99 POLY(5) VB VC VE VLP VLN 0 88E3 -50E3 50E3 50E3 -50E3
  GA    6  0 11 12 1200E-6
  GCM   0  6 10 101 50E-9
  ISS   3 10 DC  15.E-6
  ISX   3 101 DC 7.5E-6
  HLIM 90  0 VLIM .1K
  M1   11   2 10  31 MX
  M2   12   1 10  31 MX
  M3   121 99 101 31 MX
  R2    6  9  100E3
  RD1   4 11  13.35E3
  RD2   4 12  13.35E3
  RD3   4 121 13.35E3
  RO1   8  5  3.3E1
  RO2   7 99  4.46E3
  RP    3  4  4.54E3
  VB    9  0 DC 0.0
  VC    3 53 DC 90.00E-3
  VE   54  4 DC 90.00E-3
  VLIM  7  8 DC 0
  VLP  91  0 DC 0.8
  VLN   0 92 DC 0.8
  VX   31  3 DC 1.03
.MODEL DX D(IS=1.0E-15 CJO=.3P)
.MODEL DY D(IS=1.0E-15 N=.1)
.MODEL MX PMOS (LEVEL=1 VTO=-.85 KP=116E-6 CGBO=9.0E-9 GAMMA=0)
.ENDS
********
* 1706 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91.
*
* CONNECTIONS:      NON-INVERTING INPUT
*                   | INVERTING INPUT
*                   | | POSITIVE POWER SUPPLY
*                   | | | NEGATIVE POWER SUPPLY
*                   | | | | OUTPUT
*                   | | | | |
.SUBCKT ALD1706_AL  1 2 3 4 5
*
  C1   11 12 15.3E-12
  C2    6  7 10.90E-12
  D13   1  3 DX
  D41   4  1 DX
  D23   2  3 DX
  D32   4  2 DX
  DC    5 53 DY
  DE   54  5 DY
  DLP  90 91 DY
  DLN  92 90 DY
  DP    4  3 DX
  EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
  FB    7 99 POLY(5) VB VC VE VLP VLN 0 125E3 -40E3 40E3 40E3 -40E3
  GA    6  0 11 12 500E-6
  GCM   0  6 10 101 2.04E-9
  ISS   3 10  DC .34E-6
  ISX   3 101 DC .17E-6
  HLIM 90  0 VLIM 10K
  M1   11   2 10  31 MX
  M2   12   1 10  31 MX
  M3   121 99 101 31 MX
  R2    6  9 100.0E3
  RD1   4 11  15.E3
  RD2   4 12  15.E3
  RD3   4 121 15.E3
  RO1   8  5 200
  RO2   7 99 47.1E3
  RP    3  4 250E3
  VB    9  0 DC 0
  VC    3 53 DC 156E-3
  VE   54  4 DC 156E-3
  VLIM  7  8 DC 0
  VLP  91  0 DC 2
  VLN   0 92 DC 2
  VX   31  3 DC .98
.MODEL DX D(IS=1.0E-15 CJO=.3P)
.MODEL DY D(IS=1.0E-15 N=.1)
.MODEL MX PMOS (LEVEL=1 VTO=-.81 KP=45E-6 CGBO=9E-9 GAMMA=0)
.ENDS
********

* 1701 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUITS
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91. 
*
*   4701/PB/DB/SF
*           
.SUBCKT AL4701PB_AL 1 2 3 4 5 6 7 8 9 10 11 12 13 14
X1  3  2  4 11  1 ALD1701_AL
X2  5  6  4 11  7 ALD1701_AL
X3 10  9  4 11  8 ALD1701_AL
X4 12 13  4 11 14 ALD1701_AL
.ENDS
********
.SUBCKT AL4701DB_AL 1 2 3 4 5 6 7 8 9 10 11 12 13 14
X1  3  2  4 11  1 ALD1701_AL
X2  5  6  4 11  7 ALD1701_AL
X3 10  9  4 11  8 ALD1701_AL
X4 12 13  4 11 14 ALD1701_AL
.ENDS
********
.SUBCKT AL4701SF_AL 1 3 5 6 8 10 12 13 15 17 19 20 22 24
X1  5  3  6 19  1 ALD1701_AL
X2  8 10  6 19 12 ALD1701_AL
X3 17 15  6 19 13 ALD1701_AL
X4 20 22  6 19 24 ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . .  .  .  .
.SUBCKT AL2701SB_AL 1 2 4 6 7 10 11 12
X1  2  1 11  4 12 ALD1701_AL
X2  6  7 11  4 10 ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . . . . .
.SUBCKT AL2701PA_AL 1 2 3 4 5 6 7 8
X1  3  2  8  4  1 ALD1701_AL
X2  5  6  8  4  7 ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . . . . .
.SUBCKT AL2701DA_AL 1 2 3 4 5 6 7 8
X1  3  2  8  4  1 ALD1701_AL
X2  5  6  8  4  7 ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . . . .
.SUBCKT AL2701SA_AL 1 2 3 4 5 6 7 8
X1  3  2  8  4  1 ALD1701_AL
X2  5  6  8  4  7 ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . . . . .
.SUBCKT AL2711PA_AL 1 2 3 4 5 6 7 8
X1  3  2  8  4  1 ALD1701_AL
X2  5  6  8  4  7 ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . . . . .
.SUBCKT AL2711DA_AL 1 2 3 4 5 6 7 8
X1  3  2  8  4  1 ALD1701_AL
X2  5  6  8  4  7 ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . . . .
.SUBCKT AL2711SA_AL 1 2 3 4 5 6 7 8
X1  3  2  8  4  1 ALD1701_AL
X2  5  6  8  4  7 ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . .
.SUBCKT AL1701PA_AL 2 3 4 6 7
X1  3  2  7  4  6  ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . .
.SUBCKT AL1701DA_AL 2 3 4 6 7
X1  3  2  7  4  6  ALD1701_AL
.ENDS
*******
*     DEVICE PINS: . . . . .
.SUBCKT AL1701SA_AL 2 3 4 6 7
X1  3  2  7  4  6  ALD1701_AL
.ENDS
********
*
* 1702 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUITS
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91. 
*
.SUBCKT AL1702DA_AL 2 3 4 6 7
X1  3 2 7 4 6 ALD1702_AL
.ENDS
*******
*
.SUBCKT AL1702PA_AL 2 3 4 6 7
X1  3 2 7 4 6 ALD1702_AL
.ENDS
*******
*
.SUBCKT AL1702SA_AL 2 3 4 6 7
X1  3 2 7 4 6 ALD1702_AL
.ENDS
*******
*
.SUBCKT AL1712DA_AL 2 3 4 6 7
X1  3 2 7 4 6 ALD1702_AL
.ENDS
*******
*
.SUBCKT AL1712PA_AL 2 3 4 6 7
X1  3 2 7 4 6 ALD1702_AL
.ENDS
*******
*
.SUBCKT AL1712SA_AL 2 3 4 6 7
X1  3 2 7 4 6 ALD1702_AL
.ENDS
*******
*
* 1703 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUITS
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91. 
*
.SUBCKT AL1703PA_AL 2 3 4 6 7
X1  3 2 7 4 6 ALD1703_AL
.ENDS
********
.SUBCKT AL1703SA_AL 2 3 4 6 7
X1  3 2 7 4 6 ALD1703_AL
.ENDS
********
.SUBCKT AL1703DA_AL 2 3 4 6 7
X1  3 2 7 4 6 ALD1703_AL
.ENDS
********
* 1704 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91. 
*
.SUBCKT AL1704DA_AL 2 3 4 6 7
X1 3 2 7 4 6 ALD1704_AL
.ENDS
********
.SUBCKT AL1704PA_AL 2 3 4 6 7
X1 3 2 7 4 6 ALD1704_AL
.ENDS
********
.SUBCKT AL1704SA_AL 2 3 4 6 7
X1 3 2 7 4 6 ALD1704_AL
.ENDS
********
* 1706 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* CREATED BY ADVANCED LINEAR DEVICES 1/16/91. 
*
* ALD4706/PB/DB/SF
.SUBCKT AL4706PB_AL 1 2 3 4 5 6 7 8 9 10 11 12 13 14
X1  3  2  4 11  1 ALD1706_AL
X2  5  6  4 11  7 ALD1706_AL
X3 10  9  4 11  8 ALD1706_AL
X4 12 13  4 11 14 ALD1706_AL
.ENDS
********
.SUBCKT AL4706DB_AL 1 2 3 4 5 6 7 8 9 10 11 12 13 14
X1  3  2  4 11  1 ALD1706_AL
X2  5  6  4 11  7 ALD1706_AL
X3 10  9  4 11  8 ALD1706_AL
X4 12 13  4 11 14 ALD1706_AL
.ENDS
********
.SUBCKT AL4706SF_AL 1 3 5 6 8 10 12 13 15 17 19 20 22 24
X1  5  3  6 19  1 ALD1706_AL
X2  8 10  6 19 12 ALD1706_AL
X3 17 15  6 19 13 ALD1706_AL
X4 20 22  6 19 24 ALD1706_AL
.ENDS
********
.SUBCKT AL2706DA_AL 1 2 3 4 5 6 7 8
X1 3 2 8 4 1 ALD1706_AL
X2 5 6 8 4 7 ALD1706_AL
.ENDS
********
.SUBCKT AL2706PA_AL 1 2 3 4 5 6 7 8
X1 3 2 8 4 1 ALD1706_AL
X2 5 6 8 4 7 ALD1706_AL
.ENDS
********
.SUBCKT AL2706SA_AL 1 2 3 4 5 6 7 8
X1 3 2 8 4 1 ALD1706_AL
X2 5 6 8 4 7 ALD1706_AL
.ENDS
********
.SUBCKT AL2706SB_AL 1 2 4 6 7 10 11 12
X1 2 1 11 4 12 ALD1706_AL
X2 6 7 11 4 10 ALD1706_AL
.ENDS
*******
.SUBCKT AL1706DA_AL 2 3 4 6 7
X1 3 2 7 4 6 ALD1706_AL
.ENDS
********
.SUBCKT AL1706PA_AL 2 3 4 6 7
X1 3 2 7 4 6 ALD1706_AL
.ENDS
********
.SUBCKT AL1706SA_AL 2 3 4 6 7
X1 3 2 7 4 6 ALD1706_AL
.ENDS
********
