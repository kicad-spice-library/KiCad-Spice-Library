**********
* Copyright Intusoft 1995-1997
* All Rights Reserved
**********
**********
*SRC=NTSC;NTSC;!Generators;Timing;Timing signal
*SYM=NTSC
.SUBCKT NTSC 17 {FC=3.579545MEG RFL=0.0 BLANK=.75}
*FC = Chrominace Subcarrier Frequency
*RFL=Reference White Level
*Blank=Blanking Level
*This model contains no color information, only timing
.SUBCKT VEQP 1 2
*EQUALIZING PULSE
VEQP 1 2 PULSE 0 1 0 {.004*455/(2*FC)} {.004*455/(2*FC)} 
+ {.04*455/(2*FC)} {.5*455/(2*FC)} 
.ENDS
.SUBCKT VVSP 22 2
*VERTICLE SYNC PULSE
VVSP 22 2 PULSE 0 1 0 {.004*455/(2*FC)} {.004*455/(2*FC)} 
+ {.422*455/(2*FC)} {.5*455/(2*FC)} 
.ENDS
.SUBCKT VHB 3 2
*HORIZONTAL BLANKING PULSE
VHB 3 2 PULSE 0 1 0 {.004*455/(2*FC)} {.004*455/(2*FC)} 
+ {.165*455/(2*FC)} {455/(2*FC)} 
.ENDS
.SUBCKT VVB 4 2
*VERTICLE BLANKING PULSE
VVB 4 2 PULSE 0 1 0 {.0002*525*455/(4*FC)} {.0002*525*455/(4*FC)} 
+ {.07*525*455/(4*FC)} {525*455/(4*FC)}
.ENDS
.SUBCKT VHSP 5 2
*HORIZONTAL SYNC PULSE
VHSP 5 2 PULSE 0 1 {.021*455/(2*FC)} {.004*455/(2*FC)} 
+ {.004*455/(2*FC)} {.075*455/(2*FC)} {455/(2*FC)} 
.ENDS
.SUBCKT VCBC 7 2
*COLOR BURST CORRECTION
VCBC 7 2 PULSE 0 1 {.11*455/(2*FC)} {.004*455/(2*FC)} {.004*455/(2*FC)}
+ {.04*455/(2*FC)} {455/(2*FC)} 
.ENDS
.SUBCKT VEQC1 8 2
*EQUALIZING PULSE CORRECTION NUMBER 1
VEQC1 8 2 PULSE 0 1 0 {.004*455/(2*FC)} {.004*455/(2*FC)} 
+ {2.9*455/(2*FC)} {525*455/(4*FC)} 
.ENDS
.SUBCKT VEQC2 9 2
*EQUALIZING PULSE CORRECTION NUMBER 2
VEQC2 9 2 PULSE 0 1 {6*455/(2*FC)} {.004*455/(2*FC)} {.004*455/(2*FC)} 
+ {2.9*455/(2*FC)} {525*455/(4*FC)} 
.ENDS
.SUBCKT VCVSP 10 2
*EQUALIZING PULSE CORRECTION NUMBER 2
VVSPC 10 2 PULSE 0 1 {3*455/(2*FC)} {.004*455/(2*FC)} {.004*455/(2*FC)}
+ {2.9*455/(2*FC)} {525*455/(4*FC)} 
.ENDS
.SUBCKT VCHSP 11 2
*EQUALIZING PULSE CORRECTION NUMBER 2
VHSPC 11 2 PULSE 1 0 0 {.004*455/(2*FC)} {.004*455/(2*FC)} 
+ {9*455/(2*FC)} {525*455/(4*FC)} 
.ENDS
R1 1 0 1
X1 22 0 VVSP {FC=3.579545MEG}
R2 22 0 1
X2 3 0 VHB {FC=3.579545MEG}
R3 3 0 1
X3 4 0 VVB {FC=3.579545MEG}
R4 4 0 1
X4 5 0 VHSP {FC=3.579545MEG}
R5 5 0 1
X6 7 0 VCBC {FC=3.579545MEG}
X7 8 0 VEQC1 {FC=3.579545MEG}
R8 8 0 1
X8 9 0 VEQC2 {FC=3.579545MEG}
R9 9 0 1
X9 10 0 VCVSP {FC=3.579545MEG}
R10 10 0 1
X10 11 0 VCHSP {FC=3.579545MEG}
R11 11 0 1
B1 12 0 V = (V(5) * V(11)) +(.5* V(18))
B2 13 0 V = V(1) * (V(8) + V(9))
B3 14 0 V = V(22) * V(10)
B4 15 0 V = .25 * (V(12) + V(13) + V(14))
*.25 IS A SIMPLE SCALING FACTOR IN PREPARATION FOR MAX.
B5 16 0 V = {BLANK} * ((V(3) * (1 + (-1*V(4)))) + V(4))
*.75 * ((V(3) * (1 + (-1*V(4)))) + V(4))
* .1 + .5 REPRESENTS THE BLANKING LEVEL FACTOR
B6 17 0 V = {RFL} + (V(16) + V(15))
*.125 + (V(16) + V(15))
B7 18 0 V = V(11) * (V(7) * (SIN(2*3.1416*{FC}*V(19))))
R12 7 0 1
I1 0 19 1
C1 19 0 1 IC=0
R13 19 0 1E12
XV1 1 0 VEQP {FC=3.579545MEG}
.ENDS
**********


