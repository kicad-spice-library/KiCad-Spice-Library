**********
* Copyright Intusoft 1990-1997
* All Rights Reserved
**********
**********
* Models Include:
* Motor, Gears, Inertia, Inertia with test point, slipage
* dynamic friction, torsion spring, static friction, 
* rate to angle conversion, and SERVO
**********
*SRC=MOTOR;MOTOR;Mechanical;V/I in -> Speed/Torque Out;
*SYM=MOTOR
*HELP issyntax.hlp "Simple Motor Model"
.SUBCKT MOTOR 1 2 3  {KS=???}
E1 4 0 1 2 {KS}
V2 4 5  
R1 5 3 .001
R2 1 2 1MEG
F1 1 2 V2 {KS}
.ENDS
**********
*SRC=GEARS;GEARS;Mechanical;Gear Ratio;
*SYM=GEARS
*HELP issyntax.hlp "Gears"
.SUBCKT GEARS 1 2  {RATIO=???}
E1 4 0 1 0 {RATIO}
V1 4 3 
R1 3 2 .001
R3 1 0 1MEG
F1 1 0 V1 {RATIO}
.ENDS
**********
*SRC=INRTA;INRTA;Mechanical;Inertia;
*SYM=INRTA
*HELP issyntax.hlp "Inertia and Torsion Spring"
.SUBCKT INRTA 2 1 {J=???}
C1 1 0 {J}
V1 2 1 
.ENDS
**********
*SRC=INRTA2;INRTA2;Mechanical;Inertia with TP;Inertia with TP
*SYM=INRTA2
*HELP issyntax.hlp "Inertia and Torsion Spring"
.SUBCKT INRTA2 2 1 3 {J=???}
C1 1 0 {J}
V1 2 1 
H1 0 3 V1 1
R1 3 0 1K
.ENDS
**********
*SRC=SLIP;SLIP;Mechanical;Slipage;
*SYM=SLIP
.SUBCKT SLIP 1 2  {D=???}
R1 2 1 {1/D}
.ENDS
**********
*SRC=DFRCTN;DFRCTN;Mechanical;Dynamic Frict;Dynamic Friction
*SYM=DFRCTN
*HELP issyntax.hlp "Friction"
.SUBCKT DFRCTN 1
R1 1 0 {1/D}
.ENDS
**********
*SRC=TSPRNG;TSPRNG;Mechanical;Torsion Spring;
*SYM=TSPRNG
*HELP issyntax.hlp "Inertia and Torsion Spring"
.SUBCKT TSPRNG 1 2  {K=???}
L1 1 2 {1/K}
.ENDS
**********
*SRC=SFRCTN;SFRCTN;Mechanical;Static Friction;
*SYM=SFRCTN
*HELP issyntax.hlp "Friction"
.SUBCKT SFRCTN 1  {TRQ=???}
R1 1 0 1K
D2 0 2 DX
.MODEL DX D( IS=.1M N=.069 CJO=1U)
I1 2 0 {TRQ}
I2 2 1 {TRQ}
D1 1 2 DX
.ENDS
**********
*SRC=RTOA;RTOA;Mechanical;Rate-to-Angle;Rate-to-Angle Converter
*SYM=RTOA
*HELP issyntax.hlp "Speed To Angle Conversion"
.SUBCKT RTOA 1 2 
V2 4 3 
R1 3 2 .001
R2 1 0 1MEG
G1 0 7 1 0 1M
C1 7 0 1M
R3 7 0 1G
E3 0 4 0 7 1
R4 2 0 1MEG
F1 1 0 V2 1
.ENDS
**********
*SRC=SERVO;SERVO;Mechanical;Motor Load;
*SYM=SERVO
.SUBCKT SERVO 1 2 3                 RATE {KT=??? J=??? F=??? ANGLE=??? RATE=???}
*                                   здддддддддддддддддO (3)
*        здддддддд©     здддддддд©  Ё   зддддддд©
*        Ё        Ё     Ё    1   Ё  Ё   Ё   1   Ё ANGLE
*(1) OдддЁ  KT/F  ЁдддддЁ дддддддЁддддддЁ  ддд  ЁдддддO (2)
*        Ё        Ё     Ё(J/FS+1)Ё      Ё   S   Ё
*        юдддддддды     юдддддддды      юддддддды
*                                   
*
*
* CONSTANTS ARE:
*
*   TORQUE CONSTANT = {KT} FT-LB/VOLT
*   INERTIA = {J} SLUG-FT^2
*   FRICTION = {F} FT-LB/RADIAN/SEC
* 
* INITIAL CONDITIONS:
*
*    ANGLE = {ANGLE} RADIANS
*    RATE  = {RATE} RADIANS/SEC
*
R0 1 0 1E12
RE2 2 0 1E12
E1 4 0 0 1 {KT/F}
R1 4 5 1MEG
R2 5 3 1MEG
C1 3 5 {1U*J/F} IC={RATE}
E2 3 0 0 5 1E6
E3 2 0 0 7 1E6
R3 3 7 -1MEG
C2 2 7 1U IC={ANGLE}
.ENDS
**********

