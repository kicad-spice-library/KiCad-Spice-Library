**********
* Copyright Intusoft 1988-1997
* All Rights Reserved
**********
*SRC=RECTBR;RECTBR;PWM Models;Unitrode;Bridge Rectifier
*SYM=BRIDGE
.SUBCKT RECTBR  1   2   3   4
*               AC  AC  +   -
D1 1 3 DMOD
D2 2 3 DMOD
D3 4 1 DMOD
D4 4 2 DMOD
.MODEL DMOD D(IS=1E-9 RS=0.1 CJO=3P)
.ENDS RECTBR
**********
*SRC=LATCHCMP;LATCHCMP;PWM Models;Unitrode;Latching Comparator
*SYM=LATCHCMP
*Latching Comparator Model from Lloyd Dixon
.SUBCKT LATCHCMP Inv Ni Out Com {FS=100K}
*Rinv Inv Com 1G 	
BLATCH Ni1 Ni V=(V(Out,Com)-5)*V(9)
*RNi Ni1 Com 1G
BOUT Out1 Com V=100K*V(Ni1,Inv) < 0  ?  0 : 100K*V(Ni1,Inv) > 5 ? 
+ 5 : 100K*V(Ni1,Inv) 
ROUT Out1 Out 1K
COUT Out Com {2E-6/FS}
VCLOCK 9 0 PULSE 1 0 0 .01u .01u {.02/FS} {1/FS}
*RCLOCK 9 0 1G
.ENDS
**********
*SRC=BUCKAVG;BUCKAVG;PWM Models;Average;Buck Reg.
*SYM=AVGREG
.SUBCKT BUCKAVG 1 2 3 4 5 {FS=100K L=100U ILO=0}
* 1=In+ 2=In- 3=Out+ 4=Out- 5=DUTY CYCLE
L1 6 3 {L} 
*IC={ILO}
RS 7 6 .001
BO1 7 8 V=V(3,4)*(1-V(11)-V(12))
*BO1 ->   Vo*D3
BO2 8 4 V=V(1,2)*V(11)
*BO2 ->   Vi*D1
BIN 1 2 I=V(7,6)*1000*V(11)/(V(11)+V(12))
*BIN -> Iin = IL * D1/(D1+D2)
*RD 5 0 1G
*RD1 11 0 1G
BD1 11 0 V=V(5) < .01 ? .01 : V(5) > 1 ? 1 : V(5)
*BD1 -> Duty1 - Switch On - Limit .01-1
*R19 19 0 1G
B19 19 0 V=(V(1,2)-V(3,4)) < .01 ? .01 : (V(1,2)-V(3,4)) > 1000 ? 1000 : (V(1,2)-V(3,4))
*B19 -> Limit Vi-Vo to prevent div. by zero
*RD12 11A 0 1G
BD12 11A 0 V=2*V(7,6)*{1000*FS*L}/V(19)/V(11) < 0.1 ? 0.1 : 
+ 2*V(7,6)*{1000*FS*L}/V(19)/V(11) > 1 ? 1 : 
+ 2*V(7,6)*{1000*FS*L}/V(19)/V(11)
*BD12 -> Duty(1+2)
*RD2 12 0 1G
BD2 12 0 V=V(11A,11) < 0.1 ? 0.1 : V(11A,11) > 1 ? 1 : V(11A,11)
*BD2 -> Duty2 - Diode conduction
.ENDS
**********
*SRC=BOOSTAVG;BOOSTAVG;PWM Models;Average;Boost Reg.
*SYM=AVGREG
.SUBCKT BOOSTAVG 1 2 3 4 5 {FS=100K L=100U ILO=0}
* 1=In+ 2=In- 3=Out+ 4=Out- 5=DUTY CYCLE
L1 1 6 {L} 
*IC={ILO}
RS 6 7 .001
BIN1 7 8 V=V(3,4)*V(12)
*BIN1 ->   Vo*D2
BIN2 8 2 V=V(1,2)*(1-V(11)-V(12))
*BIN2 ->   Vi*D3
BO 4 3 I=V(6,7)*1000*V(12)/(V(11)+V(12))
*BO -> Io = IL * D2/(D1+D2)
*RD 5 0 1G
*RD1 11 0 1G
BD1 11 0 V=V(5) < .01 ? .01 : V(5) > 1 ? 1 : V(5)
*BD1 -> Duty1 - Switch On - Limit .01-1
*RD12 11A 0 1G
BD12 11A 0 V=2*V(6,7)*{1000*FS*L}/V(1,2)/V(11) < 0.1 ? 0.1 : 
+ 2*V(6,7)*{1000*FS*L}/V(1,2)/V(11) > 1 ? 1 : 
+ 2*V(6,7)*{1000*FS*L}/V(1,2)/V(11)
*BD12 -> Duty(1+2)
*RD2 12 0 1G
BD2 12 0 V=V(11A,11) < 0.1 ? 0.1 : V(11A,11) > 1 ? 1 : V(11A,11)
*BD2 -> Duty2 - Diode conduction
.ENDS
**********
*SRC=FLYAVG;FLYAVG;PWM Models;Average;Flyback Reg.
*SYM=AVGREG
.SUBCKT FLYAVG 1 2 3 4 5 {FS=100K L=100U ILO=0}
* 1=In+ 2=In- 3=Out+ 4=Out- 5=DUTY CYCLE
DREC 6 9 DMOD
L1 8 0 {L} IC={ILO}
RS 9 8 .001
BO1 7 6 V=V(3,4)*V(12)
*BO1 ->   -Vo*D2
BO2 7 0 V=V(1,2)*(V(11))
*BO2 ->   Vi*D1
BIN 1 2 I=V(9,8)*1000*V(11)/(V(11)+V(12))
*BIN -> Iin = IL * D1/(D1+D2)
BO 4 3 I=V(9,8)*1000*V(12)/(V(11)+V(12))
*BO -> Iout = IL * D2/(D1+D2)
*RD 5 0 1G
*RD1 11 0 1G
BD1 11 0 V=V(5) < .01 ? .01 : V(5) > 1 ? 
+ 1 : V(5)
*BD1 -> Duty1 - Switch On - Limit .01-1
*RD12 11A 0 1G
BD12 11A 0 V=2*V(9,8)*{1000*FS*L}/V(1,2)/V(11) < 0.1 ? 0.1 : 
+ 2*V(9,8)*{1000*FS*L}/V(1,2)/V(11) > 1 ? 1 : 
+ 2*V(9,8)*{1000*FS*L}/V(1,2)/V(11)
*BD12 -> Duty(1+2)
*RD2 12 0 1G
BD2 12 0 V=V(11A,11) < 0.1 ? 0.1 : V(11A,11) > 1 ? 1 : V(11A,11)
*BD2 -> Duty2 - Diode conduction
.MODEL DMOD D(IS=1E-9 RS=0.1)
.ENDS
**********
*SRC=PWMBBSCD;PWMBBSCD;PWM Models;Average;Buck-Boost, Step-Up Cont_Disc
*SYM=PWMBBSCD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBBSCD 1 2 3 4 5 6 7 8
* BUCK-BOOST PWM STEP-UP TRANSFORMER MODEL FOR CONTINUOUS & DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2
* NODE 7 CONNECTS TO HIGH SIDE OF TRANSFORMER PRIMARY
* NODE 8 IS THE MODE : LOW=DISCONTINUOUS  ;  HIGH=CONTINUOUS
* N=1 IS THE TRANSFORMER TURNS RATIO
* RI=.01 INPUT RESISTANCE
* D2:N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=VG*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/D2
G2 4 3 POLY(2) 1 20 6 0 0 0 0 0 100.0
* G2=1/(RI*N)
E1 20 2 POLY(2) 3 4 6 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO CALCULATE D'=1-D
RD1 24 0 1MEG
VD1 24 23 DC 1
ED1 23 0 5 0 -1.0
* CIRCUIT TO FORCE I=VG*D*TS/2L
E2 25 0 7 2 216.7M
* E2=1/2*L*FS=TS/2*L *
EI 26 0 1 20 100.0
* EI=1/RI
R1 26 25 1MEG
G3 0 12 26 25 1.0
R2 12 0 100K
C2 12 0 10PF
* CIRCUIT TO LIMIT D2:  0<D2<1-D
EP 15 16 24 0 100
VP 16 0 DC -1.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO CALCULATE D2
ED2 6 0 12 0 .01
RD2 6 0 1MEG
* CIRCUIT TO SHORT OUT LP FOR DISCONTINUOUS MODE
GR 0 30 26 25 1NA 0 1
VR 30 31
RR 31 0 1M
VH 32 33
RH 33 1 .01
HR 7 32 POLY(2) VH VR 0 0 0 0 1MEG
* RON=HR*(D2%/R2)**2  ;  ROFF=HR*(IO/D'-I)**2
HM 8 0 VR 1
RM 8 0 1G
.MODEL DS D(TT=1NS CJO=1PF)
.ENDS PWMBBSCD
**********
*SRC=PWMBBSD;PWMBBSD;PWM Models;Average;Buck-Boost, Step-Up Xfmr Disc
*SYM=PWMBBSD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBBSD 1 2 3 4 5 6
* BUCK-BOOST PWM STEP-UP TRANSFORMER MODEL FOR DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2
* N=1 IS THE TRANSFORMER TURNS RATIO
* D2:N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=VG*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/D2
G2 4 3 POLY(2) 1 20 6 0 0 0 0 0 100.0
* G2=1/(RI*N)
E1 20 2 POLY(2) 3 4 6 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO CALCULATE D'=1-D
RD1 24 0 1MEG
VD1 24 23 DC 1
ED1 23 0 5 0 -1.0
* CIRCUIT TO FORCE I=VG*D*TS/2L
E2 25 0 1 2 216.7M
* E2=1/2*L*FS=TS/2*L *
EI 26 0 1 20 100.0
* EI=1/RI
R1 26 25 1MEG
G3 0 12 26 25 1.0
R2 12 0 100MEG
C2 12 0 10PF
* CIRCUIT TO LIMIT D2:  0<D2<1-D
EP 15 16 24 0 100
VP 16 0 DC -1.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO CALCULATE D2
ED2 6 0 12 0 .01
RD2 6 0 1MEG
.MODEL DS D
.ENDS PWMBBSD
**********
*SRC=PWMBBSD0;PWMBBSD0;PWM Models;Average;Buck-Boost, Step-Up Xfmr Disc
*SYM=PWMBBSD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBBSD0 1 2 3 4 5 6
* BUCK-BOOST PWM STEP-UP TRANSFORMER MODEL FOR DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2
* N=1 IS THE TRANSFORMER TURNS RATIO
* D2:N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=VG*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/D2
G2 4 3 POLY(2) 1 20 6 0 0 0 0 0 100.0
* G2=1/(RI*N)
E1 20 2 POLY(2) 3 4 6 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO CALCULATE D'=1-D
RD1 24 0 1MEG
VD1 24 23 DC 1
ED1 23 0 5 0 -1.0
* CIRCUIT TO FORCE I=VG*D*TS/2L
E2 25 0 1 2 216.7M
* E2=1/2*L*FS=TS/2*L *
EI 26 0 1 20 100.0
* EI=1/RI
R1 26 25 1MEG
G3 0 12 26 25 1.0
R2 12 0 100MEG
C2 12 0 10PF
* CIRCUIT TO LIMIT D2:  0<D2<1-D
EP 15 16 24 0 100
VP 16 0 DC -1.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO FORCE D2=100% WHEN D<DMIN=1%
EDS 0 41 5 0 100.0
VDM 42 41 DC 1.5
* VDM=DMIN IN PERCENT + 0.5
DSM 42 0 DS1; OFF
G4 0 40 12 0 1M
FS 0 40 VDM -1.0
R3 40 0 1K
D1 40 15 DS; OFF
.MODEL DS1 D(RS=.1 TT=1NS CJO=1PF)
* CIRCUIT TO CALCULATE D2
ED2 6 0 40 0 .01
RD2 6 0 1MEG
.MODEL DS D
.ENDS PWMBBSD0
**********
*SRC=PWMBBSDX;PWMBBSDX;PWM Models;Average;Buck-Boost, Step-Up Xfmr Disc
*SYM=PWMBBSD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBBSDX 1 2 3 4 5 6
* BUCK-BOOST PWM STEP-UP TRANSFORMER MODEL FOR DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2
* N=1 IS THE TRANSFORMER TURNS RATIO
* D2:N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=VG*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/D2
G2 4 25 POLY(2) 1 20 6 0 0 0 0 0 100.0
* G2=1/(RI*N)
VO 25 3
E1 20 2 POLY(2) 3 4 6 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO FORCE I=VG*D*TS/2L
G3 12 0 POLY(2) 1 2 12 0 0 0 0 0 2.167M
* G3=(TS/2L)/(100N)
F3 0 12 VO 1.0
R2 12 0 100MEG
C2 12 0 10PF
* CIRCUIT TO LIMIT D2:  0<D2<1-D
VP 16 0 DC 99
EP 15 16 5 0 -100.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO CALCULATE D2
ED2 6 0 12 0 .01
RD2 6 0 1MEG
.MODEL DS D
.ENDS PWMBBSDX
**********
*SRC=PWMBCK;PWMBCK;PWM Models;Average;Buck Step-Down Xfmr
*SYM=PWMBCK
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBCK 1 2 3 4 5
* BUCK STEP-DOWN TRANSFORMER MODEL
* NODES 1 & 2 ARE THE INPUT ; 3 & 4 ARE THE OUTPUT ; 5 THE DUTY CYCLE
* N=1 IS THE TRANSFORMER TURNS RATIO
* 1:N*D IS THE EFFECTIVE TURNS RATIO
RD 5 0 1MEG
RO 6 3 10M
* RO IS THE OUTPUT RESISTANCE
G1 1 2 POLY(2) 6 3 5 0 0 0 0 0 100.0
* G1 GAIN = N/RO
E2 6 4 POLY(2) 1 2 5 0 0 0 0 0 1.0
* E2 GAIN = N , THE TRANSFORMER TURNS RATIO
.ENDS PWMBCK
**********
*SRC=PWMBCKCD;PWMBCKCD;PWM Models;Average;Buck Step-Up Xfmr Cont_Disc
*SYM=PWMBCKCD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBCKCD 1 2 3 4 5 6 7 8
* BUCK PWM STEP-UP TRANSFORMER MODEL FOR CONTINUOUS & DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2
* NODE 7 CONNECTS TO HIGH SIDE OF TRANSFORMER PRIMARY
* NODE 8 IS THE MODE : LOW=DISCONTINUOUS ; HIGH=CONTINUOUS
* N=1 IS THE TRANSFORMER TURNS RATIO
* (D+D2):N IS THE EFFECTIVE TURNS RATIO
* L=90UH , TS=25US
* FORCE I=(VG-V/N)*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/(D+D2)
G2 4 3 POLY(2) 1 20 21 0 0 0 0 0 100.0
* G2=1/(RI*N)
E1 20 2 POLY(2) 3 4 21 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO CALCULATE D+D2
EDD 21 5 6 0 1.0
RDD 21 5 1MEG
E4 2 22 POLY(2) 3 4 5 0 0 0 0 0 -1.0
* E4=-1/N
R4 2 22 1MEG
* CIRCUIT TO CALCULATE D'=1-D
RD1 24 0 1MEG
VD1 24 23 DC 1
ED1 23 0 5 0 -1.0
* CIRCUIT TO FORCE I=(VG-V/N)*D*TS/2*L
E2 25 0 7 22 138.9M
* E2=1/2*L*FS=TS/2*L *
EI 26 0 1 20 100.0
* EI=1/RI
R1 26 25 1MEG
G3 0 12 26 25 1.0
R2 12 0 100K
C2 12 0 10PF
* CIRCUIT TO LIMIT D2: 0<D2<1-D
EP 15 16 24 0 100
VP 16 0 DC -1.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO CALCULATE D2
ED2 6 0 12 0 .01
RD2 6 0 1MEG
* CIRCUIT TO SHORT OUT LP FOR DISCONTINUOUS MODE
GR 0 30 26 25 1NA 0 1
VR 30 31
RR 31 0 1M
VH 32 33
RH 33 1 .01
HR 7 32 POLY(2) VH VR 0 0 0 0 1MEG
* RON=HR*(D2%/R2)**2  ;  ROFF=HR*(IO/D'-I)**2
HM 8 0 VR 1
RM 8 0 1G
.MODEL DS D(TT=1NS CJO=1PF)
.ENDS PWMBCKCD
**********
*SRC=PWMBCKD;PWMBCKD;PWM Models;Average;Buck Step-Up Xfmr Disc
*SYM=PWMBCKD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBCKD 1 2 3 4 5 6
* BUCK PWM STEP-UP TRANSFORMER MODEL FOR DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2
* N=1 IS THE TRANSFORMER TURNS RATIO
* (D+D2):N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=(VG-V/N)*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/(D+D2)
G2 4 3 POLY(2) 1 20 21 0 0 0 0 0 100.0
* G2=1/(RI*N)
E1 20 2 POLY(2) 3 4 21 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO CALCULATE D+D2
EDD 21 5 6 0 1.0
RDD 21 5 1MEG
E4 2 22 POLY(2) 3 4 5 0 0 0 0 0 -1.0
* E4=-1/N
R4 2 22 1MEG
* CIRCUIT TO CALCULATE D'=1-D
RD1 24 0 1MEG
VD1 24 23 DC 1
ED1 23 0 5 0 -1.0
* CIRCUIT TO FORCE I=(VG-V/N)*D*TS/2*L
E2 25 0 1 22 216.7M
* E2=1/2*L*FS=TS/2*L *
EI 26 0 1 20 100.0
* EI=1/RI
R1 26 25 1MEG
G3 0 12 26 25 1.0
R2 12 0 100MEG
C2 12 0 10PF
* CIRCUIT TO LIMIT D2: 0<D2<1-D
EP 15 16 24 0 100
VP 16 0 DC -1.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO CALCULATE D2
ED2 6 0 12 0 .01
RD2 6 0 1MEG
.MODEL DS D
.ENDS PWMBCKD
**********
*SRC=PWMBCKD0;PWMBCKD0;PWM Models;Average;Buck Step-Up Xfmr Disc
*SYM=PWMBCKD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBCKD0 1 2 3 4 5 6
* BUCK PWM STEP-UP TRANSFORMER MODEL FOR DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2
* N=1 IS THE TRANSFORMER TURNS RATIO
* (D+D2):N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=(VG-V/N)*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/(D+D2)
G2 4 3 POLY(2) 1 20 21 0 0 0 0 0 100.0
* G2=1/(RI*N)
E1 20 2 POLY(2) 3 4 21 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO CALCULATE D+D2
EDD 21 5 6 0 1.0
RDD 21 5 1MEG
E4 2 22 POLY(2) 3 4 5 0 0 0 0 0 -1.0
* E4=-1/N
R4 2 22 1MEG
* CIRCUIT TO CALCULATE D'=1-D
RD1 24 0 1MEG
VD1 24 23 DC 1
ED1 23 0 5 0 -1.0
* CIRCUIT TO FORCE I=(VG-V/N)*D*TS/2*L
E2 25 0 1 22 216.7M
* E2=1/2*L*FS=TS/2*L *
EI 26 0 1 20 100.0
* EI=1/RI
R1 26 25 1MEG
G3 0 12 26 25 1.0
R2 12 0 100MEG
C2 12 0 10PF
* CIRCUIT TO LIMIT D2: 0<D2<1-D
EP 15 16 24 0 100
VP 16 0 DC -1.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO FORCE D2=100% WHEN D<DMIN=1%
EDS 0 41 5 0 100.0
VDM 42 41 DC 1.5
* VDM=DMIN IN PERCENT + 0.5
DSM 42 0 DS1; OFF
G4 0 40 12 0 1M
FS 0 40 VDM -1.0
R3 40 0 1K
D1 40 15 DS; OFF
.MODEL DS1 D(RS=.1 TT=1NS CJO=1PF)
* CIRCUIT TO CALCULATE D2
ED2 6 0 40 0 .01
RD2 6 0 1MEG
.MODEL DS D
.ENDS PWMBCKD0
**********
*SRC=PWMBCKDX;PWMBCKDX;PWM Models;Average;Buck Step-Up Xfmr Disc
*SYM=PWMBCKD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBCKDX 1 2 3 4 5 6
* BUCK PWM STEP-UP TRANSFORMER MODEL FOR DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2
* N=1 IS THE TRANSFORMER TURNS RATIO
* (D+D2):N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=(VG-V/N)*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/(D+D2)
G2 4 25 POLY(2) 1 20 12 0 0 0 0 0 1.0
* G2=1/(RI*N*100)
VO 25 3
E1 20 2 POLY(2) 3 4 12 0 0 0 0 0 .01
* E1=1/(N*100)
E4 2 22 POLY(2) 3 4 5 0 0 0 0 0 -1.0
* E4=-1/N
R4 2 22 1MEG
* CIRCUIT TO FORCE I=(VG-V/N)*D*TS/2*L
G3 12 0 POLY(2) 1 22 12 0 0 0 0 0 2.167M
* G3=(TS/2L)/(100N)
F3 0 12 VO 1.0
R2 12 0 100MEG
C2 12 0 10PF
ED 21 12 5 0 -100.0
* CIRCUIT TO LIMIT D2: 0<D2<1-D
VP 16 0 DC 99
EP 15 16 5 0 -100.0
VM 14 0 DC 1.0
DP 21 15 DS
DM 14 21 DS
* CIRCUIT TO CALCULATE D2
ED2 6 0 21 0 .01
RD2 6 0 1MEG
.MODEL DS D
.ENDS PWMBCKDX
**********
*SRC=PWMBST;PWMBST;PWM Models;Average;Boost Step-Up Xfmr
*SYM=PWMBST
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBST 1 2 3 4 5
* BOOST STEP-UP TRANSFORMER MODEL
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* N=1 IS THE TRANSFORMER TURNS RATIO
* (1-D):N IS THE EFFECTIVE TURNS RATIO
RD 5 0 1MEG
RI 1 6 10M
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
G2 4 3 POLY(2) 1 6 8 0 0 0 0 0 100.
* G2=1/(RI*N)
E1 6 2 POLY(2) 3 4 8 0 0 0 0 0 1.0
* E1=1/N
RD1 8 0 1MEG
VD1 8 7 DC 1
ED1 7 0 5 0 -1.0
.ENDS PWMBST
*R; T=0.01/0.06 15:42:59
**********
*SRC=PWMBSTCD;PWMBSTCD;PWM Models;Average;Boost Step-Up Xfmr Cont_Disc
*SYM=PWMBSTCD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBSTCD 1 2 3 4 5 6 7 8 9 10
* BOOST PWM STEP-UP TRANSFORMER MODEL FOR CONTINUOUS & DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2 ; NODES 7&8 ARE VG
* NODE 9 CONNECTS TO HIGH SIDE OF TRANSFORMER PRIMARY
* NODE 10 IS THE MODE : LOW=DISCONTINUOUS ; HIGH=CONTINUOUS
* N=1 IS THE TRANSFORMER TURNS RATIO
* D2:N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=VG*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/D2
G2 4 3 POLY(2) 1 20 6 0 0 0 0 0 100.0
* G2=1/(RI*N)
E1 20 2 POLY(2) 3 4 6 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO CALCULATE D'=1-D
RD1 24 0 1MEG
VD1 24 23 DC 1
ED1 23 0 5 0 -1.0
* CIRCUIT TO FORCE I=VG*D*TS/2*L
E2 25 0 POLY(2) 7 8 5 0 0 0 0 0 216.7M
* E2=1/2*L*FS=TS/2*L *
EI 26 0 1 20 100.0
* EI=1/RI
R1 26 25 1MEG
G3 0 12 26 25 1.0
R2 12 0 100K
C2 12 0 10PF
* CIRCUIT TO LIMIT D2: 0<D2<1-D
EP 15 16 24 0 100
VP 16 0 DC -1.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO CALCULATE D2
ED2 6 0 12 0 .01
RD2 6 0 1MEG
* CIRCUIT TO SHORT OUT LP FOR DISCONTINUOUS MODE
GR 0 30 26 25 1NA 0 1
VR 30 31
RR 31 0 1M
VH 32 33
RH 33 1 .01
HR 9 32 POLY(2) VH VR 0 0 0 0 1MEG
* RON=HR*(D2%/R2)**2  ;  ROFF=HR*(IO/D'-I)**2
HM 10 0 VR 1
RM 10 0 1G
.MODEL DS D(TT=1NS CJO=1PF)
.ENDS PWMBSTCD
**********
*SRC=PWMBSTD;PWMBSTD;PWM Models;Average;Boost Step-Up Xfmr Disc
*SYM=PWMBSTD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBSTD 1 2 3 4 5 6 7 8
* BOOST PWM STEP-UP TRANSFORMER MODEL FOR DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2 ; NODES 7&8 ARE VG
* N=1 IS THE TRANSFORMER TURNS RATIO
* D2:N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=VG*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/D2
G2 4 3 POLY(2) 1 20 6 0 0 0 0 0 100.0
* G2=1/(RI*N)
E1 20 2 POLY(2) 3 4 6 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO CALCULATE D'=1-D
RD1 24 0 1MEG
VD1 24 23 DC 1
ED1 23 0 5 0 -1.0
* CIRCUIT TO FORCE I=VG*D*TS/2*L
E2 25 0 POLY(2) 7 8 5 0 0 0 0 0 216.7M
* E2=1/2*L*FS=TS/2*L *
EI 26 0 1 20 100.0
* EI=1/RI
R1 26 25 1MEG
G3 0 12 26 25 1.0
R2 12 0 100MEG
C2 12 0 10PF
* CIRCUIT TO LIMIT D2: 0<D2<1-D
EP 15 16 24 0 100
VP 16 0 DC -1.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO CALCULATE D2
ED2 6 0 12 0 .01
RD2 6 0 1MEG
.MODEL DS D
.ENDS PWMBSTD
**********
*SRC=PWMBSTD0;PWMBSTD0;PWM Models;Average;Boost Step-Up Xfmr Disc
*SYM=PWMBSTD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBSTD0 1 2 3 4 5 6 7 8
* BOOST PWM STEP-UP TRANSFORMER MODEL FOR DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2 ; NODES 7&8 ARE VG
* N=1 IS THE TRANSFORMER TURNS RATIO
* D2:N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=VG*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/D2
G2 4 3 POLY(2) 1 20 6 0 0 0 0 0 100.0
* G2=1/(RI*N)
E1 20 2 POLY(2) 3 4 6 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO CALCULATE D'=1-D
RD1 24 0 1MEG
VD1 24 23 DC 1
ED1 23 0 5 0 -1.0
* CIRCUIT TO FORCE I=VG*D*TS/2*L
E2 25 0 POLY(2) 7 8 5 0 0 0 0 0 216.7M
* E2=1/2*L*FS=TS/2*L *
EI 26 0 1 20 100.0
* EI=1/RI
R1 26 25 1MEG
G3 0 12 26 25 1.0
R2 12 0 100MEG
C2 12 0 10PF
* CIRCUIT TO LIMIT D2: 0<D2<1-D
EP 15 16 24 0 100
VP 16 0 DC -1.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO FORCE D2=100% WHEN D<DMIN=1%
EDS 0 41 5 0 100.0
VDM 42 41 DC 1.5
* VDM=DMIN IN PERCENT + 0.5
DSM 42 0 DS1; OFF
G4 0 40 12 0 1M
FS 0 40 VDM -1.0
R3 40 0 1K
D1 40 15 DS; OFF
.MODEL DS1 D(RS=.1 TT=1NS CJO=1PF)
* CIRCUIT TO CALCULATE D2
ED2 6 0 40 0 .01
RD2 6 0 1MEG
.MODEL DS D
.ENDS PWMBSTD0
**********
*SRC=PWMBSTDX;PWMBSTDX;PWM Models;Average;Boost Step-Up Xfmr Disc
*SYM=PWMBSTD
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMBSTDX 1 2 3 4 5 6 7 8
* BOOST PWM STEP-UP TRANSFORMER MODEL FOR DISCONTINUOUS MODE
* NODES 1&2 ARE THE INPUT ; 3&4 ARE THE OUTPUT ; 5 IS THE DUTY CYCLE
* NODE 6 IS D2 ; NODES 7&8 ARE VG
* N=1 IS THE TRANSFORMER TURNS RATIO
* D2:N IS THE EFFECTIVE TURNS RATIO
* L=120UH , TS=52US
* FORCE I=VG*D*TS/2*L
RD 5 0 1MEG
RI 1 20 .01
* RI IS THE INPUT RESISTANCE
RO 3 4 100MEG
* CIRCUIT FOR IDEAL TRANSFORMER WHOSE TURNS RATIO IS N/D2
G2 4 25 POLY(2) 1 20 6 0 0 0 0 0 100.0
* G2=1/(RI*N)
VO 25 3
E1 20 2 POLY(2) 3 4 6 0 0 0 0 0 1.0
* E1=1/N
* CIRCUIT TO FORCE I=VG*D*TS/2*L
EDV 21 0 POLY(2) 7 8 5 0 0 0 0 0 1.0
RDV 21 0 1G
G3 12 0 POLY(2) 21 0 12 0 0 0 0 0 2.167M
* G3=(TS/2L)/(100N)
F3 0 12 VO 1.0
R2 12 0 100MEG
C2 12 0 10PF
* CIRCUIT TO LIMIT D2: 0<D2<1-D
VP 16 0 DC 99
EP 15 16 5 0 -100.0
VM 14 0 DC 1.0
DP 12 15 DS
DM 14 12 DS
* CIRCUIT TO CALCULATE D2
ED2 6 0 12 0 .01
RD2 6 0 1MEG
.MODEL DS D
.ENDS PWMBSTDX
**********
*SRC=PWMCCM;PWMCCM;PWM Models;Average;CC Mod w/ext Ramp Cont
*SYM=PWMCCM
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMCCM 1 2 3 4 5
* CURRENT CONTROL MODULATOR WITH EXTERNAL RAMP &
* STORAGE TIME EFFECTS ; CONTINUOUS CONDUCTION
* NODE 1 IS THE CURRENT CONTROL INPUT
* NODE 2 IS THE INDUCTOR CURRENT SENSE
* NODE 3 IS THE INPUT VOLTAGE SENSE
* NODE 4 IS THE OUTPUT VOLTAGE SENSE
* NODE 5 IS THE DUTY CYCLE (0<D<1)
* VG=12V , VO=25V , L=390UH , F=1/T=70KHZ , A1=A=1.5
* THE STORAGE TIME TS=TS0+D*TS1 , TS0=.5US , TS1=.5US
* D=((IC-IL)+TS*(M1+M))/(T*(M1/2+M))
* M1 IS THE SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M IS THE SLOPE OF THE EXTERNAL STABILIZING RAMP AT THE COMPARATOR
* FOR BUCK       : M1=A1*(VG-VO)/L & M=A*VO/L
* FOR BOOST      : M1=A1*VG/L      & M=A*(VO-VG)/L
* FOR BUCK-BOOST : M1=A1*VG/L      & M=A*VO/L
* A1 & A ARE THE RAMP GAINS AT THE COMPARATOR
* FORCE D USING HIGH GAIN & FEEDBACK
RI 1 2 1G
R1 10 6 1G
R2 4 3 1G
EI 11 0 1 2 1.0
ES 10 11 POLY(2) 15 0 12 0 0 0 0 0 1.0
EF 6 0 POLY(2) 5 0 13 0 0 0 0 0 1.0
GD 0 7 10 6 1M
RD 7 0 100G
CD 7 0 1PF
* GBW=GD*T(M+M1/2)/(100*TWOPI*CD)
D1 8 7 DS
D2 7 9 DS
VM 8 0 DC 1
* VM=100*TS/T+1 , MIN VM=1 FOR 0% DUTY CYCLE
VP 9 0 DC 99
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100% DUTY CYCLE
ED 5 0 7 0 .01
RO 5 0 1G
* STORAGE TIME GENERATOR
VTS 16 0 DC 0.0
* VTS=TS0/T
ETS 15 16 5 0 0.0
* ETS=TS1/T
R4 15 0 1G
* RAMP GENERATORS FOR M1 & M FOR VARIABLE EXTERNAL RAMP
* LET AM1=A1*T/2L & AM=A*T/L
* FOR BUCK       USE: EM1 14 0 3 4 AM1 & EM 13 17 4 0 AM
* FOR BOOST      USE: EM1 14 0 3 0 AM1 & EM 13 17 4 3 AM
* FOR BUCK-BOOST USE: EM1 14 0 3 0 AM1 & EM 13 17 4 0 AM
* FOR CONSTANT EXTERNAL RAMP FOR M USE : EM=0 & VA 17 14 DC M*T
* BOOST EXAMPLE
EM1 14 0 3 0 27.47M
* EM1=A1*T/2L
* VARIABLE EXTERNAL RAMP
* VA 17 14 DC 0.0
* EM 13 17 4 3 54.95M
* EM=A*T/L
* CONSTANT EXTERNAL RAMP
EM 13 17 4 3 0.0
VA 17 14 DC .7143
* VA=A*(VO-VG)*T/L
* SEMI-VARIABLE EXTERNAL RAMP WITH VG SENSE (BOOST)
* EM 13 17 3 0 -54.95M
* EM=-A*T/L
* VA 17 14 DC 1.374
* VA=A*VO*T/L
EM2 12 13 14 0 1.0
R3 12 0 1G
.MODEL DS D
.ENDS PWMCCM
**********
*SRC=PWMCCMD;PWMCCMD;PWM Models;Average;CC Mod w/ext Ramp Disc
*SYM=PWMCCM
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMCCMD 1 2 3 4 5
* CURRENT CONTROL MODULATOR WITH EXTERNAL RAMP &
* STORAGE TIME EFFECTS ; DISCONTINUOUS CONDUCTION
* NODE 1 IS THE CURRENT CONTROL INPUT
* NODE 2 IS THE INDUCTOR CURRENT SENSE
* NODE 3 IS THE INPUT VOLTAGE SENSE
* NODE 4 IS THE OUTPUT VOLTAGE SENSE
* NODE 5 IS THE DUTY CYCLE (0<D<1)
* VG=12V , VO=25V , L=390UH , F=1/T=70KHZ , A1=A=1.5
* THE STORAGE TIME TS=TS0+D*TS1 , TS0=.5US , TS1=.5US
* D=(IC+TS*(M1+M))/(T*(M1+M))
* M1 IS THE SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M IS THE SLOPE OF THE EXTERNAL STABILIZING RAMP AT THE COMPARATOR
* FOR BUCK       : M1=A1*(VG-VO)/L & M=A*VO/L
* FOR BOOST      : M1=A1*VG/L      & M=A*(VO-VG)/L
* FOR BUCK-BOOST : M1=A1*VG/L      & M=A*VO/L
* A1 & A ARE THE RAMP GAINS AT THE COMPARATOR
* FORCE D USING HIGH GAIN & FEEDBACK
RI 1 2 1G
R1 10 6 1G
R2 4 3 1G
EI 11 0 1 0 1.0
ES 10 11 POLY(2) 15 0 12 0 0 0 0 0 1.0
EF 6 0 POLY(2) 5 0 12 0 0 0 0 0 1.0
GD 0 7 10 6 1
RD 7 0 100MEG
D1 8 7 DS
D2 7 9 DS
VM 8 0 DC 1
* VM=100*TS/T+1 , MIN VM=1 FOR 0% DUTY CYCLE
VP 9 0 DC 99
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100% DUTY CYCLE
ED 5 0 7 0 .01
RO 5 0 1G
* STORAGE TIME GENERATOR
VTS 16 0 DC 0.0
* VTS=TS0/T
ETS 15 16 5 0 0.0
* ETS=TS1/T
R4 15 0 1G
* RAMP GENERATORS FOR M1 & M FOR VARIABLE EXTERNAL RAMP
* LET AM1=A1*T/2L & AM=A*T/L
* FOR BUCK       USE: EM1 14 0 3 4 AM1 & EM 13 17 4 0 AM
* FOR BOOST      USE: EM1 14 0 3 0 AM1 & EM 13 17 4 3 AM
* FOR BUCK-BOOST USE: EM1 14 0 3 0 AM1 & EM 13 17 4 0 AM
* FOR CONSTANT EXTERNAL RAMP FOR M USE : EM=0 & VA 17 14 DC M*T
* BOOST EXAMPLE
EM1 14 0 3 0 27.47M
* EM1=A1*T/2L
* VARIABLE EXTERNAL RAMP
* VA 17 14 DC 0.0
* EM 13 17 4 3 54.95M
* EM=A*T/L
* CONSTANT EXTERNAL RAMP
EM 13 17 4 3 0.0
VA 17 14 DC .7143
* VA=A*(VO-VG)*T/L
* SEMI-VARIABLE EXTERNAL RAMP WITH VG SENSE (BOOST)
* EM 13 17 3 0 -54.95M
* EM=-A*T/L
* VA 17 14 DC 1.374
* VA=A*VO*T/L
EM2 12 13 14 0 1.0
R3 12 0 1G
.MODEL DS D
.ENDS PWMCCMD
**********
*SRC=PWMCCMX;PWMCCMX;PWM Models;Average;CC Mod w/ext Ramp Cont
*SYM=PWMCCM
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMCCMX 1 2 3 4 5
*  CURRENT CONTROL MODULATOR WITH EXTERNAL RAMP &
* STORAGE TIME EFFECTS ; CONTINUOUS CONDUCTION
* USING HOLLAND'S WRONG CONTROL EQUATION FROM POWERCON 11
* HOLLAND'S BUCK EXAMPLE FROM POWERCON 10
* NODE 1 IS THE CURRENT CONTROL INPUT
* NODE 2 IS THE INDUCTOR CURRENT SENSE
* NODE 3 IS THE INPUT VOLTAGE SENSE
* NODE 4 IS THE OUTPUT VOLTAGE SENSE
* NODE 5 IS THE DUTY CYCLE (0<D<1)
* RS=.5 , N=.8 , VG=N*VIN=28 , L=200UH , F=88KHZ , A1=3RS*N=1.2 , A=0
* THE STORAGE TIME TS=TS0+D*TS1 , TS0=.5US , TS1=.5US
* D=((IC-IL)+TS*(M1+M)-M2*T/2)/(T*(M-M2/2))
* M1 IS THE ON SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M2 IS THE; OFF SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M IS THE SLOPE OF THE EXTERNAL STABILIZING RAMP AT THE COMPARATOR
* FOR BUCK       : M1=A1*(VG-VO)/L & M2=A1*VO/L
* FOR BOOST      : M1=A1*VG/L      & M2=A1*(VO-VG)/L
* FOR BUCK-BOOST : M1=A1*VG/L      & M2=A1*VO/L
* A1 IS THE RAMP GAIN AT THE COMPARATOR
* FOR OPTIMUM STABILIZING RAMP MAKE M=M2
* FORCE D USING HIGH GAIN & FEEDBACK
RI 1 2 1G
R1 10 6 1G
R2 4 3 1G
EI 11 0 1 2 1.0
ES 18 11 POLY(2) 15 0 12 0 0 0 0 0 1.0
ET 10 18 13 0 -1.0
EF 6 0 POLY(2) 5 0 14 13 0 0 0 0 1.0
GD 0 7 10 6 1
RD 7 0 100MEG
D1 8 7 DS
D2 7 9 DS
VM 8 0 DC 1
* VM=100*TS/T+1 , MIN VM=1 FOR 0% DUTY CYCLE
VP 9 0 DC 99
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100% DUTY CYCLE
ED 5 0 7 0 .01
RO 5 0 1G
* STORAGE TIME GENERATOR
VTS 16 0 DC 0.0
* VTS=TS0/T
ETS 15 16 5 0 0.0
* ETS=TS1/T
R4 15 0 1G
* RAMP GENERATORS FOR M1 , M2 & M FOR VARIABLE EXTERNAL RAMP
* LET AM1=A1*T/L , AM2=A1*T/2L , AM=A*T/L & VA=0
* FOR BUCK       USE: EM1 12 14 3 4 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 0 AM
* FOR BOOST      USE: EM1 12 14 3 0 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 3 AM
* FOR BUCK-BOOST USE: EM1 12 14 3 0 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 0 AM
* FOR CONSTANT EXTERNAL RAMP FOR M USE : EM=0 & VA 17 0 DC M*T
* BUCK EXAMPLE
EM1 12 14 3 4 68.18M
* EM1=A1*T/L
* VARIABLE EXTERNAL RAMP
* VA 17 0 DC 0.0
* EM 14 17 4 0 68.18M
* EM=A*T/L
* CONSTANT EXTERNAL RAMP
EM 14 17 4 0 0.0
VA 17 0 DC 0.0
* VA=A*VO*T/L
* SEMI-VARIABLE EXTERNAL RAMP WITH VG SENSE (BOOST)
* EM 14 17 3 0 0.0
* EM=-A*T/L
* VA 17 0 DC .73
* VA=A*VO*T/L
EM2 13 0 4 0 34.09M
* EM2=A1*T/2L
R3 12 13 1G
.MODEL DS D
.ENDS PWMCCMX
**********
*SRC=PWMCCMY;PWMCCMY;PWM Models;Average;CC Mod w/ext Ramp Cont
*SYM=PWMCCM
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMCCMY 1 2 3 4 5
*  CURRENT CONTROL MODULATOR WITH EXTERNAL RAMP &
* STORAGE TIME EFFECTS ; CONTINUOUS CONDUCTION
* USING HOLLAND'S WRONG CONTROL EQUATION FROM POWERCON 11
* MIDDLEBROOK'S BOOST EXAMPLE FROM PESC 1979
* NODE 1 IS THE CURRENT CONTROL INPUT
* NODE 2 IS THE INDUCTOR CURRENT SENSE
* NODE 3 IS THE INPUT VOLTAGE SENSE
* NODE 4 IS THE OUTPUT VOLTAGE SENSE
* NODE 5 IS THE DUTY CYCLE (0<D<1)
* VG=12V , VO=25V , L=390UH , F=1/T=70KHZ , A1=A=1.5
* THE STORAGE TIME TS=TS0+D*TS1 , TS0=.5US , TS1=.5US
* D=((IC-IL)+TS*(M1+M)-M2*T/2)/(T*(M-M2/2))
* M1 IS THE ON SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M2 IS THE; OFF SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M IS THE SLOPE OF THE EXTERNAL STABILIZING RAMP AT THE COMPARATOR
* FOR BUCK       : M1=A1*(VG-VO)/L & M2=A1*VO/L
* FOR BOOST      : M1=A1*VG/L      & M2=A1*(VO-VG)/L
* FOR BUCK-BOOST : M1=A1*VG/L      & M2=A1*VO/L
* A1 IS THE RAMP GAIN AT THE COMPARATOR
* FOR OPTIMUM STABILIZING RAMP MAKE M=M2
* FORCE D USING HIGH GAIN & FEEDBACK
RI 1 2 1G
R1 10 6 1G
R2 4 3 1G
EI 11 0 1 2 1.0
ES 18 11 POLY(2) 15 0 12 0 0 0 0 0 1.0
ET 10 18 13 0 -1.0
EF 6 0 POLY(2) 5 0 14 13 0 0 0 0 1.0
GD 0 7 10 6 1
RD 7 0 100MEG
D1 8 7 DS
D2 7 9 DS
VM 8 0 DC 1
* VM=100*TS/T+1 , MIN VM=1 FOR 0% DUTY CYCLE
VP 9 0 DC 99
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100% DUTY CYCLE
ED 5 0 7 0 .01
RO 5 0 1G
* STORAGE TIME GENERATOR
VTS 16 0 DC 0.0
* VTS=TS0/T
ETS 15 16 5 0 0.0
* ETS=TS1/T
R4 15 0 1G
* RAMP GENERATORS FOR M1 , M2 & M FOR VARIABLE EXTERNAL RAMP
* LET AM1=A1*T/L , AM2=A1*T/2L , AM=A*T/L & VA=0
* FOR BUCK       USE: EM1 12 14 3 4 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 0 AM
* FOR BOOST      USE: EM1 12 14 3 0 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 3 AM
* FOR BUCK-BOOST USE: EM1 12 14 3 0 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 0 AM
* FOR CONSTANT EXTERNAL RAMP FOR M USE : EM=0 & VA 17 0 DC M*T
* BOOST EXAMPLE
EM1 12 14 3 0 54.94M
* EM1=A1*T/L
* VARIABLE EXTERNAL RAMP
* VA 17 0 DC 0.0
* EM 14 17 4 3 54.94M
* EM=A*T/L
* CONSTANT EXTERNAL RAMP
EM 14 17 4 3 0.0
VA 17 0 DC .7143
* VA=A*(VO-VG)*T/L
* SEMI-VARIABLE EXTERNAL RAMP WITH VG SENSE (BOOST)
* EM 14 17 3 0 -54.94M
* EM=-A*T/L
* VA 17 0 DC 1.374
* VA=A*VO*T/L
EM2 13 0 4 0 27.47M
* EM2=A1*T/2L
R3 12 13 1G
.MODEL DS D
.ENDS PWMCCMY
**********
*SRC=PWMCCMZ;PWMCCMZ;PWM Models;Average;CC Mod w/ext Ramp Cont
*SYM=PWMCCM
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT PWMCCMZ 1 2 3 4 5
*  CURRENT CONTROL MODULATOR WITH EXTERNAL RAMP &
* STORAGE TIME EFFECTS ; CONTINUOUS CONDUCTION
* USING IMPROVED ITERATIVE CONTROL EQUATION
* HOLLAND'S BUCK EXAMPLE FROM POWERCON 10
* NODE 1 IS THE CURRENT CONTROL INPUT
* NODE 2 IS THE INDUCTOR CURRENT SENSE
* NODE 3 IS THE INPUT VOLTAGE SENSE
* NODE 4 IS THE OUTPUT VOLTAGE SENSE
* NODE 5 IS THE DUTY CYCLE (0<D<1)
* RS=.5 , N=.8 , VG=N*VIN=28 , L=200UH , F=88KHZ , A1=3RS*N=1.2 , A=0
* THE STORAGE TIME TS=TS0+D*TS1 , TS0=.5US , TS1=.5US
* D=(IC-IL+(1-D)M2*T/2)/((M1+M)*T)+TS/T
* M1 IS THE ON SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M2 IS THE; OFF SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M IS THE SLOPE OF THE EXTERNAL STABILIZING RAMP AT THE COMPARATOR
* FOR BUCK       : M1=A1*(VG-VO)/L & M2=A1*VO/L
* FOR BOOST      : M1=A1*VG/L      & M2=A1*(VO-VG)/L
* FOR BUCK-BOOST : M1=A1*VG/L      & M2=A1*VO/L
* A1 IS THE RAMP GAIN AT THE COMPARATOR
* FOR OPTIMUM STABILIZING RAMP MAKE M=M2
* FORCE D USING HIGH GAIN & FEEDBACK
RI 1 2 1G
R1 10 6 1G
R2 4 3 1G
EI 11 0 1 2 1.0
ED1 10 11 POLY(2) 21 0 13 0 0 0 0 0 1.0
EF 6 0 POLY(2) 5 15 12 0 0 0 0 0 1.0
GD 0 7 10 6 1
RD 7 0 100MEG
CD 7 0 1PF
* GBW=.01(M1+M)T/(TWOPI*CD)
D1 8 7 DS
D2 7 9 DS
VM 8 0 DC 1
* VM=100*TS/T+1 , MIN VM=1 FOR 0% DUTY CYCLE
VP 9 0 DC 99
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100% DUTY CYCLE
ED 5 0 7 0 .01
RO 5 15 1G
* DELAYED DUTY CYCLE GENERATOR
TD1 18 0 19 0 Z0=100 TD=11.36US
* TD=T=1/F
R4 5 18 100
R5 19 0 100
ED0 20 0 19 0 -1.0
V1 21 20 .5
R6 21 0 1G
* STORAGE TIME GENERATOR
VTS 16 0 DC 0.0
* VTS=TS0/T
ETS 15 16 5 0 0.0
* ETS=TS1/T
* RAMP GENERATORS FOR M1 , M2 & M FOR VARIABLE EXTERNAL RAMP
* LET AM1=A1*T/L , AM2=A1*T/L , AM=A*T/L & VA=0
* FOR BUCK       USE: EM1 12 14 3 4 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 0 AM
* FOR BOOST      USE: EM1 12 14 3 0 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 3 AM
* FOR BUCK-BOOST USE: EM1 12 14 3 0 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 0 AM
* FOR CONSTANT EXTERNAL RAMP FOR M USE : EM=0 & VA 17 0 DC M*T
* BUCK EXAMPLE
EM1 12 14 3 4 68.18M
* EM1=A1*T/L
* VARIABLE EXTERNAL RAMP
* VA 17 0 DC 0.0
* EM 14 17 4 0 68.18M
* EM=A*T/L
* CONSTANT EXTERNAL RAMP
EM 14 17 4 0 0.0
VA 17 0 DC 0.0
* VA=A*VO*T/L
* SEMI-VARIABLE EXTERNAL RAMP WITH VG SENSE (BOOST)
* EM 14 17 3 0 0.0
* EM=-A*T/L
* VA 17 0 DC .73
* VA=A*VO*T/L
EM2 13 0 4 0 68.18M
* EM2=A1*T/L
R3 12 13 1G
.MODEL DS D
.ENDS PWMCCMZ
**********
*SRC=PWMCCMZ1;PWMCCMZ1;PWM Models;Average;CC Mod w/ext Ramp Cont
*SYM=PWMCCM
.SUBCKT PWMCCMZ1 1 2 3 4 5
* CURRENT CONTROL MODULATOR WITH EXTERNAL RAMP &
* STORAGE TIME EFFECTS ; CONTINUOUS CONDUCTION
* USING IMPROVED ITERATIVE CONTROL EQUATION
* MIDDLEBROOK'S BOOST EXAMPLE FROM PESC 1979
* NODE 1 IS THE CURRENT CONTROL INPUT
* NODE 2 IS THE INDUCTOR CURRENT SENSE
* NODE 3 IS THE INPUT VOLTAGE SENSE
* NODE 4 IS THE OUTPUT VOLTAGE SENSE
* NODE 5 IS THE DUTY CYCLE (0<D<1)
* VG=12V , VO=25V , L=390UH , F=1/T=70KHZ , A1=A=1.5
* THE STORAGE TIME TS=TS0+D*TS1 , TS0=.5US , TS1=.5US
* D=(IC-IL+(1-D)M2*T/2)/((M1+M)*T)+TS/T
* M1 IS THE ON SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M2 IS THE; OFF SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M IS THE SLOPE OF THE EXTERNAL STABILIZING RAMP AT THE COMPARATOR
* FOR BUCK       : M1=A1*(VG-VO)/L & M2=A1*VO/L
* FOR BOOST      : M1=A1*VG/L      & M2=A1*(VO-VG)/L
* FOR BUCK-BOOST : M1=A1*VG/L      & M2=A1*VO/L
* A1 IS THE RAMP GAIN AT THE COMPARATOR
* FOR OPTIMUM STABILIZING RAMP MAKE M=M2
* FORCE D USING HIGH GAIN & FEEDBACK
RI 1 2 1G
R1 10 6 1G
R2 4 3 1G
EI 11 0 1 2 1.0
ED1 10 11 POLY(2) 21 0 13 0 0 0 0 0 1.0
EF 6 0 POLY(2) 5 15 12 0 0 0 0 0 1.0
GD 0 7 10 6 1
RD 7 0 100MEG
CD 7 0 1PF
* GBW=.01(M1+M)T/(TWOPI*CD)
D1 8 7 DS
D2 7 9 DS
VM 8 0 DC 1
* VM=100*TS/T+1 , MIN VM=1 FOR 0% DUTY CYCLE
VP 9 0 DC 99
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100% DUTY CYCLE
ED 5 0 7 0 .01
RO 5 15 1G
* DELAYED DUTY CYCLE GENERATOR
TD1 18 0 19 0 Z0=100 TD=14.29US
* TD=T=1/F
R4 5 18 100
R5 19 0 100
ED0 20 0 19 0 -1.0
V1 21 20 .5
R6 21 0 1G
* STORAGE TIME GENERATOR
VTS 16 0 DC 0.0
* VTS=TS0/T
ETS 15 16 5 0 0.0
* ETS=TS1/T
* RAMP GENERATORS FOR M1 , M2 & M FOR VARIABLE EXTERNAL RAMP
* LET AM1=A1*T/L , AM2=A1*T/L , AM=A*T/L & VA=0
* FOR BUCK       USE: EM1 12 14 3 4 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 0 AM
* FOR BOOST      USE: EM1 12 14 3 0 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 3 AM
* FOR BUCK-BOOST USE: EM1 12 14 3 0 AM1 , EM2 13 0 4 0 AM2 , EM 14 17 4 0 AM
* FOR CONSTANT EXTERNAL RAMP FOR M USE : EM=0 & VA 17 0 DC M*T
* BOOST EXAMPLE
EM1 12 14 3 0 54.94M
* EM1=A1*T/L
* VARIABLE EXTERNAL RAMP
* VA 17 0 DC 0.0
* EM 14 17 4 3 54.94M
* EM=A*T/L
* CONSTANT EXTERNAL RAMP
EM 14 17 4 3 0.0
VA 17 0 DC .7143
* VA=A*(VO-VG)*T/L
* SEMI-VARIABLE EXTERNAL RAMP WITH VG SENSE (BOOST)
* EM 14 17 3 0 -54.94M
* EM=-A*T/L
* VA 17 0 DC 1.374
* VA=A*VO*T/L
EM2 13 0 4 0 54.94M
* EM2=A1*T/L
R3 12 13 1G
.MODEL DS D
.ENDS PWMCCMZ1
**********
*SRC=PWMXFM;PWMXFM;PWM Models;Average;w/Transformer
*SYM=PWMXFM
.SUBCKT PWMXFM 1 2 3 4 5 {N=??? RO=???}
* NODES 1 & 2 ARE THE INPUT ; 3 & 4 ARE THE OUTPUT ; 5 THE DUTY CYCLE
*Parameters:
* N IS THE TRANSFORMER TURNS RATIO
* RO IS THE OUTPUT RESISTANCE
RD 5 0 1MEG
RO 6 3 62.5M
* RO IS THE OUTPUT RESISTANCE
G1 1 2 POLY(2) 6 3 5 0 0 0 0 0 {N/RO}
* G1 GAIN = N/RO
E2 6 4 POLY(2) 1 2 5 0 0 0 0 0 {RO}
* E2 GAIN = N , THE TRANSFORMER TURNS RATIO
.ENDS PWMXFM
*R; T=0.01/0.04 15:12:39
**********
*SRC=SG1524;SG1524;PWM Models;Average;Volt. Reg.
*SYM=SG1524
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT SG1524 1 2 9 11 16 {EO=??? EP=??? T=??? TO=??? TS=???}
* SG1524 VOLTAGE REGULATOR MODEL
* NODES 1&2 ARE ERROR AMP INPUTS
* NODE 1 IS INVERTING INPUT , 2 IS NONINVERTING INPUT
* NODE 9 IS ERROR AMP OUTPUT
* NODE 11 IS DUTY CYCLE OUTPUT
* NODE 16 IS 5V REFERENCE OUTPUT
*Parameters:
* T=25US   SWITCHING PERIOD
* TO=1US   DEAD TIME
* TS=4US   TRANSISTOR STORAGE TIME
* EP=3.5V  PEAK SAW VOLTAGE
* EO=.5V   MINIMUM SAW VOLTAGE
* OP AMP STAGE
RI 2 1 100K
CI 2 1 1PF
G1 0 9 2 1 1.5M
R1 9 0 5MEG
C1 9 0 80PF
VA 23 0 DC 4
VB 24 0 DC {EO-0.1}
* VB=EO-.1
D3 9 23 DS
D4 24 9 DS
* 5V REFERENCE
VR 16 0 DC 5
* PULSE WIDTH MODULATOR STAGE
RC 9 25 1G
V0 25 0 DC {EO}
* V0=EO , THE MINIMUM SAW VOLTAGE
GD 0 20 9 25 {0.1*(T-TO)/((EP-EO)*T)}
* GD=.1*(T-TO)/((EP-EO)*T)
IX 0 20 DC {0.1*TS/T}
* IX=.1*TS/T
R0 20 0 1K
VP 21 0 DC {100*(T+TS-TO)/T-1}
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100 PERCENT MAX DUTY CYCLE
VM 22 0 DC {100*TS/T+1}
* VM=100*TS/T+1  ,  MIN VM=1  ,  FOR 0 PERCENT MIN DUTY CYCLE
D1 20 21 DS
D2 22 20 DS
ED 11 0 20 0 .01
.MODEL DS D(TT=1NS CJO=1PF)
.ENDS
**********
*SRC=SG1524A;SG1524A;PWM Models;Average;Volt. Reg. Error Amp +-100UA
*SYM=SG1524
.SUBCKT SG1524A 1 2 9 11 16  {EO=??? EP=??? T=??? TO=??? TS=???}
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
* SG1524 VOLTAGE REGULATOR MODEL , ERROR AMP = +-100UA OUT
* NODES 1&2 ARE ERROR AMP INPUTS
* NODE 1 IS INVERTING INPUT , 2 IS NONINVERTING INPUT
* NODE 9 IS ERROR AMP OUTPUT
* NODE 11 IS DUTY CYCLE OUTPUT
* NODE 16 IS 5V REFERENCE OUTPUT
*Parameters:
* T=25US   SWITCHING PERIOD
* TO=1US   DEAD TIME
* TS=4US   TRANSISTOR STORAGE TIME
* EP=3.5V  PEAK SAW VOLTAGE
* EO=.5V   MINIMUM SAW VOLTAGE
* OP AMP STAGE
RI 2 1 100K
CI 2 1 1PF
GA 0 26 2 1 1.5M
RA 26 0 1MEG
G1 0 9 26 0 1U
R1 9 0 5MEG
C1 9 0 80PF
VA 23 0 DC 4
VB 24 0 DC {EO-0.1}
* VB=EO-.1
VC 27 0 DC 100
VD 28 0 DC -100
D3 9 23 DS
D4 24 9 DS
D5 26 27 DS
D6 28 26 DS
* 5V REFERENCE
VR 16 0 DC 5
* PULSE WIDTH MODULATOR STAGE
RC 9 25 1G
V0 25 0 DC {EO}
* V0=EO , THE MINIMUM SAW VOLTAGE
GD 0 20 9 25 {0.1*(T-TO)/((EP-EO)*T)}
* GD=.1*(T-TO)/((EP-EO)*T)
IX 0 20 DC {0.1*TS/T}
* IX=.1*TS/T
R0 20 0 1K
VP 21 0 DC {100*(T+TS-TO)/T-1}
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100 PERCENT MAX DUTY CYCLE
VM 22 0 DC {100*TS/T+1}
* VM=100*TS/T+1  ,  MIN VM=1  ,  FOR 0 PERCENT MIN DUTY CYCLE
D1 20 21 DS
D2 22 20 DS
ED 11 0 20 0 .01
.MODEL DS D(TT=1NS CJO=1PF)
.ENDS
**********
*SRC=SG1524FF;SG1524FF;PWM Models;Average;w/FeedFwd Error Amp +-100UA
*SYM=SG1524FF
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT SG1524FF 1 2 9 11 16 17  {EO=??? EP=??? T=??? TO=??? TS=???}
* SG1524 VOLTAGE REGULATOR MODEL
* NODES 1&2 ARE ERROR AMP INPUTS
* NODE 9 IS ERROR AMP OUTPUT
* NODE 11 IS DUTY CYCLE OUTPUT
* NODE 16 IS 5V REFERENCE OUTPUT
* NODE 17 IS THE FEEDFORWARD INPUT
*Parameters:
* T=31US      SWITCHING PERIOD
* TO=16.2US   DEAD TIME
* TS=.5US     TRANSISTOR STORAGE TIME
* EP=3.5V     PEAK SAW VOLTAGE
* EO=.7V      MINIMUM SAW VOLTAGE
RI 2 1 100K
R1 9 0 5MEG
R0 20 0 1K
RC 9 25 1G
V0 25 0 DC {EO}
* V0=EO , THE MINIMUM SAW VOLTAGE
VA 23 0 DC 4
VB 24 0 DC {EO-0.1}
* VB=EO-.1
CI 2 1 1P
C1 9 0 159P
G1 0 9 2 1 1.6M
ED 11 0 20 0 .01
VR 16 0 DC 5
RP 17 25 1G
ECO 30 0 9 25 {(T-TO)/T}
* ECO=(T-TO)/T
R2 30 31 1G
EPO 31 0 POLY(2) 17 25 32 0 0 0 0 0 1.0
GA 0 32 30 31 1.0
RA 32 0 1MEG
CA 30 0 10PF
GD 0 20 32 0 0.1
IX 0 20 DC {0.1*TS/T}
* IX=.1*TS/T
VP 21 0 DC {100*(T+TS-TO)/T}
* VP=100*(T+TS-TO)/T , MAX VP=100 PERCENT DUTY CYCLE
VM 22 0 DC {100*TS/T}
* VM=100*TS/T
D1 20 21 DSWIT
D2 22 20 DSWIT
D3 9 23 DSWIT
D4 24 9 DSWIT
.MODEL DSWIT D(RS=.1)
.ENDS
**********
*SRC=SG1525;SG1525;PWM Models;Average;Volt. Reg.
*SYM=SG1524
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT SG1525 1 2 9 11 16  {EO=??? EP=??? T=??? TO=??? TS=???}
* SG1525 VOLTAGE REGULATOR MODEL
* NODES 1&2 ARE ERROR AMP INPUTS
* NODE 1 IS INVERTING INPUT , 2 IS NONINVERTING INPUT
* NODE 9 IS ERROR AMP OUTPUT
* NODE 11 IS DUTY CYCLE OUTPUT
* NODE 16 IS 5.1V REFERENCE OUTPUT
*Parameters:
* T=25US      SWITCHING PERIOD
* TO=1US      DEAD TIME
* TS=4US      TRANSISTOR STORAGE TIME
* EP=3.5V     PEAK SAW VOLTAGE
* EO=.5V      MINIMUM SAW VOLTAGE
* OP AMP STAGE
RI 2 1 100K
CI 2 1 1PF
G1 0 9 2 1 1M
R1 9 0 5MEG
C1 9 0 80PF
VA 23 0 DC 5.2V
VB 24 0 DC {EO-0.1}
* VB=EO-.1
D3 9 23 DS
D4 24 9 DS
* 5.1V REFERENCE
VR 16 0 DC 5.1
* PULSE WIDTH MODULATOR STAGE
RC 9 25 1G
V0 25 0 DC {EO}
* V0=EO , THE MINIMUM SAW VOLTAGE
GD 0 20 9 25 {0.1*(T-TO)/((EP-EO)*T)}
* GD=.1*(T-TO)/((EP-EO)*T)
IX 0 20 DC {0.1*TS/T}
* IX=.1*TS/T
R0 20 0 1K
VP 21 0 DC {100*(T+TS-TO)/T-1}
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100 PERCENT MAX DUTY CYCLE
VM 22 0 DC {100*TS/T+1}
* VM=100*TS/T+1  ,  MIN VM=1  ,  FOR 0 PERCENT MIN DUTY CYCLE
D1 20 21 DS
D2 22 20 DS
ED 11 0 20 0 .01
.MODEL DS D(TT=1NS CJO=1PF)
.ENDS
**********
*SRC=SG1525A;SG1525A;PWM Models;Average;Volt. Reg. Error Amp +-100UA
*SYM=SG1524
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT SG1525A 1 2 9 11 16  {EO=??? EP=??? T=??? TO=??? TS=???}
* SG1525 VOLTAGE REGULATOR MODEL , ERROR AMP = +-100UA OUT
* NODES 1&2 ARE ERROR AMP INPUTS
* NODE 1 IS INVERTING INPUT , 2 IS NONINVERTING INPUT
* NODE 9 IS ERROR AMP OUTPUT
* NODE 11 IS DUTY CYCLE OUTPUT
* NODE 16 IS 5V REFERENCE OUTPUT
*Parameters:
* T=25US      SWITCHING PERIOD
* TO=1US      DEAD TIME
* TS=4US      TRANSISTOR STORAGE TIME
* EP=3.5V     PEAK SAW VOLTAGE
* EO=.5V      MINIMUM SAW VOLTAGE
* OP AMP STAGE
RI 2 1 100K
CI 2 1 1PF
GA 0 26 2 1 1M
RA 26 0 1MEG
G1 0 9 26 0 1U
R1 9 0 5MEG
C1 9 0 80PF
VA 23 0 DC 5.2V
VB 24 0 DC {EO-0.1}
* VB=EO-.1
VC 27 0 DC 100
VD 28 0 DC -100
D3 9 23 DS
D4 24 9 DS
D5 26 27 DS
D6 28 26 DS
* 5.1V REFERENCE
VR 16 0 DC 5.1
* PULSE WIDTH MODULATOR STAGE
RC 9 25 1G
V0 25 0 DC {EO}
* V0=EO , THE MINIMUM SAW VOLTAGE
GD 0 20 9 25 {0.1*(T-TO)/((EP-EO)*T)}
* GD=.1*(T-TO)/((EP-EO)*T)
IX 0 20 DC {0.1*TS/T}
* IX=.1*TS/T
R0 20 0 1K
VP 21 0 DC {100*(T+TS-TO)/T-1}
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100 PERCENT MAX DUTY CYCLE
VM 22 0 DC {100*TS/T+1}
* VM=100*TS/T+1  ,  MIN VM=1  ,  FOR 0 PERCENT MIN DUTY CYCLE
D1 20 21 DS
D2 22 20 DS
ED 11 0 20 0 .01
.MODEL DS D(TT=1NS CJO=1PF)
.ENDS
**********
*SRC=SG1525FF;SG1525FF;PWM Models;Average;w/FeedFwd Error Amp +-100UA
*SYM=SG1525FF
* DEVELOPED BY DR V BELLO , NORDEN SYSTEMS , NORWALK CT.  *
.SUBCKT SG1525FF 1 2 9 11 16 6  {EO=??? EP=??? T=??? TO=??? TS=??? CT=???}
* SG1525A VOLTAGE REGULATOR MODEL WITH FEEDFORWARD , ERROR AMP = +-100UA OUT
* NODES 1&2 ARE ERROR AMP INPUTS
* NODE 1 IS INVERTING INPUT , 2 IS NONINVERTING INPUT
* NODE 6 IS THE FEEDFORWARD INPUT , IR
* NODE 9 IS ERROR AMP OUTPUT
* NODE 11 IS DUTY CYCLE OUTPUT
* NODE 16 IS 5V REFERENCE OUTPUT
*Parameters:
* T=28US      SWITCHING PERIOD
* TO=5US      DEAD TIME
* TS=2US      TRANSISTOR STORAGE TIME
* EP=3.5V     PEAK SAW VOLTAGE
* EO=.5V      MINIMUM SAW VOLTAGE
* CT=2000PF  TIMING CAPACITOR
* VPP=EP-EO=IR*(T-TO)/CT=11.5K*IR , VPP=2.3V FOR IR=200UA
* VPP=PEAK-TO-PEAK RAMP VOLTAGE
* OP AMP STAGE
RI 2 1 100K
CI 2 1 1PF
GA 0 26 2 1 1M
RA 26 0 1MEG
G1 0 9 26 0 1U
R1 9 0 5MEG
C1 9 0 80PF
VA 23 0 DC 5.2V
VB 24 0 DC {EO-0.1}
* VB=EO-.1
VC 27 0 DC 100
VD 28 0 DC -100
D3 9 23 DS
D4 24 9 DS
D5 26 27 DS
D6 28 26 DS
* 5.1V REFERENCE
VR 16 0 DC 5.1
* PULSE WIDTH MODULATOR STAGE
RC 9 25 1G
V0 25 0 DC {EO}
* V0=EO , THE MINIMUM SAW VOLTAGE
VI 6 0 DC 3.8V
HPP 29 25 VI {(((T-TO)/CT)*(-1))}
* HPP=-(T-TO)/CT
RP 29 25 1G
ECO 30 0 9 25 {(T-TO)/T}
* ECO=(T-TO)/T
R2 30 31 1G
EPO 31 0 POLY(2) 29 25 32 0 0 0 0 0 1.0
GB 0 32 30 31 1.0
RB 32 0 1MEG
CB 32 0 10PF
GD 0 20 32 0 0.1
IX 0 20 DC {0.1*TS/T}
* IX=.1*TS/T
R0 20 0 1K
VP 21 0 DC {100*(T+TS-TO)/T-1}
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100 PERCENT MAX DUTY CYCLE
VM 22 0 DC {100*TS/T+1}
* VM=100*TS/T+1  ,  MIN VM=1  ,  FOR 0 PERCENT MIN DUTY CYCLE
D1 20 21 DS
D2 22 20 DS
ED 11 0 20 0 .01
* CIRCUIT TO CLAMP DUTY CYCLE TO MAX WHEN EC>MAX SAW
VSM 33 0 DC {VSM}
* VSM=MAX SAW VOLTAGE
RS 9 33 1G
ES 34 0 9 33 100
VS 34 35
DS 35 0 DS1; OFF
FS 0 20 VS 1.0
.MODEL DS1 D(RS=100 TT=1NS CJO=1PF)
.MODEL DS D(TT=1NS CJO=1PF)
.ENDS
**********
*SRC=SG1526;SG1526;PWM Models;Average;Volt. Reg.
*SYM=SG1524
.SUBCKT SG1526 1 2 3 14 18  {EO=??? EP=??? T=??? TO=??? TS=???}
* NODES 1&2 ARE ERROR AMP INPUTS
* NODE 1 IS NONINVERTING INPUT , 2 IS INVERTING INPUT
* NODE 3 IS ERROR AMP OUTPUT
* NODE 14 IS DUTY CYCLE OUTPUT
* NODE 18 IS 5V REFERENCE OUTPUT
*Parameters:
* T=10US    SWITCHING PERIOD
* TO=1US    DEAD TIME
* TS=0.1US  TRANSISTOR STORAGE TIME
* EP=3.5V   PEAK SAW VOLTAGE
* EO=1V     MINIMUM SAW VOLTAGE
* OP AMP STAGE
RI 2 1 100K
CI 2 1 1PF
G1 0 3 1 2 .8M
R1 3 0 5MEG
C1 3 0 127PF
VA 23 0 DC 3.8V
VB 24 0 DC {EO-0.1}
* VB=EO-.1
D3 3 23 DS
D4 24 3 DS
* 5V REFERENCE
VR 18 0 DC 5
* PULSE WIDTH MODULATOR STAGE
RC 3 25 1G
V0 25 0 DC {EO}
* V0=EO , THE MINIMUM SAW VOLTAGE
GD 0 20 3 25 {0.1*(T-TO)/((EP-EO)*T)}
* GD=.1*(T-TO)/((EP-EO)*T)
IX 0 20 DC {0.1*TS/T}
* IX=.1*TS/T
R0 20 0 1K
VP 21 0 DC {100*(T+TS-TO)/T-1}
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100 PERCENT DUTY CYCLE
VM 22 0 DC {100*TS/T+1}
* VM=100*TS/T+1  ,  MIN VM=1  ,  FOR 0 PERCENT MIN DUTY CYCLE
D1 20 21 DS
D2 22 20 DS
* CIRCUIT TO FORCE D=0 WHEN EC<EO
* GX 0 30 20 0 1M
RX 30 0 1K
G0 0 31 3 25 1.0
V6 32 0 DC 1
D5 30 31 DS
D6 32 30 DS
D7 0 31 DS
R7 31 0 10K
D8 31 33 DS
V8 33 0 DC 99
ED 14 0 20 0 .01
.MODEL DS D(TT=1NS CJO=1PF)
.ENDS
**********
*SRC=SG1526A;SG1526A;PWM Models;Average;Volt. Reg. Error Amp +-100UA
*SYM=SG1524
.SUBCKT SG1526A 1 2 3 14 18
* SG1526 VOLTAGE REGULATOR MODEL , ERROR AMP = +-100UA OUT
* NODES 1&2 ARE ERROR AMP INPUTS
* NODE 1 IS NONINVERTING INPUT , 2 IS INVERTING INPUT
* NODE 3 IS ERROR AMP OUTPUT
* NODE 14 IS DUTY CYCLE OUTPUT
* NODE 18 IS 5V REFERENCE OUTPUT
*Parameters:
* T=25US      SWITCHING PERIOD
* TO=0US      DEAD TIME
* TS=4US    TRANSISTOR STORAGE TIME
* EP=3V     PEAK SAW VOLTAGE
* EO=1V      MINIMUM SAW VOLTAGE
* T=25US TO=0US TS=4US EP=3V EO=1V
* OP AMP STAGE
RI 2 1 100K
CI 2 1 1PF
GA 0 26 1 2 .8M
RA 26 0 1MEG
G1 0 3 26 0 1U
R1 3 0 5MEG
C1 3 0 127PF
VA 23 0 DC 3.8V
VB 24 0 DC {EO-0.1}
* VB=EO-.1
VC 27 0 DC 100
VD 28 0 DC -100
D3 3 23 DS
D4 24 3 DS
D5 26 27 DS
D6 28 26 DS
* 5V REFERENCE
VR 18 0 DC 5
* PULSE WIDTH MODULATOR STAGE
RC 3 25 1G
V0 25 0 DC {EO}
* V0=EO , THE MINIMUM SAW VOLTAGE
GD 0 20 3 25 {0.1*(T-TO)/((EP-EO)*T)}
* GD=.1*(T-TO)/((EP-EO)*T)
IX 0 20 DC {0.1*TS/T}
* IX=.1*TS/T
R0 20 0 1K
VP 21 0 DC {100*(T+TS-TO)/T-1}
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100 PERCENT DUTY CYCLE
VM 22 0 DC {100*TS/T+1}
* VM=100*TS/T+1  ,  MIN VM=1  ,  FOR 0 PERCENT MIN DUTY CYCLE
D1 20 21 DS
D2 22 20 DS
ED 14 0 20 0 .01
.MODEL DS D(TT=1NS CJO=.1PF)
.ENDS
**********
*SRC=UC1846S;UC1846S;PWM Models;Average;Current Mode Controller
*SYM=UC1846
.SUBCKT UC1846S 1 2 3 4 5 6 7 11 15 17 18
* NODES 1-15 ARE UC1846 PIN NUMBERS
* NODE 11 IS THE DUTY CYCLE OUTPUT
* NODE 17 IS THE INPUT VOLTAGE SENSE
* NODE 18 IS THE OUTPUT VOLTAGE SENSE
* UNITRODE UC1846 CURRENT MODE PWM CONTROLLER
VR 2 0 DC 5.1
V1 23 21 DC .5
V2 2 24 DC 1
I1 21 0 DC 10UA
I3 2 7 DC .5MA
R1 1 0 1G
R3 3 0 1G
R4 4 0 1G
E3 22 0 4 3 3.0
D1 7 23 DR
D3 7 24 DR
Q4 0 1 7 BP
X1 21 22 17 18 11 PWMCCM
X2 6 5 7 15 EA
.MODEL DR D(RS=10 N=1 IS=6FA TT=2NS CJO=.95PF VJ=40)
.MODEL BN NPN(BF=209 BR=2.5 RB=670 RC=300 CCS=1.417PF TF=1.15NS
+ TR=405NS CJE=.65PF CJC=.36P IS=1.26F VA=500 C2=1653 IK=1.611M
+ NE=2 PE=.6 ME=.33 PC=.45 MC=.33)
.MODEL BP PNP(BF=75 BR=3.8 RB=500 RC=150 CCS=2.259P TF=27.4N
+ TR=2540N CJE=.1P CJC=1.05P IS=3.15F VA=500 C2=1764 IK=270U
+ NE=2 PE=.45 ME=.33 PC=.45 MC=.33)
.SUBCKT EA 1 2 3 4
* UC1846 ERROR AMP
* NODE 1 IS THE INVERTING INPUT , 2 IS THE NONINVERTING INPUT
* NODE 3 IS THE OUTPUT , 4 IS THE POSITIVE SUPPLY VOLTAGE
RI 2 1 1MEG
CI 2 1 1PF
G1 0 5 2 1 126U
R1 5 0 1.6G
C1 5 0 25PF
VO 4 6 DC 3
VN 7 0 DC 1
EO 10 0 5 0 1.0
RO 10 8 100
IO 3 9 DC 6.5MA
DO 9 3 DN
D1 9 8 DN
D2 7 5 DN
D3 5 6 DN
.MODEL DN D
.ENDS EA
.SUBCKT PWMCCM 1 2 3 4 5
*  CURRENT CONTROL MODULATOR WITH EXTERNAL RAMP &
* STORAGE TIME EFFECTS ; CONTINUOUS CONDUCTION
* NODE 1 IS THE CURRENT CONTROL INPUT
* NODE 2 IS THE INDUCTOR CURRENT SENSE
* NODE 3 IS THE INPUT VOLTAGE SENSE
* NODE 4 IS THE OUTPUT VOLTAGE SENSE
* NODE 5 IS THE DUTY CYCLE (0<D<1)
* RS=.5 , N=.8 , VG=N*VIN=28 , L=200UH , F=88KHZ , A1=3RS*N=1.2 , A=0
* THE STORAGE TIME TS=TS0+D*TS1 , TS0=.5US , TS1=.5US
* D=((IC-IL)+TS*(M1+M))/(T*(M1/2+M))
* M1 IS THE SLOPE OF THE INDUCTOR CURRENT RAMP AT THE COMPARATOR
* M IS THE SLOPE OF THE EXTERNAL STABILIZING RAMP AT THE COMPARATOR
* FOR BUCK       : M1=A1*(VG-VO)/L & M=A*VO/L
* FOR BOOST      : M1=A1*VG/L      & M=A*(VO-VG)/L
* FOR BUCK-BOOST : M1=A1*VG/L      & M=A*VO/L
* A1 & A ARE THE RAMP GAINS AT THE COMPARATOR
* FORCE D USING HIGH GAIN & FEEDBACK
RI 1 2 1G
R1 10 6 1G
R2 4 3 1G
EI 11 0 1 2 1.0
ES 10 11 POLY(2) 15 0 12 0 0 0 0 0 1.0
EF 6 0 POLY(2) 5 0 13 0 0 0 0 0 1.0
GD 0 7 10 6 1
RD 7 0 100MEG
D1 8 7 DS
D2 7 9 DS
VM 8 0 DC 1
* VM=100*TS/T+1 , MIN VM=1 FOR 0% DUTY CYCLE
VP 9 0 DC 99
* VP=100*(T+TS-TO)/T-1 , MAX VP=99 , FOR 100% DUTY CYCLE
ED 5 0 7 0 .01
RO 5 0 1G
* STORAGE TIME GENERATOR
VTS 16 0 DC 0.0
* VTS=TS0/T
ETS 15 16 5 0 0.0
* ETS=TS1/T
R4 15 0 1G
* RAMP GENERATORS FOR M1 & M FOR VARIABLE EXTERNAL RAMP
* LET AM1=A1*T/2L & AM=A*T/L & VA=0
* FOR BUCK       USE: EM1 14 0 3 4 AM1 & EM 13 17 4 0 AM
* FOR BOOST      USE: EM1 14 0 3 0 AM1 & EM 13 17 4 3 AM
* FOR BUCK-BOOST USE: EM1 14 0 3 0 AM1 & EM 13 17 4 0 AM
* FOR CONSTANT EXTERNAL RAMP FOR M USE : EM=0 & VA 17 14 DC M*T
* BUCK EXAMPLE
EM1 14 0 3 4 34.09M
* EM1=A1*T/2L
* VARIABLE EXTERNAL RAMP
* VA 17 14 DC 0.0
* EM 13 17 4 0 68.18M
* EM=A*T/L
* CONSTANT EXTERNAL RAMP
EM 13 17 4 0 0.0
VA 17 14 DC 0.0
* VA=A*VO*T/L
* SEMI-VARIABLE EXTERNAL RAMP WITH VG SENSE
* EM 13 17 3 0 0.0
* EM=-A*T/L
* VA 17 14 DC .73
* VA=A*VO*T/L
EM2 12 13 14 0 1.0
R3 12 0 1G
.MODEL DS D
.ENDS PWMCCM
.ENDS UC1846S
**********
*SRC=FLYBCK77AC;FLYBCK77AC;PWM Models;National;AC Analysis
*SYM=LM1577A
.SUBCKT FLYBCK77ac 1 2 3 4 Comp fb {T=25 L=40u N=1}
L1 8 0 {L}
RS 9 8 .001
BEo1 7 6 V=V(3,4)*V(12)*{N}
BEo2 7 0 V=V(1,2)*V(11)
BGin 1 2 I=V(9,8)*1000*V(11)/(V(11)+V(12))
BGo 4 3 I=V(9,8)*1000*V(12)*{N}/(V(11)+V(12))
*RD 5 0 1G
*RD1 11 0 1G
BED1 11 0 V=V(5)
*RD12 11A 0 1G
BED12 11A 0 V=2*V(9,8)*1000*52e3*{L}/(V(11)*V(1,2)) < .0001 ? .0001 : 
+ 2*V(9,8)*1000*52e3*{L}/(V(11)*V(1,2)) > .99 ? .99 : 
+ 2*V(9,8)*1000*52e3*{L}/(V(11)*V(1,2))
*RD2 12 0 1G
BEL1 L1 0 V=V(9,8)*1000
*RL1 L1 0 1G
BED2 12 0 V=V(11A,11) < 0.01 ? 0.01 : V(11A,11) > .99 ? .99 : V(11A,11)
drec 6 9 dmod
.MODEL dmod D is=1e-9 rs=.01
* Dmod is a bad model, it should have CJO
*
BEf a b V=(-1*(1/52e3)*{(.095+1.94e-4*T)}*V(1,2))/{(2*L)}
BEr b c V=(V(3,4)*V(12)^2*(1/52e3)*{(.095+1.94e-4*T)})/{(2*L)}
BEloop c 0 V=V(comp)-(V(L3)*{(.095+1.94e-4*T)})
Bfm 5 0  V=V(a)*(1/(((V(1,2)*{(.095+1.94e-4*T)}/{L})+(1.955e4+{(29.19*T)}-{(.2292*T*T)}))*(1/52e3)))
*Elapl L3 0 LAPLACE V(L1) = 1-(s/(2*52e3)+(s*s/(9.869*2.7e9)))
A4 100 200 DIFF 
A5 L1 100 DIFF 
B1 L3 0 V = V(L1) + {-1/114K}*V(100) + {-1/2.6646E10}*V(200)
.Model Diff d_dt
*R1 a 0 1g
*Rl l1 0 1g
*Rl3 l3 0 1g
*
*LM1577 error amp model
*
Iin fb 0 150n
Rin fb ref 1meg
Vref ref 0 1.23
BGm comp 0 I=v(fb,ref)*{(.0039-(8.88e-6*T))}
Ro comp 0 320k
*
.ENDS
*************
*SRC=LM1587AC;LM1587AC;PWM Models;National;AC analysis
*SYM=LM1577
*Averaged model of LM1587 boost regulator
*
*Vin === 1-2
*
Vout === 3-4
*
*Control input === Comp
*
.SUBCKT LM1587ac 1 2 3 4 Comp fb {T=25 L=40u}
L1 1 6 {L}
RS 6 7 .001
BEIN1 7 8 V=V(3,4)*V(12)
BEIN2 8 2 V=V(1,2)*(1-V(11)-V(12))
BGO 4 3 I=V(6,7)*1000*V(12)/(V(11)+V(12))
*RD 5 0 1G
*RD1 11 0 1G
BED1 11 0 V=V(5)
*RD12 11A 0 1G
BED12 11A 0 V=2*V(6,7)*1000*100e3*{L}/(V(11)*V(1,2)) < .01 ? .01 : 
+ 2*V(6,7)*1000*100e3*{L}/(V(11)*V(1,2)) > 1 ? 1 : 
+ 2*V(6,7)*1000*100e3*{L}/(V(11)*V(1,2))
*RD2 12 0 1G
BEL1 L1 0 V=V(6,7)*1000
*RL1 L1 0 1G
BED2 12 0 V=V(11A,11) < 0 ? 0 : V(11A,11) > .95 ? .95 : V(11A,11)
*
BEf a b V=(-1*(1/100e3)*{(.07515+1.94e-4*T)}*V(1,2))/{(2*L)}
BEr b c V=(V(3,4)*V(12)^2*(1/100e3)*{(.07515+1.94e-4*T)})/{(2*L)}
BEloop c 0 V=V(comp)-(V(l3)*{(.07515+1.94e-4*T)})
BEfm 5 0  V=V(a)*(1/(((V(1,2)*{(.07515+1.94e-4*T)}/{L})+(2.58e4+{(29.19*T)}-{(.2292*T*T)}))*(1/100e3)))
*Elapl L3 0 LAPLACE V(L1) = 1-(s/(2*100e3)+(s*s/(9.869*2.7e9)))
A4 100 200 DIFF 
A5 L1 100 DIFF 
B1 L3 0 V = V(L1) + -1/200K * V(100) +  -1/2.6646E10 *V(200)
.Model Diff d_dt
*R1 a 0 1g
*Rl l1 0 1g
*Rl3 l3 0 1g
*
*LM1587 error amp model
*
Iin fb 0 150n
Rin fb ref 1meg
Vref ref 0 1.23
BGm comp 0 I=v(fb,ref)*{(.005-(8.88e-6*T))}
Ro comp 0 200k
*
.ENDS
*************
*SRC=LM1587TR;LM1587TR;PWM Models;National;Transient analysis
*SYM=LM1577
.SUBCKT LM1587tr 1 2 3 4 comp fb {T=25 L=40u}
L1 1 6 {L}
RS 6 7 .001
BEIN1 7 8 V=V(3,4)*V(12)
BEIN2 8 2 V=V(1,2)*(1-V(11)-V(12))
BGO 4 3 I=V(6,7)*1000*V(12)/(V(11)+V(12))
*RD 5 0 1G
*RD1 11 0 1G
BED1 11 0 V=V(5)
*RD12 11A 0 1G
BED12 11A 0 V=2*V(6,7)*1000*100e3*{L}/(V(11)*V(1,2)) < .01 ? .01 : 
+ 2*V(6,7)*1000*100e3*{L}/(V(11)*V(1,2)) > 1 ? 1 : 
+ 2*V(6,7)*1000*100e3*{L}/(V(11)*V(1,2))
*RD2 12 0 1G
BEL1 L1 0 V=V(6,7)*1000
*RL1 L1 0 1G
BED2 12 0 V=V(11A,11) < 0 ? 0 : V(11A,11) > .95 ? .95 : V(11A,11)
*
BEf a b V=(-1*(1/100e3)*{(.067+1.94e-4*T)}*V(1,2))/{(2*L)}
BEr b c V=(V(3,4)*V(12)^2*(1/100e3)*{(.067+1.94e-4*T)})/{(2*L)}
BEloop c 0 V=V(comp)-(V(l1)*{(.067+1.94e-4*T)})
BEfm 5 0  V=V(a)*(1/(((V(1,2)*{(.067+1.94e-4*T)}/{L})+(1.955e4+{(29.19*T)}-{(.2292*T*T)}))*(1/100e3)))
*R1 a 0 1g
*Rl l1 0 1g
*
*LM1587 error amp model
*
Iin fb 0 150n
Rin fb ref 1meg
Vref ref 0 1.23
BGm comp 0 I=v(fb,ref)*{(.0029-(8.88e-6*T))}
Ro comp 0 200k
*
.ENDS
*
****************
*SRC=FLYBCK87AC;FLYBCK87AC;PWM Models;National;AC analysis
*SYM=LM1577A
.SUBCKT FLYBCK87ac 1 2 3 4 Comp fb {T=25 L=40u N=1}
L1 8 0 {L}
RS 9 8 .001
BEo1 7 6 V=V(3,4)*V(12)*{N}
BEo2 7 0 V=V(1,2)*V(11)
BGin 1 2 I=V(9,8)*1000*V(11)/(V(11)+V(12))
BGo 4 3 I=V(9,8)*1000*V(12)*{N}/(V(11)+V(12))
*RD 5 0 1G
*RD1 11 0 1G
BED1 11 0 V=V(5)
*RD12 11A 0 1G
BED12 11A 0 V=2*V(9,8)*1000*100e3*{L}/(V(11)*V(1,2)) < .0001 ? .0001 : 
+ 2*V(9,8)*1000*100e3*{L}/(V(11)*V(1,2)) > .99 ? .99 : 
+ 2*V(9,8)*1000*100e3*{L}/(V(11)*V(1,2))
*RD2 12 0 1G
BEL1 L1 0 V=V(9,8)*1000
*RL1 L1 0 1G
BED2 12 0 V=V(11A,11) < 0.01 ? 0.01 : V(11A,11) > .99 ? .99 : V(11A,11)
drec 6 9 dmod
.model dmod d(is=1e-9 rs=.01)
*
*
BEf a b V=(-1*(1/100e3)*{(.095+1.94e-4*T)}*V(1,2))/{(2*L)}
BEr b c V=(V(3,4)*V(12)^2*(1/100e3)*{(.095+1.94e-4*T)})/{(2*L)}
BEloop c 0 V=V(comp)-(V(L3)*{(.095+1.94e-4*T)})
BEfm 5 0  V=V(a)*(1/(((V(1,2)*{(.095+1.94e-4*T)}/{L})+(1.955e4+{(29.19*T)}-{(.2292*T*T)}))*(1/100e3)))
*Elapl L3 0 LAPLACE V(L1) = 1-(s/(2*100e3)+(s*s/(9.869*2.7e9)))
A4 100 200 DIFF 
A5 L1 100 DIFF 
B1 L3 0 V = V(L1) + -1/200K * V(100) +  -1/2.6646E10 * V(200)
.Model Diff d_dt
*R1 a 0 1g
*Rl l1 0 1g
*Rl3 l3 0 1g
*
*LM1587 error amp model
*
Iin fb 0 150n
Rin fb ref 1meg
Vref ref 0 1.23
BGm comp 0 I=v(fb,ref)*{(.0039-(8.88e-6*T))}
Ro comp 0 320k
*
.ENDS
****************
*SRC=GLUEAC;GLUEAC;PWM Models;National;AC analysis
*SYM=LM1577B
*Averaged model of GLUE boost regulator
*
*Vin === 1-2
*
*Vout === 3-4
*
*Control input === Comp
*
.SUBCKT GLUEac 1 2 3 4 Comp fb {T=25 L=40u F=52K MCV=2E4 RI=.08}
L1 1 6 {L}
RS 6 7 .001
BEIN1 7 8 V=V(3,4)*V(12)
BEIN2 8 2 V=V(1,2)*(1-V(11)-V(12))
BGO 4 3 I=V(6,7)*1000*V(12)/(V(11)+V(12))
*RD 5 0 1G
*RD1 11 0 1G
BED1 11 0 V=V(5)
*RD12 11A 0 1G
BED12 11A 0 V=2*V(6,7)*1000*100e3*{L}/(V(11)*V(1,2)) < .01 ? .01 : 
+ 2*V(6,7)*{1000*F*L}/(V(11)*V(1,2)) > 1 ? 1 : 
+ 2*V(6,7)*{1000*F*L}/(V(11)*V(1,2))
*RD2 12 0 1G
BEL1 L1 0 V=V(6,7)*1000
*RL1 L1 0 1G
BED2 12 0 V=V(11A,11) < 0.01 ? 0.01 : V(11A,11) > .95 ? .95 : V(11A,11)
*
*
BEf a b V=(-1*{(1/F)*(RI)}*V(1,2))/{(2*L)}
BEr b c V=(v(3,4)*V(12)^2*{(1/F)*(RI)})/{(2*L)}
BEloop c 0 V=V(comp)-(V(L3)*{(RI)})
BEfm 5 0  V=V(a)*(1/(((V(1,2)*{(RI)}/{L})+{(MCV)})*{(1/F)}))
*Elapl L3 0 LAPLACE V(L1) = 1-(s/{(2*F)}+(s*s/(9.869*F*F)))
A4 100 200 DIFF 
A5 L1 100 DIFF 
B1 L3 0 V = V(L1) + {-1/2*F}*V(100) + {-1/9.869*F*F}*V(200)
.Model Diff d_dt
*R1 a 0 1g
*Rl l1 0 1g
*Rl3 l3 0 1g
*
*
*GLUE error amp model
*
Vref ref 0 1.23
BGm comp 0 I=v(fb,ref)*.003
Ro comp 0 300k
*
.ENDS
****************

