* 				DIAC PSpice Models                    *
***********************************************************************
.SUBCKT DIAC DIAC_IN DIAC_OUT PARAMS:
+Tr=0.342
+Vbo=13.6V
+Delta_V=19V
+Ibo=15uA
*
*Tr:      Rise time (in µs)
*Vbo:	  Break over voltage	
*Delta_V: Dynamic breakover voltage
*Ibo:     Breakover current          
C1         N02098 DIAC_OUT 1u IC=0   
V_IDIAC1     DIAC_IN N01041 0 
R1         N02098 TRG  {1.462*Tr}  
D1         N06161 N01041 DZ19V 
E_ABM1       TRG DIAC_OUT VALUE { IF(ABS(I(V_IDIAC1))>{Ibo},1,0)}    
D2         N06161 N01060 DZ19V 
D3         N10655 N01060 DZ14V 
D4         N10655 DIAC_OUT DZ14V 
S1         N01041 N01060 N02098 DIAC_OUT _S1
*RS_S1        N02098 DIAC_OUT 1G
C2         DIAC_IN DIAC_OUT 10p
****************
* Switch Model *
****************
.MODEL        _S1 VSWITCH Roff=1e7 Ron=2.2 Voff=0.1V Von=0.99V
*.MODEL        _S1 SW Roff=1e7 Ron=2.2 Von=0.4V Vh=0.2V
****************
* Diodes Model *
****************
.model DZ14V  	D(Is=3.142f Rs=0.1 Ikf=0 N=1 Xti=3 Eg=1.11 M=.3282
+		Vj=.75 Fc=.5 Isr=1.973n Nr=2 Bv={Vbo} Ibv=.14467 Nbv=1.093
+		Ibvl=.1m Nbvl=1.2722 Tbv1=001433.3u)
*
.model DZ19V D(Is=6.994f Rs=5.612 Ikf=0 N=1 Eg=1.11 M=.2906
+	     Vj=.75 Fc=.5 Isr=2.088n Nr=2 Bv={Delta_V} Ibv=.17098 Nbv=1.2072
+	     Ibvl=2.002m Nbvl=1.1457 Tbv1=888.89u)
*	Vz = 18 @ 14mA, Zz = 37 @ 1mA, Zz = 11 @ 5mA, Zz = 7.9 @ 20mA
.ends   
*
*********************************************************************
* 				Standard DIACs			    *
*********************************************************************
*
*$
.subckt DB3TG DIAC_IN DIAC_OUT
X1 DIAC_IN DIAC_OUT DIAC params:
+Tr=0.342
+Vbo=13.6V
+Delta_V=19V
+Ibo=6uA
* 2008 / ST / Rev 1
.ends
*$
.subckt DB3 DIAC_IN DIAC_OUT
X1 DIAC_IN DIAC_OUT DIAC params:
+Tr=0.342
+Vbo=13.6V
+Delta_V=19V
+Ibo=10uA
* 2008 / ST / Rev 0
.ends
*$
.subckt DB4 DIAC_IN DIAC_OUT
X1 DIAC_IN DIAC_OUT DIAC params:
+Tr=0.342
+Vbo=21.6V
+Delta_V=19V
+Ibo=10uA
* 2008 / ST / Rev 1
.ends
*$
.subckt SMDB3 DIAC_IN DIAC_OUT
X1 DIAC_IN DIAC_OUT DIAC params:
+Tr=0.102
+Vbo=13.6V
+Delta_V=19V
+Ibo=4uA
* 2008 / ST / Rev 0
.ends
*$
