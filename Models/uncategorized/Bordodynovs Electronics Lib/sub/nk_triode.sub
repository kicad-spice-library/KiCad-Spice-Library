******************
* Small-signal Triode models by Norman Koren
******************

******************
.SUBCKT 6DJ8 1 2 3 ; A G C	(Triode) ;
* Tom Mitchell
X1 1 2 3 TRIODE MU=30.51 EX=1.532 KG1=453.9 KP=233.17 KVB=190.9 VCT=0.50  RGI=2000 CCG=2.3P  CGP=2.1P CCP=.7P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS.
.ENDS	6DJ8

******************
.SUBCKT 6SL7GT  1 2 3 ; A G C (Triode)
* GE 
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=75.89 EX=1.233 KG1=1735.2 KP=1725.27 KVB=7.0 VCT=0.50 RGI=2000 CCG=2.9p CGP=4.1p CCP=1.5p ; 
.ENDS    6SL7GT

******************
.SUBCKT 6SN7  1 2 3 ; A G C (Triode)
* Sylvania Technical Manual 
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=21.07 EX=1.341 KG1=1446.2 KP=157.81 KVB=179.4 VCT=0.00 RGI=2000 CCG=3.1p CGP=4.7p CCP=1.4p ; 
.ENDS    6SN7

******************
.SUBCKT 6SN7GTA  1 2 3 ; A G C (Triode)
* Sylvania data sheet  
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=21.54 EX=1.316 KG1=1261.1 KP=156.78 KVB=296.4 VCT=0.00 RGI=2000 CCG=3.1p CGP=4.7p CCP=1.4p ; 
.ENDS    6SN7GTA

******************
.SUBCKT 6SN7GTB  1 2 3 ; A G C (Triode)
* Sylvania 
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=21.54 EX=1.316 KG1=1261.1 KP=156.78 KVB=296.4 VCT=0.00 RGI=2000 CCG=3.1p CGP=4.7p CCP=1.4p ; 
.ENDS    6SN7GTB

******************
.SUBCKT 12AT7 1 2 3 ; A G C	(Triode)	AKA ECC81
* Tom Mitchell
X1 1 2 3 TRIODE MU=67.49 EX=1.234 KG1=419.1  KP=213.96 KVB= 300.0 VCT=0.00 RGI=2000 CCG=2.3P CGP=2.2P CCP=1.0P	; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
.ENDS	12AT7

******************
.SUBCKT 12AT7WA  1 2 3 ; A G C (Triode)
* MAZDA BELVU Data Sheet AKA ECC81  
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=72.80 EX=1.167 KG1=341.8 KP=233.21 KVB=267.8 VCT=0.00 RGI=2000 CCG=2.3p CGP=2.2p CCP=1.0p ; 
.ENDS    12AT7WA

******************
.SUBCKT 12AU7  1 2 3 ; A G C (Triode)
* Sylvania Technical Manual AKA ECC82 ECC802 5814 6067 6189 
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=20.21 EX=1.230 KG1=1108.7 KP=84.96 KVB=551.3 VCT=0.00 RGI=2000 CCG=2.3p CGP=2.2p CCP=1.0p ; 
.ENDS    12AU7

******************
.SUBCKT 12AU7A  1 2 3 ; A G C (Triode)
* MAZDA BELVU Data sheet AKA ECC82 ECC802 5814 6067 6189 
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=19.41 EX=1.226 KG1=1233.8 KP=104.87 KVB=300.0 VCT=0.00 RGI=2000 CCG=2.3p CGP=2.2p CCP=1.0p ; 
.ENDS    12AU7A

******************
.SUBCKT 12AX7 1 2 3 ; A G C	(Triode)  OLD MODEL	AKA ECC83
* Original Koren Model
X1 1 2 3 TRIODE MU=100 EX=1.4 KG1=1060 KP=600 KVB=300 VCT=0.00 RGI=2000 CCG=2.3P CGP=2.4P CCP=.9P	; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
.ENDS	12AX7

******************
.SUBCKT 12AX7A  1 2 3 ; A G C (Triode)
* RCA data sheet AKA ECC83 7025 
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=100.26 EX=1.394 KG1=1651.8 KP=17950.75 KVB=524.4 VCT=0.50 RGI=2000 CCG=2.3p CGP=2.4p CCP=0.9p ; 
.ENDS    12AX7A

******************
.SUBCKT 12AY7  1 2 3 ; A G C (Triode)
* GE data book June 1955 
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=44.16 EX=1.113 KG1=1192.4 KP=409.96 KVB=300.0 VCT=0.00 RGI=2000 CCG=2.0p CGP=2.0p CCP=1.0p ; 
.ENDS    12AY7

******************
.SUBCKT 6C33C  1 2 3 ; A G C (Triode)
* Soviet (1970) Using both cathodes ie both heaters working 
* library format: LTSpice 02-Jun-2008
X1 1 2 3 TRIODE MU=3.27 EX=1.397 KG1=150.9 KP=13.94 KVB=300.0 VCT=0.00 RGI=1000 CCG=30.0p CGP=31.0p CCP=10.5p ; 
.ENDS    6C33C

******************
.SUBCKT E80CC  1 2 3 ; A G C (Triode)
* Philips data sheet Dec 1968 
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=32.74 EX=1.468 KG1=2447.9 KP=235.61 KVB=2119.4 VCT=0.50 RGI=2000 CCG=3.0p CGP=2.2p CCP=2.3p ; 
.ENDS    E80CC

******************
.SUBCKT ECC81  1 2 3 ; A G C (Triode)
* Mullard data book Jan 1969 12AT7 
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=77.09 EX=1.077 KG1=320.1 KP=231.05 KVB=300.0 VCT=0.00 RGI=2000 CCG=2.3p CGP=2.2p CCP=1.0p ; 
.ENDS    ECC81

******************
.SUBCKT ECC82  1 2 3 ; A G C (Triode)
* Mullard data book Jan 1969 AKA 12AU7A 12AU7 
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=19.98 EX=1.291 KG1=1359.0 KP=87.34 KVB=300.0 VCT=0.00 RGI=2000 CCG=2.3p CGP=2.2p CCP=1.0p ; 
.ENDS    ECC82

******************
.SUBCKT ECC83  1 2 3 ; A G C (Triode)
* Mullard data sheet 1970 AKA 12AX7A 7025 
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=98.11 EX=1.459 KG1=1734.7 KP=754.39 KVB=119.9 VCT=0.50 RGI=2000 CCG=2.3p CGP=2.4p CCP=0.9p ; 
.ENDS    ECC83

******************
.SUBCKT ECC84  1 2 3 ; A G C (Triode)
* Philips data book 1955 
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=27.76 EX=1.309 KG1=492.0 KP=95.80 KVB=379.2 VCT=0.50 RGI=2000 CCG=2.8p CGP=1.9p CCP=1.0p ; 
.ENDS    ECC84

******************
.SUBCKT ECC85  1 2 3 ; A G C (Triode)
* Philips data book 1954 
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=69.50 EX=1.481 KG1=551.4 KP=224.40 KVB=2689.3 VCT=0.50 RGI=2000 CCG=2.8p CGP=3.7p CCP=1.0p ; 
.ENDS    ECC85

******************
.SUBCKT ECC86  1 2 3 ; A G C (Triode)
* Philips data book 1959 
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=29.91 EX=1.596 KG1=294.6 KP=48.11 KVB=210.6 VCT=0.50 RGI=2000 CCG=3.7p CGP=1.8p CCP=2.3p ; 
.ENDS    ECC86

******************
.SUBCKT ECC87  1 2 3 ; A G C (Triode)
* Same as E80CC 
* library format: LTSpice 31-May-2008
X1 1 2 3 E80CC  
.ENDS    ECC87

******************
.SUBCKT ECC88  1 2 3 ; A G C (Triode)
* Philips Data sheet March 1957 AKA 6DJ8  
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=31.04 EX=1.069 KG1=210.9 KP=369.74 KVB=300.0 VCT=0.00 RGI=2000 CCG=3.7p CGP=2.3p CCP=2.2p ; 
.ENDS    ECC88

******************
.SUBCKT E88CC  1 2 3 ; A G C (Triode)
* Mullard Data sheet December 1968 AKA 6DJ8, ECC88, 6922  
* library format: LTSpice 31-May-2008
X1 1 2 3 TRIODE MU=35.56 EX=1.134 KG1=196.7 KP=278.19 KVB=300.0 VCT=0.00 RGI=2000 CCG=3.7p CGP=2.3p CCP=2.2p ; 
.ENDS    E88CC

******************
.SUBCKT ECC801S  1 2 3 ; A G C (Triode)
* Telefunken Data Sheet AKA 6201 
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=77.15 EX=1.078 KG1=320.0 KP=230.75 KVB=300.0 VCT=0.00 RGI=2000 CCG=2.3p CGP=2.2p CCP=1.0p ; 
.ENDS    ECC801S

******************
.SUBCKT ECC802S  1 2 3 ; A G C (Triode)
* Telefunken Data sheet AKA 6189 
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=24.10 EX=1.254 KG1=1023.1 KP=65.28 KVB=300.0 VCT=0.00 RGI=2000 CCG=2.3p CGP=2.2p CCP=1.0p ; 
.ENDS    ECC802S

******************
.SUBCKT ECC803S  1 2 3 ; A G C (Triode)
* Telefunken Data sheet AKA 6057 
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=98.32 EX=1.456 KG1=1722.8 KP=749.41 KVB=131.2 VCT=0.50 RGI=2000 CCG=2.3p CGP=2.4p CCP=1.1p ; 
.ENDS    ECC803S

******************
.SUBCKT 6N1P  1 2 3 ; A G C (Triode, Svetlana)
* Svetlana's Data sheet 
* library format: LTSpice 01-Jun-2008
X1 1 2 3 TRIODE MU=36.65 EX=1.537 KG1=1020.2 KP=193.29 KVB=300.0 VCT=-0.50 RGI=2000 CCG=3.7p CGP=2.3p CCP=2.2p ; 
.ENDS    6N1P

******************
.SUBCKT TRIODE 1 2 3 ; A G C
E1  7 0 VALUE={V(1,3)/KP*LOG(1+EXP(KP*(1/MU+(V(2,3)+VCT)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1  1 3 VALUE={(PWR(V(7,0),EX)+PWRS(V(7,0),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1  2 3 {CCG}  ; CATHODE-GRID
C2  2 1 {CGP}  ; GRID=PLATE
C3  1 3 {CCP}  ; CATHODE-PLATE
D3  5 3 DX     ; FOR GRID CURRENT
R1  2 5 {RGI}  ; FOR GRID CURRENT
.ENDS	TRIODE

******************
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N) ; diode used by the Triode models
