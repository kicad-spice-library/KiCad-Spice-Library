*
* MOC3043M ZERO VOLTAGE SWITCHING TRIAC DRIVER OPTOCOUPLER
*
.subckt MOC3043M A K MT2 MT1
XU1 MT2 VG MT1 MOC3043M_TRIAC
R2 VG MT1 10K
D1 A N003 IRLED
V3 N003 K 0
B1 MT1 VG I=if(abs(V(MT2,MT1)/40),0,1.06*V(VIP))
C1 VIP 0 10n
R4 VIP N001 1.45K
D2 VIP N001 DIDEAL
B2 N001 0 V=I(V3)
D3 N002 N001 DZ
R3 VIP N002 1.51K
*

*
***************************************************
* MOC3043M TRIAC MODEL
* Based on ST Microelectronics Sensitive Gate Triac Model
*
.subckt MOC3043M_TRIAC A G K
*
* NOTE: Pin order changed from STMicro to match LTspice Triac.asy
*
X1 A K G Triac_ST params:
+ Vdrm=400v
+ Igt=5ma
+ Ih=400ua
+ Rt=18
+ Standard=1
* 2008 / ST / Rev 1
.ends MOC3043M_TRIAC
*
***************************************************
.subckt Triac_ST A K G PARAMS:
+ Vdrm=400v
+ Igt=20ma
+ Ih=6ma
+ Rt=0.01
+ Standard=1
*
* Vdrm : Repetitive forward off-state voltage
* Ih   : Holding current
* Igt  : Gate trigger current
* Rt   : Dynamic on-state resistance
* Standard : Differenciation between Snubberless and Standard Triacs
* (Standard=0 => Snubberless Triacs, Standard=1 => Standard Triacs)
*
*****************
* Power circuit *
*****************
*
****************
*Switch circuit*
****************
* Q1 & Q2 Conduction
S_S3         A Plip1 positive 0 Smain
*RS_S3        positive 0 1G
D_DAK1         Plip1 Plip2 Dak
R_Rlip         Plip1 Plip2  1k
V_Viak         Plip2 K DC 0 AC 0
*
* Q3 & Q4 Conduction
S_S4         A Plin1 negative 0 Smain
*RS_S4        negative 0 1G
D_DKA1         Plin2 Plin1 Dak
R_Rlin         Plin1 Plin2  1k
V_Vika         K Plin2 DC 0 AC 0
**************
*Gate circuit*
**************
R_Rgk         G K  10G
D_DGKi         Pg2 G Dgk
D_DGKd         G Pg2 Dgk
V_Vig         Pg2 K DC 0 AC 0
R_Rlig         G Pg2  1k
*******************
*Interface circuit*
*******************
* positive pilot
R_Rp         Controlp positive  2.2
C_Cp         0 positive  1u
E_IF15OR3         Controlp 0 VALUE {IF(  ( (V(CMDIG)>0.5) | (V(CMDILIH)>0.5) |
+ (V(CMDVdrm)>0.5) ),400,0 )}
*
* negative pilot
R_Rn         Controln negative  2.2
C_Cn         0 negative  1u
E_IF14OR3         Controln 0 VALUE {IF(  ( (V(CMDIG)>0.5) | (V(CMDILIHN)>0.5) |
+ (V(CMDVdrm)>0.5) ),400,0 )}
*
*
******************
* Pilots circuit *
******************
******************
* Pilot Gate     *
******************
E_IF1IG         inIG 0 VALUE {IF(  ( ABS(I(V_Vig)) ) > Igt ,1,0 )}
E_MULT2MULT         CMDIG 0 VALUE {V(Q4)*V(inIG)}
E_IF2Quadrant4         Q4 0 VALUE
+ {IF(((I(V_Vig)>Igt)&((V(A)-V(K))<0)&(Standard==0)),0,1)}
*
******************
* Pilot IHIL     *
******************
*
E_IF10IL         inIL 0 VALUE {IF( ((I(V_Viak))>2.5*Igt),1,0 )}
E_IF5IH         inIH 0 VALUE {IF( ((I(V_Viak))>(Ih/3)),1,0 )}
*
* Flip_flop IHIL
E_IF6DIHIL         SDIHIL 0 VALUE {IF(
+ (V(inIL)*V(inIH)+V(inIH)*(1-V(inIL))*(V(CMDILIH)) )>0.5,1,0)}
C_CIHIL         CMDILIH 0  1n
R_RIHIL         SDIHIL CMDILIH  1K
R_RIHIL2         CMDILIH 0  100Meg
******************
* Pilot IHILN    *
******************
*
E_IF11ILn         inILn 0 VALUE {IF( ((I(V_Vika))>2.5*Igt),1,0 )}
E_IF3IHn         inIHn 0 VALUE {IF( ((I(V_Vika))>(Ih/3)),1,0 )}
* Flip_flop IHILn
E_IF4DIHILN         SDIHILN 0 VALUE {IF(
+ (V(inILn)*V(inIHn)+V(inIHn)*(1-V(inILn))*(V(CMDILIHN)) )>0.5,1,0)}
C_CIHILn         CMDILIHN 0  1n
R_RIHILn         SDIHILN CMDILIHN  1K
R_RIHILn2         CMDILIHN 0  100Meg
******************
* Pilot VDRM     *
******************
E_IF8Vdrm         inVdrm 0 VALUE {IF( (ABS(V(A)-V(K))>(Vdrm*1.3)),1,0 )}
E_IF9IHVDRM         inIhVdrm 0 VALUE {IF( (I(V_Viak)>(Vdrm*1.3)/1.2meg)|
+  (I(V_Vika)>(Vdrm*1.3)/1.2meg),1,0)}
* Flip_flop VDRM
E_IF7DVDRM         SDVDRM 0 VALUE {IF(
+ (V(inVdrm)+(1-V(inVdrm))*V(inIhVdrm)*V(CMDVdrm) )>0.5,1,0)}
C_CVdrm         CMDVdrm 0  1n
R_RVdrm         SDVDRM CMDVdrm  100
R_RVdrm2        CMDVdrm 0  100Meg
****************
* Switch Model *
****************
.MODEL Smain VSWITCH Roff=1.2meg Ron={Rt} Voff=0 Von=100
****************
* Diodes Model *
****************
.MODEL  Dak D( Is=3E-12 Cjo=5pf)
.MODEL  Dgk D( Is=1E-16 Cjo=50pf Rs=5)
.ends Triac_ST
*
****************
*
.MODEL IRLED D (IS=44P RS=42M N=2.375 BV=6 IBV=10n
+ CJO=35P VJ=.75 M=.333 TT=4.32U)
*
.MODEL DZ D (IS=2.61339e-12 RS=0.0110501 N=1.20576 EG=0.6
+XTI=3.1271 BV=13.5 IBV=1e-05 CJO=1e-11 VJ=0.7 M=0.5 FC=0.5 TT=1e-09
+KF=0 AF=1)
*
.model DIDEAL D(Vfwd=100u Epsilon=2m)
*
.ends MOC3043M