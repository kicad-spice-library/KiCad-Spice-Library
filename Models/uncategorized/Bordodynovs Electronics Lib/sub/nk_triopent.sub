******************
* Triode Power Pentode models by Norman Koren
******************

******************
.SUBCKT ECL83 1 2 3 4 5 6 7 ; At Gt Ct Ap G1p G2p Cp (Triode power pentode)
X1 1 2 3   TRIODE MU=119.15 EX=1.321 KG1=732.5 KP=305.21 KVB=1.8 VCT=0.50 RGI=2000 CCG=3.2p CGP=2.7p CCP=1.8p ; 
X2 4 5 6 7 PENTODE1 MU=10.96 EX=1.350 KG1=1249.7 KG2=4500 KP=54.82 KVB=14.5 VCT=0.00 RGI=2000 CCG=6.7p CPG1=0.6p CCP=4.1p ; 
.ENDS   ECL83

******************
.SUBCKT ECL85 1 2 3 4 5 6 7 ; At Gt Ct Ap G1p G2p Cp (Triode power pentode)
X1 1 2 3   TRIODE MU=56.54 EX=1.608 KG1=719.6 KP=247.12 KVB=3.0 VCT=0.50 RGI=2000 CCG=3.2p CGP=2.7p CCP=1.8p ; 
X2 4 5 6 7 PENTODE1 MU=9.35 EX=1.350 KG1=556.4 KG2=4500 KP=35.28 KVB=21.3 VCT=0.00 RGI=2000 CCG=6.7p CPG1=0.6p CCP=4.1p ; 
.ENDS   ECL85

******************
.SUBCKT PENTODE1 1 2 3 4 ; A G2 G1 C
RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE={V(2,4)/KP*LOG(1+EXP((1/MU+V(3,4)/V(2,4))*KP))}	; E1 BREAKS UP LONG EQUATION FOR G1.
G1   1 4  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,4)/KVB)}
G2   2 4  VALUE={(EXP(EX*(LOG((V(2,4)/MU)+V(3,4)))))/KG2}
* G2   2 4  VALUE={PWR(V(2,4)/MU+V(3,4),EX)/KG2}
RCP  1 4  1G      ; FOR CONVERGENCE	A  - C
C1   3 4  {CCG}   ; CATHODE-GRID 1	C  - G1
C2   1 3  {CPG1}  ; GRID 1-PLATE	G1 - A
C3   1 4  {CCP}   ; CATHODE-PLATE	A  - C
R1   3 5  {RGI}   ; FOR GRID CURRENT	G1 - 5
D3   5 4  DX      ; FOR GRID CURRENT	5  - C
.ENDS PENTODE1

******************
.SUBCKT TRIODE 1 2 3 ; A G C
E1  7 0 VALUE={V(1,3)/KP*LOG(1+EXP(KP*(1/MU+(V(2,3)+VCT)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1  1 3 VALUE={(PWR(V(7,0),EX)+PWRS(V(7,0),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1  2 3 {CCG}  ; CATHODE-GRID
C2  2 1 {CGP}  ; GRID=PLATE
C3  1 3 {CCP}  ; CATHODE-PLATE
D3  5 3 DX     ; FOR GRID CURRENT
R1  2 5 {RGI}  ; FOR GRID CURRENT
.ENDS	TRIODE

******************
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N) ; diode used by the Triode models
