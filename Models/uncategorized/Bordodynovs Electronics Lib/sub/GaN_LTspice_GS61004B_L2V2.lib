* Created in LTspice Version 4.13h                              *
*                                            					*
* GaN Systems Inc. Power Transistors                            *
* LTSpice Library for GaN Transistors                           *
* Level 2  Version 2.0                                          *
*                                                               *
*****************************************************************
*****************************************************************
*                                                               *
* Models provided by GaN Systems Inc. are not warranted by      *
* GaN Systems Inc. as                                           *
* fully representing all of the specifications and operating    *
* characteristics of the semiconductor product to which the     *
* model relates. The model describe the characteristics of a    *
* typical device.                                               *
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. GaN Systems Inc. therefore   *
* does not assume any liability arising from their use.         *
* GaN Systems Inc. reserves the right to change models without  *
* prior notice.                                                 *
*                                                               *
* This library contains models of the following GaN Systems     *
* Inc. transistors:                                             *
*                                                               *
*   GS61004B                                                    *
*****************************************************************
*Level 2 Version 2.0. In addition to Level 1, this level includes the thermal model.
*The leakage current, and stray inductance in the package are NOT included.
*
*$
.subckt Res_d 1 2 TJ
.param rTC=-0.013
ERES 1 3 value={I(VSENSE)*(((3.6/4)*0.95*1.1*(1-1*rTC*(V(TJ)-25)))*3.7/403)}
VSENSE 3 2 DC 0
.ENDS
*$
*
*$
.subckt Res_ds 1 2 TJ
.param rTC=-0.013
ERES 1 3 value={I(VSENSE)*((1*3.6)*(0.238*1.1*(1-1*rTc*(V(TJ)-25)))/403)}
VSENSE 3 2 DC 0
.ENDS
*$
.subckt GS61004B_L2V2 gatein drainin sourcein TC TJ
*
.param conv_aide=1
.param Rth_CasetoAmbient=0
*
.param aDi=0.25       cur={(1.4/3.6)*(0.069*75/80)*403/0.71}   slp=2.0   rpara=1.1
+      ITc=0.004       	  rTc=-0.015      		       x0_0=0.31		   x0_1=0.255
+      di_gs1={7*4.3e-5}  di_gs2={2.6e-8}  	       di_gs3={100*0.8}    di_gs4={80*0.23}
+      Igs1=1.42e-10   	  Igs2={(3.0e-010)*(5)/20}       Igs3=4.9 	   Igs4=6.83e-01
+      Igs5=-7.85e-011    Igs6=-3.30     	       Igs7=6.0
+      Igd1=5.49e-012  	  Igd2={2.6e-11*(0.2)/3}  	 Igd3=-3.09	   Igd4=12
+      Isd1=1.7e-013 	  Isd2=1e-12  	  	Isd3=0 	 Isd4=2.5
+      Isd5=5e-013 	  Isd6=10 	      	Isd7=4.5 	 of1=100 	of2=35
+	 ff1=0.345		  ff2=1.2 		  	ff3=4.5 	 ff4=0.5 	ff5=8    ff6=0.14
*
*
Rth_1 T11 TJ {0.04}
Cth_1 0 TJ {1.96e-5}
Rth_2 T22 T11 {0.651}
Cth_2 0 T11  {(1.37e-3)}
Rth_3 T33 T22 {0.358}
Cth_3 0 T22 {(3.07e-4)}
Rth_4 TC T33 {0.051}
Cth_4 0 T33 {(4.65e-4)}
*
bdtemp 0 TJ I = (if(v(drain,source)>0,
+   (cur*(0.8*PWR((v(TJ)-25+273)/300,-2.7))*log(1.0+exp(21*(v(gate,source)-(7.9+0)+6.1-0.000*(-25))/slp))*
+	v(drain,source)/(1 + max(x0_0+x0_1*(v(gate,source)+9.1),0.2)*v(drain,source)))*
+	v(drainin,sourcein),
+	(cur*(0.8*PWR((v(TJ)-25+273)/300,-2.7))*log(1.0+exp(21*(v(gate,drain)-(7.9+0)+6.1-0.000*(-25))/slp))*
+	v(source,drain)/(1 + max(x0_0+x0_1*(v(gate,drain)+15),1.44)*1.0*v(source,drain)))*
+	v(sourcein,drainin)))
*
Xrd drainin drain TJ Res_d
Xrs sourcein source TJ Res_ds
rg gatein gate {1.5}
*
Rcsdconv drain source {1000Meg/aDi}
Rcgsconv gate source {1000Meg/aDi}
Rcgdconv gate drain {1000Meg/aDi}
*
bswitch drain2 source2 I = (if (v(drain2,source2)>0,
+	(cur*(0.8*PWR((v(TJ)-25+273)/300,-2.7))*log(1.0+exp(21*(v(gate,source2)-(7.9+0)+6.1-0.00*(-25))/slp))*
+	v(drain2,source2)/(1 + max(x0_0+x0_1*(v(gate,source2)+9.1),0.2)*v(drain2,source2))),
+	(-cur*(0.8*PWR((v(TJ)-25+273)/300,-2.7))*log(1.0+exp(21*(v(gate,drain2)-(7.9+0)+6.1-0.00*(-25))/slp))*
+	v(source2,drain2)/(1 + max(x0_0+x0_1*(v(gate,drain2)+15),1.44)*v(source2,drain2)))) )
*
R_drain2 drain2 drain {(14e-4)}
R_source2 source2 source {(14e-4)}
*
*
C_GS	 gate source  	{(0.0052*Igs1)*403}
C_GS1 gate source Q = ((4/120/2.18)*403*0.5*((0.5*Igs2*Igs4*log(1+exp(ff5*(v(gate,source)-Igs3+
+					4.0)/0.9933))-Igs5*Igs7*log(1+exp(ff6*(v(source,drain)-Igs6)/Igs7)))))
*
C_GD   gate drain  	{(0.01*Igd1/30/2.18)*403}
C_GD1 gate drain  Q = ((0.45/25/2.18)*403*((0.5*Igs2*Igs4*log(1+exp(2*ff1*(v(gate,drain)-Igs3+of1-95)/
+					(Igs4*ff3)))+2*Igd2*Igd4*log(1+exp(ff2*(v(gate,drain)-Igd3+of2-35)/(Igd4
+					*ff4))))))
*
C_SD	 source drain  	{(3.6*Isd1/2.18)*403}
C_SD1 source drain  Q = (2/2.18*403*(Isd2*Isd4*log(1+exp(0.1*(v(source,drain)-Isd3)/Isd4))+
+                              Isd5*Isd7*log(1+exp(0.1*(v(source,drain)-Isd6)/Isd7))
+                             +5*0.5*(0.5*2.5e-12*0.643*log(1+exp(0.6*v(source,drain)-4.68+15))/(0.643
+					*3.5))))
.ends
*$
