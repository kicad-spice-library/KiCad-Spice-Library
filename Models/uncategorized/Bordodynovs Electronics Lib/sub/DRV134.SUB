* DRV134
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: DRV134
* Date: 08JUL2011
* Model Type: ALL IN ONE
* Simulator: PSPICE
* Simulator Version: 16.0.0.p001
* EVM Order Number: N/A
* EVM Users Guide: N/A
* Datasheet: SBOS094A – JANUARY 1998 – REVISED APRIL 2007
*
* Model Version: 1.0
*
*****************************************************************************
*
* Updates:
*
* Version 1.0 : 
* Release to Web
*
*****************************************************************************
* DRV134 SUB-CIRCUIT CONNECTIONS:
*		-VO
*		|  -SENSE
*		|  |  GND
*		|  |  |  VIN
*		|  |  |  |  -V
*		|  |  |  |  |  V+  
*		|  |  |  |  |  |  +SENSE
*		|  |  |  |  |  |  |  +VO
*		|  |  |  |  |  |  |  |
.SUBCKT DRV134	1  2  3  4  5  6  7  8
XA1      21  22  6   5   23  OP1
DC7B     5   7    SIMPLED 1
DC7A     7   6    SIMPLED 1
DC8A     8   6    SIMPLED 1
DC8B     5   8    SIMPLED 1
R1A      4   24   30K
R2A      24  28   32.5K
CN       24  28   4PF
XAA      31  24  6   5   28  OP1
R3B      4   25   30K
R4B      25  2    33K
R3A      23  26   30K
R4A      26  7    33.01K
R1B      23  27   30K
RNULL    32  21   15K
CP       27  29   4PF
R2B      27  29   32.509K
XAB      30  27  6   5   29  OP1
ROA      28  1    50.02
ROB      29  8    50.0
RCA      2   3    10K
RCB      3   7    10K
IQ       6   5    DC 4.2M
RQ       6   5    80K
VOSAB    30  25   DC 500U
VOSAA    31  26   DC 500U
VOSA1    3   32   DC 500U
R1       4   22   30K
DC1B     5   1    SIMPLED 1
DC1A     1   6    SIMPLED 1
DC2A     2   6    SIMPLED 1
DC2B     5   2    SIMPLED 1
R2       22  23   30K
DC3A     3   6    SIMPLED 1
DC3B     5   3    SIMPLED 1
DC4A     4   6    SIMPLED 1
DC4B     5   4    SIMPLED 1
*.ENDS
*
***** OP1 SUB-CIRCUIT
* CONNECTIONS:
*				NON-INVERTING INPUT
*				|  INVERTING INPUT
*				|  |  POSITIVE POWER SUPPLY
*				|  |  |  NEGATIVE POWER SUPPLY
*				|  |  |  |  OUTPUT
*				|  |  |  |  |
.SUBCKT OP1 			1  2  3  4  5  
DL1      20  13   SIMPLED 1
DL2      13  20   SIMPLED 1
VSNP     0   18   DC 0
VSNN     19  0    DC 0
DSP      18  17   SIMPLED 1
DSN      17  19   SIMPLED 1
F2       3   0    VSNP 1.0
F0       0   4    VSNN 1.0
RO2      20  5    10.0
VC       3   14   DC 2.8
VE       15  4    DC 2.8
VSN1     3   6    DC 0
VSN2     3   7    DC 0
Q1       6   1   8    SIMPLEQ 1
Q2       7   2   9    SIMPLEQ 1
IEE      10  4    DC 200UA
F1       0   13   VSN3 1.0
DC       13  14   SIMPLED 1
RG       13  0    3.3G
C2       0   13   22PF
RE2      9   10   3.3K
DE       15  13   SIMPLED 1
RE1      8   10   3.3K
FB       17  13   VLIM .99999
VLIM     16  20   DC 0
RO1      13  16   7.0
F3       0   11   POLY (2) VSN1 VSN2 0 1.0 -1.0
C1       0   11   18PF
R1       11  12   3.3K
VSN3     12  0    DC 0
DP       4   3    SIMPLED 1
.ENDS
*
**** DEVICE MODELS
.MODEL SIMPLED D RS=1
.MODEL SIMPLEQ NPN RB=1 RC=1 RE=1 KF=.08F

*XD1      VON VON GD  VIN VN  VP  VOP VOP DRV134

.ENDS DRV134