.SUBCKT SE_OUTPUT_XFRMR 1 2 3 4 5 6  ; SE Transformer
+PARAMS: LPRIM=68 LLKG=.040 RPRIM=125 CPRIM=1.04NF ZPRIM=5000

* LPRIM  IS THE TOTAL PRIMARY L (VARIES WITH MEASUREMENT).
* LLKG   IS THE LEAKAGE L (MEASURABLE: CONSISTENT).
* RPRIM  IS THE TOTAL PRIMARY R.
* CPRIM  IS THE MEASURED PRIMARY CAPACITANCE.
* ZPRIM  IS THE PRIMARY Z

.PARAM QFCTR={LPRIM/LLKG}  ; Q-FACTOR.
.PARAM LRATIO={4/ZPRIM}    ; INDUCTANCE RATIO: (LOAD OHMS)/(PRIMARY Z).

CS1 1 2    {CPRIM}     ; PRIMARY CAPACITANCE
RS1 1 2    300K        ; SHUNT R FOR HIGH FREQUENCY EFFECTS.

LP1 1 12   {LPRIM} ; PRIMARY
RP1 12 2   {RPRIM}

LP2 4 3    {.34315*LPRIM*LRATIO}  ; SECONDARY
LP3 5 4    {.17157*LPRIM*LRATIO}
LP4 6 5    {LPRIM*LRATIO}

KALL LP1 LP2 LP3 LP4 {1-1/(2*QFCTR)}  ; COUPLING
.ENDS SE_OUTPUT_XFRMR