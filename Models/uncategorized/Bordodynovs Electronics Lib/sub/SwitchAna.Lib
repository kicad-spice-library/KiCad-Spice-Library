Librería PSpice de switchs analógicos Cmos 4016/4066 & librería de muxs & demuxs Cmos para LtSpice 40151/40152/40153 de Robertugo_2005.
* Nota cada subcircuito representa a una sola de las cuatro llaves del chip.
.SUBCKT 54Hc4016 1 2 3 4 5
* 1 ANALOG INPUT
* 2 ANALOG OUTPUT
* 3 CONTROL
* 4 VDD (POSITIVE SUPPLY)
* 5 VSS (NEGATIVE SUPPLY)
*
RINP  3 11 500
CINP 11 5 4P
D1 11 4 D1
D2 5 11 D1
*
M1 12 11 4 4 MP3U L=3U W=10U AD=768P AS=768P PD=208U PS=208U
M2 12 11 5 5 MN3U L=3U W=4U AD=256P AS=256P PD=80U PS=80U
*
M3 13 12 4 4 MP3U L=3U W=30U AD=3600P AS=3600P PD=900U PS=900U
M4 13 12 5 5 MN3U L=3U W=10U AD=1080P AS=1080P PD=286U PS=286U
*
M5 14 13 4 4 MP3U L=3U W=150U AD=256P AS=256P PD=80U PS=80U
M6 14 13 5 5 MN3U L=3U W=50U AD=128P AS=128P PD=48U PS=48U
*TRANSMISSION GATE DEVICES 
M7 2 14 1  4 MP3U L=3U W=1500U AD=4500P AS=4500P PD=3000U PS=3000U
M8 1 13 2 20 MN3U L=3U W=500U  AD=2500P AS=2500P PD=1000U PS=1000U
M9 1 13 20 5 MN3U L=3U W=8U
M10 20  14 5 5 MN3U L=3U  W=8U
M11 20 14 1 4 MP3U L=3U W=32U
*
.MODEL MN3U NMOS LEVEL=3 VTO=1.0 TOX=5E-8 NSUB=1E17 
+XJ=1.5U LD=0.3U KP=40U RD=5 RS=5 CJ=3E-4 CJSW=3E-10
+KAPPA=1 THETA=1E-4 ETA=1.4 GAMMA=1.1 VMAX=4E5
*
.MODEL MP3U PMOS LEVEL=3 VTO=-1.0 TOX=5E-8 NSUB=3E15 
+XJ=0.7U LD=0.3U KP=18U RD=5 RS=5 CJ=1.8E-4 CJSW=2E-10
+KAPPA=.33 THETA=4E-2 ETA=0.6 GAMMA=0.6
.MODEL D1 D IS=923.17E-18 RS=10 CJO=1.0000E-12 M=.3333  VJ=.75
+ ISR=100.00E-12  BV=35.357  IBV=10U  TT=5.0000E-9
*
.ENDS
.SUBCKT 54Hc4066 1 2 3 4 5
X1 1 2 3 4 5 54Hc4016
.ENDS
.SUBCKT CD4016A 1 2 3 4 5
* Specifications from RCA (CD4016A) and National Semiconductor data books.
* Nat. Semi. part numbers:  CD4016C, CD4016M
* 1 ANALOG INPUT
* 2 ANALOG OUTPUT
* 3 CONTROL
* 4 VDD (POSITIVE SUPPLY)
* 5 VSS (NEGATIVE SUPPLY)
*
RINPUT  3 11 1500
CRIN 11 5 4.4P
D1 11 4 D1
D2 5 11 D1
*
MP1 12 11 4 4 MP4016A L=8U W=24U  AD=400P AS=400P PD=120U PS=120U
MN1 12 11 5 5 MN4016A L=8U W=8U  AD=128P AS=128P PD=48U PS=48U
*
M3 1 12 2 4  MP4016A L=8U W=360U AD=3600P AS=3600P PD=900U PS=900U
M4 2 11 1 5 MN4016A L=8U W=120U AD=1500P AS=1500P PD=326U PS=326U
*
.MODEL MN4016A NMOS LEVEL=2 VTO=1.45 TOX=1000E-10 NSUB=4.7E16
+XJ=3U LD=2U UO=625 UCRIT=1E5 UEXP=0.45 UTRA=0.25 RD=15 RS=15 
+NEFF=2.5 VMAX=1E6 CGBO=3E-10 CGDO=10E-10 CGSO=10E-10 CJSW=2F
+NFS=4E12  GAMMA=2.2 LAMBDA=0.02
*
.MODEL MP4016A PMOS LEVEL=2 VTO=-1.5 TOX=1000E-10 NSUB=7.6E15
+XJ=3U LD=1.5U UO=225 UCRIT=3E5 UEXP=.5 UTRA=0.25 RD=15 RS=15
+NEFF=2.5 VMAX=1E5 CGBO=3E-10 CGDO=10E-10 CGSO=10E-10 CJSW=1F
+NFS=1E12  GAMMA=1.8 LAMBDA=0.02
*
.MODEL D1 D IS=923.17E-18 RS=10 CJO=1.0000E-12 M=.3333  VJ=.75
+ ISR=100.00E-12  BV=35.357  IBV=10U  TT=5.0000E-9
*
.ENDS
.SUBCKT CD4016B 1 2 3 4 5
* Specifications Harris data books
* 1 ANALOG INPUT
* 2 ANALOG OUTPUT
* 3 CONTROL
* 4 VDD (POSITIVE SUPPLY)
* 5 VSS (NEGATIVE SUPPLY)
*
RINP  3 11 1500
D1 11 4 D1
D2 5 11 D1
*
M1 12 11 4 4 MP4016A L=8U W=48U  AD=500P AS=500P PD=110U PS=110U
M2 12 11 5 5 MN4016A L=8U W=16U  AD=160P AS=160P PD=48U PS=48U
*
M1A 13 12 4 4 MP4016A L=8U W=96U  AD=500P AS=500P PD=110U PS=110U
M2A 13 12 5 5 MN4016A L=8U W=32U  AD=160P AS=160P PD=48U PS=48U
*
M3 1 12 2 4  MP4016A L=8U W=360U AD=3600P AS=3600P PD=900U PS=900U
M4 2 13 1 5  MN4016A L=8U W=120U AD=1080P AS=1080P PD=286U PS=286U
*
.MODEL MN4016A NMOS LEVEL=2 VTO=1.45 TOX=1000E-10 NSUB=4.7E16
+XJ=3U LD=2U UO=625 UCRIT=1E5 UEXP=0.45 UTRA=0.25 RD=15 RS=15 
+NEFF=2.5 VMAX=1E6 CGBO=3E-10 CGDO=10E-10 CGSO=10E-10 CJSW=2F
+NFS=4E12  GAMMA=2.2 LAMBDA=0.02
*
.MODEL MP4016A PMOS LEVEL=2 VTO=-1.5 TOX=1000E-10 NSUB=7.6E15
+XJ=3U LD=1.5U UO=225 UCRIT=3E5 UEXP=.5 UTRA=0.25 RD=15 RS=15
+NEFF=2.5 VMAX=1E5 CGBO=3E-10 CGDO=10E-10 CGSO=10E-10 CJSW=1F
+NFS=1E12  GAMMA=1.8 LAMBDA=0.02
* 
.MODEL D1 D IS=923.17E-18 RS=10 CJO=1.0000E-12 M=.3333  VJ=.75
+ ISR=100.00E-12  BV=35.357  IBV=10U  TT=5.0000E-9
.ENDS
.SUBCKT HCF4016B 1 2 3 4 5
* Specifications from SGS
* 1 ANALOG INPUT
* 2 ANALOG OUTPUT
* 3 CONTROL
* 4 VDD (POSITIVE SUPPLY)
* 5 VSS (NEGATIVE SUPPLY)
*
RINP  3 11 1500
D1 11 4 D1
D2 5 11 D1
*
M1 12 11 4 4 MP4016A L=8U W=48U  AD=500P AS=500P PD=110U PS=110U
M2 12 11 5 5 MN4016A L=8U W=16U  AD=160P AS=160P PD=48U PS=48U
*
M1A 13 12 4 4 MP4016A L=8U W=96U  AD=500P AS=500P PD=110U PS=110U
M2A 13 12 5 5 MN4016A L=8U W=32U  AD=160P AS=160P PD=48U PS=48U
*
M3 1 12 2 4  MP4016A L=8U W=360U AD=3600P AS=3600P PD=900U PS=900U
M4 2 13 1 5  MN4016A L=8U W=120U AD=1080P AS=1080P PD=286U PS=286U
*
.MODEL MN4016A NMOS LEVEL=2 VTO=1.45 TOX=1000E-10 NSUB=4.7E16
+XJ=3U LD=2U UO=625 UCRIT=1E5 UEXP=0.45 UTRA=0.25 RD=15 RS=15 
+NEFF=2.5 VMAX=1E6 CGBO=3E-10 CGDO=10E-10 CGSO=10E-10 CJSW=2F
+NFS=4E12  GAMMA=2.2 LAMBDA=0.02
*
.MODEL MP4016A PMOS LEVEL=2 VTO=-1.5 TOX=1000E-10 NSUB=7.6E15
+XJ=3U LD=1.5U UO=225 UCRIT=3E5 UEXP=.5 UTRA=0.25 RD=15 RS=15
+NEFF=2.5 VMAX=1E5 CGBO=3E-10 CGDO=10E-10 CGSO=10E-10 CJSW=1F
+NFS=1E12  GAMMA=1.8 LAMBDA=0.02
* 
.MODEL D1 D IS=923.17E-18 RS=10 CJO=1.0000E-12 M=.3333  VJ=.75
+ ISR=100.00E-12  BV=35.357  IBV=10U  TT=5.0000E-9
.ENDS
.SUBCKT TLC4016 1 2 3 4 5
* MODEL FOR 54/74HC4016, 54/74HC4016, TLC4016, 54/74HC4066, 
* 54/74HC4066, and TLC4066
* 1 ANALOG INPUT
* 2 ANALOG OUTPUT
* 3 CONTROL
* 4 VDD (POSITIVE SUPPLY)
* 5 VSS (NEGATIVE SUPPLY)
*
RINP  3 11 500
CINP 11 5 4P
D1 11 4 D1
D2 5 11 D1
*
M1 12 11 4 4 MP3U L=3U W=10U AD=768P AS=768P PD=208U PS=208U
M2 12 11 5 5 MN3U L=3U W=4U AD=256P AS=256P PD=80U PS=80U
*
M3 13 12 4 4 MP3U L=3U W=30U AD=3600P AS=3600P PD=900U PS=900U
M4 13 12 5 5 MN3U L=3U W=10U AD=1080P AS=1080P PD=286U PS=286U
*
M5 14 13 4 4 MP3U L=3U W=150U AD=256P AS=256P PD=80U PS=80U
M6 14 13 5 5 MN3U L=3U W=50U AD=128P AS=128P PD=48U PS=48U
*TRANSMISSION GATE DEVICES 
M7 2 14 1  4 MP3U L=3U W=1500U AD=4500P AS=4500P PD=3000U PS=3000U
M8 1 13 2 20 MN3U L=3U W=500U  AD=2500P AS=2500P PD=1000U PS=1000U
M9 1 13 20 5 MN3U L=3U W=8U
M10 20  14 5 5 MN3U L=3U  W=8U
M11 20 14 1 4 MP3U L=3U W=32U
*
.MODEL MN3U NMOS LEVEL=3 VTO=1.0 TOX=5E-8 NSUB=1E17 
+XJ=1.5U LD=0.3U KP=40U RD=5 RS=5 CJ=3E-4 CJSW=3E-10
+KAPPA=1 THETA=1E-4 ETA=1.4 GAMMA=1.1 VMAX=4E5
*
.MODEL MP3U PMOS LEVEL=3 VTO=-1.0 TOX=5E-8 NSUB=3E15 
+XJ=0.7U LD=0.3U KP=18U RD=5 RS=5 CJ=1.8E-4 CJSW=2E-10
+KAPPA=.33 THETA=4E-2 ETA=0.6 GAMMA=0.6
.MODEL D1 D IS=923.17E-18 RS=10 CJO=1.0000E-12 M=.3333  VJ=.75
+ ISR=100.00E-12  BV=35.357  IBV=10U  TT=5.0000E-9
*
.ENDS
* Based on Toshiba TLC4066 IC.
.SUBCKT CD4066 1 2 3 4 5
* 1 ANALOG INPUT
* 2 ANALOG OUTPUT
* 3 CONTROL
* 4 VDD (POSITIVE SUPPLY)
* 5 VSS (NEGATIVE SUPPLY)
*
RINP  3 11 500
CINP 11 5 4P
D1 11 4 D1
D2 5 11 D1
*
M1 12 11 4 4 MP3U L=3U W=10U AD=768P AS=768P PD=208U PS=208U
M2 12 11 5 5 MN3U L=3U W=4U AD=256P AS=256P PD=80U PS=80U
*
M3 13 12 4 4 MP3U L=3U W=30U AD=3600P AS=3600P PD=900U PS=900U
M4 13 12 5 5 MN3U L=3U W=10U AD=1080P AS=1080P PD=286U PS=286U
*
M5 14 13 4 4 MP3U L=3U W=150U AD=256P AS=256P PD=80U PS=80U
M6 14 13 5 5 MN3U L=3U W=50U AD=128P AS=128P PD=48U PS=48U
*TRANSMISSION GATE DEVICES 
M7 2 14 1  4 MP3U L=3U W=1500U AD=4500P AS=4500P PD=3000U PS=3000U
M8 1 13 2 20 MN3U L=3U W=500U  AD=2500P AS=2500P PD=1000U PS=1000U
M9 1 13 20 5 MN3U L=3U W=8U
M10 20  14 5 5 MN3U L=3U  W=8U
M11 20 14 1 4 MP3U L=3U W=32U
*
.MODEL MN3U NMOS LEVEL=3 VTO=1.0 TOX=5E-8 NSUB=1E17 
+XJ=1.5U LD=0.3U KP=40U RD=5 RS=5 CJ=3E-4 CJSW=3E-10
+KAPPA=1 THETA=1E-4 ETA=1.4 GAMMA=1.1 VMAX=4E5
*
.MODEL MP3U PMOS LEVEL=3 VTO=-1.0 TOX=5E-8 NSUB=3E15 
+XJ=0.7U LD=0.3U KP=18U RD=5 RS=5 CJ=1.8E-4 CJSW=2E-10
+KAPPA=.33 THETA=4E-2 ETA=0.6 GAMMA=0.6
.MODEL D1 D IS=923.17E-18 RS=10 CJO=1.0000E-12 M=.3333  VJ=.75
+ ISR=100.00E-12  BV=35.357  IBV=10U  TT=5.0000E-9
*
.ENDS
*
*SRC=CD4066B;CD4066B;Analog Switch;;
.SUBCKT 4066 11 4 2 10 7
*Analog Switch In Out Control Vdd Vss
X1 2 6 10 7 INVERT
X2 6 1 10 7 INVERT
M1 14 6 7 7 CD4007N
M7 11 6 14 10 CD4007P
M3 11 1 14 14 CD4007N
M4 11 1 4 14 CD4007N
M8 11 6 4 10 CD4007P
.SUBCKT INVERT 1 2 3 4
* Inverter In Out Vcc Vss
M1 2 1 3 3 CD4007P
M2 2 1 4 4 CD4007N
.ENDS
.MODEL CD4007N NMOS (
+ LEVEL=1 VTO=1.44 KP=320u L=10u W=30u GAMMA=0 PHI=.6 LAMBDA=10m
+ RD=23.2 RS=90.1 IS=16.64p CBD=2.0p CBS=2.0p CGSO=0.1p CGDO=0.1p
+ PB=.8 TOX=1200n)

.MODEL CD4007P PMOS (
+ LEVEL=1 VTO=-1.2 KP=110u L=10U W=60U GAMMA=0 PHI=.6 LAMBDA=40m
+ RD=21.2 RS=62.2 IS=16.64P CBD=4.0P CBS=4.0P CGSO=0.2P CGDO=0.2P
+ PB=.8 TOX=1200N)
.ENDS
*	*	*	*	*	*	*	*	*	*	*	*	*
* CD4051 Cmos Analog & Digital Mux Demux 1 to 8. Subcircuit model featured By Robertugo_2005.
*
* The model needs two params: Vcc & Vel see the circuit example...
*	*	*	*	*	*	*	*	*	*	*	*	*
.Subckt 4051a A B C INH IO0 IO1 IO2 IO3 IO4 IO5 IO6 IO7 IOC V+ Vss Vee 
XU2 IO0 IOC Y0 V+ Vee 4066
XU3 IO1 IOC Y1 V+ Vee 4066
XU4 IO2 IOC Y2 V+ Vee 4066
XU5 IO3 IOC Y3 V+ Vee 4066
XU6 IO4 IOC Y4 V+ Vee 4066
XU7 IO5 IOC Y5 V+ Vee 4066
XU8 IO6 IOC Y6 V+ Vee 4066
XU9 IO7 IOC Y7 V+ Vee 4066
XU1 A B C INH Y0 Y1 Y2 Y3 Y4 Y5 Y6 Y7 VDD Vss 3To8 VDD={Vcc} SPEED={Vel} TRIPDT=5e-9
.Ends
*
.SUBCKT 3To8  A B C INH Y0 Y1 Y2 Y3 Y4 Y5 Y6 Y7  VDD VGND  vdd1={vdd} speed1={speed} tripdt1={tripdt}
.param td1=1e-9*(475)*5/{vdd1}*{speed1}
.param td2=1e-9*(375)*5/{vdd1}*{speed1}
*
XIN1  A Ai  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
XIN2  B Bi  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
XIN3  C Ci  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
XIN4  INH INHi  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
*
AINH   INHi 0 0 0 0  ENA 0 0  BUF  tripdt={tripdt1} 
AAN  Ai 0 0 0 0  An 0 0  BUF  tripdt={tripdt1} 
ABN  Bi 0 0 0 0  Bn 0 0  BUF  tripdt={tripdt1} 
ACN  Ci 0 0 0 0  Cn 0 0  BUF  tripdt={tripdt1} 
AY0  An Bn Cn ENA 0  0 Y0i 0  AND  tripdt={tripdt1}  td={td1} 
AY1  Ai Bn Cn ENA 0  0 Y1i 0  AND  tripdt={tripdt1}  td={td1} 
AY2  An Bi Cn ENA 0  0 Y2i 0  AND  tripdt={tripdt1}  td={td1} 
AY3  Ai Bi Cn ENA 0  0 Y3i 0  AND  tripdt={tripdt1}  td={td1} 
AY4  An Bn Ci ENA 0  0 Y4i 0  AND  tripdt={tripdt1}  td={td1}
AY5  Ai Bn Ci ENA 0  0 Y5i 0  AND  tripdt={tripdt1}  td={td1} 
AY6  An Bi Ci ENA 0  0 Y6i 0  AND  tripdt={tripdt1}  td={td1} 
AY7  Ai Bi Ci ENA 0  0 Y7i 0  AND  tripdt={tripdt1}  td={td1} 
*
XOUT1  Y0i Y0  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT2  Y1i Y1  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT3  Y2i Y2  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT4  Y3i Y3  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT5  Y4i Y4  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT6  Y5i Y5  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT7  Y6i Y6  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT8  Y7i Y7  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
.Ends
*
*	*	*	*	*	*	*	*	*	*	*	*	*
* CD4052 Cmos Analog & Digital Mux Demux 2x(1 to 4) subcircuit model featured By Robertugo_2005.*
*
* The model needs two params: Vcc & Vel see the circuit example...				*
*	*	*	*	*	*	*	*	*	*	*	*	*
.Subckt 4052a A B INH yio0 yio1 yio2 yio3 ycom xio0 xio1 xio2 xio3 xcom V+ Vss Vee 
* Optimus for full simulation of 4052. Symbol 4052B.asy
XU1 A B INH Y0 Y1 Y2 Y3 VDD Vss 2TO4 VDD={Vcc} SPEED={Vel} TRIPDT=5e-9
XU2 xio0 xcom Y0 V+ Vee 4066
XU3 xio1 xcom Y1 V+ Vee 4066
XU4 xio2 xcom Y2 V+ Vee 4066
XU5 xio3 xcom Y3 V+ Vee 4066
XU7 yio0 ycom Y0 V+ Vee 4066
XU8 yio1 ycom Y1 V+ Vee 4066
XU9 yio2 ycom Y2 V+ Vee 4066
XU10 yio3 ycom Y3 V+ Vee 4066
.Ends
*
.SUBCKT 2TO4  A B INH Y0 Y1 Y2 Y3  VDD VGND  vdd1={vdd} speed1={speed} tripdt1={tripdt}
.param td1=1e-9*(475)*5/{vdd1}*{speed1}
.param td2=1e-9*(375)*5.0/{vdd1}*{speed1}
*
XIN1  A Ai  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
XIN2  B Bi  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
XIN3  INH INHi  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
*
AINH  INHi 0 0 0 0  ENA 0   0  BUF  tripdt={tripdt1}  td={td2}
AA  Ai 0 0 0 0  An Ap 0  BUF  tripdt={tripdt1}  td={td1} 
AB  Bi 0 0 0 0  Bn Bp 0  BUF  tripdt={tripdt1}  td={td1} 
AY0  An Bn ENA 0 0  0 Y0i 0  AND  tripdt={tripdt1}  td={td1} 
AY1  Ap Bn ENA 0 0  0 Y1i 0  AND  tripdt={tripdt1}  td={td1} 
AY2  An Bp ENA 0 0  0 Y2i 0  AND  tripdt={tripdt1}  td={td1} 
AY3  Ap Bp ENA 0 0  0 Y3i 0  AND  tripdt={tripdt1}  td={td1} 
*
XOUT1  Y0i Y0  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT2  Y1i Y1  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT3  Y2i Y2  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT4  Y3i Y3  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
.Ends
*
.Subckt 4052_2a A B INH yio0 yio1 yio2 yio3 ycom xio0 xio1 xio2 xio3 xcom V+ Vss Vee
XX3 A B INH xio0 xio1 xio2 xio3 xcom V+ Vss Vee 4052halfa
XX4 A B INH yio0 yio1 yio2 yio3 ycom V+ Vss Vee 4052halfa
.Ends
*
.Subckt 4052halfa A B INH io0 io1 io2 io3 ioc V+ Vss Vee
* Optimus for 1/2 part simulation of full 4052. Use with symbol 4052Half.asy
XU1 A B INH Y0 Y1 Y2 Y3 VDD 0 2TO4 VDD={Vcc} SPEED=1.0 TRIPDT=5e-9
XU2 io0 ioc Y0 V+ Vee 4066
XU3 io1 ioc Y1 V+ Vee 4066
XU4 io2 ioc Y2 V+ Vee 4066
XU5 io3 ioc Y3 V+ Vee 4066
.Ends
*
*
*	*	*	*	*	*	*	*	*	*	*	*	*
* CD4053 Cmos Analog & Digital Mux Demux 3X (1 to 2). Subcircuit model featured By Robertugo_2005.
*
* The model needs two params: Vcc & Vel see the circuit example...
*	*	*	*	*	*	*	*	*	*	*	*	*
.Subckt 4053a A B C INH ax ay axOay bx by bxOby cx cy cxOcy V+ Vss Vee
* Optimus for full simulation of 4053.
XU2 A B C INH A0 A1 B0 B1 C0 C1 VDD Vss Deco53 VDD={Vcc} SPEED={Vel} TRIPDT=5e-9
XU3 ax axOay A0 V+ Vee 4066
XU4 ay axOay A1 V+ Vee 4066
XU1 bx bxOby B0 V+ Vee 4066
XU5 by bxOby B1 V+ Vee 4066
XU6 cx cxOcy C0 V+ Vee 4066
XU7 cy cxOcy C1 V+ Vee 4066
.Ends
*
.Subckt 4053_3a A B C INH ax ay axOay bx by bxOby cx cy cxOcy V+ Vss Vee
XX1 A inh ax ay axOay 4053_tercia
XX3 B inh bx by bxOby 4053_tercia
XX4 C inh cx cy cxOcy 4053_tercia
.Ends
*
.Subckt 4053_tercia I inh xio yio xoy
*Optimus for 1/3 or 2/3 simulation of full 4053. Use with symbol 4053_tercio.asy
XU1 I INH Y0 Y1 VDD 0 1To2 VDD={Vcc} SPEED=1.0 TRIPDT=5e-9
XU2 xio xOy Y0 V+ Vee 4066
XU3 yio xOy Y1 V+ Vee 4066
.Ends
*
.SUBCKT Deco53  A B C INH A0 A1 B0 B1 C0 C1 VDD VGND  vdd1={vdd} speed1={speed} tripdt1={tripdt}
.param td1=1e-9*(475)*5/{vdd1}*{speed1}
.param td2=1e-9*(375)*5.0/{vdd1}*{speed1}
*
XIN1  A Ai  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
XIN2  B Bi  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
XIN3  C Ci  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
XIN4  INH INHi  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
*
AINH   INHi 0 0 0 0  ENA 0 0  BUF  tripdt={tripdt1} 
AAN  Ai 0 0 0 0  An Ap 0  BUF  tripdt={tripdt1} 
ABN  Bi 0 0 0 0  Bn Bp 0  BUF  tripdt={tripdt1} 
ACN  Ci 0 0 0 0  Cn Cp 0  BUF  tripdt={tripdt1} 
AA0  An ENA 0  0  0  0 A0i 0  AND  tripdt={tripdt1}  td={td1} 
AA1  Ap ENA 0  0  0  0 A1i 0  AND  tripdt={tripdt1}  td={td1} 
AB0  Bn ENA 0  0  0  0 B0i 0  AND  tripdt={tripdt1}  td={td1} 
AB1  Bp ENA 0  0  0  0 B1i 0  AND  tripdt={tripdt1}  td={td1} 
AC0  Cn ENA 0  0  0  0 C0i 0  AND  tripdt={tripdt1}  td={td1}
AC1  Cp ENA 0  0  0  0 C1i 0  AND  tripdt={tripdt1}  td={td1} 

*
XOUT1  A0i A0  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT2  A1i A1  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT3  B0i B0  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT4  B1i B1  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT5  C0i C0  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT6  C1i C1  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
.Ends
*
.subckt 4051 A B C INH IO0 IO1 IO2 IO3 IO4 IO5 IO6 IO7 IOC V+ Vss Vee
XU1 A B C INH Y0 Y1 Y2 Y3 Y4 Y5 Y6 Y7 VDD Vss 3To8 VDD={Vcc} SPEED={Vel} TRIPDT=5e-9
S0 IO0 IOC Y0 Vss Sw
S1 IO1 IOC Y1 Vss Sw
S2 IO2 IOC Y2 Vss Sw
S3 IO3 IOC Y3 Vss Sw
S4 IO4 IOC Y4 Vss Sw
S5 IO5 IOC Y5 Vss Sw
S6 IO6 IOC Y6 Vss Sw
S7 IO7 IOC Y7 Vss Sw
.model Sw Sw(Ron=1e2 Roff=1e9 Vt={Vcc/2} Vh=1m)
.ends
*
.Subckt 4052 A B INH yio0 yio1 yio2 yio3 ycom xio0 xio1 xio2 xio3 xcom V+ Vss Vee 
* Optimus for full simulation of 4052. Symbol 4052B.asy
XU1 A B INH Y0 Y1 Y2 Y3 VDD Vss 2TO4 VDD={Vcc} SPEED={Vel} TRIPDT=5e-9
S0 xio0 xcom Y0 Vss Sw
S1 xio1 xcom Y1 Vss Sw
S2 xio2 xcom Y2 Vss Sw
S3 xio3 xcom Y3 Vss Sw
S4 yio0 ycom Y0 Vss Sw
S5 yio1 ycom Y1 Vss Sw
S6 yio2 ycom Y2 Vss Sw
S7 yio3 ycom Y3 Vss Sw
.model Sw Sw(Ron=1e2 Roff=1e9 Vt={Vcc/2} Vh=1m)
.Ends
*
.Subckt 4052_2 A B INH yio0 yio1 yio2 yio3 ycom xio0 xio1 xio2 xio3 xcom V+ Vss Vee
XX3 A B INH xio0 xio1 xio2 xio3 xcom V+ Vss Vee 4052half
XX4 A B INH yio0 yio1 yio2 yio3 ycom V+ Vss Vee 4052half
.Ends
*
.Subckt 4052half A B INH io0 io1 io2 io3 ioc V+ Vss Vee
* Optimus for 1/2 part simulation of full 4052. Use with symbol 4052Half.asy
XU1 A B INH Y0 Y1 Y2 Y3 VDD 0 2TO4 VDD={Vcc} SPEED=1.0 TRIPDT=5e-9
S0 io0 ioc Y0 Vss Sw
S1 io1 ioc Y1 Vss Sw
S2 io2 ioc Y2 Vss Sw
S3 io3 ioc Y3 Vss Sw
.model Sw Sw(Ron=1e2 Roff=1e9 Vt={Vcc/2} Vh=1m)
.Ends
*
.Subckt 4053 A B C INH ax ay axOay bx by bxOby cx cy cxOcy V+ Vss Vee
* Optimus for full simulation of 4053.
XU2 A B C INH A0 A1 B0 B1 C0 C1 VDD Vss Deco53 VDD={Vcc} SPEED={Vel} TRIPDT=5e-9
S0 ax axOay A0 Vss Sw
S1 ay axOay A1 Vss Sw
S2 bx bxOby B0 Vss Sw
S3 by bxOby B1 Vss Sw
S4 cx cxOcy C0 Vss Sw
S5 cy cxOcy C1 Vss Sw
.model Sw Sw(Ron=1e2 Roff=1e9 Vt={Vcc/2} Vh=1m)
.Ends
*
.Subckt 4053_3 A B C INH ax ay axOay bx by bxOby cx cy cxOcy V+ Vss Vee
XX1 A inh ax ay axOay 4053_tercio
XX3 B inh bx by bxOby 4053_tercio
XX4 C inh cx cy cxOcy 4053_tercio
.Ends
*
.Subckt 4053_tercio I inh xio yio xoy
*Optimus for 1/3 or 2/3 simulation of full 4053. Use with symbol 4053_tercio.asy
XU1 I INH Y0 Y1 VDD 0 1To2 VDD={Vcc} SPEED=1.0 TRIPDT=5e-9
S0 xio xOy Y0 Vss Sw
S1 yio xOy Y1 Vss Sw
.model Sw Sw(Ron=1e2 Roff=1e9 Vt={Vcc/2} Vh=1m)
.Ends
*
.SUBCKT 1TO2  I INH Y0 Y1 VDD VGND  vdd1={vdd} speed1={speed} tripdt1={tripdt}
.param td1=1e-9*(475)*5/{vdd1}*{speed1}
.param td2=1e-9*(375)*5.0/{vdd1}*{speed1}
XIN1  I Ii  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1} 
XIN2  INH INHi  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1}
AINH  INHi 0 0 0 0  ENA 0   0  BUF  tripdt={tripdt1}  td={td2}
AI  Ii 0 0 0 0  In Ip 0  BUF  tripdt={tripdt1}  td={td1}
AY0  In ENA 0 0 0  0 Y0i 0  AND  tripdt={tripdt1}  td={td1} 
AY1  Ip ENA 0 0 0  0 Y1i 0  AND  tripdt={tripdt1}  td={td1}
XOUT1  Y0i Y0  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
XOUT2  Y1i Y1  VDD VGND  CD40_OUT_1X  vdd2={vdd1} speed2={speed1}  tripdt2={tripdt1}
.Ends
*
*
*	*	*	*	*	*	*	*	*	*	*	*
*	Helmut Sennewald  I/O Translators adapters subcircuits.	*
*	*	*	*	*	*	*	*
*
.SUBCKT  CD40_IN_1  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2} 
* 3ns input delay
*.param Cval = 0.55e-12*4/({vdd3}-0.5)*{speed3}
* 10ns delay @5V
.param Cval = 1.8e-12*5/{vdd3}*{speed3}
.param vt1=0.5
.param gain=(1/{vdd3})
*
D1 0   in  CD40DIO1 
D2 in VDD  CD40DIO1
R1 in out10 10k
C1 out10 VGND {Cval}
R2 in VGND 1e8
*E1 out20 0 out10 VGND {gain}
B1 out20 0 V=LIMIT(0,V(out10,VGND)*{gain},1)
AE1  out20 0 0 0 0  0 out 0  BUF  ref={vt1}  vhigh=1  tripdt={tripdt3}
.MODEL CD40DIO1 D(Is=1e-12 Rs=100)
.MODEL DIO2 D(Is=1e-12 Rs=10)
.ends
*
* Standard output driver
.SUBCKT  CD40_OUT_1X  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param trise1=80e-9*5.0/{vdd3}*{speed3}
.param Rout=500*5.0/{vdd3}*{speed3}
*
AE1  in 0 0 0 0  0 out10 0  BUF  tripdt={tripdt3}  trise={trise1}
*
E1 out20 VGND out10 0 {vdd3}
Rout out20 out {Rout}
D1 0   out  DIO2 
D2 out VDD  DIO2
.MODEL CD40DIO1 D(Is=1e-12 Rs=100)
.MODEL DIO2 D(Is=1e-12 Rs=10 )
.ends
.SUBCKT ANALOGSW X Y CONT VDD GND
M_MP6         N_2 N_1 VDD  VDD PMOS L=1u W=285u AD=0.43n AS=0.52n
PD=0.326m PS=0.392m NRD=3.509m NRS=3.509m
M_MN8         N_2 N_1 GND  0 NMOS L=1u W=57u AD=0.09n AS=0.17n
PD=72.69u PS=0.132m  NRD=17.544m NRS=17.544m
M_MN7         X N_3 Y  0 NMOS L=1u W=360u AD=0.55n AS=0.82n PD=0.424m
PS=0.625m  NRD=2.778m NRS=2.778m
M_MP5         X N_2 Y  VDD PMOS L=1u W=1260u AD=0.00u AS=0.00u
PD=1.382m PS=1.579m NRD=0.794m NRS=0.794m
M_MP14         N_3 N_4 VDD  VDD PMOS L=1u W=285u AD=0.43n AS=0.52n
PD=0.326m PS=0.392m NRD=3.509m NRS=3.509m
M_MN16         N_3 N_4 GND  0 NMOS L=1u W=57u AD=0.09n AS=0.17n
PD=72.69u PS=0.132m  NRD=17.544m NRS=17.544m
M_MP7         N_4 CONT VDD  VDD PMOS L=1u W=180u AD=0.27n AS=0.30n
PD=0.213m PS=0.234m NRD=5.556m NRS=5.556m
M_MN9         N_4 CONT GND  0 NMOS L=1u W=36u AD=0.05n AS=0.08n
PD=48.37u PS=62.53u  NRD=27.778m NRS=27.778m
M_MP9         N_1 N_4 VDD  VDD PMOS L=1u W=180u AD=0.27n AS=0.30n
PD=0.213m PS=0.234m NRD=5.556m NRS=5.556m
M_MN11         N_1 N_4 GND  0 NMOS L=1u W=36u AD=0.05n AS=0.08n
PD=48.37u PS=62.53u  NRD=27.778m NRS=27.778m
.MODEL NMOS NMOS (LEVEL=3  VTO= 0.600  UO=515.0  TOX= 15.0N
+ NSUB= 4.46E+16  NFS = 5.6E+12  VMAX= 7.7E+05  ETA=0.022  KAPPA= 0.00
+ THETA=0.092  DELTA= 3.09  XJ = 0.59U  RSH = 725.0  LD = 0.135U
+ CGDO= 3.0E-10  CGSO= 3.0E-10  AF= 1.0  KF= 7.5E-29  CJ = 4.3E-04
+ MJ = 0.350  CJSW= 3.8E-10  MJSW= 0.290  PB = 0.710)
**
.MODEL PMOS PMOS (LEVEL=3  VTO=-0.700  UO=180.0  TOX= 15.0N
+ NSUB= 7.19E+16  NFS = 1.1E+12  VMAX= 7.8E+05  ETA=0.033  KAPPA=83.50
+ THETA=0.161  DELTA= 1.54  XJ = 0.00U  RSH = 996.0  LD = 0.095U
+ CGDO= 2.7E-10  CGSO= 2.7E-10  AF= 1.0  KF= 1.4E-30  CJ = 3.6E-04
+ MJ = 0.280  CJSW= 3.0E-10  MJSW= 0.380  PB = 0.450)
**
.ENDS ANALOGSW