.SUBCKT PP_OUTPUT_XFRMR 1 2 3 4 5 6 7 8 9  ; PP UL Transformer
+PARAMS: LPRIM=68 LLKG=.040 RPRIM=125 CPRIM=1.04NF ZPRIM=5000 TAPPOS=0.4

* LPRIM  IS THE TOTAL PRIMARY L (VARIES WITH MEASUREMENT).
* LLKG   IS THE LEAKAGE L (MEASURABLE: CONSISTENT).
* RPRIM  IS THE TOTAL PRIMARY R.
* CPRIM  IS THE MEASURED PRIMARY CAPACITANCE.
* TAPPOS IS THE GRID TAP POSITION
* ZPRIM  IS THE PRIMARY Z

.PARAM QFCTR={LPRIM/LLKG}  ; Q-FACTOR.
.PARAM PTAP={1-TAPPOS}  ; PRIMARY TAP POSITION
.PARAM LRATIO={4/ZPRIM}    ; INDUCTANCE RATIO: (LOAD OHMS)/(PRIMARY Z).

CS1 1 5    {CPRIM}     ; PRIMARY CAPACITANCE
RS1 1 5    300K        ; SHUNT R FOR HIGH FREQUENCY EFFECTS.

LP1 1 12   {LPRIM*PTAP/2} ; PRIMARY
RP1 12 2   {RPRIM*PTAP/2}
LP2 2 32   {LPRIM*TAPPOS/2}
RP2 32 3   {RPRIM*TAPPOS/2}
LP3 3 34   {LPRIM*TAPPOS/2}
RP3 34 4   {RPRIM*TAPPOS/2}
LP4 4 45   {LPRIM*PTAP/2}
RP4 45 5   {RPRIM*PTAP/2}

LP5 7 6    {.34315*LPRIM*LRATIO}  ; SECONDARY
LP6 8 7    {.17157*LPRIM*LRATIO}
LP7 9 8    {LPRIM*LRATIO}

KALL LP1 LP2 LP3 LP4 LP5 LP6 LP7 {1-1/(2*QFCTR)}  ; COUPLING
.ENDS PP_OUTPUT_XFRMR 
