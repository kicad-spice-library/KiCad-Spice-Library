******************
* Pentode  models by Norman Koren
******************


******************
.SUBCKT 6AC7  1 2 3 4 ; A G2 G1 C (Pentode)
* RCA Data sheet
* library format: LTSpice 11-Nov-2014
X1 1 2 3 4 PENTODE1 MU=49.00 EX=1.350 KG1=334.9 KG2=486 KP=211.87 KVB=59.7 VCT=0.00 RGI=2000 CCG=11p CPG1=0.015p CCP=5p ;
.ENDS    6AC7 

******************
.SUBCKT 6AG7  1 2 3 4 ; A G2 G1 C (Pentode)
* GE Data sheet
* library format: LTSpice 11-Nov-2014
X1 1 2 3 4 PENTODE1 MU=27.90 EX=0.971 KG1=169.6 KG2=298 KP=293.92 KVB=114.2 VCT=0.00 RGI=2000 CCG=5.2p CPG1=0.060p CCP=7.5p ;
.ENDS    6AG7 


******************
.SUBCKT 5879  1 2 3 4 ; A G2 G1 C (Pentode)
* RCA data sheet
* library format: LTSpice 14-Nov-2009
X1 1 2 3 4 PENTODE1 MU=22.01 EX=1.395 KG1=3488.1 KG2=4500 KP=117.73 KVB=15.1 VCT=0.00 RGI=2000 CCG=4.3p CPG1=0.6p CCP=5.1p ;
.ENDS    5879 

******************
.SUBCKT  6267 1 2 3 4 ; A G2 G1 C (Pentode)
*  same as EF86
X1 1 2 3 4 EF86
.ENDS	6267 

******************
.SUBCKT 6AK5 1 2 3 4 ; A G2 G1 C (Pentode)
* Telefunken data sheet  AKA 5654, EF95
* library format: LTSpice 17-Apr-2010
X1 1 2 3 4 EF95 ;
.ENDS    6AK5
 
******************
.SUBCKT 6AU6  1 2 3 4 ; A G2 G1 C (Pentode)
* GE data sheet AKA EF94 6AU6A
* library format: LTSpice 17-Apr-2010
X1 1 2 3 4 PENTODE1 MU=36.65 EX=1.376 KG1=1129.4 KG2=1059 KP=215.42 KVB=15.0 VCT=0.00 RGI=2000 CCG=4.3p CPG1=0.6p CCP=5.1p ;
.ENDS    6AU6 

******************
.SUBCKT 6AU6A  1 2 3 4 ; A G2 G1 C (Pentode)
* GE data sheet AKA EF94 6AU6
* library format: LTSpice 17-Apr-2010
X1 1 2 3 4 6AU6
.ENDS    6AU6A 

******************
.SUBCKT  6CF8 1 2 3 4 ; A G2 G1 C (Pentode)
*  same as EF86
X1 1 2 3 4 EF86
.ENDS	6CF8

******************
.SUBCKT  6BK8 1 2 3 4 ; A G2 G1 C (Pentode)
*  same as EF86
X1 1 2 3 4 EF86
.ENDS	6BK8

******************
.SUBCKT  6BY7 1 2 3 4 ; A G2 G1 C (Pentode)
*  same as EF85
X1 1 2 3 4 EF85
.ENDS	6BY7

******************
.SUBCKT  6DA6 1 2 3 4 ; A G2 G1 C (Pentode)
*  same as EF89
X1 1 2 3 4 EF89
.ENDS	6DA6
 
******************
.SUBCKT 814  1 2 3 4 ; A G2 G1 C (Pentode)
* RCA
* library format: LTSpice 20-Apr-2014
X1 1 2 3 4 PENTODE1 MU=9.52 EX=1.350 KG1=2981.5 KG2=9293 KP=421.77 KVB=84.3 VCT=0.00 RGI=1000 CCG=13.5p CPG1=0.150p CCP=13.5p ;
.ENDS    814 

******************
.SUBCKT 6686  1 2 3 4 ; A G2 G1 C (Pentode) AKA E81L, EL861
* Philips data sheet
* library format: LTSpice 15-Nov-2009
X1 1 2 3 4 E81L ; CALL E81L
.ENDS    6686

******************
.SUBCKT 6973  1 2 3 4 ; A G2 G1 C (Pentode)
* RCA data sheet
* library format: LTSpice 14-Nov-2009
X1 1 2 3 4 PENTODE1 MU=13.58 EX=1.350 KG1=1142.2 KG2=4500 KP=27.70 KVB=18.4 VCT=0.00 RGI=2000 CCG=4.3p CPG1=0.6p CCP=5.1p ;
.ENDS    6973 

******************
.SUBCKT 7189  1 2 3 4 ; A G2 G1 C (Pentode)
* Mullard Data Sheet AKA 6BQ5 EL84 N709
* library format: LTSpice 02-Jun-2008
X1 1 2 3 4 6BQ5 ; call 6BQ5
.ENDS    7189 

******************
.SUBCKT 7868  1 2 3 4 ; A G2 G1 C (Pentode)
* RCA data sheet
* library format: LTSpice 24-Nov-2009
X1 1 2 3 4 PENTODE1 MU=25.18 EX=1.350 KG1=401.2 KG2=4500 KP=55.60 KVB=24.9 VCT=0.00 RGI=2000 CCG=4.3p CPG1=0.6p CCP=5.1p ;
.ENDS    7868

******************
.SUBCKT 6CK5  1 2 3 4 ; A G2 G1 C (Pentode) AKA EL41 BF61 N150 6CK5
* Philips data sheet
* library format: LTSpice 16-Nov-2009
X1 1 2 3 4 EL80 ; Call EL80
.ENDS    6CK5 

******************
.SUBCKT 6CL6  1 2 3 4 ; A G2 G1 C (PENTODE)
* RCA
* library format: LTSpice 12-Oct-2011
X1 1 2 3 4 PENTODE1 MU=20.64 EX=1.351 KG1=469.3 KG2=812 KP=159.49 KVB=67.9 VCT=0.00 RGI=2000 CCG=5.7p CPG1=0.560p CCP=2.5p ;
.ENDS    6CL6 

******************
.SUBCKT 6EH5  1 2 3 4 ; A G2 G1 C (Pentode)
* GE data sheet
* library format: LTSpice 14-Nov-2009
X1 1 2 3 4 PENTODE1 MU=16.46 EX=1.350 KG1=415.4 KG2=4500 KP=85.16 KVB=13.4 VCT=0.00 RGI=2000 CCG=4.3p CPG1=0.6p CCP=5.1p ;
.ENDS    6EH5
 












******************
.SUBCKT EF80  1 2 3 4 ; A G2 G1 C (Pentode)
* Philips data sheet AKA 6BX6 
* library format: LTSpice 05-Feb-2009
X1 1 2 3 4 PENTODE2 MU=49.11 EX=1.420 KG1=667.0 KG2=4500 KP=375.96 KVB=42.9 VCT=0.50 RGI=2000 CCG=4.5p CPG1=0.6p CCP=5.6p ; 
.ENDS    EF80

******************
.SUBCKT EF83  1 2 3 4 ; A G2 G1 C (Pentode)
* Philips data sheet  
* library format: LTSpice 30-May-2008
X1 1 2 3 4 PENTODE1 MU=12.01 EX=1.350 KG1=2830.0 KG2=4500 KP=35.24 KVB=39.1 VCT=0.00 RGI=2000 CCG=4.5p CPG1=0.6p CCP=5.6p ; 
.ENDS    EF83

******************
.SUBCKT EF85  1 2 3 4 ; A G2 G1 C (Pentode)
* La RadioTechique 
* library format: LTSpice 30-May-2008
X1 1 2 3 4 PENTODE2 MU=22.39 EX=1.350 KG1=974.2 KG2=4500 KP=160.84 KVB=40.9 VCT=0.00 RGI=2000 CCG=4.5p CPG1=0.6p CCP=5.6p ; 
.ENDS    EF85

******************
.SUBCKT EF86  1 2 3 4 ; A G2 G1 C (Pentode)
* Philips data sheet AKA 6CF8 and 6267 Z729 EF806
* library format: LTSpice 02-Jun-2008
X1 1 2 3 4 PENTODE1 MU=34.90 EX=1.350 KG1=2648.1 KG2=4500 KP=222.06 KVB=4.7 VCT=0.00 RGI=2000 CCG=4.3p CPG1=0.6p CCP=5.1p ; 
.ENDS    EF86

******************
.SUBCKT EF89  1 2 3 4 ; A G2 G1 C (Pentode)
* La Radiotechnique AKA 6DA6
* library format: LTSpice 30-May-2008
X1 1 2 3 4 PENTODE1 MU=30.52 EX=1.350 KG1=926.7 KG2=4500 KP=39.49 KVB=23.6 VCT=0.00 RGI=2000 CCG=5.5p CPG1=0.0p CCP=5.1p ; 
.ENDS    EF89

******************
.SUBCKT 6CA7 1 2 3 4 ; A G2 G1 C (Pentode) AKA EL34
* Mullard
X1 1 2 3 4 EL34 ; call the el34
.ENDS	6CA7

******************
.SUBCKT EL34  1 2 3 4 ; P G1 C G2 (Pentode)
* Mullard Data Book Apr 1962 
* library format: PSPICE 21-Sep-2008
X1 1 2 3 4 PENTODE1 MU=12.02 EX=1.169 KG1=353.9 KG2=4500 KP=61.11 KVB=29.9 VCT=0.00 RGI=1000 CCG=15.0p CPG1=1.0p CCP=8.0p ; 
.ENDS    EL34

******************
.SUBCKT 6BQ5  1 2 3 4 ; A G2 G1 C (Beam Tetrode)
* Mullard Data Sheet AKA 7189 EL84 N709 
* library format: LTSpice 02-Jun-2008
X1 1 2 3 4 PENTODE1 MU=21.29 EX=1.240 KG1=401.7 KG2=4500 KP=111.04 KVB=17.9 VCT=0.00 RGI=1000 CCG=10.0p CPG1=0.6p CCP=5.1p ; 
.ENDS    6BQ5

******************
.SUBCKT 6L6GB 1 2 3 4 ; A G2 G1 C (Beam Tetrode)
* Sylvania
X1 1 2 3 4 PENTODE MU=8.77 EX=1.265 KG1=1210.3 KP=47.50 KG2=4500 KVB=11.6 VCT=0.00 CCG=14P CPG1=.85P CCP=12P RGI=1K
.ENDS	6L6GB

******************
.SUBCKT 6L6GC  1 2 3 4 ; A G2 G1 C (Beam Tetrode)
* GE Data sheet 
* library format: LTSpice 07-Oct-2008
X1 1 2 3 4 PENTODE1 MU=9.88 EX=1.442 KG1=1686.6 KG2=4500 KP=30.98 KVB=19.4 VCT=0.00 RGI=1000 CCG=10.0p CPG1=0.6p CCP=6.5p ; 
.ENDS    6L6GC

******************
.SUBCKT 6L6WGT 1 2 3 4 ; A G2 G1 C (Beam Tetrode)
* same as 6L6GB
X1 1 2 3 4 PENTODE MU=8.77 EX=1.265 KG1=1210.3 KP=47.50 KG2=4500 KVB=11.6 VCT=0.00 CCG=14P CPG1=.85P CCP=12P RGI=1K
.ENDS	6L6WGT

******************
.SUBCKT EL84  1 2 3 4 ; A G2 G1 C (Beam Tetrode)
* Mullard Data Sheet AKA 7189 6BQ5 N709 
* library format: LTSpice 02-Jun-2008
X1 1 2 3 4 PENTODE1 MU=21.29 EX=1.240 KG1=401.7 KG2=4500 KP=111.04 KVB=17.9 VCT=0.00 RGI=1000 CCG=10.0p CPG1=0.6p CCP=5.1p ; 
.ENDS    EL84

******************
.SUBCKT KT66  1 2 3 4 ; A G2 G1 C (Beam Tetrode)
* M-O Valve Co AKA Genalex 
* library format: LTSpice 21-Sep-2008
X1 1 2 3 4 PENTODE1 MU=11.68 EX=1.197 KG1=510.9 KG2=4500 KP=34.89 KVB=22.3 VCT=0.00 RGI=1000 CCG=16.0p CPG1=2.3p CCP=10.0p ; 
.ENDS    KT66

******************
.SUBCKT KT77  1 2 3 4 ; A G2 G1 C (Beam Tetrode)
* The M-O Valve Co AKA Genalex 
* library format: LTSpice 19-Sep-2008
X1 1 2 3 4 PENTODE1 MU=12.49 EX=1.487 KG1=1058.5 KG2=4500 KP=56.64 KVB=19.4 VCT=0.00 RGI=1000 CCG=16.5p CPG1=1.0p CCP=9.0p ; 
.ENDS    KT77

******************
.SUBCKT KT88  1 2 3 4 ; A G2 G1 C (Beam Tetrode)
* The M-O Valve Co AKA Genalex 
* library format: LTSpice 19-Sep-2008
X1 1 2 3 4 PENTODE1 MU=12.38 EX=1.246 KG1=340.4 KG2=4500 KP=26.48 KVB=36.5 VCT=0.00 RGI=1000 CCG=14.0p CPG1=0.8p CCP=12.0p ; 
.ENDS    KT88

******************
.SUBCKT EL509  1 2 3 4 ; A G2 G1 C (Beam Tetrode)
* Svetlana's Data sheet AKA 6KG6, 6P45S
* library format: LTSpice 29-May-2008
X1 1 2 3 4 PENTODE1 MU=8.08 EX=1.350 KG1=263.9 KG2=4500 KP=11.68 KVB=42.1 VCT=0.00 RGI=1000 CCG=10.0p CPG1=3.0p CCP=6.5p ; 
.ENDS    EL509

******************
.SUBCKT PENTODE 1 2 3 4 ; A G2 G1 C
RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE={V(2,4)/KP*LOG(1+EXP((1/MU+V(3,4)/V(2,4))*KP))}	; E1 BREAKS UP LONG EQUATION FOR G1.
G1   1 4  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,4)/KVB)}
G2   2 4  VALUE={(EXP(EX*(LOG((V(2,4)/MU)+V(3,4)))))/KG2}
* G2   2 4  VALUE={PWR(V(2,4)/MU+V(3,4),EX)/KG2}
RCP  1 4  1G      ; FOR CONVERGENCE	A  - C
C1   3 4  {CCG}   ; CATHODE-GRID 1	C  - G1
C2   1 3  {CPG1}  ; GRID 1-PLATE	G1 - A
C3   1 4  {CCP}   ; CATHODE-PLATE	A  - C
R1   3 5  {RGI}   ; FOR GRID CURRENT	G1 - 5
D3   5 4  DX      ; FOR GRID CURRENT	5  - C
.ENDS PENTODE

******************
.SUBCKT PENTODE1 1 2 3 4 ; A G2 G1 C
RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE={V(2,4)/KP*LOG(1+EXP((1/MU+V(3,4)/V(2,4))*KP))}	; E1 BREAKS UP LONG EQUATION FOR G1.
G1   1 4  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,4)/KVB)}
G2   2 4  VALUE={(EXP(EX*(LOG((V(2,4)/MU)+V(3,4)))))/KG2}
* G2   2 4  VALUE={PWR(V(2,4)/MU+V(3,4),EX)/KG2}
RCP  1 4  1G      ; FOR CONVERGENCE	A  - C
C1   3 4  {CCG}   ; CATHODE-GRID 1	C  - G1
C2   1 3  {CPG1}  ; GRID 1-PLATE	G1 - A
C3   1 4  {CCP}   ; CATHODE-PLATE	A  - C
R1   3 5  {RGI}   ; FOR GRID CURRENT	G1 - 5
D3   5 4  DX      ; FOR GRID CURRENT	5  - C
.ENDS PENTODE1

******************
.SUBCKT PENTODE2 1 2 3 4 ; A G2 G1 C
RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE={V(2,4)/KP*LOG(1+EXP((1/MU+V(3,4)/V(2,4))*KP))}	; E1 BREAKS UP LONG EQUATION FOR G1.
* G1   1 4  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,4)/KVB)}
G1   1 4  VALUE={pi/2*(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*(1-exp(-2*V(1,4)/(pi*KVB)))}
G2   2 4  VALUE={(EXP(EX*(LOG((V(2,4)/MU)+V(3,4)))))/KG2}
* G2   2 4  VALUE={PWR(V(2,4)/MU+V(3,4),EX)/KG2}
RCP  1 4  1G      ; FOR CONVERGENCE	A  - C
C1   3 4  {CCG}   ; CATHODE-GRID 1	C  - G1
C2   1 3  {CPG1}  ; GRID 1-PLATE	G1 - A
C3   1 4  {CCP}   ; CATHODE-PLATE	A  - C
R1   3 5  {RGI}   ; FOR GRID CURRENT	G1 - 5
D3   5 4  DX      ; FOR GRID CURRENT	5  - C
.ENDS PENTODE2

******************
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N) ; diode used by the Pentode models