* Created in LTspice Version 4.13h                              *
*                                            					*
* GaN Systems Inc. Power Transistors                            *
* LTSpice Library for GaN Transistors                           *
* Level 1  Version 2.0                                          *
*                                                               *
*****************************************************************
*****************************************************************
*                                                               *
* Models provided by GaN Systems Inc. are not warranted by      *
* GaN Systems Inc. as                                           *
* fully representing all of the specifications and operating    *
* characteristics of the semiconductor product to which the     *
* model relates. The model describe the characteristics of a    *
* typical device.                                               *
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. GaN Systems Inc. therefore   *
* does not assume any liability arising from their use.         *
* GaN Systems Inc. reserves the right to change models without  *
* prior notice.                                                 *
*                                                               *
* This library contains models of the following GaN Systems     *
* Inc. transistors:                                             *
*                                                               *
*   GS66516T                                                    *
*****************************************************************
*Level 1 Version 2.0_1. This level focus on the simulation speed.
*The thermal model, leakage current, and stray inductance in the package are NOT included.
*Parenthesis issue solved.
*
.subckt GS66516T_L1V2_1 gatein drainin sourcein
*
.param conv_aide=1
.param Rth_CasetoAmbient=0
*
.param aDi=0.25       cur={(0.85/3.6)*(0.069*75/80)*592/1}   slp=2.0   rpara=0.82
+      ITc=0.003       	  rTc=-0.0128      		       x0_0=0.31		   x0_1=0.255
+      di_gs1={7*4.3e-5}  di_gs2={2.6e-8}  	       di_gs3={100*0.8}    di_gs4={80*0.23}
+      Igs1=1.42e-10   	  Igs2={(3.0e-010)*(5.7)/20}       Igs3=4.9 	   Igs4=6.83e-01
+      Igs5=-7.85e-011    Igs6=-3.30     	       Igs7=6.0
+      Igd1=5.49e-012  	  Igd2={2.6e-11*(7.5)/3}  	 Igd3=-3.09	   Igd4=12
+      Isd1=1.7e-013 	  Isd2=1e-12  	  	Isd3=0 	 Isd4=2.5
+      Isd5=5e-013 	  Isd6=10 	      	Isd7=4.5 	 of1=100 	of2=35
+	 ff1=0.345		  ff2=1.2 		  	ff3=4.5 	 ff4=0.5 	ff5=8    ff6=0.14
*
*
rd drainin drain {(3.6/4)*(0.95*rpara*(1-0*rTC*(Temp-25)))*18.2/592} tc=0.0135
rs sourcein source {(1*3.6)*(0.238*rpara*(1-0*rTc*(Temp-25)))/592} tc=0.0135
rg gatein gate {0.38}
*
*
Rcsdconv drain source {1000Meg/aDi}
Rcgsconv gate source {1000Meg/aDi}
Rcgdconv gate drain {1000Meg/aDi}
*
bswitch drain2 source2 I = (if (v(drain2,source2)>0,
+	(cur*(0.8*PWR((Temp-25+273)/300,-2.7))*log(1.0+exp(26*(v(gate,source2)-7.9+6.2-0.00*(Temp-25))/slp))*
+	v(drain2,source2)/(1 + max(x0_0+x0_1*(v(gate,source2)+4.1),0.2)*v(drain2,source2))),
+	(-cur*(0.8*PWR((Temp-25+273)/300,-2.7))*log(1.0+exp(21*(v(gate,drain2)-7.9+6.2-0.00*(Temp-25))/slp))*
+	v(source2,drain2)/(1 + max(x0_0+x0_1*(v(gate,drain2)+6.1),0.2)*1.0*v(source2,drain2)))) }
R_drain2 drain2 drain {(1e-4)}
R_source2 source2 source {(1e-4)}
*
*
C_GS	 gate source  	{(2/7*Igs1/120/2.18)*3.5*592}
C_GS1 gate source Q = ((1/7*10/120/2.18*1.5)*592*1.5*((0.5*Igs2*Igs4*log(1+exp(ff5*0.5*(v(gate,source)-Igs3+
+					4.6)/0.9933))-Igs5*Igs7*log(1+exp(ff6*(v(source,drain)-Igs6)/Igs7)))))
*
C_GD   gate drain  	{((2.6+0.8*0)/17*Igd1/30/2.18)*1.5*592}
C_GD1  gate drain Q = ((0.7/7*1/25/2.18*0.8)*592*((0.5*Igs2*Igs4*log(1+exp(ff1*6*(v(gate,drain)-Igs3+of1-50)/
+					(Igs4*ff3)))+Igd2*Igd4*log(1+exp(0.5*ff2*(v(gate,drain)-Igd3+of2-30)/(Igd4
+					*ff4))))))
*
C_SD	 source drain  	{(2/7*Isd1/2.18)*10*592}
C_SD1 source drain Q = (1/7*1/2.18*18*592*(4*Isd2*Isd4*log(1+exp(0.1*(v(source,drain)-Isd3+145)/Isd4))+
+                              Isd5*Isd7*log(1+exp(1.5*(v(source,drain)-Isd6+55)/Isd7))
+                             +5.7*0.0*(0.5*2.5e-12*0.643*log(1+exp(v(source,drain)-4.68+80))/(0.643
+					*3.5))))
.ends
*$
