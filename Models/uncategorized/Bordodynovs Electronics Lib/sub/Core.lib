*Core 

.SUBCKT  CoreJA  1  2   3  b1     area=24u  LeN=32m
+ ms=381.82k a=28.067 k=18.57 c=0.24753 
.param RS=1m 
.param c1=1/(1+c) c2=c1*C   
.param mU0=1.256637061436u
.func f(x) IF(aBS(x)>1m,(1/taNh(x)-1/x),0.33333*x)
b1 3 0 V=V(1,2)/LeN
b2 4 0  V=f(v(3)/A)
b3 5 0 v=v(1,2)
C1 5 7 1m rser={rs}  ic=0
V1 7 0 0
bi4 0 9 i=k*1m*SGN(I(V1))  rpar=1k 
C2 9 0 1pF
b5 11 0 v=Uramp(V(4,12)/((V(9)+1m)*len))
b6 12 0 V=idt(1000*I(V1)*v(11)*c1)+c2*V(4)  tripdt=5n
b7 13 0 V=aRea*mU0*(mS*V(12)+V(3))
bi1 1 2 i=v(13) 
b b1  0  V=V(13)/aRea   ;   +mU0*v(3)
.ends CoreJA

.subckt CoreChan in out H 1 Area=24u Len=32m Hc=15 Br=0.45 BS=0.57 
*Bh H 0 V=V(in,out)/Len tripdt=50n
Bh 0 H I=V(in,out)/Len tripdt=50n Rpar=1
Cbh h 0 10.0p 
BiH 0 L i=V(H) tripdt=50n
L L 0 Bs={Bs} Br={Br} Hc={Hc} A=1 Lm=1 Lg=0 N=1 Rser=0  Rpar=10Meg
LL l 0 {10*Bs/Hc} rser=200
Cl L 0 25p Rser=200 
BBB 1 0 V=idt(V(L)) 
B5 in out I=area*v(1) tripdt=50n  rpar=1gig
.ends

.subckt CoreChan0 in out H 1 Area=24u Len=32m Hc=15 Br=0.45 BS=0.57 
*Bh H 0 V=V(in,out)/Len tripdt=50n
Bh 0 H I=V(in,out)/Len tripdt=50n Rpar=1
Cbh h 0 1p
BiH 0 L i=V(H) tripdt=5n
L L 0 Bs={Bs} Br={Br} Hc={Hc} A=1 Lm=1  Lg=0 N=1 Rser=0  Rpar=10Meg
LL l 0 {Bs/Hc} rser=800
Cl L 0  10p Rser=800
BBB 1 0 V=idt(V(L)) 
B5 in out I=area*v(1) rpar=1gig
.ends

.subckt CoreChan2 in out H 1 Area=24u Len=32m Hc=15 Br=0.45 BS=0.57 
*Bh H 0 V=V(in,out)/Len tripdt=50n
Bh 0 H I=V(in,out)/Len tripdt=50n Rpar=1
Cbh h 0 10.0p 
BiH 0 L i=V(H) tripdt=50n
L L 0 Bs={Bs} Br={Br} Hc={Hc} A=1 Lm=1 Lg=0 N=1 Rser=0  Rpar=10Meg
LL l 2 {10*Bs/Hc} rser=2000
Cl L 2 2.5p Rser=2000 
LcRRR 2 0 10u rpar=1000k rser=200 
BBB 1 0 V=idt(V(L))
B5 in out I=area*v(1) tripdt=50n  rpar=1gig
.ends

.subckt CoreChan1 in out H 1 Area=24u Len=32m Hc=15 Br=0.45 BS=0.57 tau=5.0n
.param u0=4*Pi*1e-7
.param alfa={Hc*(Bs/Br-1)}

Bh H 0 V=V(in,out)/Len tripdt=50n
*Bh 0 H I=V(in,out)/Len tripdt=50n Rpar=1
*Cbh h 0 1.00p 
BiH 0 L i=V(H) tripdt=50n
L  L  0  1  Rpar={1/tau} Cpar=.1f 


B1 sqr1 0 V={(Abs(V(H)+Hc)+alfa)**2}
B2 sqr2 0 V={(Abs(V(H)-Hc)+alfa)**2}
Bud dBu 0 V={Bs*alfa/V(sqr1)+u0}
Bdd dBd 0 V={Bs*alfa/V(sqr2)+u0}
B0 dBdH 0 V={0.5*(V(dBu)+V(dBd))}

B10 Bu 0  V={Bs*(V(H)+Hc)/(Abs(V(H)+Hc)+alfa)+u0*V(H)} ic=0
B20 Bd 0  V={Bs*(V(H)-Hc)/(Abs(V(H)-Hc)+alfa)+u0*V(H)} ic=0


b100 10 0 V=0.95*idt(V(L)*V(dBdH),0)+0.05*0.5*(v(Bd)+v(bu))
BBB 1 0 V=limit(v(Bd),v(10),v(Bu))
B5 in out I=area*v(1) tripdt=50n  rpar=1gig
.ends


.SUBCKT  CoreMy  1  2   3  b1     area=24u  LeN=32m
+ ms=381.82k a=28.067 k=30 c=0.24753 
.param RS=1m 
.param oA=1/A  oLen=1/Len oArea=1/Area 
.param  c1=1/(1+c) c2=c1*C
*.param  c1=0.5 c2=0.1
.param mU0=1.256637061436u
.func f(x) sgn(x)*(1-exp(-abs(k*x)))
b1 3 0 V=V(1,2)*oLeN
b2 4 0  V=f(v(3)*oA)
b3 5 0 v=v(1,2)
C1 5 7 1m rser={rs}  ic=0
V1 7 0 0
bi4 0 9 i=k*1m*SGN(I(V1))  rpar=1k 
C2 9 0 1pF
b5 11 0 v=Uramp(V(4,12)/((V(9)+1m)*len))
b6 12 0 V=idt(1000*I(V1)*v(11)*c1)+c2*V(4)
b7 13 0 V=aRea*mU0*(mS*V(12)+V(3))
bi1 1 2 i=v(13) 
b b1  0  V=V(13)*oaRea+mU0*v(3)
.ends CoreMy


.SUBCKT CORELIN   1  2 H B1 Ur=1000 AREA=24u Len=32m 
.param rm=Len/(1.256637061436u*Ur*AREA)
R 1 2 {rm}
b1 B1 0 V=V(H)/rm
b2 H 0 V=V(1,2)/len
.ENDS CORELIN	

.SUBCKT CORELIN0   1  2 Ur=1000 AREA=24u Len=32m 
R 1 2 {Len/(1.256637061436u*Ur*AREA)}
.ENDS CORELIN0	


.SUBCKT GAP   1  2 H B1 AREA=24u gap=0.1m
.param rm=gap/(1.256637061436u*AREA)
R 1 2 {rm}
b1 B1 0 V=V(H)/rm
b2 H 0 V=V(1,2)/gap
.ENDS GAP


.Subckt Core in out H 1 params: Area=24u Len=32m
.param mU0=1.256637061436u 
B1 1 0 V=tbl(v(h),
*____H____B___
+ -30000, -2.,
+ -10000, -1.92,
+ -5000, -1.85,
+ -2500, -1.8,
+ -1500, -1.74,
+ -900,  -1.67,
+ -610,  -1.6,
+ -440,  -1.5,
+ -340,  -1.4,
+ -280,  -1.3,
+ -230,  -1.2,
+ -200,  -1.1,
+ -170., -1,
+  0.,    0.,
+ 170.,   1.,
+ 200.,   1.1,
+ 230.,   1.2,
+ 280.,   1.3,
+ 340.,   1.4,
+ 440.,   1.5,
+ 610.,   1.6,
+ 900.,   1.67,
+ 1500,   1.74,
+ 2500,   1.8,
+ 5000,   1.85,
+ 10000,  1.92,
+ 30000,  2.
+)+mU0*v(H)
B2 H 0 V=V(in,out)/Len 
B3 in out I=Area*v(1)
.ends 

.Subckt IronCore in out H 1 params: Area=24u Len=32m 
.param mU0=1.256637061436u
B1 1 0 V=tbl(v(h),
*____H_______B___
+ -25000 , -1.278 ,
+ -22500 , -1.269 ,
+ -20000 , -1.26 ,
+ -17500 , -1.25 ,
+ -15000 , -1.237 ,
+ -12500 , -1.2 ,
+ -10000 , -1.16 ,
+ -7500 ,  -1.065 ,
+ -5000 ,  -0.94 ,
+ -2500 ,  -0.7 ,
+ 0 ,       0,
+ 2500,     0.7, 
+ 5000 ,    0.94,
+ 7500 ,    1.065,
+ 10000 ,   1.16 ,
+ 12500 ,   1.2 ,
+ 15000 ,   1.237 ,
+ 17500 ,   1.25 ,
+ 20000 ,   1.26 ,
+ 22500 ,   1.269 ,
+ 25000 ,   1.278
+)+mU0*v(H)
B2 H 0 V=V(in,out)/Len
B3 in out I=Area*v(1) 
.ends 

.subckt CoreSquare1 in out H 1 Area=24u Len=32m Hc=5 Br=1.95 BS=2; Bad model
bv1  bu 0 v=Bs*(v(h)+Hc)/(abs(v(h)+Hc)+Hc*(Bs/Br-1))*(1-v(c))  
bv2  bd 0 v=Bs*(v(h)-Hc)/(abs(v(h)-Hc)+Hc*(Bs/Br-1))*v(c)   
Bv 1 0 v=V(bu)+v(bd) tripdt=1n
B2 H 0 V=V(in,out)/Len
rf h hf 1k
cf hf 0 10p
b4 0 2 i=ddt(v(hf)) rpar=1
c1 2 0 1u
bc c 0 v=u(v(2))
B3 in out1 I=Area*v(1) Rpar={Len*795775/Area} tripdt=10n
.ends


.subckt CoreSquareLoop in out H 1 Area=24u Len=32m Hc=15 Br=0.45 BS=0.57 p=0.02
.param Bsp=Bs*p
.param Bsk=Bsp+Bs Brk=Bsp+Br
.param gamma=Hc*(Bsk/Brk-1)
.param mU0=1.256637061436u
bv1  bu 0 v=Bsk*(v(h)+Hc)/(abs(v(h)+Hc)+gamma)-BSp  
bv2  bd 0 v=Bsk*(v(h)-Hc)/(abs(v(h)-Hc)+gamma)+BSp
Bv 1 0 v=V(bu)*v(c)+v(bd)*(1-v(c))+mU0*v(h)  tripdt=5n
B2 H 0 V=V(in,out)/Len
B3 u 0 v=if((v(bd)>v(bu))&(V(h)<0),1,0) tripdt=5n 
B4 d 0 v=if((v(bd)>v(bu))&(V(h)>0),1,0) tripdt=5n
A1 u 10 0 0 0 C 0 0 OR Trise=5n
A2 d C 0 0 0 10 0 0 OR Trise=5n
B5 in out I=Area*v(1)  tripdt=10n
.ic v(c)=0
.ends

.subckt CoreSquareLoop2 in out H 1 Area=24u Len=32m Hc=15  BS=0.57 p=0.01 mUmax=100000
.func f(x) tanh(k*x)
.param Bsp=Bs*p Bsk=Bsp+Bs
.param mU0=1.256637061436u k=mU0*mUmax/Bsk 
bv1  bu 0 v=Bsk*F(v(h)+Hc)-BSp  tripdt=5n tripdv=10m
bv2  bd 0 v=Bsk*F(v(h)-Hc)+BSp  tripdt=5n tripdv=10m
Bv 1 0 v=V(bu)*v(c)+v(bd)*(1-v(c))+mU0*v(h)  tripdt=5n
B2 H 0 V=V(in,out)/Len  tripdv=1m
B3 u 0 v=if((v(bd)>v(bu))&(V(h)<0),1,0) tripdt=5n 
B4 d 0 v=if((v(bd)>v(bu))&(V(h)>0),1,0) tripdt=5n
A1 u 10 0 0 0 C 0 0 OR Trise=5n
A2 d C 0 0 0 10 0 0 OR Trise=5n
B5 in out I=Area*v(1)  tripdt=10n
.ic v(c)=0
.ends

.subckt CoreSquareLoop3 in out H 1 Area=24u Len=32m Hc=15  BS=0.57 p=0.01 mUmax=100000
.func f(x) sgn(x)*(1-exp(-abs(k*x)))
.param Bsp=Bs*p Bsk=Bsp+Bs
.param mU0=1.256637061436u k=mU0*mUmax/Bsk 
bv1  bu 0 v=Bsk*F(v(h)+Hc)-BSp  tripdt=5n tripdv=10m
bv2  bd 0 v=Bsk*F(v(h)-Hc)+BSp  tripdt=5n tripdv=10m
Bv 1 0 v=V(bu)*v(c)+v(bd)*(1-v(c))+mU0*v(h)  tripdt=5n
B2 H 0 V=V(in,out)/Len  tripdv=1m
B3 u 0 v=if((v(bd)>v(bu))&(V(h)<0),1,0) tripdt=5n 
B4 d 0 v=if((v(bd)>v(bu))&(V(h)>0),1,0) tripdt=5n
A1 u 10 0 0 0 C 0 0 OR Trise=5n
A2 d C 0 0 0 10 0 0 OR Trise=5n
B5 in out I=Area*v(1)  tripdt=10n
.ic v(c)=0
.ends

.subckt CoreSquareLoop4 in out H 1 Area=24u Len=32m Hc=15  BS=0.57 p=0.01 mUmax=100000
.func f(x) sgn(x)*(1-1/(1+abs(k*x)))
.param Bsp=Bs*p Bsk=Bsp+Bs
.param mU0=1.256637061436u k=mU0*mUmax/Bsk 
bv1  bu 0 v=Bsk*F(v(h)+Hc)-BSp  tripdt=5n tripdv=10m
bv2  bd 0 v=Bsk*F(v(h)-Hc)+BSp  tripdt=5n tripdv=10m
Bv 1 0 v=V(bu)*v(c)+v(bd)*(1-v(c))+mU0*v(h)  tripdt=5n
B2 H 0 V=V(in,out)/Len  tripdv=1m
B3 u 0 v=if((v(bd)>v(bu))&(V(h)<0),1,0) tripdt=5n 
B4 d 0 v=if((v(bd)>v(bu))&(V(h)>0),1,0) tripdt=5n
A1 u 10 0 0 0 C 0 0 OR Trise=5n
A2 d C 0 0 0 10 0 0 OR Trise=5n
B5 in out I=Area*v(1)  tripdt=10n
.ic v(c)=0
.ends

.subckt CoreSquareLoop2m in out H 1 Area=24u Len=32m Hc=15  BS=0.57 p=0.01 mUmax=100000 mUsat=100
.func f(x) tanh(k*x)
.param Bsp=Bs*p Bsk=Bsp+Bs
.param mU0=1.256637061436u k=mU0*(mUmax-mUsat)/Bsk
.param mu=mu0*mUsat 
bv1  bu 0 v=Bsk*F(v(h)+Hc)-BSp  tripdt=5n tripdv=10m
bv2  bd 0 v=Bsk*F(v(h)-Hc)+BSp  tripdt=5n tripdv=10m
Bv 1 0 v=V(bu)*v(c)+v(bd)*(1-v(c))+mU*v(h)  tripdt=5n
B2 H 0 V=V(in,out)/Len  tripdv=1m
B3 u 0 v=if((v(bd)>v(bu))&(V(h)<0),1,0) tripdt=5n 
B4 d 0 v=if((v(bd)>v(bu))&(V(h)>0),1,0) tripdt=5n
A1 u 10 0 0 0 C 0 0 OR Trise=5n
A2 d C 0 0 0 10 0 0 OR Trise=5n
B5 in out I=Area*v(1)  tripdt=10n
.ic v(c)=0
.ends

.subckt CoreSquareLoop3m in out H 1 Area=24u Len=32m Hc=15  BS=0.57 p=0.01 mUmax=100000 mUsat=100
.func f(x) sgn(x)*(1-exp(-abs(k*x)))
.param Bsp=Bs*p Bsk=Bsp+Bs
.param mU0=1.256637061436u k=mU0*(mUmax-mUsat)/Bsk 
.param mu=mu0*mUsat 
bv1  bu 0 v=Bsk*F(v(h)+Hc)-BSp  tripdt=5n tripdv=10m
bv2  bd 0 v=Bsk*F(v(h)-Hc)+BSp  tripdt=5n tripdv=10m
Bv 1 0 v=V(bu)*v(c)+v(bd)*(1-v(c))+mU*v(h)  tripdt=5n
B2 H 0 V=V(in,out)/Len  tripdv=1m
B3 u 0 v=if((v(bd)>v(bu))&(V(h)<0),1,0) tripdt=5n 
B4 d 0 v=if((v(bd)>v(bu))&(V(h)>0),1,0) tripdt=5n
A1 u 10 0 0 0 C 0 0 OR Trise=5n
A2 d C 0 0 0 10 0 0 OR Trise=5n
B5 in out I=Area*v(1)  tripdt=10n
.ic v(c)=0
.ends

.Subckt Winding0 l1 l2 mmf_out2 mmf_out params: N=100 rs=1m Tau=50n
B1  l1 50 v=v(7)*N  
r1 50 5 {rs}
v1 5 l2 dc 0
bH2  mmf_out 6 v=i(v1)*N
v2  6 mmf_out2  dc  0 
BF  7   0 i=i(v2) tripdt=50n
L  7  0  1 Rpar={1/tau} Cpar=.1f 
.ends Winding0

.Subckt Winding_V l1 l2 mmf_out2 mmf_out V params: N=100 Tau=50n
B1  l1 5 v=v(7)*N*V(V)  
v1 5 l2 dc 0
bH2  mmf_out 6 v=i(v1)*N*V(V)
v2  6 mmf_out2  dc  0 
BF  7   0 i=i(v2) tripdt=50n
L  7  0  1 Rpar={1/tau} Cpar=.1f 
.ends Winding_V

.Subckt Winding l1 l2 mmf_out2 mmf_out params: N=100 rs=1m Tau=50n Tau2=50n
*.param Tau=50n Tau2=50n
B1 5 l1  i=v(7)*N/Rs Rpar={Rs}
Cb1 5 l1 {tau2/rs} Rser=0 
v1 5 l2 dc 0
bH2  mmf_out 6 v=i(v1)*{N}
v2  6 mmf_out2  dc  0 
BF  7   0 i=i(v2) tripdt=50n
L  7  0  1 Rpar={1/tau} Cpar=.1f 
.ends Winding


.Subckt Winding_RLCR l1 l2 mmf_out2 mmf_out params: N=100 Rs=1m Rp=1T Cp=0.1p Ls=10n Tau=50n Tau2=50n
*.param Tau=50n Tau2=15n
LR l l1 {Ls} rser=0
Cp l1 l2 {Cp} Rpar={rp} rser={Rs/2}

*E1 l 5 7 0 {N}
*v1 5 l2 dc 0
*H2 mmf_out 6 v1 {N}
B1 5 l1  i=v(7)*N/Rs Rpar={Rs}
Cb1 5 l1 {tau2/rs} Rser=0
v1 5 l2 dc 0
bH2  mmf_out 6 v=i(v1)*{N}
*bH2 6 mmf_out i=i(v1)*{N} Rpar=1
*cbh2 mmf_out 6 1.00p

v2  6 mmf_out2  dc  0 
BF  7   0 i=i(v2) tripdt=50n
L  7  0  1 Rpar={1/tau} Cpar=.1f
.ends Winding_RLCR

.subckt Mutual_Ind k p1 p2 s1 s2 params: L1=1m L2=4m  r1=1m r2=1m
R1 0 N003 R=1/(V(k)*{L1})
R2 0 N001 R=1/((1-V(k))*{L1})
XU1 p1 N004 N001 0 Winding0 N=1 Rs={r1*0.9} Tau=50n
XU2 N004 p2 N003 P001 Winding0 N=1 Rs={r1*0.1} Tau=50n
XU3 N005 s2 P001 0 Winding0 N={(L2/L1)**0.5} Rs={r2*0.1} Tau=50n
XU4 s1 N005 N002 0 Winding0 N={(L2/L1)**0.5} Rs={r2*0.9} Tau=50n
R3 0 N002 R=1/((1-V(k))*{L1})
R4 k 0 1Gig
.ends Mutual_Ind

.subckt autotransformer_V a b c v   Area=10e-4 Len=300m N=1000 Ur=1000 Rs=1m
.param tau=50n
B10 0 Aa I=limit(1u,V(V),0.999999) Rpar=1
C10 Aa 0 100p
B20 0 bb I=limit(1u,1-V(V),0.999999) Rpar=1
C20 bb 0 100p
R20 V 0 1G
R10 N001 b R=Rs*v(bb)
R30 b N003 R=Rs*v(aa)
B19  a 5 v=v(7)*N*V(bb)  
v19 5 N001 dc 0
bH29  N002 6 v=i(v19)*N*V(bb)
v2  6 P001  dc  0 
BF  7   0 i=i(v2) tripdt=50n
L  7  0  1 Rpar={1/tau} Cpar=.1f      
B1  N003 50 v=v(7)*N*V(aa)  
v1 50 c dc 0
bH2  Aa n002 v=i(v1)*N*V(aa)
R 0 P001 {Len/(1.256637061436u*Ur*AREA)}
.ends autotransformer_V

