* Digital Library 
* Collected by: Helmut Sennewald, Kubov V.I. 
* Version 0.6

**** INV_1 ****
*
* in1 out
.subckt INV_1 1 q  
A1 1 0 0 0 0 q 0 0 BUF Vhigh=5
.ends
*
*
**** NINV_1 ****
*
* in1 out
.subckt NINV_1 1 q  
A1 1 0 0 0 0 0 q 0 BUF Vhigh=5
.ends
*
*
**** BUF_1 ****
*
* in1 out
.subckt BUF_1 1 q  
A1 1 0 0 0 0 0 q 0 BUF Vhigh=5
.ends
*
*
**** SCHMITT ****
*
* in1 out
.subckt SCHMITT_1 1 q  
A1 1 0 0 0 0 0 q 0 SCHMITT Vt=2.5 Vh=1.3333 td=1n Vhigh=5
.ends
*
*
**** SCHMITT_INV_1 ****
*
* in1 out
.subckt SCHMITT_INV_1 1 q  
A1 1 0 0 0 0 q 0 0 SCHMITT Vt=2.5 Vh=1.3333 td=1n Vhigh=5
.ends
*
*
**** SCHMITT_NAND_2 ****
*
* in1 out
.subckt SCHMITT_NAND_2 1 2 q  
A1 1 0 0 0 0 0 r 0 SCHMITT Vt=2.5 Vh=1.3333 td=1n Vhigh=5
A2 2 0 0 0 0 0 s 0 SCHMITT Vt=2.5 Vh=1.3333 td=1n Vhigh=5
A3 r s 0 0 0 q 0 0 AND Vhigh=5
.ends
*
*

**** AND ****
*
* in1 in2 out
.subckt AND_2 1 2 q  
A1 i1 i2 0 0 0 0 q 0 AND Vhigh=5
R1 1 i1 1
R2 2 i2 1
.ends
*
*
* in1 in2 in3 out
.subckt AND_3 1 2 3 q
A1 i1 i2 i3 0 0 0 q 0 AND Vhigh=5
R1 1 i1 1
R2 2 i2 1
R3 3 i3 1
.ends
*
*
* in1 in2 in3 in4 out
.subckt AND_4 1 2 3 4 q
A1 i1 i2 i3 i4 0 0 q 0 AND Vhigh=5
R1 1 i1 1
R2 2 i2 1
R3 3 i3 1
R4 4 i4 1
.ends
*
*
* in1 in2 in3 in4 in5 out
.subckt AND_5 1 2 3 4 5 q
A1 i1 i2 i3 i4 i5 0 q 0 AND Vhigh=5
R1 1 i1 1
R2 2 i2 1
R3 3 i3 1
R4 4 i4 1
R5 5 i5 1
.ends
*
*
**** NAND ****
*
* in1 in2 out
.subckt NAND_2 1 2 q  
A1 i1 i2 0 0 0 q 0 0 AND Vhigh=5
R1 1 i1 1
R2 2 i2 1
.ends
*
*
* in1 in2 in3 out
.subckt NAND_3 1 2 3 q
A1 i1 i2 i3 0 0 q 0 0 AND Vhigh=5
R1 1 i1 1
R2 2 i2 1
R3 3 i3 1
.ends
*
*
* in1 in2 in3 in4 out
.subckt NAND_4 1 2 3 4 q
A1 i1 i2 i3 i4 0 q 0 0 AND Vhigh=5
R1 1 i1 1
R2 2 i2 1
R3 3 i3 1
R4 4 i4 1
.ends
*
*
* in1 in2 in3 in4 in5 out
.subckt NAND_5 1 2 3 4 5 q
A1 i1 i2 i3 i4 i5 q 0 0 AND Vhigh=5
R1 1 i1 1
R2 2 i2 1
R3 3 i3 1
R4 4 i4 1
R5 5 i5 1
.ends
*
*
**** OR ****
*
* in1 in2 out
.subckt OR_2 1 2 q  
A1 1 2 0 0 0 0 q 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 out
.subckt OR_3 1 2 3 q
A1 1 2 3 0 0 0 q 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 out
.subckt OR_4 1 2 3 4 q
A1 1 2 3 4 0 0 q 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 out
.subckt OR_5 1 2 3 4 5 q
A1 1 2 3 4 5 0 q 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 out
.subckt OR_6 1 2 3 4 5 6 q
A1 1 2 3 4 5 0 a 0 OR Vhigh=5
A2 a 6 0 0 0 0 q 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 in7 out
.subckt OR_7 1 2 3 4 5 6 7 q
A1 1 2 3 4 5 0 a 0 OR Vhigh=5
A2 a 6 7 0 0 0 q 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 in7 in8 out
.subckt OR_8 1 2 3 4 5 6 7 8 q
A1 1 2 3 4 5 0 a 0 OR Vhigh=5
A2 a 6 7 8 0 0 q 0 OR Vhigh=5
.ends
*
*
*
**** NOR ****
*
* in1 in2 out
.subckt NOR_2 1 2 q  
A1 1 2 0 0 0 q 0 0 OR Vhigh=5 
.ends
*
*
* in1 in2 in3 out
.subckt NOR_3 1 2 3 q
A1 1 2 3 0 0 q 0 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 out
.subckt NOR_4 1 2 3 4 q
A1 1 2 3 4 0 q 0 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 out
.subckt NOR_5 1 2 3 4 5 q
A1 1 2 3 4 5 q 0 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 out
.subckt NOR_6 1 2 3 4 5 6 q
A1 1 2 3 4 5 0 a 0 OR Vhigh=5
A2 a 6 0 0 0 q 0 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 in7 out
.subckt NOR_7 1 2 3 4 5 6 7 q
A1 1 2 3 4 5 0 a 0 OR Vhigh=5
A2 a 6 7 0 0 q 0 0 OR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 in7 in8 out
.subckt NOR_8 1 2 3 4 5 6 7 8 q
A1 1 2 3 4 5 0 a 0 OR Vhigh=5
A2 a 6 7 8 0 q 0 0 OR Vhigh=5
.ends
*
*
**** XOR ****
*
* in1 in2 out
.subckt XOR_2 1 2 q  
A1 1 2 0 0 0 0 q 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 out
.subckt XOR_3 1 2 3 q
A1 1 2 3 0 0 0 q 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 out
.subckt XOR_4 1 2 3 4 q
A1 1 2 3 4 0 0 q 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 out
.subckt XOR_5 1 2 3 4 5 q
A1 1 2 3 4 5 0 q 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 out
.subckt XOR_6 1 2 3 4 5 6 q
A1 1 2 3 4 5 0 a 0 XOR Vhigh=5
A2 a 6 0 0 0 0 q 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 in7 out
.subckt XOR_7 1 2 3 4 5 6 7 q
A1 1 2 3 4 5 0 a 0 XOR Vhigh=5
A2 a 6 7 0 0 0 q 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 in7 in8 out
.subckt XOR_8 1 2 3 4 5 6 7 8 q
A1 1 2 3 4 5 0 a 0 XOR Vhigh=5
A2 a 6 7 8 0 0 q 0 XOR Vhigh=5
.ends
*
*
*
**** XNOR ****
*
* in1 in2 out
.subckt XNOR_2 1 2 q  
A1 1 2 0 0 0 q 0 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 out
.subckt XNOR_3 1 2 3 q
A1 1 2 3 0 0 q 0 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 out
.subckt XNOR_4 1 2 3 4 q
A1 1 2 3 4 0 q 0 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 out
.subckt XNOR_5 1 2 3 4 5 q
A1 1 2 3 4 5 q 0 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 out
.subckt XNOR_6 1 2 3 4 5 6 q
A1 1 2 3 4 5 0 a 0 XOR Vhigh=5
A2 a 6 0 0 0 q 0 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 in7 out
.subckt XNOR_7 1 2 3 4 5 6 7 q
A1 1 2 3 4 5 0 a 0 XOR Vhigh=5
A2 a 6 7 0 0 q 0 0 XOR Vhigh=5
.ends
*
*
* in1 in2 in3 in4 in5 in6 in7 in8 out
.subckt XNOR_8 1 2 3 4 5 6 7 8 q
A1 1 2 3 4 5 0 a 0 XOR Vhigh=5
A1 a 6 7 8 0 q 0 0 XOR Vhigh=5
.ends
*
*
*
**** SR-FLIPFLOP ****
*
* s r q _q
.subckt SR_FF s r q _q
A1 s r 0 0 0 _q q 0 SRFLOP td=1n Vhigh=5
.ends
*
*
**** D-FLIPFLOP with set and reset ****
*
* d c s r q _q
.subckt D_FF_SR d c s r q _q
A1 d 0 c s r _q q 0 DFLOP td=1n Vhigh=5
.ends
*
*
**** D-FLIPFLOP with set on rise CLK ****
*
* d c q _q
.subckt D_FF d c q _q
A1 d 0 c 0 0 _q q 0 DFLOP td=1n Vhigh=5
.ends
*
*
**** D-FLIPFLOP with set on fall CLK ****
*
* d c q _q
.subckt D_FF_FC d c q _q
A1 c 0 0 0 0 ic 0 0 BUF Vhigh=5
A2 d 0 ic 0 0 _q q 0 DFLOP td=1n Vhigh=5
.ends
*
*

**** JK-FLIPFLOP with set and reset ****
*
.SUBCKT JK_FF_SR  J K C S R QN Q   
* 
Rs S 0 1G
Rc R 0 1G
*
* J=1, K=0    Q=1
* J=1, K=1    Q=toggle
* J=0, K=0    Q=no change
* J=0, K=1    Q=0  
AKNF  K 0 0 0 0  Kn 0 0  BUF Vhigh=5 
AJNF  J 0 0 0 0  Jn 0 0  BUF Vhigh=5 
A11   J Kn 0   0 0  0 D1 0  AND Vhigh=5 
A10   J K QN 0 0  0 D2 0  AND Vhigh=5  
A01   Jn Kn Q  0 0  0 D3 0  AND Vhigh=5 
AOR3  D1 D2 D3  0 0  0 Di 0  OR Vhigh=5  
ADFF  Di 0 C S R  QN Q 0  DFLOP td=1n Vhigh=5
.ends
*
*
**** JK-FLIPFLOP ****
*
.SUBCKT JK_FF J K C QN Q   
* 
* J=1, K=0    Q=1
* J=1, K=1    Q=toggle
* J=0, K=0    Q=no change
* J=0, K=1    Q=0  
AKNF  K 0 0 0 0  Kn 0 0  BUF Vhigh=5 
AJNF  J 0 0 0 0  Jn 0 0  BUF Vhigh=5 
A11   J Kn 0 0 0  0 D1 0  AND Vhigh=5 
A10   J K QN 0 0  0 D2 0  AND Vhigh=5  
A01   Jn Kn Q 0 0  0 D3 0  AND Vhigh=5 
AOR3  D1 D2 D3 0 0  0 Di 0  OR Vhigh=5  
ADFF  Di 0 C 0 0  QN Q 0  DFLOP td=1n Vhigh=5
.ends
*
*
**** T-FLIPFLOP with T-enable, set and reset ****
*
.SUBCKT T_FF_SR  T C S R QN Q   
* 
Rs S 0 1G
Rc R 0 1G
Rt  T vcc 1G
VCC vcc 0 5
AXOR  T Q 0 0 0   0 TX 0  XOR Vhigh=5
ADFF  TX 0 C S R  QN Q 0  DFLOP td=1n Vhigh=5
.ends
*
*
**** T-FLIPFLOP with T-enable ****
*
.SUBCKT T_FF  T C QN Q   
* Subcircuit for a T-Flipflop with T-enable
* 
Rt  T vcc 1G
VCC vcc 0 5
AXOR  T Q 0 0 0   0 TX 0  XOR Vhigh=5
ADFF  TX 0 C 0 0  QN Q 0  DFLOP td=1n Vhigh=5
.ends
*
*
**** DIVIDER/COUNTER ****
*
.SUBCKT DIVIDER_N  CLK Q QN  
.param divn=16 
.param width=0.5
* 
A2 clk 0 0 0 0 QN Q 0 COUNTER cycles={divn} duty={width} trise=1n Vhigh=5
.ends
*
**** DIVIDER mod 2 with set on rise CLK 
.SUBCKT DIV_2  c Q   
* 
A1 c 0 0 0 0 0 Q 0 COUNTER cycles=2 duty=0.5 Vhigh=5
.ends
*
**** DIVIDER mod 2 with set on fall CLK 
.SUBCKT DIV_2_FC  c Q   
* 
A1 c 0 0 0 0 ic 0 0 BUF Vhigh=5
A2 ic 0 0 0 0 0 Q 0 COUNTER cycles=2 duty=0.5 Vhigh=5
.ends
*
**** DIVIDER mod 2 4 bit with set on rise CLK 
.SUBCKT DIV_2x4  c Q0 Q1 Q2 Q3   
* 
A2 c 0 0 0 0 0 Q0 0 COUNTER cycles=2 duty=0.5 Vhigh=5
A4 Q0 0 0 0 0 0 Q1 0 COUNTER cycles=2 duty=0.5 Vhigh=5
A6 Q1 0 0 0 0 0 Q2 0 COUNTER cycles=2 duty=0.5 Vhigh=5
A8 Q2 0 0 0 0 0 Q3 0 COUNTER cycles=2 duty=0.5 Vhigh=5
.ends
*
**** DIVIDER mod 2 4 bit with set on fall CLK 
.SUBCKT DIV_2x4_FC  c Q0 Q1 Q2 Q3   
* 
A1 c 0 0 0 0 ic 0 0 BUF Vhigh=5
A2 ic 0 0 0 0 iQ0 Q0 0 COUNTER cycles=2 duty=0.5 Vhigh=5
A4 iQ0 0 0 0 0 iQ1 Q1 0 COUNTER cycles=2 duty=0.5 Vhigh=5
A6 iQ1 0 0 0 0 iQ2 Q2 0 COUNTER cycles=2 duty=0.5 Vhigh=5
A8 iQ2 0 0 0 0 IQ3 Q3 0 COUNTER cycles=2 duty=0.5 Vhigh=5
.ends
***********************************************************  
.SUBCKT XJKFLOP  J K C S R QN Q 
.param td=5n
* 
Rs S 0 1G
Rc R 0 1G
*
* J=1, K=0    Q=1
* J=1, K=1    Q=toggle
* J=0, K=0    Q=no change
* J=0, K=1    Q=0  
AKNF  K 0 0 0 0  Kn 0 0  BUF 
AJNF  J 0 0 0 0  Jn 0 0  BUF 
A11   J Kn 0   0 0  0 D1 0  AND 
A10   J K QN 0 0  0 D2 0  AND  
A01   Jn Kn Q  0 0  0 D3 0  AND 
AOR3  D1 D2 D3  0 0  0 Di 0  OR  
ADFF  Di 0 C S R  QN Q 0  DFLOP   td={td} tripdt=1n
.ends
***********************************************************  
.SUBCKT XTFLOP  T C S R QN Q  
.param td=5n 
*
Rs S 0 1G
Rc R 0 1G
Rt  T vcc 1G
VCC vcc 0 1
*
AXOR  T Q 0 0 0   0 TX 0  XOR
ADFF  TX 0 C S R  QN Q 0  DFLOP   td={td} tripdt=1n
.ends

