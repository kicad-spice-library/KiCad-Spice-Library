.SUBCKT AO3407 4 1 2
.param A=1
M1  3 5 8 8 PMOS W={A*998956u}  L=0.7u 
M2  8 5 8 3 NMOS W={A*998956u}  L=0.54u
R1  7 3     RTEMP {24E-3/A}
LD  4 7 1E-9
LS  8 9 1.5E-9
RWire 9 2 RTWIRE 3e-3 
RG 1 6 6.4
LG 6 5 3E-9
CGS 5 2     {A*200E-12}
DBD 3 8     DBD
**************************************************************************
.MODEL  PMOS       PMOS  (LEVEL  = 3               TOX    = 4.5E-8
+ RS     = 0              RD     = 0               NSUB   = 2E17  
+ UO     = 110             THETA  = 0.03		   VTO=-2.4
+ VMAX   = 5e6             XJ     = 4E-7            KAPPA  = 1.1
+ ETA    = 0              TPG    = 1  
+ IS     = 0              LD     = 0                           
+ CGSO   = 0              CGDO   = 0               CGBO   = 0 
+ NFS    = 0         DELTA  = 0)
*************************************************************************
.MODEL  NMOS       NMOS (LEVEL   = 3               TOX    = 4.5E-8
+NSUB    = 4e16    VTO=1.5    TPG    = -1)   
*************************************************************************
.MODEL DBD D (CJO={A*150E-12}     VJ=0.6    M=0.35
+RS=0.0 FC=0.5 IS={A*0.3E-12} TT=1.5e-8 N=1.0 BV=36 IBV=1E-4)
*************************************************************************
.MODEL RTEMP RES (TC1=4.5E-3   TC2=0)
.MODEL RTWIRE RES (TC1=1E-4   TC2=0)
*************************************************************************
.ENDS  
