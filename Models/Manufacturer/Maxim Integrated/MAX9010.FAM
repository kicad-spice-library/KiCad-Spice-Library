* MAX9010 FAMILY MACROMODELS
* -------------------------
* Rev. 0, 6/2001
* -------------------------
* FEATURES:
* Ultra-Fast, 5ns Propagation Delay
* Very Low Power: 900uA at 5V
* MAX9010 - Available in 6-Pin SC70
*
* PART NUMBER    DESCRIPTION
* ___________    ________________________
* MAX9010        Single, High-Speed Comparator
*
*
*   ////////////// MAX9010 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX9010 DATA SHEET       <====
*
* connections:        non-inverting input, +IN
*                     |   inverting input, -IN
*                     |   |   positive power-supply, V+
*                     |   |   |   ground
*                     |   |   |   |   output, OUT
*                     |   |   |   |   |
* NODE CONNECTIONS:   1   2   3   4   97
*
.subckt max9010   1 2 3 4 97
*
  f101    3  9 v1 1
  Iee100   7  400 dc 100.0E-6
  q101    9  20  7 qin
  Q2    8  21  7 qin
  Q3    9  8  399 qmo
  Q4    8  8  399 qmi
  VMB 400 4 0V
  VPB 399 3 0V
Ibiase 1 2 0ua
***note data sheet specifies +/-5uA, so this is left up to the user, change 0uA to user specified
***================
VIN1 2 23 .95
VIN2 1 25 .95
***
IPSUP 3 0 .8ma
INSUP 0 4 .8ma
***
EHYST 23 20 POLY(1) 0 60 0 1
VS2 21 25 0V
.model qin NPN(Is=800.0E-18 Bf=100)
.model qmi PNP(Is=800.0E-18 Bf=1002)
.MODEL PMOS PMOS 
*(VTO=-1.7 KP=1.8E-3)
  e1   10  4  3  9  2
  v1   10 11 dc 0
  q5    5 11  44 qoc
  vshift 44 4 0v
  *clamps output, Q5 collector load.
  R55 3 5 1meg
  DP5 5 3 DP
  DP6 4 5 DP
***============= Hysterisis section
GH 0 51 97 101 1E-6
ECM 101 0 3 4 0.5
RCM 101 0 10MEG
****==============COMPARATOR POINT FOR CREATING LOGIC OUTPUT, +-1, hi,lo.
RH1 3 51 1E11
RH2 4 51 1E11
DP1 51 52 DP
DP2 53 51 DP
VP1 52 0 1V
VP2 53 0 -1V
***=================
IHYST 55 0 -1E-9
*GENERATES 2MV OF HYST.
RREF 55 0 1E6
*LOGIC OUTPUT, NODE 60 ALTERS THE POLARITY, SO 55 SHOULD ALWAYS BE POS.
GMULT 60 0 POLY(2) 51 0 55 0 0 0  0 0 1E-6
RMULT 60 0 1E6
*================
VVIRTUAL 98 0 0V
F5 3 38 VA8 1
D9 40 38 DX
D10 38 3 DX
VA7 3 40 0
F6 3  4 VA7 1
G12 98 32 5 0 7.04E-3
R15 98 32 140
CRF 98 32 6.5p
*CRF assists with rise/fall time tweak
D3 36 41 DP
D4 42 37 DP
V4 37 34  .3
V5 34 36 1.3
***V4,V5 SET ISC, V4 VOL, V5 VOH.
R16 41 97 50
*was 150
R17 42 97 50
E11 3 33 3 32 1
VA8 33 34 0V
*========================
.model qoc NPN(Is=800.0E-18 Bf=2.069E4 Cjc=1E-15 Tf=2.84e-12 Tr=2.19e-9)
*
.model qmo PNP(Is=800.0E-18 Bf=1000 Cjc=1E-15 Tr=3.26e-9)
.MODEL DX  D(Is=800.0E-18)
.MODEL DP D(N=0.001)
*=======================
***== MODELS USED ==***
.MODEL DX2 D(IS=1E-15 n=0.001) 
.ends

