* MAX435 FAMILY MACROMODELS
* -------------------------
** FEATURES:
* 275MHz BW, 800V/uS SR, 300uV VOS (MAX435) 
* 200MHZ BW, 850V/uS SR, 300uV VOS (MAX436)
* True Differential High-Impedance Inputs
* No Feedback
* Low Noise, 7nV/rt.Hz at 1KHz
* Available in 14-Pin DIP/SO
*
* PART NUMBER    DESCRIPTION
* ___________    ___________________________________
* MAX435         275MHz BW, 800V/uS SR, 300uV VOS        
* MAX436         200MHz BW, 850V/uS SR, 300uV VOS
*
* NOTE THAT THE -3dB BANDWIDTH HAS BEEN OPTIMIZED FOR Zt=200OHMS,
* ZL+/-=25OHMS ACCORDINGLY. SIMULATIONS THAT VARY FROM THESE
* OPERATING POINTS MAY RESULT IN ERROR IN THE TRANSFER RESPONSE. 
* THIS SHOULD NOT EFFECT APPLICATION CONDITIONS WHERE THE
* BANDWIDTH IS LIMITED BY EXTERNAL COMPONENTS.  FOR MORE
* INFORMATION CONTACT THE MAXIM APPLICATIONS GROUP.
*
*   ////////////// MAX435 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX435 DATA SHEET       <====
*
* SUBCIRCUIT CONNECTIONS +VIN -VIN -Z +Z +V -V +IOUT -IOUT  RSET
*
.SUBCKT MAX435 9 29 14 34 99 50 16 36 51 
***************************************
F10 99 16 POLY(1) VSET .003 6.383 -188.9
*POLY SOURCE FOR SETTING QUIESCENT CURRENTS VIA RSET.
F11 99 14 POLY(1) VSET  7.5E-4  1.596 -47.23
Q1 99 9 8 Q
Q2 12 8 14 QPNP
F1 8 50 VSENSE1 0.5
F2 16 50 VSENSE1 4
VSENSE1 12 50 0V
CBW1 16 50 23P
******OUTPUT CLAMPING
D1 40 99 DX
D2 50 41 DX
V1 40 16 2.26V
V2 16 41 2.26V
********************** 2nd Half of Circuit
F30 99 36 POLY(1) VSET 0.003 6.383 -188.9    
F31 99 34 POLY(1) VSET  7.5E-4  1.596 -47.23
Q21 99 29 28 Q
Q22 32 28 34 QPNP
F21 28 50 VSENSE21 0.5
F22 36 50 VSENSE21 4
VSENSE21 32 50 0V
CBW2 36 50 23P
******OUTPUT CLAMPING
D21 60 99 DX
D22 50 61 DX
V21 60 36 2.26V
V22 36 61 2.26V
***********
VSET 0 51 -3.7V
.MODEL QPNP PNP (Bf=10E5)
.MODEL QNPN NPN (Bf=10E5)
.MODEL Q   NPN (Bf=15E5)
.MODEL DX D(IS=1E-15)
.ENDS
*
*   ////////////// MAX436 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX436 DATA SHEET       <====
*
* SUBCIRCUIT CONNECTIONS +VIN -VIN -Z +Z +V -V +IOUT  RSET
*
.SUBCKT MAX436 9 29 14 34 99 50 16 51 
***************************************
F10 99 16 POLY(1) VSET .003 6.383 -188.9
*POLY SOURCE FOR SETTING QUIESCENT CURRENTS VIA RSET.
F11 99 14 POLY(1) VSET  7.5E-4  1.596 -47.23
Q1 99 9 8 Q
Q2 12 8 14 QPNP
F1 8 50 VSENSE1 0.5
F2 16 50 VSENSE1 8
VSENSE1 12 50 0V
CBW1 16 50 23P
******OUTPUT CLAMPING
D1 40 99 DX
D2 50 41 DX
V1 40 16 2.26V
V2 16 41 2.26V
********************** 2nd Half of Circuit
F30 99 16 POLY(1) VSET 0.003 6.383 -188.9    
F31 99 34 POLY(1) VSET  7.5E-4  1.596 -47.23
Q21 99 29 28 Q
Q22 32 28 34 QPNP
F21 28 50 VSENSE21 0.5
VSENSE21 32 50 0V
***********
VSET 0 51 -3.7V
.MODEL QPNP PNP (Bf=10E5)
.MODEL QNPN NPN (Bf=10E5)
.MODEL Q   NPN (Bf=15E5)
.MODEL DX D(IS=1E-15)
.ENDS
