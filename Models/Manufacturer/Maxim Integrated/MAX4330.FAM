* MAX4330 FAMILY MACROMODELS
* -------------------------
* FEATURES:
* 3MHz Gain Bandwidth Product
* 245uA Typical Supply Current
* Unity-Gain Stable
* Rail-to-Rail I/O
* Available in 5-Pin SOT23-5 (MAX4330)
*              8-Pin SO      (MAX4332)
*             14-Pin SO      (MAX4334) 
*
* PART NUMBER    DESCRIPTION
* ___________    ________________________________
* MAX4330        Single 3MHz, Unity-Gain Stable
* MAX4332        Dual 3MHz, Unity-Gain Stable
* MAX4334        Quad 3MHz, Unity-Gain Stable  
*
*
*   ////////////// MAX4330 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX4330 DATA SHEET       <====
*
* connections:        non-inverting input
*                     |   inverting input
*                     |   |   positive power-supply
*                     |   |   |   negative power-supply
*                     |   |   |   |   output
*                     |   |   |   |   |
* OUTPUT CONNECTS:    1   2   99  50  97
*
*
.SUBCKT MAX4330 1 2 99 50 97
****************INPUT STAGE**********************
I1 99 4 .5e-3
M1   5  2 4 99 MOSFET
R3   5 50 2828
M2   6  7 4 99 MOSFET
R4   6 50 2828
DP1  1 99 DA
DP2 50  1 DA
DP3  2 99 DB
DP4 50  2 DB
Ibiase 7 4 25na
************************
************ GAIN, 1ST POLE, SLEW STAGE************
EH 99 98 99 50 0.5
G0  98  9 5 6 7.87e-1
VB 9 10 0V 
R0  98  9 127E3
C3 10 98 10.8e-8
*C3 10 98 1.3E-10
*** ***********
D107 10 99 Dx 
D103 10 100 Dx
D104 100 99 Dx
F100 100 99 POLY(1) VB -1.6E-1 1
****
D108 50 10 Dx
D105 101 10 Dx
D106 50 101 Dx
F2 50 101 POLY(1) VB -1.6E-1 -1
*** **********
D1 9 111 DX
D2 112 9 DX
V11 99 111 .3V
V12 112 50 .3V
************
I2 99 50 -.5ma
Isupply 99 50 275ua
VOS 7 1 0v
*CHANGE OFFSET VOLTAGE TO 0V FOR OPEN-LOOP, OTHERWISE VOS
*********COMMON-MODE ZERO STAGE********
******* POLE STAGE **********
G3 98 15 10 98 1E-3
R12 98 15 1E3
***************************
*********** change for second pole
*C5 98 15 8p
*  
*************OUTPUT STAGE****************
F5 99 38 VA8 1
D9 40 38 DX
D10 38 99 DX
VA7 99 40 0
G12 98 32 15 98 1E-6
***                ^ INSERT NODE FROM LAST STAGE HERE
R15 98 32 1E6
D3 32 36 Dx
D4 37 32 Dx
V5 97 37 3.5V
V4 36 97 3.5V
*V5,V4 SET ISC
R16 34 97 130
*LOUT 197 97 .4nh
E1 99 33 99 32 1
VA8 33 34 0V
******************************************
.MODEL DA D(IS=100E-14 RS=.5K)
.MODEL DB D(IS=100E-14 RS=.5K)
.MODEL DX D(IS=100E-14)
.MODEL Dp D(N=0.001)
.MODEL MOSFET PMOS(VTO=.250 KP=1.8E-3)
*VTO ESTABLISHES INPUT VOLT. RANGE  , was -1.7
*
.Ends
*
*   ////////////// MAX4332 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX4330 DATA SHEET       <====
*
* connections:        non-inverting input
*                     |   inverting input
*                     |   |   positive power-supply
*                     |   |   |   negative power-supply
*                     |   |   |   |   output
*                     |   |   |   |   |
* OUTPUT CONNECTS:    1   2   99  50  97
*
*
.SUBCKT MAX4332 1 2 99 50 97
****************INPUT STAGE**********************
I1 99 4 .5e-3
M1   5  2 4 99 MOSFET
R3   5 50 2828
M2   6  7 4 99 MOSFET
R4   6 50 2828
DP1  1 99 DA
DP2 50  1 DA
DP3  2 99 DB
DP4 50  2 DB
Ibiase 7 4 25na
************************
************ GAIN, 1ST POLE, SLEW STAGE************
EH 99 98 99 50 0.5
G0  98  9 5 6 7.87e-1
VB 9 10 0V 
R0  98  9 127E3
C3 10 98 10.8e-8
*C3 10 98 1.3E-10
*** ***********
D107 10 99 Dx 
D103 10 100 Dx
D104 100 99 Dx
F100 100 99 POLY(1) VB -1.6E-1 1
****
D108 50 10 Dx
D105 101 10 Dx
D106 50 101 Dx
F2 50 101 POLY(1) VB -1.6E-1 -1
*** **********
D1 9 111 DX
D2 112 9 DX
V11 99 111 .3V
V12 112 50 .3V
************
I2 99 50 -.5ma
Isupply 99 50 275ua
VOS 7 1 0v
*CHANGE OFFSET VOLTAGE TO 0V FOR OPEN-LOOP, OTHERWISE VOS
*********COMMON-MODE ZERO STAGE********
******* POLE STAGE **********
G3 98 15 10 98 1E-3
R12 98 15 1E3
***************************
*********** change for second pole
*C5 98 15 8p
*  
*************OUTPUT STAGE****************
F5 99 38 VA8 1
D9 40 38 DX
D10 38 99 DX
VA7 99 40 0
G12 98 32 15 98 1E-6
***                ^ INSERT NODE FROM LAST STAGE HERE
R15 98 32 1E6
D3 32 36 Dx
D4 37 32 Dx
V5 97 37 3.5V
V4 36 97 3.5V
*V5,V4 SET ISC
R16 34 97 130
*LOUT 197 97 .4nh
E1 99 33 99 32 1
VA8 33 34 0V
******************************************
.MODEL DA D(IS=100E-14 RS=.5K)
.MODEL DB D(IS=100E-14 RS=.5K)
.MODEL DX D(IS=100E-14)
.MODEL Dp D(N=0.001)
.MODEL MOSFET PMOS(VTO=.250 KP=1.8E-3)
*VTO ESTABLISHES INPUT VOLT. RANGE  , was -1.7
*
.Ends
*
*   ////////////// MAX4334 MACROMODEL //////////////////
*
*   ====>      REFER TO MAX4330 DATA SHEET       <====
*
* connections:        non-inverting input
*                     |   inverting input
*                     |   |   positive power-supply
*                     |   |   |   negative power-supply
*                     |   |   |   |   output
*                     |   |   |   |   |
* OUTPUT CONNECTS:    1   2   99  50  97
*
*
.SUBCKT MAX4334 1 2 99 50 97
****************INPUT STAGE**********************
I1 99 4 .5e-3
M1   5  2 4 99 MOSFET
R3   5 50 2828
M2   6  7 4 99 MOSFET
R4   6 50 2828
DP1  1 99 DA
DP2 50  1 DA
DP3  2 99 DB
DP4 50  2 DB
Ibiase 7 4 25na
************************
************ GAIN, 1ST POLE, SLEW STAGE************
EH 99 98 99 50 0.5
G0  98  9 5 6 7.87e-1
VB 9 10 0V 
R0  98  9 127E3
C3 10 98 10.8e-8
*C3 10 98 1.3E-10
*** ***********
D107 10 99 Dx 
D103 10 100 Dx
D104 100 99 Dx
F100 100 99 POLY(1) VB -1.6E-1 1
****
D108 50 10 Dx
D105 101 10 Dx
D106 50 101 Dx
F2 50 101 POLY(1) VB -1.6E-1 -1
*** **********
D1 9 111 DX
D2 112 9 DX
V11 99 111 .3V
V12 112 50 .3V
************
I2 99 50 -.5ma
Isupply 99 50 275ua
VOS 7 1 0v
*CHANGE OFFSET VOLTAGE TO 0V FOR OPEN-LOOP, OTHERWISE VOS
*********COMMON-MODE ZERO STAGE********
******* POLE STAGE **********
G3 98 15 10 98 1E-3
R12 98 15 1E3
***************************
*********** change for second pole
*C5 98 15 8p
*  
*************OUTPUT STAGE****************
F5 99 38 VA8 1
D9 40 38 DX
D10 38 99 DX
VA7 99 40 0
G12 98 32 15 98 1E-6
***                ^ INSERT NODE FROM LAST STAGE HERE
R15 98 32 1E6
D3 32 36 Dx
D4 37 32 Dx
V5 97 37 3.5V
V4 36 97 3.5V
*V5,V4 SET ISC
R16 34 97 130
*LOUT 197 97 .4nh
E1 99 33 99 32 1
VA8 33 34 0V
******************************************
.MODEL DA D(IS=100E-14 RS=.5K)
.MODEL DB D(IS=100E-14 RS=.5K)
.MODEL DX D(IS=100E-14)
.MODEL Dp D(N=0.001)
.MODEL MOSFET PMOS(VTO=.250 KP=1.8E-3)
*VTO ESTABLISHES INPUT VOLT. RANGE  , was -1.7
*
.Ends
